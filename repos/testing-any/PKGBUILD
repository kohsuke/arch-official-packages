# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=initscripts
pkgver=2012.08.3
pkgrel=3
pkgdesc="System initialization/bootup scripts"
arch=('any')
url="http://www.archlinux.org"
license=('GPL2')
groups=('base')
provides=('initscripts-systemd')
conflicts=('initscripts-systemd')
replaces=('initscripts-systemd')
backup=(etc/inittab etc/rc.conf etc/rc.local etc/rc.local.shutdown)
depends=('glibc' 'bash' 'coreutils' 'systemd-tools' 'iproute2' 'ncurses' 'findutils' 'sysvinit')
optdepends=('dhcpcd: DHCP network configuration'
            'bridge-utils: Legacy network bridging support'
            'net-tools: Legacy network support'
            'wireless_tools: Legacy wireless networking')
makedepends=(asciidoc)
install=initscripts.install
source=("ftp://ftp.archlinux.org/other/initscripts/${pkgname}-${pkgver}.tar.xz"
        'split-hwclock-calls.diff')
md5sums=('b5e8de40bf798637cb1634a1555d0f1f'
         'd381ed1fbdc3780496317844241b0568')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # http://mailman.archlinux.org/pipermail/arch-projects/2012-September/003252.html
  # patch based on master does not apply, so this is the exact patch posted to
  # the bug report it fixes (FS#31416).
  patch -Np1 <"$srcdir/split-hwclock-calls.diff"

  make DESTDIR=${pkgdir} install

  # moved to filesystem
  rm -rf ${pkgdir}/etc/profile.d
  rm ${pkgdir}/usr/share/man/man7/archlinux.7
}
