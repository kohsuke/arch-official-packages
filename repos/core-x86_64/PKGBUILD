# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: mario <mario_vazq@hotmail.com>

pkgname=dnsutils

# Use a period and not a hyphen before the patch level for proper versioning.
pkgver=9.9.0
_pkgver=9.9.0
pkgrel=1

pkgdesc='DNS utilities: dig host nslookup'
url='http://www.isc.org/software/bind/'
license=('custom:ISC')
arch=('i686' 'x86_64')
options=('!makeflags')
depends=('openssl' 'krb5' 'idnkit')
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz"
        'remove-bind.patch'
        'trusted-key.key')
sha1sums=('6be77c75c8649088b0ae7124d819b5f412bb0094'
          'b465ef6160b004838f04de9978fe1be8422af777'
          'ca93da30c89de4365b846325d6ec4674a3e4fc02')

replaces=('bind-tools' 'host')

build() {
	cd "${srcdir}/bind-${_pkgver}"

	patch -p1 -i ../remove-bind.patch
	export STD_CDEFINES='-DDIG_SIGCHASE'

	# hack to remove unused bloat from the binaries
	CFLAGS+=' -fdata-sections -ffunction-sections'
	LDFLAGS+=' -Wl,--gc-sections'

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--disable-linux-caps \
		--with-openssl \
		--with-idn \

	make
}

package() {
	cd "${srcdir}/bind-${_pkgver}"
	install -Dm644 ../trusted-key.key "${pkgdir}"/etc/trusted-key.key
	install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cd bin; make DESTDIR="${pkgdir}" install
}
