# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=procps
pkgver=3.2.8
pkgrel=4
pkgdesc="Utilities for monitoring your system and processes on your system"
arch=('i686' 'x86_64')
url="http://procps.sourceforge.net/"
license=('GPL' 'LGPL')
groups=('base')
depends=('ncurses')
backup=('etc/sysctl.conf')
source=(http://procps.sourceforge.net/procps-${pkgver}.tar.gz sysctl.conf
        groff-top-manpage.patch
        procps-3.2.7-top-manpage.patch
        procps-3.2.7-free-hlmem.patch
        procps-3.2.7-ps-man-fmt.patch
        procps-3.2.7-longcmd.patch
        procps-3.2.7-psman.patch
        procps-3.2.7-slabtop-once.patch
        procps-3.2.7-top-clrscr.patch
        procps-3.2.7-top-cpu0.patch
        procps-3.2.7-top-env-cpuloop.patch
        procps-3.2.7-top-remcpu.patch
        procps-3.2.7-top-sorthigh.patch
        procps-3.2.7-watch-unicode.patch
        procps-3.2.7-w-best.patch
        procps-3.2.8-gmake-3.82.patch
        procps-3.2.8-setlocale.patch
        procps-3.2.8-threads.patch
        procps-3.2.8-linux-ver-init.patch
        procps-3.2.8-kernel3-uts.patch)
sha1sums=('a0c86790569dec26b5d9037e8868ca907acc9829'
          '9b9a314010d042b5aa487893c06f361531525e6a'
          'bdd627f602ed2e38994d49309de0edbd90b41aff'
          '16737700b22eec8f4e8d16bce099f4b1662501e2'
          'da1b9e61f8df4a8e23a7032b245dd99e32e2c01c'
          'ba9ad9f0bde3e84484b5a0238e53534438e924be'
          '1a656d8094be41e019b2a9598b07c8a3d47f9de3'
          '9944224ed2f5bd7d7fb505127bd71d6f01d88373'
          '954026785a9e6c90d796e93563fcea2a693691e8'
          '8582cd15346fa939f3fd01081620de0ffc617335'
          '80e31310da53baf095e905d94485324932e433df'
          '15fe886a2b4bed5885e6d457954e1145d1dcd2a2'
          'bce21809ee0cb917ddf5b0c0bf2a91ea50feb0c6'
          '1be00d71e8e16ff2d656e5dd1f9988f2f3c0d5a0'
          '52068a433535ec3d5d974141be3d6d3b6a41725a'
          '3a8f1ba912112d7520becb28746e97b11d9b0c86'
          '3a4908ed8dbc936e39cb9094af8098ef9e656581'
          '09b1f6a3f9691b9f1432549a7a74a90ee5cfbdde'
          '926b92913201f21c45d48436c0902cffc059ce27'
          '171d5352940200c209e0c2d43c17f01c02173a66'
          'e2b2917e5462bd0aa3cb1b62942dbe8e8e81c16b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../groff-top-manpage.patch
  patch -p1 < ../procps-3.2.7-top-manpage.patch
  patch -p1 < ../procps-3.2.7-free-hlmem.patch
  patch -p1 < ../procps-3.2.7-ps-man-fmt.patch
  patch -p1 < ../procps-3.2.7-psman.patch
  patch -p1 < ../procps-3.2.7-longcmd.patch
  patch -p1 < ../procps-3.2.7-slabtop-once.patch
  patch -p1 < ../procps-3.2.7-top-clrscr.patch
  patch -p1 < ../procps-3.2.7-top-cpu0.patch
  patch -p1 < ../procps-3.2.7-top-env-cpuloop.patch
  patch -p1 < ../procps-3.2.7-top-remcpu.patch
  patch -p1 < ../procps-3.2.7-top-sorthigh.patch
  patch -p1 < ../procps-3.2.7-watch-unicode.patch
  patch -p1 < ../procps-3.2.7-w-best.patch
  patch -p1 < ../procps-3.2.8-gmake-3.82.patch
  patch -p1 < ../procps-3.2.8-setlocale.patch
  patch -p1 < ../procps-3.2.8-threads.patch
  patch -p0 < ../procps-3.2.8-linux-ver-init.patch
  patch -p1 < ../procps-3.2.8-kernel3-uts.patch
  sed -i "s:\$(lib64):lib:" Makefile
  make CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" ldconfig=/bin/true install
  install -D -m644 "${srcdir}/sysctl.conf" "${pkgdir}/etc/sysctl.conf"
  install -d "${pkgdir}/usr/include/proc"
  install -m644 proc/*.h "${pkgdir}/usr/include/proc"
}
