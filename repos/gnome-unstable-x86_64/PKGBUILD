# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=wimax
pkgver=1.5.1
pkgrel=1
pkgdesc="WiMAX Network Service"
arch=(i686 x86_64)
license=('custom:BSD')
url="http://www.linuxwimax.org/"
depends=(libeap wimax-tools dhclient)
options=('!libtool')
source=("$pkgname-$pkgver.tar.gz::http://www.linuxwimax.org/Download?action=AttachFile&do=get&target=$pkgname-$pkgver.tar.gz"
        LICENSE wimaxd.rc)
sha256sums=('c04d4617e99e836b78f9c4e6eb86a88bc20ab789c05eb201bb8f3846899186a5'
            '00337c026ccf1ac2c624c325801378129e7daa5294e98a5b79ae4ceb60b14624'
            '8446ec0148b594fc4d6f82e53484f3771fee3c5dc998f91d7fc2d58e90bbf6ee')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Suppress a metric asston of messages
  sed -i '/CFLAGS="/s/-Wall/-Wno-pointer-to-int-cast -Wno-int-to-pointer-cast/' configure

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-linux=/usr \
    --with-libwimaxll=/usr \
    --enable-instrument \
    --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Fix dhclient path
  sed -i 's:\B/sbin/dhclient:/usr/sbin/dhclient:' \
    "$pkgdir/usr/share/wimax/dhcp_renew.sh"
  grep dhclient "$pkgdir/usr/share/wimax/dhcp_renew.sh"

  install -D "$srcdir/wimaxd.rc" "$pkgdir/etc/rc.d/wimaxd"

  install -Dm 644 "$srcdir/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
