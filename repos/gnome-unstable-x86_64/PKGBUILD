# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem-plparser
pkgver=3.4.0
pkgrel=1
url="http://www.gnome.org"
pkgdesc="Totem playlist parser library"
license=('LGPL')
arch=(i686 x86_64)
depends=('gmime' 'libsoup-gnome' 'libarchive' 'libquvi')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/totem-pl-parser/${pkgver%.*}/totem-pl-parser-$pkgver.tar.xz)
sha256sums=('a9c9dd1c815f0a407546dd209c209284a846ae2978a82faf2b8d72e86a3b8cc7')

build() {
  cd totem-pl-parser-$pkgver
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static
  make
}

package(){
  cd totem-pl-parser-$pkgver
  make DESTDIR="$pkgdir" install
}
