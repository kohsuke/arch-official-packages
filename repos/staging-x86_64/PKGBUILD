# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kcolorchooser
pkgver=4.6.90
pkgrel=1
pkgdesc="Color Chooser"
url="http://kde.org/applications/graphics/kcolorchooser/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kcolorchooser-${pkgver}.tar.bz2")
sha1sums=('598742387491f91038fa4a0914fc79df4fdbea51')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kcolorchooser-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
