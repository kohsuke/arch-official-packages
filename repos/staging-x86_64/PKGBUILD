# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Bächler <thomas.archlinux.org>

pkgbase=ntfs-3g_ntfsprogs
pkgname=('ntfs-3g' 'ntfsprogs')
pkgver=2013.1.13
pkgrel=1
url="http://www.tuxera.com/community/ntfs-3g-download/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc' 'fuse')
options=('!libtool')
source=(http://tuxera.com/opensource/$pkgbase-$pkgver.tgz)
sha1sums=('8c12b7644d90ae9fb8d0aca0d7ebd5f8fac2c818')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr \
    --with-fuse=external --disable-static \
    --enable-posix-acls --disable-ldconfig \
    --mandir=/usr/share/man
  make
}

package_ntfs-3g() {
  pkgdesc="Stable read and write NTFS driver"

  for DIR in src libntfs-3g include; do
    cd "$srcdir/$pkgbase-$pkgver/$DIR"
    make DESTDIR="$pkgdir" install
  done

  mv "$pkgdir"/bin/* "$pkgdir"/usr/bin/
  rm -rf "$pkgdir"/{s,}bin
  rmdir "$pkgdir/lib"

  cd "$pkgdir"/usr/bin
  ln -s ntfs-3g mount.ntfs
  ln -s ntfs-3g mount.ntfs-3g
  ln -s lowntfs-3g mount.lowntfs-3g
}

package_ntfsprogs() {
  pkgdesc="NTFS filesystem utilities"
  depends=('util-linux' 'ntfs-3g')

  cd "$srcdir/$pkgbase-$pkgver/ntfsprogs"
  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/sbin/* "$pkgdir"/usr/bin/
  rm -rf "$pkgdir"/sbin
}
