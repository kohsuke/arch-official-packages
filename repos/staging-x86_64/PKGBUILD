# $Id$
# Maintainer: dale <dale@archlinux.org>

# todo: add perl support, DESTDIR has no affect on perl
# and it seems that moving the files manually doesn't work

pkgname=pilot-link
pkgver=0.12.5
pkgrel=5
pkgdesc="A suite of tools for connecting to PalmOS handheld devices"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('readline>=6.0' 'libpng>=1.6.0' 'libusb-compat' 'popt' 'bluez-libs')
options=('!libtool')
url="http://www.pilot-link.org/"
source=(http://downloads.pilot-link.org/${pkgname}-${pkgver}.7z{,.asc}
        pilot-link-png14.patch)
md5sums=('cd409c09b93654f0fc45015a7fc17b2c'
         'SKIP'
         'a21a2eeb01544a8a261a75e64a3120c3')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}/pilot-link-png14.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-conduits --enable-libusb \
    --with-libiconv --with-libpng --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
