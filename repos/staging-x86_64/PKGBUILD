# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=xchat
pkgver=2.8.8
pkgrel=3
pkgdesc="A GTK+ based IRC client"
arch=('i686' 'x86_64')
url="http://www.xchat.org/"
license=('GPL')
depends=('gtk2' 'openssl' 'dbus-glib')
makedepends=('tcl' 'perl' 'python2')
optdepends=('enchant: for spell checking support' 'tcl: for tcl plugin' \
            'python2: for python plugin')
options=('!libtool')
changelog=ChangeLog
source=(http://www.xchat.org/files/source/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
md5sums=('9a11f13121ff0af787bba3b014378e58')
sha1sums=('e12305da42d1aacc26c2ca25e239f393d4dd3532')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-openssl --enable-dbus \
    --disable-textfe --enable-ipv6 --enable-shm --enable-spell=static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
