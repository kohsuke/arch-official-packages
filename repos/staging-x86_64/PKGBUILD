# $Id$
# Maintainer:
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: Tom K <tomk@runbox.com>

pkgname=mod_perl
pkgver=2.0.8
pkgrel=1
pkgdesc="Apache module that embeds the Perl interpreter within the server"
arch=('i686' 'x86_64')
url="http://perl.apache.org/"
license=('APACHE')
depends=('perl' 'apache' 'db' 'apr-util' 'perl-linux-pid')
options=('!emptydirs')
source=(http://apache.org/dist/perl/$pkgname-$pkgver.tar.gz
        mod_perl-2.0.6-nolfs.patch)
sha256sums=('35dc1b7a40a90a395ce88bba2df84f22289975f34d1757de6d715560c20a20e6'
            '5d1b6a051d847108b23b5dc06ea5885c37dd81d3a9156b69d179d95b5cf92f08')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Patch from openSUSE to fix segfaults on i686 (FS#25008):
  #   avoid a conflict between apache's LARGEFILE64_SOURCE and perl's
  #   FILE_OFFSET_BITS=64 because the conflict isn't real and causes the perl
  #   structures to be invalid if only the apache flags are used
  patch -Np1 -i "$srcdir/mod_perl-2.0.6-nolfs.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor MP_APXS=/usr/sbin/apxs
  make
}

package() {
  cd  "$srcdir/$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
