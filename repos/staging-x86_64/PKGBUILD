# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-qtruby
pkgver=4.8.1
pkgrel=1
pkgdesc="Qt bindings for ruby"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt' 'ruby')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen')
conflicts=('kdebindings-ruby')
source=("http://download.kde.org/stable/${pkgver}/src/xz/qtruby-${pkgver}.tar.xz")
sha1sums=('b2532f9e9e04760fce5d9ac87768e6085f0c231b')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../qtruby-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCUSTOM_RUBY_SITE_LIB_DIR=`ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]'`
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
