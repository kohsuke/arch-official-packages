# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xournal
pkgver=0.4.5
pkgrel=5
pkgdesc='Notetaking and sketching application'
arch=('i686' 'x86_64')
url='http://xournal.sourceforge.net/'
license=('GPL')
depends=('libgnomecanvas' 'poppler-glib' 'shared-mime-info' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('ghostscript: import PS/PDF files as bitmap backgrounds')
source=("http://downloads.sourceforge.net/xournal/xournal-${pkgver}.tar.gz"
        'pdf-export-64.patch'
        'poppler-api.patch')
sha1sums=('390cb275774469ed1b04b7268dd625bd456c895e'
          'f23f53b9f69ba8a5773c53d7bca99abf9d8504f8'
          '5aedd5610b42df96e964bb889d4bf0831c2080bc')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../pdf-export-64.patch # from xournal CVS, see FS#21693
	patch -p1 -i ../poppler-api.patch

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install desktop-install
}
