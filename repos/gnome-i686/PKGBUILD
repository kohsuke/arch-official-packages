# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gcalctool
pkgver=5.23.90
pkgrel=1
pkgdesc="GNOME Scientific calculator"
arch=(i686 x86_64)
license=('GPL')
depends=('gconf>=2.23.2' 'libglade>=2.6.2')
makedepends=('perlxml' 'pkgconfig' 'gnome-doc-utils>=0.12.2')
groups=('gnome-extra')
options=(!emptydirs)
url="http://www.gnome.org"
install=gcalctool.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/5.23/${pkgname}-${pkgver}.tar.bz2)
md5sums=('6d9ae9c978f57d6b9d17d403ec589b3d')
	
build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  install -m755 -d ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
