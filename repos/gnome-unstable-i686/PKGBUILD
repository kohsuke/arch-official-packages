# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gconf-editor
pkgver=2.91.91.1
pkgrel=1
pkgdesc="Graphical gconf registry editor"
arch=(i686 x86_64)
license=('GPL')
depends=('gconf' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils')
url="http://www.gnome.org"
groups=('gnome-extra')
options=(!emptydirs)
install=gconf-editor.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('c3f5d83bab175828fa0ad06dd5be532ad9c1d47d2c8ce384b3d68d507e12a95b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
     --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gconf-editor ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
sha256sums=('047865100700ba2512084b9eeceeb07473eda3839b0a285ad73eea7c48647385')
