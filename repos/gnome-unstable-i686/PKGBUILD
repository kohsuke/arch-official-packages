# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=libeap
pkgver=1.5.1
pkgrel=1
pkgdesc="WiMAX EAP Library"
arch=(i686 x86_64)
license=('custom:BSD')
url="http://www.linuxwimax.org"
depends=(openssl)
_patchname=wpa_supplicant-0.7.2-generate-libeap-peer.patch
source=("$_patchname::$url/Download?action=AttachFile&do=get&target=$_patchname"
        http://hostap.epitest.fi/releases/wpa_supplicant-0.7.3.tar.gz
        LICENSE)
sha256sums=('aaaf08dc2149c0ce8d6622c2cf6b893ba899baf3df0087725974c22f44543f56'
            'd0cd50caa85346ccc376dcda5ed3c258eef19a93b3cade39d25760118ad59443'
            '00337c026ccf1ac2c624c325801378129e7daa5294e98a5b79ae4ceb60b14624')

build() {
  cd "$srcdir/wpa_supplicant-0.7.3"
  patch -Np1 -i "$srcdir/$_patchname"
  make -C src/eap_peer
}

package() {
  cd "$srcdir/wpa_supplicant-0.7.3/src/eap_peer"
  make DESTDIR="$pkgdir" install

  ln -s libeap.so.0.0.0 "$pkgdir/usr/lib/libeap.so.0"
  
  install -Dm 644 "$srcdir/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
