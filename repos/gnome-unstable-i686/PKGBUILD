# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=pygtk
pkgver=2.22.0
pkgrel=1
pkgdesc="Python bindings for the GTK widget set"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libglade>=2.6.4' 'pycairo>=1.8.10' 'pygobject>=2.21.5')
makedepends=('python-numpy')
optdepends=('python-numpy')
options=('!libtool')
url="http://www.pygtk.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/pygtk/2.22/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('4acf0ef2bde8574913c40ee4a43d9c4f43bb77b577b67147271b534501a54cc8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  sed -i -e 's#env python#env python2#' "${pkgdir}"/usr/lib/pygtk/2.0/demos/*.py
  install -m644 gtk/gtk-extrafuncs.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
}
