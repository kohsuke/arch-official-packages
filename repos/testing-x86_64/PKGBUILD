# $Id$
# Maintainer:
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andries Radu <admiral0@live.it>

pkgname=opengtl
pkgver=0.9.18
pkgrel=3
pkgdesc="A set of libraries for using and integrating transformation algorithms (such as filter or color conversion) in graphics applications"
url="http://www.opengtl.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gcc-libs' 'llvm-libs')
makedepends=('cmake' 'libpng' 'llvm')
optdepends=('libpng: for using the png extension')
source=(http://download.opengtl.org/OpenGTL-$pkgver.tar.bz2
        opengtl-0.9.18-llvm-3.3.patch
        opengtl-0.9.18-memcpy.patch)
sha256sums=('f094b3d2a1a60519975bb3badc05e704f2b93c7a3d2cb753041f2ff27cfcd9f8'
            'c43418bc039660ae86cb86b8b8175342793cb1361c699faa9b6342bc896e3d6c'
            '357cc2e538bbaaffe7d58956959d8a7fbae09741d4a87488a5a8c51ec645d23a')

prepare() {
  mkdir build

  # Fix build with LLVM 3.3 (patch from Fedora)
  patch -d OpenGTL-$pkgver -Np1 -i "$srcdir/opengtl-0.9.18-llvm-3.3.patch"

  # Fix build with libpng 1.6
  patch -d OpenGTL-$pkgver -Np1 -i "$srcdir/opengtl-0.9.18-memcpy.patch"
}

build() {
  cd "$srcdir/build"
  cmake ../OpenGTL-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
