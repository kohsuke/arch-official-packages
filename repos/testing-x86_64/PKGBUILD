# $Id$
# Contributor: Dennis "Gyroplast" Herbrich <dennis@archlinux.org>,  dorphell <dorphell@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=isdn4k-utils
pkgver=3.22_20120509
pkgrel=1
pkgdesc="User space administration programs and tools for ISDN"
arch=('i686' 'x86_64')
url="http://www.isdn4linux.de/"
license=('GPL')
depends=('ncurses' 'sh') 
backup=(etc/isdn/{callerid.conf,isdn.conf,rate.conf,isdnlog.isdnctrl0.options})
options=('!makeflags' '!libtool')
source=(ftp://ftp.archlinux.org/other/isdn4k-utils/${pkgname}-${pkgver}.tar.gz
        config)
replaces=('capi4k-utils')

build() {
  cd $srcdir/$pkgname-$pkgver
  cp $srcdir/config ./.config
  make subconfig
  cd capi20
  libtoolize -fc
  aclocal
  automake -fc
  cd $srcdir/$pkgname-$pkgver
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -dm755 $pkgdir/usr/sbin
  make DESTDIR=$pkgdir install
  chmod 755 $pkgdir/usr/sbin/*
}
md5sums=('4137dcb232b13bcf7069b37eb1e1b124'
         '00b130a283ba3a08c6a9020e1018d05a')
