# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=consolekit
pkgver=0.4.2
pkgrel=1
pkgdesc="A framework for defining and tracking users, login sessions, and seats"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/ConsoleKit"
license=('GPL')
depends=('polkit>=0.98' 'zlib' 'libx11>=1.3.5' 'dbus-glib>=0.86')
makedepends=('pkgconfig' 'xmlto' 'docbook-xsl')
options=(!libtool)
source=(http://www.freedesktop.org/software/ConsoleKit/dist/ConsoleKit-${pkgver}.tar.bz2
        pam-foreground-compat.ck
        consolekit.logrotate
        check-for-VT_WAITEVENT-ioctl-during-runtime.patch)
md5sums=('285acb35bfcb2b8dc21c6071e6f6e116'
         'a8a4de71d9b0549b8143e5f6c2a36fc7'
         '6fefa451d9fe2fc6d6269629d3529793'
         'dea270ebf52ef0e1b8130f1c6da73d65')

build() {
  cd "${srcdir}/ConsoleKit-${pkgver}"
  patch -Np1 -i "${srcdir}/check-for-VT_WAITEVENT-ioctl-during-runtime.patch"
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/ConsoleKit \
      --enable-pam-module \
      --enable-docbook-docs
  make
  make DESTDIR="${pkgdir}" install
  install -m755 "${srcdir}/pam-foreground-compat.ck" "${pkgdir}/usr/lib/ConsoleKit/run-session.d/"

  # install the logrotate config
  install -D -m644 ${srcdir}/consolekit.logrotate ${pkgdir}/etc/logrotate.d/consolekit
}
