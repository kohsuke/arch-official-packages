# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=monodevelop
pkgver=3.0.3.5
pkgrel=1
pkgdesc="An IDE primarily designed for C# and other .NET languages"

# should probably changed to "any" - no ELF files - OpenSUSE builds it as NOARCH 
arch=('i686' 'x86_64')

url="http://www.monodevelop.com"
license=('GPL')
depends=('mono>=2.10.8' 'mono-addins>=0.6.2' 'gnome-sharp' 'desktop-file-utils' 'hicolor-icon-theme')
options=(!makeflags)
install=monodevelop.install
source=(http://download.mono-project.com/sources/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ca46b77b49266112b63a8f5c451c5565')

build() {
  export MONO_SHARED_DIR=$srcdir/src/.wabi
  mkdir -p $MONO_SHARED_DIR

  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --disable-update-mimedb --disable-update-desktopdb
  LD_PRELOAD="" make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  LD_PRELOAD="" make DESTDIR=$pkgdir install
  rm -r $MONO_SHARED_DIR
}

