# $Id$
# Maintainer: tobias [tobias.archlinux.org]
# Contributor: tobias [tobias.justdreams.de]

pkgname=cinepaint
pkgver=1.0
pkgrel=2
pkgdesc="Sophisticated graphics manipulation programm supporting > 8bit pictures"
arch=('i686' 'x86_64')
license=('LGPL' 'GPL' 'MIT')
url="http://www.cinepaint.org"
depends=('gtk2' 'openexr' 'lcms' 'libxpm' 'fltk' 'ftgl' 'libxxf86vm')
makedepends=('python2' 'gutenprint')
optdepends=('python2: for python plug-ins' 
            'gutenprint: for print plug-ins'
            'ghostscript: for pdf plug-ins')
options=('!libtool')
install=cinepaint.install
source=(http://sourceforge.net/projects/cinepaint/files/CinePaint/${pkgname}-${pkgver}.tgz
        LICENSE cinepaint-libpng15.patch)
md5sums=('7dfdb005d246578392ac9bd500534804'
         '169085743f667c250ea2e4022efecf5f'
         '682de65ad358512d00a8b76730453664')

build() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i ../cinepaint-libpng15.patch
  find plug-ins/pygimp -type f -exec sed -i 's#env python#env python2#' {} +
  sed -i 's|$(LDFLAGS) -o|$(LDFLAGS) $(GTK_LIBS) -o|' lib/Makefile.in

  aclocal -I aclocal
  autoconf
  LIBS+="-lstdc++ -lm -lX11" ./configure --prefix=/usr --libdir=/usr/lib --mandir=/usr/share/man \
    --enable-gtk2 --enable-pygimp --with-python=/usr/bin/python2
  sed -i 's/-Wl,,/-Wl,/' lib/fl_i18n/Makefile plug-ins/bracketing_to_hdr/Makefile \
    plug-ins/collect/Makefile plug-ins/icc_examin/icc_examin/Makefile plug-ins/pdf/Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  sed -i -e "s|-I$srcdir/cinepaint||" -e "s|-I$srcdir/cinepaint/lib||" -e "/libcinepaint.la/d" \
    -e "s|$srcdir/cinepaint/lib/.libs/\$dlname||" "${pkgdir}/usr/bin/cinepainttool"
  sed -i "s/cinepaint.png/cinepaint/" "${pkgdir}/usr/share/applications/cinepaint.desktop"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
