# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rfkill
pkgver=0.4
pkgrel=6
pkgdesc='Tool for enabling and disabling wireless devices'
url='http://linuxwireless.org/en/users/Documentation/rfkill'
license=('custom')
arch=('i686' 'x86_64')
source=("http://wireless.kernel.org/download/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'service.unblock'
        'service.block'
        'rules.d'
        'conf.d'
        'rc.d')
sha1sums=('fccabf5a272ac2891f24dc35449dc6f4c45ff44b'
          '8b4f0effcad564e929cf65c50773881d1209e54b'
          '10febfa26656bc395aeca477d4d44a1e0482375c'
          '6390eaa793b473098aaa11b13c5254d611a4c13a'
          'd969fe927a9fb2926af43a311a7c1fa126fe59a9'
          '380af0b002f51543557306ed68a27f0059fd5a6f')

backup=('etc/conf.d/rfkill')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../rules.d "${pkgdir}/usr/lib/udev/rules.d/60-rfkill.rules"
	install -Dm644 ../service.block "${pkgdir}/usr/lib/systemd/system/rfkill-block@.service"
	install -Dm644 ../service.unblock "${pkgdir}/usr/lib/systemd/system/rfkill-unblock@.service"
	install -Dm644 ../conf.d "${pkgdir}/etc/conf.d/rfkill"
	install -Dm755 ../rc.d "${pkgdir}/etc/rc.d/rfkill"
}
