# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-print-manager
pkgver=4.10.0
pkgrel=1
pkgdesc="A tool for managing print jobs and printers"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdeutils/print-manager'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-workspace' 'system-config-printer-common')
makedepends=('cmake' 'automoc4')
optdepends=('system-config-printer-gnome: auto-detect the printer driver')
replaces=('print-manager' 'kdeutils-printer-applet' 'kdeadmin-system-config-printer-kde')
conflicts=('print-manager')
groups=('kde' 'kdeutils')
source=("http://download.kde.org/stable/${pkgver}/src/print-manager-${pkgver}.tar.xz")
sha1sums=('182a514ed62baf1892be6c4b1977260ba365c634')

build() {
  mkdir build
  cd build
  cmake ../print-manager-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
