# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xchat
pkgver=2.8.8
pkgrel=11
pkgdesc="A GTK+ based IRC client"
arch=('i686' 'x86_64')
url="http://www.xchat.org/"
license=('GPL')
depends=('gtk2' 'openssl' 'dbus-glib' 'libnotify' 'hicolor-icon-theme')
makedepends=('tcl' 'perl' 'python2' 'librsvg')
optdepends=('enchant: for spell checking support' 'tcl: for tcl plugin'
            'python2: for python plugin')
options=('!libtool')
install=xchat.install
source=(http://www.xchat.org/files/source/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        http://xchat.org/files/icons/xchat-svg.tar.bz2
        xchat-2.8.8-libnotify07.patch
        xchat-2.8.8-link-against-libnotify.patch
        xchat-2.8.8-glib-2.31.patch)
sha1sums=('e12305da42d1aacc26c2ca25e239f393d4dd3532'
          '0d366346cc11e0efb57fc2648fe423c94a3469bd'
          'a053fba4e1911d1ee6a8248fe19e344797920fe3'
          '70c3cc29fc55ff35f701ef8ac23078b6e3761ce1'
          'aecaf6176a7cfd62555207b02f2793b360aa39da')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/xchat-2.8.8-libnotify07.patch"
  patch -p1 -i "${srcdir}/xchat-2.8.8-link-against-libnotify.patch"
  patch -p1 -i "${srcdir}/xchat-2.8.8-glib-2.31.patch"
  autoconf
  autoheader

  LIBS+="-lgmodule-2.0" ./configure --prefix=/usr --sysconfdir=/etc --datadir=/usr/share \
    --enable-openssl --enable-dbus --disable-textfe --enable-ipv6 --enable-shm --enable-spell=static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  for i in 24 32 48 64 128 256 ; do
    install -d "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps"
    rsvg-convert -w $i -h $i -o "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/xchat.png" ../xchat-cloned.svg
  done
  install -D -m644 ../xchat-cloned.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/xchat.svg"
  install -D -m644 ../xchat-used.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/xchat2.svg"
}
