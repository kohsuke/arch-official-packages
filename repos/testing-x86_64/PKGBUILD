# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-qt4
pkgver=0.9.1
pkgrel=1
pkgdesc="A library for Qt-based Telepathy clients"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/"
license=('LGPL')
options=('!libtool')
depends=('qt' 'telepathy-farstream')
makedepends=('libxslt' 'python2' 'cmake' 'doxygen')
source=("http://telepathy.freedesktop.org/releases/telepathy-qt/telepathy-qt-$pkgver.tar.gz"
        gcc47.patch)
md5sums=('e0e6c1a987bbeae44c778344a2cc9d80'
         '32aa8eb06c76cb03af5ced27a674ebfb')

build() {
    cd "$srcdir"
    patch -Np0 -i gcc47.patch
    mkdir build
    cd build
    cmake ../telepathy-qt-$pkgver \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "$srcdir"/build
    make DESTDIR="$pkgdir" install
}
