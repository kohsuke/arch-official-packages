# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=0.3.3
pkgrel=4
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('i686' 'x86_64')
url="http://www.weechat.org/"
license=('GPL')
depends=('gnutls')
makedepends=('pkgconfig' 'perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell')
optdepends=('perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell')
options=('!libtool')
source=(http://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.bz2
	ruby-1.9.2-fixes.patch)
md5sums=('01648f8717ab1ea5233f9818d45a7c24'
	'03271e31bee52d671c7c8299a02d77d2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's#python2.2#python2.2 python2#' configure
  # fixes for ruby 1.9.2 (in upstream development sources)
  patch -Np1 -i "$srcdir/ruby-1.9.2-fixes.patch"
  ./configure --prefix=/usr \
              --includedir=${pkgdir}/usr/include \
              --with-debug=0 LDFLAGS="-lm -ldl"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
