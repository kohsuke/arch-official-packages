# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: ezzetabi <ezzetabi at gawab dot com>

pkgname=ppl
pkgver=1.0
pkgrel=1
pkgdesc="A modern library for convex polyhedra and other numerical abstractions."
arch=('i686' 'x86_64')
url="http://bugseng.com/products/ppl"
license=('GPL3')
depends=('gmp>=5.0')
options=('!docs' '!libtool')
source=(http://bugseng.com/products/ppl/download/ftp/releases/$pkgver/ppl-$pkgver.tar.gz{,.sign})
md5sums=('c4566928c6b7bb63e95f96d1a7b5cb4c'
         '5f490df826c76fed296060ca8fca4eb5')

build() {
  cd $srcdir/ppl-$pkgver
  ./configure --prefix=/usr --enable-interfaces="c,cxx"
  make
}

check() {
  cd $srcdir/ppl-$pkgver
  make check
}

package() {
  cd $srcdir/ppl-$pkgver
  make DESTDIR=$pkgdir install
}

