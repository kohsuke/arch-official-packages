# $Id$
# Maintainer: 
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-quicklauncher-plugin
pkgver=1.9.4
pkgrel=5
pkgdesc="plugin that creates 4 little application launcher in the Xfce4 panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://xfce-goodies.berlios.de/"
groups=('xfce4-goodies')
depends=('xfce4-panel>=4.7.4' 'libxfcegui4')
makedepends=('intltool')
options=('!libtool')
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/1.9/${pkgname}-${pkgver}.tar.bz2
	xfce4-quicklauncher-plugin-1.9.4-desktop-file.patch
	xfce4-quicklauncher-plugin-1.9.4-fix-missing-english-translation.patch
	xfce4-quicklauncher-plugin-1.9.4-fix-multiscreen.patch
	xfce4-quicklauncher-plugin-1.9.4-save-settings.patch
	xfce4-quicklauncher-plugin-1.9.4-xfce4-settings-manager.patch)
md5sums=('299e17f196ecfa5fb018cf65abb19b56'
         'a7826c9f8199a2f1e914fd39b7f9e2bf'
         '4ea4d06ab7284e78ddc4d60304f02cdf'
         '5e8126c05def1211fb4a2a65f2812a1b'
         '474237b205035214df1c723407251ade'
         '9e7f789129b08c787978c7a72a55fae7')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # Fedora patches
  patch -Np0 -i ${srcdir}/xfce4-quicklauncher-plugin-1.9.4-desktop-file.patch
  patch -Np0 -i ${srcdir}/xfce4-quicklauncher-plugin-1.9.4-fix-multiscreen.patch
  patch -Np1 -i ${srcdir}/xfce4-quicklauncher-plugin-1.9.4-save-settings.patch
  patch -Np1 -i ${srcdir}/xfce4-quicklauncher-plugin-1.9.4-fix-missing-english-translation.patch
  patch -Np0 -i ${srcdir}/xfce4-quicklauncher-plugin-1.9.4-xfce4-settings-manager.patch
  
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
