# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kgeography
pkgver=4.7.0
pkgrel=1
pkgdesc="Geography Trainer"
url="http://kde.org/applications/education/kgeography/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kgeography-${pkgver}.tar.bz2")
sha1sums=('5fa6bff0e67e11657d55435c3faffdfcaf318e1e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kgeography-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
