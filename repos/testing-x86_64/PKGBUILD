# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=1.6.5
pkgrel=2
pkgdesc='Input method server supporting various input methods'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://hyperrate.com/dir.php?eid=67'
depends=('gtk2' 'libxtst')
makedepends=('qt' 'gtk3' 'anthy' 'libchewing')
optdepends=('qt: support for qt4 input method'
            'gtk3: support for gtk3 input method'
            'anthy: support for anthy input method'
            'libchewing: support for chewing input method')
source=("http://www.csie.nctu.edu.tw/~cp76/gcin/download/${pkgname}-${pkgver}.tar.bz2"
        'gtk3-crash.patch'
        'qt4-lib64.patch')
sha1sums=('4ca040981b5a473a8ff70d46e4a92bba39e22405'
          '5529884d655cf9c95e7bf5ddced3ffed140bbc20'
          '4307b0d7d6dc10f15909c94973c4357caf31de7e')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../qt4-lib64.patch
	patch -p1 -i ../gtk3-crash.patch
	./configure --prefix=/usr --use_i18n=Y
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
