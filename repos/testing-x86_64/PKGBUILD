# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>

pkgname=mediastreamer
pkgver=2.8.2
pkgrel=3
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=('i686' 'x86_64')
url="http://www.linphone.org/"
license=('GPL')
depends=('ortp' 'speex' 'ffmpeg' 'v4l-utils' 'libxv' 'libpulse')
makedepends=('intltool')
options=('!libtool')
source=("http://download-mirror.savannah.gnu.org/releases/linphone/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig}
        mediastreamer-ffmpeg.patch)
md5sums=('e51ea9d5fce1396b374d10473dfbadec'
         '805da7def98f367e621363fa0c951fe8'
         'f147546489a973f148ce3dd2dba36834')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -p1 -i "${srcdir}"/mediastreamer-ffmpeg.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
