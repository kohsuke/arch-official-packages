# $Id$
# Maintainer: damir <damir@archlinux.org>

pkgname=octave
pkgver=2.1.73
pkgrel=6
pkgdesc="Octave is a high-level language, primarily intended for numerical computations."
arch=(i686 x86_64)
url="http://www.octave.org"
depends=('gcc-libs>=4.3.0' 'readline' 'fftw>=3.1.2-2' 'blas>=19980702-4')
mirror="ftp://ftp.octave.org/pub/octave/obsolete/"
#mirror="ftp://ftp.math.uni-hamburg.de/pub/soft/math/octave/"
#source=("ftp://ftp.octave.org/pub/octave/$pkgname-$pkgver.tar.bz2")
source=(${mirror}${pkgname}-${pkgver}.tar.bz2
	octave.png
	octave.desktop
	gcc-4.3.patch)
install=octave.install
license=('GPL2')
md5sums=('c356475aa06d81a10f8bf27f1a1d27a0'
         '5bf187e2c47f534be9524db9d68fa305'
         'dcac0dbefb011d538c4b290fe9b96f7c'
         '52f37b3438b59f812034b8d0ff7e125e')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np1 -i ${startdir}/src/gcc-4.3.patch || return 1
  FFLAGS="-O -ffloat-store" ./configure \
        --prefix=/usr \
        --enable-shared \
        --enable-dl \
	--without-hdf5 \
	--mandir=/usr/share/man \
	--infodir=/usr/share/info 
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1

  # install some freedesktop.org compatibility
  install -d -m755 ${startdir}/pkg/usr/share/applications
  install -d -m755 ${startdir}/pkg/usr/share/pixmaps
  install -m644 ${startdir}/src/octave.desktop \
  	${startdir}/pkg/usr/share/applications/ || return 1
  install -m644 ${startdir}/src/octave.png \
	${startdir}/pkg/usr/share/pixmaps/ || return 1
  rm ${pkgdir}/usr/share/info/dir
}
