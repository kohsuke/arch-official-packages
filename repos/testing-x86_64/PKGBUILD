# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-perlqt
pkgver=4.7.0
pkgrel=1
pkgdesc="Perl bindings for the Qt libraries"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen')
source=("http://download.kde.org/stable/${pkgver}/src/perlqt-${pkgver}.tar.bz2")
sha1sums=('a48f2f34374258793c7394914645cf2b8e597aaa')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../perlqt-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
