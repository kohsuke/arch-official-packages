# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=imlib
pkgver=1.9.15
pkgrel=13
pkgdesc="General image handling library for X11 and Gtk"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/imlib/"
license=('GPL')
depends=('gtk' 'giflib' 'libpng' 'libtiff')
makedepends=('libxt')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/imlib/1.9/${pkgname}-${pkgver}.tar.bz2
        debian-bug448360.patch CAN-2004-1026.patch aclocal-fixes.patch 
        imlib-1.9.15-libpng15.patch imlib-giflib5.patch)
sha1sums=('c9a732a354fbb3c7e1a426e5d19fc92d73f8f720'
          'fe2fd9ce4d7bc62271e724153de39012de8ec5ee'
          'ec1b47281c1a7fb21abe841f948ecc56ed13f310'
          'af54cb1ee0c8c6122b277284ebdd1022e31df3b5'
          '33c3aaa43eb624f2a6ab0406285a8515903333e3'
          '56e9d65e05a347452fd608d2dfc8538f7ce09893')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/debian-bug448360.patch"
  patch -p1 -i "${srcdir}/CAN-2004-1026.patch"
  patch -p0 -i "${srcdir}/aclocal-fixes.patch"
  patch -p0 -i "${srcdir}/imlib-1.9.15-libpng15.patch"
  patch -p1 -i "${srcdir}/imlib-giflib5.patch"
  sed '/AC_PATH_XTRA/d' -i configure.in
  libtoolize --force
  cp /usr/bin/libtool .
}

build() {
  cd ${pkgname}-${pkgver}
  autoreconf --force --install
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --enable-shm --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
