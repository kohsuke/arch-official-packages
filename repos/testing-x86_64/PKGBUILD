# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>

pkgname=udisks
pkgver=1.0.4
pkgrel=6
pkgdesc="Disk Management Service"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/udisks"
license=('GPL')
depends=('systemd' 'util-linux' 'sg3_utils' 'glib2' 'dbus-glib' 'polkit' 'parted' 'device-mapper' 'libatasmart' 'lsof')
makedepends=('intltool' 'docbook-xsl')
options=(!libtool)
source=(http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz udisks.service drop-pci-db.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/udisks --disable-static

  patch -p1 < ../drop-pci-db.patch

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make profiledir=/usr/share/bash-completion/completions DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}/usr/share/bash-completion/completions/udisks-bash-completion.sh"

  # move udev helpers and rules to /usr/lib
  mv "${pkgdir}"/lib/udev "${pkgdir}"/usr/lib/
  rm -r "${pkgdir}"/lib

  install -Dm644 "$srcdir/udisks.service" "$pkgdir/usr/lib/systemd/system/udisks.service"
  echo "SystemdService=udisks.service" \
    >> "$pkgdir/usr/share/dbus-1/system-services/org.freedesktop.UDisks.service"

  # install the /media folder
  install -d -m 755 $pkgdir/media
}
md5sums=('86c63b2b5484f2060499a052b5b6256b'
         '5d80832b4d4da72beeb45c15b63a1481'
         '773cccd70afd18ace4332037feff67ec')
