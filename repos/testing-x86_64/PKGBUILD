# $Id$
# Maintainer:  Jan de Groot <jan@archlinux.org>

pkgname=gconf
pkgver=2.24.0
pkgrel=1
pkgdesc="A configuration database system"
arch=(i686 x86_64)
license=('LGPL')
depends=('orbit2>=2.14.16' 'gtk2>=2.14.3' 'libxml2>=2.6.32' 'policykit>=0.9' 'libldap>=2.3.43')
makedepends=('pkgconfig' 'intltool>=0.40.4' 'gtk-doc>=1.10')
options=('!libtool')
install=gconf.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/GConf/2.24/GConf-${pkgver}.tar.bz2
	gconf-merge-schema
	gconfpkg
	gconf-reload.patch)
md5sums=('4971d96f5ba94fe4a69396267bd5afe8'
         '1412bafb06f7d8a9601c8f1c4d72cc06'
         '0a43077786fe85ee10002b753752379a'
         'cfcc8e15be7b8a48de4aa34336ff6090')

build() {
  cd ${startdir}/src/GConf-${pkgver}
  patch -Np1 -i ${startdir}/src/gconf-reload.patch || return 1
  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --libexecdir=/usr/lib/GConf \
	      --disable-static || return 1
  make pkglibdir=/usr/lib/GConf || return 1
  make DESTDIR=${startdir}/pkg install || return 1
  install -m755 ${startdir}/src/gconf-merge-schema ${startdir}/pkg/usr/bin/ || return 1
  install -d -m755 ${startdir}/pkg/usr/sbin
  install -m755 ${startdir}/src/gconfpkg ${startdir}/pkg/usr/sbin/ || return 1
}
