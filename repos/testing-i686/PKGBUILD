# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.2.1
pkgrel=2
pkgdesc="KDE Base Runtime Environment"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
groups=('kde')
depends=('kdelibs' 'hicolor-icon-theme' 'rarian' 'smbclient' 'gstreamer0.10-base-plugins' 'alsa-lib')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs' 'imagemagick' 'gzip')
options=('docs')
install='kdebase-runtime.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'http://gstreamer.freedesktop.org/data/images/artwork/gstreamer-logo.svg')
md5sums=('93ef480ed5376ce02324d1b85b8bae0f'
         '8e3924f417fea67f72b2105faed2119c')

build() {
	cd $srcdir
	mkdir build
	cd build
	# disable xine options plugin
	sed -ie '/add_subdirectory(xine)/d' \
		../${pkgname}-${pkgver}/phonon/kcm/CMakeLists.txt
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_PulseAudio=OFF
	make
	make DESTDIR=$pkgdir install

	rm -f ${pkgdir}/usr/share/icons/hicolor/index.theme
	ln -sf /usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/

	# add missing gstreamer icons
	install -d -m755 $pkgdir/usr/share/icons/oxygen/scalable/apps/
	gzip -c $srcdir/gstreamer-logo.svg > $pkgdir/usr/share/icons/oxygen/scalable/apps/phonon-gstreamer.svgz
	for i in 16 22 32 48 64 128; do
		install -d -m755 $pkgdir/usr/share/icons/oxygen/${i}x${i}/apps
		convert -background None -geometry ${i}x${i} $srcdir/gstreamer-logo.svg \
			$pkgdir/usr/share/icons/oxygen/${i}x${i}/apps/phonon-gstreamer.png
	done
}
