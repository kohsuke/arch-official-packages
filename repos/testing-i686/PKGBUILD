# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=lilo
pkgver=23.2
pkgrel=2
pkgdesc="A bootloader for Linux"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/lilo/"
license=('BSD')
# While lilo should stay in the base category,
# it usually makes no sense if it is installed
# with pacman -S base, therefore, don't add
# the base group here.
#groups=('base')
backup=('etc/lilo.conf')
depends=('device-mapper' 'coreutils')
makedepends=('bin86' 'sharutils')
optdepends=('perl: to use keytab-lilo')
install=lilo.install
options=('!makeflags')
source=("http://lilo.alioth.debian.org/ftp/sources/${pkgname}-${pkgver}.tar.gz"
        'lilo.conf')
md5sums=('51b9b9db665d3b8724919e3d46054d12'
         '4ae5986fe2f6bb7e20771ac7fea5343d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export LC_ALL=C
  make all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/lilo.conf" "${pkgdir}/etc/lilo.conf"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
