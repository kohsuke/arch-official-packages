# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-jovie
pkgver=4.10.2
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A text to speech application'
url='http://kde.org/applications/utilities/jovie/'
license=('GPL' 'FDL')
groups=('kde' 'kdeaccessibility')
depends=('kdebase-runtime' 'speech-dispatcher')
makedepends=('cmake' 'automoc4')
replaces=('kdeaccessibility-kttsd')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/jovie-${pkgver}.tar.xz"
        "jovie-speechd-fix.patch::http://bugsfiles.kde.org/attachment.cgi?id=78588")
sha1sums=('3292fb9a1bbae4cfe2e37073b004f78e23936f6d'
          'f106707ec2c325719afe8e919d58ffa2574f6e45')

build() {
  patch -Np1 -d jovie-${pkgver} < ${srcdir}/jovie-speechd-fix.patch
  mkdir build
  cd build
  cmake ../jovie-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
