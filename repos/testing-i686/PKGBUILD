# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-joystick
_gitdate=20110318
pkgver=1.5.99_git${_gitdate} # see configure.ac
pkgrel=1
pkgdesc="X.Org Joystick input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel')
conflicts=('xorg-server<1.10.0')
groups=('xorg-drivers' 'xorg')
options=('!libtool')
source=(#${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	ftp://ftp.archlinux.org/other/$pkgname/${pkgname}-${_gitdate}.tar.bz2
        50-joystick.conf
        LICENSE)
sha1sums=('e1eb55c0556984530ab87836b5ade78085c7201b'
          'e1ff3699a0470c6bb78a53f718df9d8521621e11'
          'ee4c79491b36a680ed9d86f5fc41ae447f26353e')

# source PKGBUILD && mksource
mksource() {
	mkdir /tmp/${pkgname}-${_gitdate}
	pushd /tmp/${pkgname}-${_gitdate}
	git clone -v --depth 1 git://anongit.freedesktop.org/xorg/driver/xf86-input-joystick
	cd xf86-input-joystick
	git archive --prefix=xf86-video-joystick-${_gitdate}/ --format=tar HEAD | bzip2 > /tmp/${pkgname}-${_gitdate}/${pkgname}-${_gitdate}.tar.bz2
	popd
}

build() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
#  ./configure --prefix=/usr
  cd ${srcdir}/xf86-video-joystick-${_gitdate}
  ./autogen.sh --prefix=/usr
  make
}

package() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
  cd ${srcdir}/xf86-video-joystick-${_gitdate}
  
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/50-joystick.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
