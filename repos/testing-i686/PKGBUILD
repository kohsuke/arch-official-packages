# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-parley
pkgver=4.7.0
pkgrel=1
pkgdesc="Vocabulary Trainer"
url="http://kde.org/applications/education/parley/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libkdeedu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/parley-${pkgver}.tar.bz2")
sha1sums=('b0925b3658656933410eaee701ec5f30eebee88e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../parley-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
