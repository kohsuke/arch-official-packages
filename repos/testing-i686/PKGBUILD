# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.5.0
pkgrel=1
pkgdesc="KDE Base Runtime Environment"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
depends=('kdelibs' 'oxygen-icons' 'xorg-xauth' 'hicolor-icon-theme'
         'smbclient' 'rarian' 'libssh')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs' 'openslp'
             'xine-lib' 'docbook-xsl')
optdepends=('htdig: to build the search index'
            'gdb: drkonq crash handler')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
	'kded-freeze-fix.patch')
sha1sums=('4296b78ae517418cf25a385697c7476aef54f684'
          '8d38a72e2c2b7202073f2ded8c2dba1924b008e8')

build() {
	# fix already in kde 4.5.1
	cd ${srcdir}/${pkgname}-${pkgver}
        patch -Np5 -i ${srcdir}/kded-freeze-fix.patch

	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_PulseAudio=OFF
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
	rm -f ${pkgdir}/usr/share/icons/hicolor/index.theme
	ln -sf /usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/
}
