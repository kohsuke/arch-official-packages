# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.2.2
pkgrel=1
pkgdesc="KDE Base Runtime Environment"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
groups=('kde')
depends=('kdelibs' 'hicolor-icon-theme' 'rarian' 'smbclient')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs')
options=('docs')
install='kdebase-runtime.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('cff3fbdb98faeec0ae65d8caea7a5b0f')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_PulseAudio=OFF
	make
	make DESTDIR=$pkgdir install
	rm -f ${pkgdir}/usr/share/icons/hicolor/index.theme
	ln -sf /usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/
}
