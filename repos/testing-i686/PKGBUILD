# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevplatform
pkgver=1.3.90
_pkgver=4.3.90
pkgrel=1
pkgdesc="A C/C++ development platform for KDE"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdelibs' 'subversion' 'qjson')
optdepends=("kdesdk-kompare: difference checking")
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/unstable/kdevelop/${_pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('88300217681caa5c69f09b6428f2a6298cbd4733')

build() {
  cd "${srcdir}"
  mkdir build
  cd build 
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
