# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-vfs
pkgver=2.24.4
pkgrel=2
pkgdesc="The GNOME Virtual File System"
arch=(i686 x86_64)
license=('LGPL')
depends=('fam' 'gconf' 'bzip2' 'avahi' 'smbclient' 'gnome-mime-data' 'heimdal' 'gnutls')
makedepends=('pkgconfig' 'intltool' 'gtk-doc' 'gnome-common')
options=('!libtool' '!emptydirs' '!makeflags')
conflicts=(gnome-vfs-samba)
provides=(gnome-vfs-samba)
replaces=(gnome-vfs-samba gnome-vfs-extras)
url="http://www.gnome.org"
install=gnome-vfs.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/gnome-vfs-${pkgver}.tar.bz2
        gnutls-config.patch)
sha256sums=('62de64b5b804eb04104ff98fcd6a8b7276d510a49fbd9c0feb568f8996444faa'
            '66c7cfb12995c0dd94a2caea95c7e3c55981993f05a79c585d60915ff131955d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #Fix build with new gnutls
  patch -Np1 -i "${srcdir}/gnutls-config.patch"
  libtoolize --force
  gtkdocize
  aclocal
  autoconf
  automake
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-vfs-2.0 \
      --enable-samba --disable-hal \
      --enable-avahi --disable-howl \
      --disable-openssl --enable-gnutls
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-vfs-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
