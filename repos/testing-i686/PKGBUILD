# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=libusbx
pkgver=1.0.16
pkgrel=2
depends=('glibc' 'systemd')
pkgdesc="Library that provides generic access to USB device"
arch=(i686 x86_64)
url="http://libusbx.org"
license=('LGPL')
source=(http://downloads.sourceforge.net/${pkgname}/releases/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        0001-linux-Use-a-separate-lock-to-serialize-start-stop-vs.patch
        0002-hotplug-Remove-use-of-pthread_cancel-from-linux_udev.patch)
options=(!libtool)
replaces=('libusb1' 'libusb')
provides=("libusb=$pkgver")
conflicts=("libusb")
md5sums=('7f5715d624cd6c26b30a317eb6c2fe5e'
         'dad04322621fe7cabd2fe631c6d9929f'
         '259238c92c92f8b3df5dcad0db8aaf2f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # fix #36418
  patch -Np1 -i ../0001-linux-Use-a-separate-lock-to-serialize-start-stop-vs.patch
  patch -Np1 -i ../0002-hotplug-Remove-use-of-pthread_cancel-from-linux_udev.patch
}
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
