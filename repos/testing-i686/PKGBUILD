# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gimp-devel
pkgver=2.7.2
pkgrel=1
pkgdesc="GNU Image Manipulation Program (Development Version)"
arch=('i686' 'x86_64')
url="http://www.gimp.org/"
license=('GPL')
depends=('gtk2' 'lcms' 'libxpm' 'libwmf' 'libxmu' 'librsvg' 'libmng' 'dbus-glib' \
         'libexif' 'pygtk' 'desktop-file-utils' 'gegl' 'hicolor-icon-theme')
makedepends=('gutenprint' 'intltool' 'libwebkit' 'gnome-python' 'poppler-glib' \
             'pkg-config' 'alsa-lib' 'iso-codes')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'libwebkit: for the help browser'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module')
options=('!libtool' '!makeflags')
conflicts=('gimp')
provides=("gimp=${pkgver}")
install=gimp-devel.install
source=(ftp://ftp.gimp.org/pub/gimp/v${pkgver%.*}/gimp-${pkgver}.tar.bz2 linux.gpl)
md5sums=('6996138ab70b0bfebfe9f563284e5f78'
         'bb27bc214261d36484093e857f015f38')
sha1sums=('4690420961d110f99448c32fe61aae7d4869a863'
          '110ce9798173b19a662d086ed7b882b4729f06cf')

build() {
  cd "${srcdir}/gimp-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-mp --enable-gimp-console --enable-gimp-remote \
    --enable-python --with-gif-compression=lzw \
    --without-aa --without-hal
  make
}

package() {
  cd "${srcdir}/gimp-${pkgver}"
  make DESTDIR="${pkgdir}" install-strip
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/lib/gimp/2.0/plug-ins/*.py

  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  ln -sf gimp-console-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -sf gimp-remote-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-remote.1.gz"
  ln -sf gimprc-${pkgver%.*}.5.gz "${pkgdir}/usr/share/man/man5/gimprc.5.gz"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
  ln -s gimp-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp.1.gz"

  ln -s gimp-${pkgver%.*} "${pkgdir}/usr/bin/gimp"
  ln -s gimp-console-${pkgver%.*} "${pkgdir}/usr/bin/gimp-console"
  ln -s gimp-remote-${pkgver%.*} "${pkgdir}/usr/bin/gimp-remote"
  ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool"
}
