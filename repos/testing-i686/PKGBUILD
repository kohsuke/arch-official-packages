# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor:  Jan de Groot <jan@archlinux.org>

_pkgname=gnome-desktop
pkgname=${_pkgname}2
pkgver=2.32.1
pkgrel=1
pkgdesc="The GNOME Desktop"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(gconf gtk2 startup-notification)
makedepends=(gnome-doc-utils intltool)
url="http://www.gnome.org"
options=(!libtool !emptydirs)
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.bz2)
sha256sums=('55cbecf67efe1fa1e57ac966520a7c46d799c8ba3c652a1219f60cafccb3739d')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-gnome-distributor="Archlinux" \
      --disable-scrollkeeper \
      --disable-gnome-about --disable-desktop-docs
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
