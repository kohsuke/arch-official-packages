# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=wpa_supplicant_gui
pkgver=2.0
pkgrel=1
pkgdesc="A Qt frontend to wpa_supplicant"
url="http://hostap.epitest.fi/wpa_supplicant"
arch=('i686' 'x86_64')
#depends=('qt' "wpa_supplicant=$pkgver")
depends=('qt' "wpa_supplicant")
license=('GPL')
source=("http://hostap.epitest.fi/releases/wpa_supplicant-$pkgver.tar.gz"
        compile-fix.patch)
sha256sums=('2c115609fbb5223d51381084a5c944455a8afcda81d584173ff55ba233379e09'
            '044be1032faf93c5f0abf22d7ae77dc0d1d1fef0c2c30646d54950b86601709f')

build() {
  cd wpa_supplicant-${pkgver}
  #patch -p1 <../compile-fix.patch
  cd wpa_supplicant/wpa_gui-qt4
  qmake QMAKE_CFLAGS_RELEASE="$CFLAGS" QMAKE_CXXFLAGS_RELEASE="$CXXFLAGS" 
  make
}

package() {
  cd "${srcdir}/wpa_supplicant-${pkgver}/wpa_supplicant/wpa_gui-qt4"
  install -Dm755 wpa_gui "${pkgdir}/usr/sbin/wpa_gui"
  install -Dm644 icons/wpa_gui.svg "${pkgdir}/usr/share/pixmaps/wpa_gui.svg"
  install -Dm644 wpa_gui.desktop "${pkgdir}/usr/share/applications/wpa_gui.desktop"
  install -Dm644 "${srcdir}/wpa_supplicant-${pkgver}/wpa_supplicant/doc/docbook/wpa_gui.8" \
     "${pkgdir}/usr/share/man/man8/wpa_gui.8"
}
