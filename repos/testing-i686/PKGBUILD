# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=abs
pkgver=2.4.0
pkgrel=1
pkgdesc="Utilities to download and work with the Arch Build System (ABS)"
arch=('i686' 'x86_64')
url="http://projects.archlinux.org/abs.git/"
license=('GPL')
depends=('bash' 'rsync')
backup=(etc/abs.conf)
install=abs.install
source=(ftp://ftp.archlinux.org/other/abs/${pkgname}-${pkgver}.tar.gz)
md5sums=('4b845bc583489484e9c649228d72de5c')

build() {
  cd ${srcdir}/${pkgname}

  make CONFDIR=/etc/
  make CONFDIR=/etc/ DESTDIR=${pkgdir} install

  # Add readme file, and make base /var/abs path
  install -dm0755 ${pkgdir}/var/abs/local/
  install -Dm0644 ${srcdir}/abs/README ${pkgdir}/var/abs/README

  # make adjustments to abs.conf
  if [[ $CARCH = "i686" ]]; then
    sed -i 's| multilib||'  ${pkgdir}/etc/abs.conf
  fi
  if [[ $CARCH = "x86_64" ]]; then
    sed -i '/ARCH=/s|i686|x86_64|' ${pkgdir}/etc/abs.conf
  fi
}
