# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-cantor
pkgver=4.9.0
pkgrel=1
pkgdesc="KDE Frontend to Mathematical Software"
url="http://kde.org/applications/education/cantor/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libspectre' 'kdeedu-analitza' 'libqalculate')
makedepends=('cmake' 'automoc4' 'r')
optdepends=('maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/cantor-${pkgver}.tar.xz")
sha1sums=('2d97209780542b6d2081d2c6f855c3a3751dd0e1')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../cantor-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
