# $Id$
# Maintainer:
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andries Radu <admiral0@live.it>

pkgname=opengtl
pkgver=0.9.17
pkgrel=2
pkgdesc="A set of libraries for using and integrating transformation algorithms (such as filter or color conversion) in graphics applications"
url="http://www.opengtl.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gcc-libs' 'llvm')
makedepends=('cmake' 'libpng')
optdepends=('libpng: for using the png extension')
source=(http://download.opengtl.org/OpenGTL-$pkgver.tar.bz2
        opengtl-0.9.17-fix-ftbfs-with-llvm-3.2.patch)
sha256sums=('89a37394fe71f2e771d7230333c86b93706f0083f86a58a86a670bca7e4f905e'
            'd3e12d964c927e6f659df00e6210815c803c9126b34e9e92d4ccfebd6d30c8d7')

build() {
  cd "$srcdir"

  #  Fix build with LLVM 3.2
  patch -d OpenGTL-$pkgver -Np1 -i \
    "$srcdir/opengtl-0.9.17-fix-ftbfs-with-llvm-3.2.patch"

  mkdir build
  cd build
  cmake ../OpenGTL-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
