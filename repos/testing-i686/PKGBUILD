# $Id$
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
pkgname=ghc
pkgver=6.8.2
pkgrel=2
pkgdesc="A state-of-the-art, open source, compiler and interactive environment for Haskell."
arch=(i686 x86_64)
url="http://www.haskell.org/ghc/"
license="custom"
depends=('gmp' 'perl' 'readline')
makedepends=('ghc' 'happy')
source=("http://www.haskell.org/ghc/dist/$pkgver/$pkgname-$pkgver-src.tar.bz2" \
    "http://www.haskell.org/ghc/dist/$pkgver/ghc-$pkgver-src-extralibs.tar.bz2"
    network.patch)
md5sums=('43108417594be7eba0918c459e871e40'
         'd199c50814188fb77355d41058b8613c'
         'dc94374c53aff2ab2d4f6cb75a349bb0')

build() {
  #since this is a compiler, it needs itself to compile
  #installer automatically bootstraps itself, so this actually compiles twice
  #In order to compile, you need ghc installed on your system
  cd $startdir/src/$pkgname-$pkgver

  # http://hackage.haskell.org/trac/ghc/ticket/2314
  patch -p1 -i ../network.patch || return 1
  cd libraries/network && autoreconf || return 1
  cd - 

  ./configure --prefix=/usr
  make -j1 || return 1
  make DESTDIR=$startdir/pkg install

  install -Dm644 LICENSE ${pkgdir}/usr/share/license/ghc/license
}
