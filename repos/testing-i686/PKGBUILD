# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-kscd
pkgver=4.9.5
pkgrel=1
pkgdesc="KDE CD player"
url='http://kde.org/applications/multimedia/kscd/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
depends=('kdebase-runtime' 'libkcddb' 'libkcompactdisc' 'libmusicbrainz3')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/kscd-${pkgver}.tar.xz")
sha1sums=('5de57f0b6778379bcb1a95ff6e53572de38d2419')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kscd-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
