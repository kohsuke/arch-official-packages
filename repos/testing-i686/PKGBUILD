# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xf86-video-chips
pkgver=1.2.5
pkgrel=3
pkgdesc="X.org Chips and Technologies video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(glibc)
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=14' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.14.0' 'X-ABI-VIDEODRV_VERSION<14' 'X-ABI-VIDEODRV_VERSION>=15')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2 iopl.h git-fix.diff)
sha256sums=('8d3c744d035b3d769049647bb8022ec24500d31b1b224cd0ea4efe61f86bfed2'
            'd04607e51f9064fb128beceda9660feadb7775b585466a5b9fb04f942effc670'
            '36165ebc70926ab5a8fe4331c5ce88c7f5f2e6979170b849ac1c2c99b3d1a804')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cp ${srcdir}/iopl.h util/
  patch -Np1 -i ${srcdir}/git-fix.diff
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
