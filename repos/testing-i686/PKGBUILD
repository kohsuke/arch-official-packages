# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.2.1
pkgrel=2
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('qt' 'shared-mime-info' 'libxpm' 'hal'  'heimdal'
	 'enchant' 'jasper' 'openexr' 'strigi' 'libxtst'
	 'giflib' 'soprano' 'ca-certificates' 'xdg-utils' 'libxft')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl' 'hspell')
replaces=('arts')
options=('docs')
install='kdelibs.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kde-applications-menu.patch' 'archlinux-menu.patch'
        'gstreamer.desktop' 'org.kde.Phonon.AudioOutput.xml')
md5sums=('3ccc094df33db9d61f5ad065b9b857ac'
         'e94450ba5430ea9c1e33bad9ae38ca2d'
         '0ad42200df2d2b8aab01d2ef4b21e02f'
         '568099330c280a9571e7dd1669e42afb'
         'a2e5400548bd08296d4e4c59ba80b1fd')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	# avoid file conflict with gnome-menu
	patch -p1 -i $srcdir/kde-applications-menu.patch || return 1
	# add Archlinux menu entry
	patch -p0 -i $srcdir/archlinux-menu.patch || return 1
	cd ..
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_DISTRIBUTION_TEXT='Arch Linux' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF
	make
	make DESTDIR=$pkgdir install

	# cert bundle seems to be hardcoded
	# link it to the one from ca-certificates
	rm -f $pkgdir/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/usr/share/apps/kssl/ca-bundle.crt

	# ensure compatibility with Qt's bundled phonon
	ln -sf /usr/include/phonon $pkgdir/usr/include/KDE/Phonon
	install -d -m755 $pkgdir/usr/include/phonon
	ln -sf /usr/include/phonon/phononnamespace.h $pkgdir/usr/include/phonon/Global
	install -D -m644 $srcdir/org.kde.Phonon.AudioOutput.xml $pkgdir/usr/share/dbus-1/interfaces/org.kde.Phonon.AudioOutput.xml
	install -D -m644 $srcdir/gstreamer.desktop $pkgdir/usr/share/kde4/services/phononbackends/gstreamer.desktop
}
