# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=ddd
pkgver=3.3.12
pkgrel=2
pkgdesc="A graphical front-end for command-line debuggers such as GDB, JDB, pydb, perl debugger..."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/ddd/"
license=('GPL3' 'LGPL3')
depends=('gcc-libs' 'ncurses' 'lesstif' 'libxaw' 'elfutils' 'libxext')
optdepends=('gdb: to use the Gnu debugger' 'jdk: to use the Java debugger' 'pydb: to use the Python debugger' 'perl: to use the Perl debugger')
install=ddd.install
changelog=ChangeLog
source=(http://ftp.gnu.org/gnu/ddd/${pkgname}-${pkgver}.tar.gz ddd-3.3.12-gcc44.patch)
md5sums=('c50396db7bac3862a6d2555b3b22c34e' '41dc59d8cf502569bab1b1a4a222a1c6')
sha1sums=('b91d2dfb1145af409138bd34517a898341724e56' '3d43c9d56347f248732b1d72f29c7bf799f03864')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../ddd-3.3.12-gcc44.patch
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 icons/ddd.xpm "${pkgdir}/usr/share/pixmaps/ddd.xpm"
}
