# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kmplot
pkgver=4.10.3
pkgrel=1
pkgdesc="Mathematical Function Plotter"
url="http://kde.org/applications/education/kmplot/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kmplot-${pkgver}.tar.xz")
sha1sums=('0579eb13e5a764afc390e11acbb3da5ee40f63aa')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kmplot-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
