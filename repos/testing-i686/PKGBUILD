# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-exchange
pkgver=2.24.0
pkgrel=1
pkgdesc="Ximian Connector Exchange plugin for Evolution"
arch=(i686 x86_64)
license=('GPL')
url="http://www.ximian.com"
depends=('evolution-data-server>=2.24.0' 'libgnomeui>=2.24.0')
makedepends=('perlxml' 'pkgconfig' 'evolution>=2.24.0')
replaces=('ximian-connector')
conflicts=('ximian-connector')
options=('!libtool' '!emptydirs')
install=evolution-exchange.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('5e8499a1d4205c703235710c5a22b194')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Look for correct DB, we want 4.1
  sed -e 's|DB_CFLAGS="-I$withval/include"|DB_CFLAGS="-I$withval/include/db4.1"|' \
      -e 's|DB_LIBS="-L$withval/lib -ldb"|DB_LIBS="-L$withval/lib -ldb-4.1"|' \
      -i configure
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
  	--libexecdir=/usr/lib/evolution --disable-static --with-libdb=/usr  || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
