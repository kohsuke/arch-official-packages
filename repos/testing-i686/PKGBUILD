# $Id$
# Maintainer: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2009.4.0
pkgrel=3
pkgdesc="A frontend to the panorama-tools"
arch=('i686' 'x86_64')
url="http://hugin.sourceforge.net/"
license=('LGPL')
depends=('wxgtk' 'libpano13' 'boost' 'enblend-enfuse' 'exiv2' 'openexr'
	'autopano-sift-c' 'desktop-file-utils')
makedepends=('zip' 'cmake')
optdepends=('perl-exiftool: write metadata on output images')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
	'libpng14.patch')
md5sums=('ed197d35213a46c77489f512cb8ff191'
	'b892397406da9a1b366bf44d051f404c')
sha1sums=('a801a2521d66a9b0c8680bdfe84afe67bc79d1c8'
	'159d592ecbcab399ab8beffb920b91616b4bdfff')

build() {
  cd ${srcdir}
  patch -p0 -i libpng14.patch || return 1
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_EXE_LINKER_FLAGS="-lpthread" || return 1
  make || return 1
}

package(){
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1
}
