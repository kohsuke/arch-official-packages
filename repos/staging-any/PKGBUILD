# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sebastien Binet <binet@farnsworth>
pkgname=python-distribute
pkgver=0.6.14
pkgrel=6
pkgdesc="Easily build and distribute Python packages"
arch=('any')
license=('PSF')
url="http://packages.python.org/distribute"
depends=('python')
source=(http://pypi.python.org/packages/source/d/distribute/distribute-${pkgver}.tar.gz
        distribute-${pkgver}_python2_and_3.patch)
sha1sums=('6bea50b97f36bf751548bb486a534204a690aa4d'
          'de635febfb11dc6878e5bdbac4a0e1d539c19d79')

build() {
   cd "${srcdir}/distribute-${pkgver}"
   patch -Np1 -i ../distribute-${pkgver}_python2_and_3.patch
   python setup.py build
}

package() {
   cd "${srcdir}/distribute-${pkgver}"
   python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}
