# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-kimono
pkgver=4.8.80
pkgrel=1
pkgdesc=".NET/Mono bindings for the KDE libraries"
url="https://projects.kde.org/projects/kde/kdebindings/csharp/kimono"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-qyoto' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost' 'kdepimlibs')
conflicts=('kdebindings-csharp')
source=("http://download.kde.org/unstable/${pkgver}/src/kimono-${pkgver}.tar.xz")
sha1sums=('348529351170cba46ea3bb071416f54cedd05f9c')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kimono-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Soprano=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
