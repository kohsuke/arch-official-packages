# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdeedu
pkgver=4.8.90
pkgrel=1
pkgdesc="Libraries used by KDE Education applications"
url="https://projects.kde.org/projects/kde/kdeedu/libkdeedu"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
replaces=('kdeedu-libkdeedu' 'kdeedu-data')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('4dbdf73fcb6a1da184dfec37dbf3052c60f401f3')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
