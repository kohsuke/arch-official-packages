# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksirk
pkgver=4.9.95
pkgrel=1
pkgdesc="A computerized version of a well known strategy game"
url="http://kde.org/applications/games/ksirk/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/ksirk-${pkgver}.tar.xz")
sha1sums=('22e163387d29e77c72c67ca02cfe97c4d40ee9d3')

build() {
  mkdir build
  cd build
  cmake ../ksirk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
