# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-analitza
pkgver=4.7.95
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/analitza-${pkgver}.tar.bz2")
sha1sums=('393d227dd839f113a58f018f47a08bc36c681335')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../analitza-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
