# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Paul Mattal <paul@archlinux.org>
pkgname=apache-ant
pkgver=1.9.1
pkgrel=1
pkgdesc='A java-based build tool'
arch=('any')
url='http://ant.apache.org/'
license=('APACHE')
depends=('java-environment')
makedepends=('java-environment' 'apache-ant' 'junit')
optdepends=('junit: to use the junit tasks')
install=${pkgname}.install
source=(http://apache.org/dist/ant/source/${pkgname}-${pkgver}-src.tar.bz2
        ${pkgname}.sh
        ${pkgname}.csh
        ${pkgname}.install)
sha256sums=('550473e1a64b99e6cfa9ba6b9c142318bd1ea0d902e7ba7b33389528a0c1d113'
            '182b9212610790966d3a018d3cbab2e5327dd9fd11dc15dd1f377853216f5718'
            '919a3ab1acae1d0d190fe6e0034b00975caab40e55359a720bfccd098fe2d995'
            '8219443864c2e199e679b2f5521162244fb605732d912a51991ee28e476da907')

_ant_home=/usr/share/${pkgname}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # ant needs HOME to download libs to $HOME/.m2/repository
  export ANT_OPTS="-Duser.home=${srcdir}"
  ant -Ddest=optional -f fetch.xml

  rm lib/optional/junit-4.11.jar
  ln -s /usr/share/java/junit.jar lib/optional/junit-4.11.jar
  rm lib/optional/hamcrest-core-1.3.jar
  ln -s /usr/share/java/hamcrest-core.jar lib/optional/hamcrest-core-1.3.jar

  ant dist
}

#check() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
#  ant test
#}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}-${pkgver}"

  install -dm755 ${pkgdir}${_ant_home}

  install -dm755 ${pkgdir}/etc/${pkgname}
  cp -Rp etc/* ${pkgdir}/etc/${pkgname}
  rm ${pkgdir}/etc/${pkgname}/ant-bootstrap.jar
  # Done in .install for now
  #ln -s /etc/${pkgname} ${pkgdir}${_ant_home}/etc

  install -dm755 ${pkgdir}${_ant_home}/bin
  find bin -type f -a ! -name \*.bat -a ! -name \*.cmd \
    -exec install -m755 {} ${pkgdir}${_ant_home}/bin \;

  install -dm755 ${pkgdir}/usr/bin
  ln -s ${_ant_home}/bin/ant ${pkgdir}/usr/bin/ant

  install -dm755 ${pkgdir}/usr/share/java/${pkgname}
  install -m644 lib/*.jar \
                ../lib/optional/*.jar \
                ${pkgdir}/usr/share/java/${pkgname}
  ln -s /usr/share/java/${pkgname} ${pkgdir}${_ant_home}/lib
  # Provided
  # http://bugs.archlinux.org/task/15229
  rm ${pkgdir}/usr/share/java/${pkgname}/junit-4.11.jar
  ln -sf ../junit.jar ${pkgdir}/usr/share/java/apache-ant/
  rm ${pkgdir}/usr/share/java/${pkgname}/hamcrest-core-1.3.jar
  ln -sf ../hamcrest-core.jar ${pkgdir}/usr/share/java/apache-ant/

  # fix python2 path
  sed -i 's|/usr/bin/python|/usr/bin/python2|' ${pkgdir}${_ant_home}/bin/runant.py

  # install profile.d script
  install -dm755 ${pkgdir}/etc/profile.d
  install -m755 "${srcdir}"/${pkgname}.{csh,sh} ${pkgdir}/etc/profile.d/

  # License says NOTICE file should be redistributed for derivative works
  cd ${srcdir}/${pkgname}-${pkgver}
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE NOTICE ${pkgdir}/usr/share/licenses/${pkgname}
}
