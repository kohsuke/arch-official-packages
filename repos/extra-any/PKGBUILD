# Contributor : Keshav Padram <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

_gitroot="git://git.kernel.org/pub/scm/boot/efilinux/efilinux.git"
_gitname="efilinux"
_gitbranch="master"

pkgname="efilinux-efi"
pkgver="1.0"
pkgrel="7"
pkgdesc="Simple UEFI Linux bootloader - Precursor to EFISTUB"
url="https://github.com/mfleming/efilinux"
arch=('any')
license=('GPL')
makedepends=('gcc-multilib' 'gnu-efi-libs')
depends=('dosfstools' 'efibootmgr')
options=('!strip')
install="${pkgname}.install"

conflicts=('efilinux-efi-x86_64' 'efilinux-efi-i386')
provides=('efilinux-efi-x86_64' 'efilinux-efi-i386')
replaces=('efilinux-efi-x86_64' 'efilinux-efi-i386')

source=("${_gitname}::git+${_gitroot}#branch=${_gitbranch}")
sha1sums=('SKIP')

_build_efilinux-efi-x86_64() {
	
	cp -r "${srcdir}/efilinux-${pkgver}" "${srcdir}/efilinux-${pkgver}-x86_64"
	cd "${srcdir}/efilinux-${pkgver}-x86_64/"
	
	## Unset all compiler FLAGS
	unset CFLAGS
	unset CPPFLAGS
	unset CXXFLAGS
	unset LDFLAGS
	unset MAKEFLAGS
	
	make ARCH="x86_64" LIBDIR="/usr/lib" CRT0="/usr/lib/crt0-efi-x86_64.o" LDSCRIPT="/usr/lib/elf_x86_64_efi.lds"
	
}

_build_efilinux-efi-ia32() {
	
	cp -r "${srcdir}/efilinux-${pkgver}" "${srcdir}/efilinux-${pkgver}-ia32"
	cd "${srcdir}/efilinux-${pkgver}-ia32/"
	
	## Unset all compiler FLAGS
	unset CFLAGS
	unset CPPFLAGS
	unset CXXFLAGS
	unset LDFLAGS
	unset MAKEFLAGS
	
	make ARCH="ia32" LIBDIR="/usr/lib32" CRT0="/usr/lib32/crt0-efi-ia32.o" LDSCRIPT="/usr/lib32/elf_ia32_efi.lds"
	
}

build() {
	
	if [[ "${CARCH}" != "x86_64" ]]; then
		msg "efilinux-efi can be built only in an x86_64 system. Exiting."
		exit 1
	fi
	
	rm -rf "${srcdir}/efilinux-${pkgver}/" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/efilinux-${pkgver}"
	
	## Fix Makefile variables
	sed 's|ARCH=|ARCH :=|g' -i "${srcdir}/efilinux-${pkgver}/Makefile"
	sed 's|LIBDIR=|LIBDIR :=|g' -i "${srcdir}/efilinux-${pkgver}/Makefile"
	
	## Use gnu-efi ms_abi support
	sed 's|-DEFI_FUNCTION_WRAPPER|-DEFI_FUNCTION_WRAPPER -DGNU_EFI_USE_MS_ABI|g' -i "${srcdir}/efilinux-${pkgver}/Makefile"
	
	## Add "-fno-strict-aliasing -fno-stack-protector -fno-stack-check -mno-sse -mno-mmx" to CFLAGS
	sed 's|-DEFI_FUNCTION_WRAPPER|-fno-strict-aliasing -fno-stack-protector -fno-stack-check -mno-sse -mno-mmx -DEFI_FUNCTION_WRAPPER -DGNU_EFI_USE_MS_ABI|g' -i "${srcdir}/efilinux-${pkgver}/Makefile"
	
	## Add -m64 for x86_64 build
	sed 's|CFLAGS += -mno-red-zone|CFLAGS += -m64 -mno-red-zone|g' -i "${srcdir}/efilinux-${pkgver}/Makefile"
	
	## Compile efilinuxx64.efi
	_build_efilinux-efi-x86_64
	
	## Compile efilinuxia32.efi
	_build_efilinux-efi-ia32
	
}

package() {
	
	install -d "${pkgdir}/usr/lib/efilinux/"
	install -D -m0644 "${srcdir}/efilinux-${pkgver}-x86_64/efilinux.efi" "${pkgdir}/usr/lib/efilinux/efilinuxx64.efi"
	install -D -m0644 "${srcdir}/efilinux-${pkgver}-ia32/efilinux.efi" "${pkgdir}/usr/lib/efilinux/efilinuxia32.efi"
	
}
