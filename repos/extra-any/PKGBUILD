# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=python-logilab-common
pkgname=('python2-logilab-common' 'python-logilab-common')
pkgver=0.59.1
pkgrel=2
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="http://www.logilab.org/project/logilab-common"
license=('LGPL')
makedepends=('python2' 'python')
source=(http://download.logilab.org/pub/common/logilab-common-${pkgver}.tar.gz
logilab_modutils.patch)
sha1sums=('30d88ccb3f3cc1915a9233b123f3ba8481a1cd4c'
          '7a839e940813fb4ddf9ae8045f8798f53f0e7c0b')

build() {
  cd "${srcdir}"

  cp -a logilab-common-${pkgver}{,-py3}

  cd logilab-common-${pkgver}
  python2 setup.py build

  cd ../logilab-common-${pkgver}-py3
  python3 setup.py build
}

package_python2-logilab-common() {
  depends=('python2')
  cd "${srcdir}"/logilab-common-${pkgver}

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  mv "${pkgdir}"/usr/bin/pytest{,2}

  # fix permissions ...
  find "${pkgdir}" -type f -exec chmod +r {} \;
}

package_python-logilab-common() {
  depends=('python')
  cd "${srcdir}"/logilab-common-${pkgver}-py3

  python3 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  # fix permissions ...
  find "${pkgdir}" -type f -exec chmod +r {} \;
  patch -Np1 -i "${srcdir}"/logilab_modutils.patch "${pkgdir}"/usr/lib/python3.3/site-packages/logilab/common/modutils.py
}
