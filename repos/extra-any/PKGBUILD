# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=font-misc-ethiopic
pkgver=1.0.3
pkgrel=1
pkgdesc="X.org Misc Ethiopic fonts"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('MIT')
groups=('xorg' 'xorg-fonts')
depends=('xorg-mkfontdir' 'xorg-mkfontscale' 'fontconfig')
makedepends=('xorg-font-util' 'xorg-util-macros')
install=xfonts.install
source=(http://xorg.freedesktop.org/releases/individual/font/${pkgname}-${pkgver}.tar.bz2)
md5sums=('6306c808f7d7e7d660dfb3859f9091d2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --with-ttf-fontdir=/usr/share/fonts/TTF \
      --with-otf-fontdir=/usr/share/fonts/OTF
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/share/fonts"/*/fonts.*

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
