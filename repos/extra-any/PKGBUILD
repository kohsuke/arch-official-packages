# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
pkgver=0.14.2
pkgrel=2
pkgdesc="A full featured and easy to use Jabber client"
arch=('any')
url="http://www.gajim.org/"
license=('GPL3')
depends=('pygtk' 'python2' 'python2-pyopenssl' 'hicolor-icon-theme' 'dnsutils')
makedepends=('gettext' 'intltool')
optdepends=('dbus-python: dbus support'
            'farsight2: video/voice support'
            'gstreamer0.10-good: video/voice support'
            'gstreamer0.10-bad: video/voice support'
            'gtkspell: spelling support'
            'libxss: for idle module'
            'notification-daemon: for desktop notification'
            'python-gnomekeyring: for GnomeKeyring support'
            'pycrypto: support for E2E encryption')
options=('!libtool')
install=gajim.install
source=(http://www.gajim.org/downloads/${pkgver%.*}/gajim-${pkgver}.tar.bz2 \
        fix_roster_closing_73523ed11fc3.diff)
md5sums=('03c34227174dc0db021b82cd58fb95a8'
         '7af66a05c49276a2f63921c1e07a5288')
sha1sums=('15c5d5c485e32694b9affbeaca00b1bb86556f2b'
          '959fc67e88be7d2a9bd0d600d5719738d1b85d61')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i ../fix_roster_closing_73523ed11fc3.diff
  ./configure --prefix=/usr --enable-site-packages
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
