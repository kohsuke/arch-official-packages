# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>
pkgname=flickrnet
_pkgname=FlickrNet
pkgver=2.2
_pkgver=48055
pkgrel=2
pkgdesc="Flickr.Net API Library"
arch=('any')
url="http://www.codeplex.com/FlickrNet"
license=('LGPL')
depends=('mono')
makedepends=('unzip')
source=($pkgname-$pkgver.zip::"http://download.codeplex.com/Project/Download/SourceControlFileDownload.ashx?ProjectName=${_pkgname}&changeSetId=$_pkgver"
        "$pkgname.pc")
md5sums=('9ad3fde83ca2a74a4cade2f1b11a77e1'
         '131dd6e01f99c0533e9367b5cfda3a13')

build() {
  cd $srcdir/$_pkgname
  sed -i 's/AssemblyKeyFile("..\\\\..\\\\FlickrNet.snk")/AssemblyKeyFile("FlickrNet.snk")/' ./AssemblyInfo.cs
  sed -i "s/@VERSION@/$pkgver/" $srcdir/$pkgname.pc

  gmcs -t:library -out:$_pkgname.dll -r:System,System.Data,System.Xml,System.Web,System.Drawing *.cs
}

package() {
  cd $srcdir/$_pkgname

  gacutil -i $_pkgname.dll -package $pkgname-$pkgver -root $pkgdir/usr/lib/
  install -Dm644 $srcdir/$pkgname.pc $pkgdir/usr/lib/pkgconfig/$pkgname.pc
}
