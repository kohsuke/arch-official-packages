# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
pkgver=0.14.4
pkgrel=2
pkgdesc="A full featured and easy to use Jabber client"
arch=('any')
url="http://www.gajim.org/"
license=('GPL3')
depends=('pygtk' 'python2' 'python2-pyopenssl' 'hicolor-icon-theme' 'dnsutils')
makedepends=('gettext' 'intltool')
optdepends=('dbus-python: dbus support'
            'farsight2: video/voice support'
            'gstreamer0.10-good: video/voice support'
            'gstreamer0.10-bad: video/voice support'
            'gtkspell: spelling support'
            'libxss: for idle module'
            'notification-daemon: for desktop notification'
            'python-gnomekeyring: for GnomeKeyring support'
            'pycrypto: support for E2E encryption')
options=('!libtool')
install=gajim.install
source=("http://www.gajim.org/downloads/${pkgver%.*}/gajim-${pkgver}.tar.bz2"
        'fix-unicode-characters.patch')
md5sums=('b6b88b084de38d9fb34d39c37d4f185b'
         '37faebd9b3d2aca56d6cc17c8e7d7112')
sha1sums=('0343af3c4bae39aa49e547b641caac197a887bb4'
          '7650dd14ce16d0d8b73f623083071a7249d27cff')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-site-packages
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
