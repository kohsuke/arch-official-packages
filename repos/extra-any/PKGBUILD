# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-icon-theme
pkgver=3.0.0
pkgrel=2
pkgdesc="GNOME icon theme"
arch=(any)
depends=('hicolor-icon-theme' 'icon-naming-utils' 'gtk-update-icon-cache' 'gnome-icon-theme-symbolic')
makedepends=('intltool')
url="http://www.gnome.org"
license=('GPL')
groups=('gnome')
install=gnome-icon-theme.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('42da7c4ab1521e7beb1c26f5588c35a644ef57585754d8ceec427d9d7f94bb09')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    #don't include icon cache
    rm -f  "${pkgdir}/usr/share/icons/gnome/icon-theme.cache"
}
