# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=python-logilab-common
pkgname=('python2-logilab-common')
pkgver=0.58.2
pkgrel=3
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="http://www.logilab.org/project/logilab-common"
license=('LGPL')
conflicts=('python-logilab-common<=0.58.2-1')
replaces=('python-logilab-common<=0.58.2-1')
makedepends=('python2' 'python2-distribute' 'python2-egenix-mx-base')
source=(http://download.logilab.org/pub/common/logilab-common-${pkgver}.tar.gz)
sha1sums=('a1df7965bb9382f56047e616e420f49d6181a0b2')

build() {
  cd "${srcdir}/logilab-common-${pkgver}"

  python2 setup.py build
}

package_python2-logilab-common() {
  depends=('python2' 'python2-egenix-mx-base')
  cd "${srcdir}/logilab-common-${pkgver}"

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  # fix permissions ...
  find "${pkgdir}" -type f -exec chmod +r {} \;
}
