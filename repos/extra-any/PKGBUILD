#$Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Christophe Gueret <tolgam@homegnu.net>

pkgbase=python-gdata
pkgname=('python2-gdata')
pkgver=2.0.17
pkgrel=3
pkgdesc="Python client library for Google data APIs"
arch=('any')
url="http://code.google.com/p/gdata-python-client/"
license=('APACHE')
depends=('python2')
conflicts=('python-gdata<=2.0.17-1')
replaces=('python-gdata<=2.0.17-1')
source=(http://gdata-python-client.googlecode.com/files/gdata-${pkgver}.tar.gz)
sha1sums=('d2d9f60699611f95dd8c328691a2555e76191c0c')

package_python2-gdata() {
  cd "${srcdir}/gdata-${pkgver}"
  find . -type f -exec sed -i -e 's@^#!/usr/bin/env python@#!/usr/bin/env python2@' \
    -e 's@^#!/usr/.*bin/python.*@#!/usr/bin/python2@'  {} \;
  python2 setup.py install --prefix=/usr --root "${pkgdir}" 
}
