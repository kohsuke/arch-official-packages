# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sebastien Binet <binet@farnsworth>
pkgbase=python-distribute
pkgname=('python-distribute' 'python2-distribute')
pkgver=0.6.15
pkgrel=1
pkgdesc="Easily build and distribute Python packages"
arch=('any')
license=('PSF')
url="http://pypi.python.org/pypi/distribute"
makedepends=('python' 'python2')
source=(http://pypi.python.org/packages/source/d/distribute/distribute-${pkgver}.tar.gz
        distribute-python2_and_3.patch)
sha1sums=('af088df456ea6b25c0e23647648fd317ed6e0d1b'
          'de635febfb11dc6878e5bdbac4a0e1d539c19d79')

build() {
   cd "${srcdir}"
   cp -a distribute-${pkgver}{,-python2}
   
   # Build python 3 module
   cd distribute-${pkgver}
   patch -Np1 -i ../distribute-python2_and_3.patch
   python setup.py build
   python setup.py test
   
   # Build python 2 module
   cd ../distribute-${pkgver}-python2
   patch -Np1 -i ../distribute-python2_and_3.patch
   python2 setup.py build
   python2 setup.py test
}

package_python-distribute() {
   depends=('python>=3.2')
   
   cd "${srcdir}/distribute-${pkgver}"
   python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-distribute() {
   depends=('python2>=2.7')
   
   cd "${srcdir}/distribute-${pkgver}-python2"
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
}
