# $Id$
# Maintainer:

pkgname=ttf-tlwg
pkgver=0.5.0
pkgrel=1
pkgdesc="A collection of basic Thai fonts"
arch=('any')
url="http://linux.thai.net/taxonomy/term/3"
license=('GPL' 'custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
conflicts=('ttf-thai')
provides=('ttf-thai')
replaces=('ttf-thai')
install=ttf.install
source=(ftp://linux.thai.net/pub/ThaiLinux/software/ttf-tlwg/${pkgname}-${pkgver}.tar.gz
        LICENSE)
sha1sums=('28203b7f9e4625ca4bbce949dcb16dcd47da24f5'
          '39a8c3eb83856f9dff63a3493f891a702d3fa802')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # install fonts to right place
  install -d "${pkgdir}"/usr/share/fonts/TTF
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/TTF/

  # install specification for fonts to right place
  install -d "${pkgdir}"/etc/fonts/conf.avail/
  install -m644 etc/fonts/conf.avail/64-ttf-thai-tlwg.conf "${pkgdir}"/etc/fonts/conf.avail/
  install -m644 etc/fonts/conf.avail/89-ttf-thai-tlwg-synthetic.conf "${pkgdir}"/etc/fonts/conf.avail/

  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
