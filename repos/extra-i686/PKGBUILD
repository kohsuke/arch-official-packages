# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=racket
pkgver=5.1.1
pkgrel=1
pkgdesc="PLT Racket (a variant of Scheme) Environment"
arch=('i686' 'x86_64')
url="http://racket-lang.org/"
license=('LGPL')
depends=('gtk2' 'libjpeg' 'libffi' 'desktop-file-utils')
makedepends=('gsfonts')
provides=('drscheme')
conflicts=('drscheme')
replaces=('drscheme')
options=('!strip')
install=racket.install
source=(http://download.racket-lang.org/installers/${pkgver}/racket/${pkgname}-${pkgver}-src-unix.tgz \
        drracket.desktop)
md5sums=('26d6b320218497dba71630b9ad4dcf59'
         '9223c9736ead677431542b41271c7b3c')
sha1sums=('be339495aef3334530ea8fc63ef4014ad8a4b0b2'
          'a20808f6b250225704856f82a544681a962a299d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../collects/icons/plt.xpm "${pkgdir}/usr/share/pixmaps/drracket.xpm"
  install -D -m644 "${srcdir}/drracket.desktop" "${pkgdir}/usr/share/applications/drracket.desktop"
}
