# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-rocs
pkgver=4.7.2
pkgrel=1
pkgdesc="Rocs Graph Theory"
url="http://kde.org/applications/education/rocs/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'boost-libs')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/stable/${pkgver}/src/rocs-${pkgver}.tar.bz2")
sha1sums=('19f2bf19dfe7a0787180c3fc9d1f32c14316f985')
options=('!makeflags')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../rocs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
