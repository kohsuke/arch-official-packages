# $Id$
#  Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=glsof
pkgver=1.6.1
_fmver=1.6.1
_qver=1.0.0
pkgrel=1
pkgdesc="Two separate GUI utilities (Queries and Filemonitor) to the lsof command line utility"
arch=('i686' 'x86_64')
url="http://glsof.sourceforge.net/"
license=('GPL3')
depends=('sh' 'java-runtime' 'lsof')
source=(http://downloads.sourceforge.net/sourceforge/glsof/filemonitor-${_fmver}.tar.gz)
if [ "$CARCH" = "i686" ]; then
  source=(${source[@]} http://downloads.sourceforge.net/sourceforge/glsof/queries-${_qver}/queries_linux32.tar.gz \
          glsof-filemonitor glsof-queries)
  sha1sums=('afafaafefcfc5f4bb76f6cbe7c4a39a478e351c2'
            'd5a17d66dd02cd78bad5ffdda0b936c7848e4890'
            '197b58f40880995d6442fd42a58fa1f4594fd19d'
            '2797c326904af16a254c3e39b8c529ea14ef37e5')
elif [ "$CARCH" = "x86_64" ]; then
  source=(${source[@]} http://downloads.sourceforge.net/sourceforge/glsof/queries-${_qver}/queries_linux64.tar.gz \
          glsof-filemonitor glsof-queries)
  sha1sums=('afafaafefcfc5f4bb76f6cbe7c4a39a478e351c2'
            'c5dd12e9b2860f1c427ed7cd482e898978051d2c'
            '197b58f40880995d6442fd42a58fa1f4594fd19d'
            '2797c326904af16a254c3e39b8c529ea14ef37e5')
fi

package() {
  cd "${srcdir}"
  install -D -m755 glsof-filemonitor "${pkgdir}/usr/bin/glsof-filemonitor"
  install -D -m755 glsof-queries "${pkgdir}/usr/bin/glsof-queries"
  install -D -m644 filemonitor/filemonitor.jar "${pkgdir}/usr/share/java/glsof/filemonitor.jar"
  install -D -m644 queries_linux??/queries.jar "${pkgdir}/usr/share/java/glsof/queries.jar"
  install -D -m644 queries_linux??/queries.pdf "${pkgdir}/usr/share/doc/glsof/queries.pdf"
}
