# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=seamonkey
pkgver=2.4.1
pkgrel=1
pkgdesc="SeaMonkey internet suite"
arch=('i686' 'x86_64')
license=('MPL')
depends=('gtk2' 'mozilla-common' 'hunspell' 'libxt' 'startup-notification' 'alsa-lib' 'dbus-glib' 'libnotify' 'desktop-file-utils' 'sqlite3' 'libvpx' 'nss' 'libevent')
makedepends=('unzip' 'zip' 'pkg-config'  'python2' 'libidl2' 'yasm' 'wireless_tools' 'mesa')
install=$pkgname.install
url="http://www.mozilla.org/projects/seamonkey"
source=(ftp://releases.mozilla.org/pub/mozilla.org/${pkgname}/releases/${pkgver}/source/${pkgname}-${pkgver}.source.tar.bz2
        mozconfig
        seamonkey.desktop
        seamonkey-2.0-lang.patch)
md5sums=('ccac3a0971a75cb0798347d60f12f6cf'
         'afc79e29c651803bfc5e6032d5ffb0e2'
         '6119a2254716752c9d08e366f8d4c048'
         '25b6fe16ac24cd5c852213e5c1adb272')

build() {
  cd "${srcdir}/comm-release"
  patch -Np1 -i "${srcdir}/seamonkey-2.0-lang.patch"

  cp "${srcdir}/mozconfig" .mozconfig
  export LDFLAGS="-Wl,-rpath,/usr/lib/seamonkey-${pkgver} -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
}

package() {
  cd "${srcdir}/comm-release"

  make -j1 -f client.mk DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/lib/seamonkey-${pkgver}/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "${pkgdir}/usr/lib/seamonkey-${pkgver}/dictionaries"
  ln -sf /usr/share/hyphen "${pkgdir}/usr/lib/seamonkey-${pkgver}/hyphenation"

  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m644 suite/branding/nightly/icons/gtk/seamonkey.png \
                "${pkgdir}/usr/share/pixmaps/"
  install -m644 "${srcdir}/seamonkey.desktop" "${pkgdir}/usr/share/applications/"

  rm -f "${pkgdir}/usr/lib/pkgconfig/"seamonkey-ns{s,pr}.pc

  ln -sf /usr/lib/seamonkey-${pkgver}/seamonkey-bin "${pkgdir}/usr/lib/seamonkey-${pkgver}/seamonkey"
}
