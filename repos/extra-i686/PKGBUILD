# $Id$
# Maintainer:
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Michel Brabants <michel.brabants@euphonynet.be>

pkgname=libzrtpcpp
pkgver=2.0.0
pkgrel=1
pkgdesc="An extension to Real-time Transport Protocol (RTP) which describes a method of Diffie-Hellman key agreement for Secure Real-time Transport Protocol (SRTP)"
arch=('i686' 'x86_64')
url='http://www.gnutelephony.org/index.php/GNU_ZRTP'
license=('GPL3')
depends=('ccrtp')
makedepends=('cmake')
source=("ftp://ftp.gnu.org/pub/gnu/ccrtp/$pkgname-$pkgver.tar.gz"{,.sig})
md5sums=('dc1501e7e8a46a1608fc3820c7476727'
         '7216f652aa2e080de114e02a85127dbc')

build() {
  sed 's/lib64/lib/g' -i "${srcdir}"/${pkgname}-${pkgver}/CMakeLists.txt

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
