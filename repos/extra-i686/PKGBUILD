# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: aurelien <aurelien@archlinux.org>
# Contributor: Herb Rose (hrose56@yahoo.com)

pkgname=indent
pkgver=2.2.11
pkgrel=2
pkgdesc="C language source code formatting program"
arch=('i686' 'x86_64')
url="http://indent.isidore-it.eu/beautify.html"
depends=('glibc')
makedepends=('texi2html')
license=('GPL3')
install=indent.install
source=(http://indent.isidore-it.eu/${pkgname}-${pkgver}.tar.gz
        indent-2.2.11-segfault.patch
        indent-2.2.11-LC_ALL.patch
        indent-2.2.11-decimal_format.patch)
sha1sums=('9b242528bbc4914464117c69ffae9df24d3fa4a7'
          '31a6dcd5edcac4decac81dbb52bc9415b8b148e4'
          '91b210b666de59ca44524288a4c1ebd213570daa'
          '2b7d5a725fe307ce81565f920a0a66b319b50c91')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   
   patch -Np1 -i ../indent-2.2.11-segfault.patch

   # http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=205692
   patch -Np1 -i ../indent-2.2.11-LC_ALL.patch

   # https://lists.gnu.org/archive/html/bug-indent/2011-08/msg00000.html
   patch -Np1 -i ../indent-2.2.11-decimal_format.patch
   
   sed -i 's/-number/-number-sections/' doc/Makefile.in
   ./configure --prefix=/usr
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" docdir=/usr/share/doc/indent install
}
