# $Id$
# Maintainer:
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=netkit-bsd-finger
pkgver=0.17
pkgrel=6
pkgdesc="BSD-finger ported to Linux"
arch=('i686' 'x86_64')
url='ftp://ftp.uk.linux.org/pub/linux/Networking/netkit'
license=('BSD')
depends=('glibc' 'xinetd')
backup=('etc/xinetd.d/finger')
source=("ftp://ftp.archlinux.org/other/${pkgname}/bsd-finger-${pkgver}.tar.gz"
        'finger.xinetd' 'LICENSE')
md5sums=('52bf281aac8814bf56cdc92f7661ee75'
         'a8682004dc8dee356065162bde892b47'
         '5fc568418a0379fc3a358ed1c2523b44')

build() {
   cd ${srcdir}/bsd-finger-${pkgver}
   ./configure --prefix=/usr
   sed -i 's@include <sys/time.h>@include <time.h>@' finger/lprint.c
   sed -i 's@include <sys/time.h>@include <time.h>@' finger/sprint.c
   make
}

package() {
   cd ${srcdir}/bsd-finger-${pkgver}
   install -d ${pkgdir}/usr/{bin,sbin} ${pkgdir}/usr/share/man/{man1,man8}
   make INSTALLROOT=${pkgdir} MANDIR=/usr/share/man install

   install -D -m644 ${srcdir}/finger.xinetd ${pkgdir}/etc/xinetd.d/finger
   install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
