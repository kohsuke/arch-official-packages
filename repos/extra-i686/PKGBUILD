# $Id$
# Maintainer:  Jan de Groot <jan@archlinux.org>

pkgname=gnome-desktop
pkgver=3.8.2
pkgrel=1
epoch=1
pkgdesc="Library with common API for various GNOME modules"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gsettings-desktop-schemas' 'gtk3' 'xkeyboard-config' 'iso-codes')
makedepends=('intltool' 'gobject-introspection' 'itstool')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver::3}/$pkgname-$pkgver.tar.xz)
sha256sums=('af4ec330fa61ee761daf2d5e481e2f9ffbd068d135d5e31a3eaf274524cba33d')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-desktop \
      --with-gnome-distributor="Arch Linux"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
