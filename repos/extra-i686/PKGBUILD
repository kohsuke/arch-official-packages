# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fbida
pkgver=2.07
pkgrel=2
pkgdesc="Few applications to display and elementary edit images: fbi, fbgs, ida, exiftran"
arch=('i686' 'x86_64')
url="http://linux.bytesex.org/fbida/"
license=('GPL2')
depends=('giflib' 'libtiff' 'libjpeg>=7' 'libexif' 'lesstif' 'libxpm' 'libpng' 'libx11' 'libxext' 'fontconfig')
optdepends=('ghostscript: to use fbgs')
replaces=('fbi')
provides=('fbi')
backup=('etc/X11/app-defaults/Ida')
source=(http://dl.bytesex.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('3e05910fb7c1d9b2bd3e272d96db069c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make Make.config || return 1
  for config in HAVE_LIB{SANE,CURL,LIRC}; do
     sed -i "s/$config.*/$config := no/" Make.config || return 1
  done
  make CC=gcc || return 1
  make DESTDIR="${pkgdir}" prefix=/usr install || return 1
}

