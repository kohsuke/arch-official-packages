# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=audacity
pkgver=1.3.5
pkgrel=1
pkgdesc="Audacity is a program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'flac' 'libsamplerate' \
         'lame' 'shared-mime-info')
makedepends=('zip' 'python' 'pkgconfig')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/audacity/${pkgname}-src-${pkgver}.tar.bz2 \
        audacity.xpm
        audacity16.xpm)
md5sums=('b15503c331e7145942fa0e4b27760d61' 'c8f7f7851638eedf84d05a1afde797b2'\
         '5e94ccbfb665e8356dc4172a3673b4d4')
sha1sums=('6ff24769fdef7bb0db7702eecb128f242fc85fce'
          'a2b6626cd1cdcd9facb262116804d9349e1e6144'
          'a208a4ed129c73f97347bab929c7758a345e8e9a')

build() {
  cd ${startdir}/src/${pkgname}-src-${pkgver}-beta

  WX_CONFIG=/usr/bin/wx-config \
    ./configure --prefix=/usr \
                --with-portaudio=v19 \
                --with-libsamplerate=system \
                --without-libresample \
                --with-libmad=system \
                --with-id3tag=system \
                --with-flac=system \
                --with-vorbis=system \
                --with-libexpat=system \
                --with-libsndfile=system \
                --enable-unicode
  make || return 1
  make DESTDIR=${startdir}/pkg INSTALL_PATH=${startdir}/pkg install

  install -m644 ${startdir}/src/audacity.xpm \
                ${startdir}/pkg/usr/share/${pkgname}/audacity.xpm
  install -m644 ${startdir}/src/audacity16.xpm \
                ${startdir}/pkg/usr/share/${pkgname}/audacity16.xpm
}
