# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libfs
pkgver=1.0.4
pkgrel=1
pkgdesc="X11 Font Services Library"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc' 'xproto' 'fontsproto')
makedepends=('xorg-util-macros' 'xtrans')
options=('!libtool')
source=("${url}/releases/individual/lib/libFS-${pkgver}.tar.bz2")
sha1sums=('f114f32f60eb84d3e1d0e6f0dfe7679b07e91295')

build() {
  cd "${srcdir}/libFS-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/libFS-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
