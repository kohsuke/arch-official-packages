# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=fetchmail
pkgver=6.3.8
pkgrel=6
pkgdesc="A remote-mail retrieval utility"
url="http://fetchmail.berlios.de/"
arch=('i686' 'x86_64')
depends=('glibc' 'openssl' 'python')
optdepends=('tk: fetchmailconf')
license=('GPL')
install=fetchmail.install
source=(http://download2.berlios.de/fetchmail/$pkgname-$pkgver.tar.bz2
        http://fetchmail.berlios.de/fetchmail-SA-2007-02.txt
        fetchmail.rc fetchmail.conf)
md5sums=('66b97500b0a1e3c0916b3b5314f597f5' '0105fd95ac563a02f20a40355d731159'\
         'b5f4d1da0c61d3d8a93fdf4e238f4949' '8fcd6a22dfbc247755fdd050c5c5c4d3')

build() {
  export MAKEFLAGS="-j1"
  cd $srcdir/$pkgname-$pkgver

  patch -up0 < $srcdir/fetchmail-SA-2007-02.txt

  ./configure --prefix=/usr --with-ssl=/usr
  make || return 1
  make DESTDIR=$pkgdir install

  install -Dm755 $srcdir/fetchmail.rc $pkgdir/etc/rc.d/fetchmail
  install -Dm644 $srcdir/fetchmail.conf $pkgdir/etc/conf.d/fetchmail

  # "home" directory for pid file.
  install -d -o 90 -g nobody -m700 $pkgdir/var/run/fetchmail
}
