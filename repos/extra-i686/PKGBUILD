# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=0.9.6
pkgrel=2
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="http://www.indilib.org/index.php?title=Main_Page"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libnova' 'cfitsio' 'boost-libs' 'libusb-compat')
makedepends=('cmake' 'boost')
options=('!libtool')
source=("http://downloads.sourceforge.net/indi/${pkgname}_${pkgver}.tar.gz")
md5sums=('c1456544a36f543e2884f88913cf3eb0')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/lib/udev/rules.d
  mv "${pkgdir}"/etc/udev/rules.d/99-gpusb.rules \
    "${pkgdir}"/usr/lib/udev/rules.d/
  rmdir "${pkgdir}"/etc/udev/rules.d "${pkgdir}"/etc/udev "${pkgdir}"/etc
}
