# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fluxter
pkgver=0.1.0
pkgrel=6
pkgdesc="A workspace pager for fluxbox"
arch=('i686' 'x86_64')
url="http://benedict.isomedia.com/homes/stevencooper/projects/fluxter.html"
license=('GPL')
depends=('gcc-libs' 'libx11')
source=(http://benedict.isomedia.com/homes/stevencooper/files/${pkgname}-${pkgver}.tar.gz
        fluxter-0.1.0-asneeded.patch)
sha1sums=('d0da4759a21fdadc8a6457195c87b6648b5d69bc'
          '2fc912d40233577b34cb6c641e066863e487fab0')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p0 -i ../fluxter-0.1.0-asneeded.patch
}

build() {
  cd ${pkgname}-${pkgver}
  autoreconf --force --install
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
