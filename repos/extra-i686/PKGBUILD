# $Id$
# Maintainer:
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=eventlog
pkgver=0.2.12
pkgrel=3
pkgdesc="API to format and send structured log messages"
arch=('i686' 'x86_64')
url="http://www.balabit.com/support/community/products/"
license=('BSD')
depends=('glibc')
options=('!libtool')
changelog=ChangeLog
source=("http://www.balabit.com/downloads/files/${pkgname}/${pkgver%.*}/${pkgname}_${pkgver}.tar.gz")
sha1sums=('3e35a634e7de029ab9d36995a085bfcb00ed6a4d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
