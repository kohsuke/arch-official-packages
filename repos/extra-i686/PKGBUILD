# $Id: PKGBUILD 62098 2010-01-02 09:13:44Z tpowa $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=0.7.2
pkgrel=1
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="http://www.indilib.org/index.php?title=Main_Page"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'zlib' 'libnova' 'cfitsio')
makedepends=('pkgconfig' 'cmake')
provides=('indilib')
replaces=('indilib')
conflicts=('indilib')
options=('!libtool')
source=("http://downloads.sourceforge.net/indi/${pkgname}_${pkgver}.tar.gz"
        'linking-pthread.patch')
md5sums=('a78a77dc2322a46f5bf4c5d75380e8b0'
        '1ebc282e259cb0c9c52cad3dadd5e044')

build() {
  # fixed upstream
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np2 -i ${srcdir}/linking-pthread.patch

  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
