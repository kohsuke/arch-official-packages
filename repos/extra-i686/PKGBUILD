# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-sis
pkgver=0.10.1
pkgrel=5
pkgdesc="X.org SiS video driver"
_mesaver="7.5"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=("libgl>=${_mesaver}" 'expat')
makedepends=('pkgconfig' 'xorg-server>=1.6.2' 'xf86driproto' 'mesa>=7.5' 'glproto>=1.4.9' 'dri2proto>=2.1' 'xf86dgaproto' 'xineramaproto')
conflicts=('xorg-server<1.6.2')
options=('!libtool')
groups=('xorg-video-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        http://downloads.sourceforge.net/mesa3d/MesaLib-${_mesaver}.tar.bz2
        mesa-7.1-link-shared.patch
        LICENSE)
md5sums=('9f57c2f5ccbd8340db32da1c74083771'
         '459f332551f6ebb86f384d21dd15e1f0'
         '9eddc02e23ec08295822911e726ae89a'
         'e33e023e3d811d28a8ed3bfbd2bf5672')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-dri || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  cd "${srcdir}/Mesa-${_mesaver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=sis \
    --enable-glx-tls \
    --disable-ttm-api \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
