# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>
pkgname=('wireshark-cli' 'wireshark-gtk')
pkgbase=wireshark
pkgver=1.8.3
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('bison' 'flex' 'gtk2' 'krb5' 'libcap' 'libpcap' 'bash' 'gnutls'
             'libgcrypt' 'lua' 'python' 'portaudio')
url="http://www.wireshark.org/"
options=(!libtool)
source=(http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2
        enable_version.patch)
sha1sums=('3e1322eea5794c71de752b7923af9379bcc95299'
          '9a56a9153b6903e508b0375cde4f6e57ffc544e6')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  patch -p0 < ${srcdir}/enable_version.patch

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --with-ssl \
      --with-zlib=yes \
      --with-lua \
      --with-portaudio
  make all
}

package_wireshark-cli() {
  pkgdesc="A free network protocol analyzer for Unix/Linux and Windows - CLI version"
  depends=('krb5' 'libcap' 'libpcap' 'bash' 'gnutls' 'libgcrypt' 'glib2' 'lua' 'portaudio')
  install=wireshark.install
  conflicts=(wireshark)

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm "${pkgdir}/usr/bin/wireshark"

  # Headers
  install -dm755 ${pkgdir}/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

  install -m644 color.h config.h register.h "${pkgdir}/usr/include/${pkgbase}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
    install -m644 ${d}/*.h ${pkgdir}/usr/include/${pkgbase}/${d}
  done
}

package_wireshark-gtk() {
  pkgdesc="A free network protocol analyzer for Unix/Linux and Windows - GTK frontend"
  depends=('gtk2' 'wireshark-cli' 'desktop-file-utils' 'hicolor-icon-theme')
  install=wireshark-gtk.install
  replaces=(wireshark)
  conflicts=(wireshark)

  cd "${srcdir}/${pkgbase}-${pkgver}"

  install -Dm755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  for d in 16 32 48; do
    install -Dm644 image/hi${d}-app-wireshark.png  \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
  done

  for d in 16 24 32 48 64 128 256 ; do
    install -Dm644 image/WiresharkDoc-${d}.png \
                   "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
  done
  install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}
