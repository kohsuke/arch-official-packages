# $Id$
# Maintainer: Your Name <youremail@domain.com>

pkgname=wcslib
pkgver=4.17
pkgrel=1
pkgdesc="A C library that implements the 'World Coordinate System' (WCS) standard in FITS"
arch=('i686' 'x86_64')
url="http://www.atnf.csiro.au/people/Mark.Calabretta/WCS/"
license=('GPL3')
depends=('cfitsio')
makedepends=('gcc-fortran')
source=("ftp://ftp.atnf.csiro.au/pub/software/${pkgname}/${pkgname}.tar.bz2")
md5sums=('81f739c84789b84626db9fe4cd866081')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --without-pgplot
  make
}

#check() {
#  cd $pkgname-$pkgver
#  make -k check
#}

package() {
  # Fix install on i686
  install -d "${pkgdir}"/usr/include/${pkgname}-${pkgver}

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  
  # Fix wrong permissions
  chmod g=rx,-s "${pkgdir}"/usr/{bin/,/lib/{,pkgconfig},share/man/man1/}
  chmod -s "${pkgdir}"/usr/{include/wcslib-${pkgver}/,share/doc/wcslib-${pkgver}/{,html/}}
}
