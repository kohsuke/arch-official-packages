# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: Brice Carpentier <brice@dlfp.org>

pkgname=boo
pkgver=0.9.3.3457
pkgrel=1
pkgdesc="A wrist friendly language for the CLI"
arch=('i686' 'x86_64')
url="http://boo.codehaus.org/"
license=('GPL')
depends=('sh' 'mono' 'shared-mime-info')
options=('!makeflags')
install=boo.install
source=(http://dist.codehaus.org/boo/distributions/${pkgname}-${pkgver}.tar.gz)
md5sums=('3988e0a7f58e780717692c1264182e10')
sha1sums=('54d43231fa923496331bf5e0a97ef04564d44bca')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p $MONO_SHARED_DIR
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's|/lib|/lib/mono|' extras/boo.pc.in
  ./configure --prefix=/usr --libdir=/usr/lib
  make VERBOSE=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/share/"
  rm -rf "${pkgdir}/usr/share/gtksourceview-1.0/language-specs/boo.lang"
}
