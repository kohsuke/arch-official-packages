# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=python-egenix-mx-base
_origname=egenix-mx-base
pkgver=3.2.1
pkgrel=1
pkgdesc="A collection of Python tools"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.egenix.com/products/python/mxBase/"
depends=('python2')
makedepends=('python2-distribute')
replaces=('egenix-mx-base')
provides=('egenix-mx-base')
source=("http://downloads.egenix.com/python/${_origname}-${pkgver}.tar.gz")
sha1sums=('a82f7f155ea92f26f40a6a3bf9160bfac7c52a00')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  find . -name \*.py -exec sed -i -e "s|#!/usr/bin/env python|#!/usr/bin/env python2|" \
                                  -e "s|#!/usr/local/.*python.*|#!/usr/bin/env python2|" {} \;

  python2 setup.py build
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1
  install -D -m644 COPYRIGHT "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
