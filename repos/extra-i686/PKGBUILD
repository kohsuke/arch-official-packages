# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gnome-python-extras
pkgname=('gnome-python-extras' 'python2-egg' 'python2-gksu2' 'python2-gtkhtml2' 'python2-gtkspell')
pkgver=2.25.3
pkgrel=11
arch=('i686' 'x86_64')
url="http://www.daa.com.au/~james/pygtk/"
license=('GPL' 'LGPL')
makedepends=('libgtkhtml' 'gtkspell' 'libgksu' 'libgnomeui' 'python2' 'pygtk' 'gnome-python')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/gnome-python-extras/2.25/gnome-python-extras-${pkgver}.tar.bz2
	gnome-python-extras-2.25.3-update-for-2.27.2.patch)
md5sums=('9f3b7ec5c57130b96061cb486b79c076' '0b0a4d93016d14f2306682301de3bda3')
sha1sums=('e69e5288466c960ed6f985bb10c9969b21ed85a4' 'b065efb7f7f5baa7b95b63bcadb6a4623d1bc9e5')

build() {
    cd "${srcdir}/gnome-python-extras-${pkgver}"
    patch -Np1 -i "${srcdir}"/gnome-python-extras-2.25.3-update-for-2.27.2.patch
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr
    make
}

package_gnome-python-extras() {
    pkgdesc="Gnome Python interfaces for libraries not part of the core platform"
    depends=('python2-egg' 'python2-gksu2' 'python2-gtkhtml2' 'python2-gtkspell')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make install-pkgconfigDATA DESTDIR="${pkgdir}"
}

package_python2-egg(){
    pkgdesc="Python bindings for interacting with egg"
    depends=('libgnomeui' 'pygtk')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C egg install DESTDIR="${pkgdir}"
}

package_python2-gksu2() {
    pkgdesc="Python bindings for the gksu2"
    depends=('libgksu' 'pygtk')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C gksu2 install DESTDIR="${pkgdir}"
}

package_python2-gtkhtml2() {
    pkgdesc="Python bindings for the gtkhtml2"
    depends=('libgtkhtml' 'pygtk')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C gtkhtml2 install DESTDIR="${pkgdir}"
}

package_python2-gtkspell() {
    pkgdesc="Python bindings for the gtkspell"
    depends=('gtkspell' 'pygtk')

    cd "${srcdir}/gnome-python-extras-${pkgver}"
    make -C gtkspell install DESTDIR="${pkgdir}"
}
