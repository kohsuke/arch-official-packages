# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=alsaplayer
pkgver=0.99.81
pkgrel=1
pkgdesc="A heavily multi-threaded PCM player that tries to excercise the ALSA library and driver quite a bit"
arch=('i686' 'x86_64')
url="http://www.alsaplayer.org/"
license=('GPL')
depends=('gcc-libs')
makedepends=('gtk2' 'libid3tag' 'libvorbis' 'libmad' 'libmikmod' 'jack' 'esound' 'mesa')
optdepends=('gtk2: for the gtk2 GUI interface' \
            'libgl: for the OpenGL visual plugins' \
            'esound: for ESD audio server output support' \
            'jack: for JACK audio server output support' \
            'audiofile: for support of various audio formats like AIFF, WAVE, .snd/.au' \
            'libid3tag: for flac support' 'flac: for flac support' 'libmad: for MPEG support' \
            'libsndfile: for sndfile support' 'libvorbis: for ogg vorbis support' \
            'libmikmod: for mod, s3m, it and xm formats supports')
options=('!libtool')
source=(http://www.alsaplayer.org/${pkgname}-${pkgver}.tar.bz2)
md5sums=('845c53975791ba342e316ceac8aa1527')
sha1sums=('25817ebd1e5c36da31a88363315a1bf7a945ddfd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-audiofile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
