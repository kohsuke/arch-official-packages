# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=pyxml
pkgver=0.8.4
pkgrel=10
pkgdesc="Python XML parsing library"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/pyxml/"
license=('custom')
depends=('python2')
source=("http://downloads.sourceforge.net/sourceforge/pyxml/PyXML-${pkgver}.tar.gz"
        'fix-python2.6.patch'
        'foreigncharsfix.patch')
md5sums=('1f7655050cebbb664db976405fdba209'
         '4b652e0c866e3cca7e2386e8e383d5ba'
         'c9c54fdf0f7d1515a3b7aa34b01d1c46')

build() {
  cd "${srcdir}/PyXML-${pkgver}"

  patch -Np1 -i "${srcdir}/fix-python2.6.patch"

  # as warranted by eric (the python IDE)
  patch -Np1 -i "${srcdir}/foreigncharsfix.patch"

  python2 setup.py build
}

package() {
  cd "${srcdir}/PyXML-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  sed -e 's|#! /usr/bin/env python|#!/usr/bin/env python2|' \
      -i ${pkgdir}/usr/lib/python2.7/site-packages/_xmlplus/dom/ext/c14n.py
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      -i ${pkgdir}/usr/lib/python2.7/site-packages/_xmlplus/dom/html/GenerateHtml.py
}
