# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=smpeg2
pkgver=412
pkgrel=2
pkgdesc="SDL2 MPEG Player Library"
arch=('i686' 'x86_64')
url="http://icculus.org/smpeg/"
license=('LGPL')
depends=('sdl2')
makedepends=('gtk' 'mesa' 'glu' 'svn')
optdepends=('gtk: to use gtv ' 'glu: to use glmovie')
options=('!libtool')
source=(svn://svn.icculus.org/smpeg/trunk#revision=$pkgver)
md5sums=('SKIP')

build() {
  cd ${srcdir}/trunk
  sed -i s/SDL_CONFIG/SDL2_CONFIG/g smpeg2-config.in
  ./autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man --disable-static
  make
}

package() {
  cd ${srcdir}/trunk
  make DESTDIR=${pkgdir} install

  # hacky way of resolving conflicts
  rm ${pkgdir}/usr/bin/plaympeg
  rm -r ${pkgdir}/usr/share/man
}
