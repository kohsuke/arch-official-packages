# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim
pkgver=4.2.1
pkgrel=2
pkgdesc="KDE Personal Information Management suite"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdepimlibs' 'gnokii' 'libxss' 'kdebase-runtime' 'pilot-link' 'libmal')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost')
options=('docs')
install='kdepim.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2" 'korganizer.patch')
md5sums=('2ebc3730aaee53f494e8e6abd26029bd'
         'cab0c7dfac884ac4f9efe92a34fc1af8')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	# fix KOrganizer; see https://bugs.kde.org/show_bug.cgi?id=186653
	patch -p1 -i $srcdir/korganizer.patch || return 1
	cd ../
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
}
