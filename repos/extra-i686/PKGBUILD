# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kalzium
pkgver=4.9.3
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="http://kde.org/applications/education/kalzium/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdelibs' 'avogadro' 'ocaml' 'facile')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kalzium-${pkgver}.tar.xz"
        'fix-linking.patch')
sha1sums=('7b4a50aabd960752d6b746740cc6b4bf6cae3b30'
          '478697adc8ae191355ee64b824f30e11bd7af682')

build() {
  # KDEBUG#308939 (Fixed upstream)
  cd kalzium-${pkgver}
  patch -p1 -i "${srcdir}"/fix-linking.patch
  cd ../

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kalzium-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
