# $Id: PKGBUILD 30749 2010-10-20 11:18:33Z schuay $
# Maintainer:
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Pulphix <crimea.v@libero.it>

pkgname=gamin
pkgver=0.1.10
pkgrel=7
pkgdesc='File and directory monitoring system defined to be a subset of the FAM (File Alteration Monitor)'
url='http://www.gnome.org/~veillard/gamin'
license=('GPL')
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('python2')
options=('libtool')
optdepends=('python2: for the python module.')
provides=('fam')
conflicts=('fam')
source=("http://www.gnome.org/~veillard/${pkgname}/sources/${pkgname}-${pkgver}.tar.gz"
        'fix-deprecated-const.patch'
        '18_gam_server_deadlocks.patch')
md5sums=('b4ec549e57da470c04edd5ec2876a028'
         'f679aeb48fe9dd376c8828cc9b6941ab'
         '4784359a3206bfa3c0dce1c23468f87f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugs.archlinux.org/task/33642
  patch -p1 -i "${srcdir}"/18_gam_server_deadlocks.patch

  patch -p1 -i "${srcdir}"/fix-deprecated-const.patch

  # python 2
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' python/gamin.py

  ./configure --prefix=/usr --disable-static --with-threads \
    --disable-debug-api --disable-debug --libexecdir=/usr/lib/gamin \
    --with-python=/usr/bin/python2
  make
}
package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
# vim:set ts=2 sw=2 et:
