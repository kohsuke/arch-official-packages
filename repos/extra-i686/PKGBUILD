# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=konq-plugins
pkgver=4.6.1
pkgrel=1
pkgdesc="Extra plugins for Konqueror"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('kdebase-konqueror' 'tidyhtml')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'docbook-xsl')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('38facbeb5627ff6affa0fa0d676d1d19')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
