# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir.archlinux.org>

pkgname=qtiplot
pkgver=0.9.7.7
pkgrel=1
pkgdesc="Data analysis and scientific plotting - free clone of Origin"
arch=('i686' 'x86_64')
url="http://soft.proindependent.com/qtiplot.html"
depends=('muparser>=1.30' 'gsl' 'python>=2.6.2' 'pyqt' 'sip' \
	'boost>=1.37' 'libgl' 'qwt')
# build against liborigin provided in the package ...
# build against qwtplot3d provided in the package ...
# as systemwide one doesn't provide all needed functions
makedepends=('pkgconfig')
license=('GPL2')

source=("http://soft.proindependent.com/src/qtiplot-$pkgver.tar.bz2" \
	"$pkgname.desktop" "$pkgname.png" "qtiplot.pro.archlinux"
	"qwtplot3d_gcc.patch" "scriptedit.patch")
md5sums=('c923fb5c5f161e0d6cb31c3beebe6165'         '56bd53f4f1367c285086acb969f13348'
         'ad8affbd6f0d5cbdcde46c923ee2668a'         '7af559c2986f5c65616331dc36859642'
         'b326dec9d13f838293b982e76fe9d5d1'         'fd36e086195ca70b4628a14eb2ba4fcd')

build() {

  cd ${srcdir}/${pkgname}-${pkgver}/qtiplot/src/scripting
  patch -Np0 -i ${srcdir}/scriptedit.patch || return 1

  cd ${srcdir}/${pkgname}-${pkgver}/3rdparty/qwtplot3d/src
  patch -Np0 -i ${srcdir}/qwtplot3d_gcc.patch || return 1 

  cd ${srcdir}/${pkgname}-${pkgver}/${pkgname}
  install -Dm644 ${srcdir}/qtiplot.pro.archlinux qtiplot.pro

  sed -i 's#d_python_config_folder + "#"/usr/share/qtiplot#' src/core/ApplicationWindow.cpp

  export QMAKESPEC=linux-g++

  qmake qtiplot.pro || return 1
  make QTDIR=/usr/ QMAKESPEC=linux-g++ || return 1
  INSTALL_ROOT=${pkgdir} make install || return 1

  # make it nice:
  install -D -m644 ${srcdir}/${pkgname}.png \
  	${pkgdir}/usr/share/pixmaps/${pkgname}.png || return 1
  install -D -m644 ${srcdir}/${pkgname}.desktop \
  	${pkgdir}/usr/share/applications/${pkgname}.desktop || return 1
}
