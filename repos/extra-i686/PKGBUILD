# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=monodevelop
pkgver=2.4.2
pkgrel=2
pkgdesc="An IDE primarily designed for C# and other .NET languages"
arch=('i686' 'x86_64')
url="http://www.monodevelop.org"
license=('GPL')
depends=('mono>=2.10.1' 'mono-addins>=0.5' 'gnome-sharp' 'gecko-sharp-2.0>=0.10')
options=(!makeflags)
install=monodevelop.install
source=(http://ftp.novell.com/pub/mono/sources/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	monodevelop_gnome3.patch)
md5sums=('05d20deada26a115418cb1b4526b8d15'
         'fd011f41af212fe87fb9997ebf074188')

build() {
  export MONO_SHARED_DIR=$srcdir/src/.wabi
  mkdir -p $MONO_SHARED_DIR

  cd $srcdir/$pkgname-$pkgver
  patch -p1 < ${srcdir}/monodevelop_gnome3.patch || return 1
  ./configure --prefix=/usr --disable-update-mimedb --disable-update-desktopdb
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  rm -r $MONO_SHARED_DIR
}

