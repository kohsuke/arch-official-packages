# $Id$
# Maintainer: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2010.4.0
pkgrel=3
pkgdesc="A frontend to the panorama-tools"
arch=('i686' 'x86_64')
url="http://hugin.sourceforge.net/"
license=('LGPL')
depends=('wxgtk' 'libpano13' 'boost-libs' 'enblend-enfuse' 'exiv2' 'openexr'
         'autopano-sift-c' 'lapack' 'desktop-file-utils' 'make' 'perl-exiftool')
makedepends=('zip' 'cmake' 'boost')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2 hugin-gcc46.patch)
md5sums=('bfd716655461987312b2f2f00c90dcc5'
         '4393c7b31da60f230c85e63a6a2e43a0')
sha1sums=('47f25c0a8b97c27108e567bcd22203c87f268d07'
          '52798a155a037f853f41402f797e8a4cb6bb3e07')

build() {
  cd "${srcdir}"
  (cd ${pkgname}-${pkgver}; patch -p1 -i ../hugin-gcc46.patch)
  mkdir build && cd build

  cmake "${srcdir}/${pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_EXE_LINKER_FLAGS="-lpthread" \
    -DENABLE_LAPACK=yes

  make
}

package(){
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
