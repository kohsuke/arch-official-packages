# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ypserv
pkgver=2.28
pkgrel=1
pkgdesc='Linux NIS Server'
arch=('i686' 'x86_64')
url='http://www.linux-nis.org/nis/ypserv/'
license=('GPL2')
depends=('gdbm' 'openslp' 'inetutils')
optdepends=('gawk: for printcap support')
backup=('etc/ypserv.conf' 'etc/netgroup' 'var/yp/securenets')
source=("http://www.linux-nis.org/download/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
	'confpost.patch'
        'ypxfrd.service'
        'yppasswdd.service'
	'ypserv.service'
        'yppasswd'
        'ypserv')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../confpost.patch
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -D -m644 etc/netgroup "${pkgdir}"/etc/netgroup
	install -D -m644 etc/ypserv.conf "${pkgdir}"/etc/ypserv.conf
	install -D -m644 etc/securenets "${pkgdir}"/var/yp/securenets
	install -D -m755 ../ypserv "${pkgdir}"/etc/rc.d/ypserv
	install -D -m755 ../yppasswd "${pkgdir}"/etc/rc.d/yppasswd
	install -D -m644 ../ypxfrd.service "${pkgdir}"/usr/lib/systemd/system/ypxfrd.service
	install -D -m644 ../ypserv.service "${pkgdir}"/usr/lib/systemd/system/ypserv.service
	install -D -m644 ../yppasswdd.service "${pkgdir}"/usr/lib/systemd/system/yppasswdd.service

	# Provide native service for arch-daemons generator
	ln -s yppasswdd.service "$pkgdir/usr/lib/systemd/system/yppasswd.service"
}
md5sums=('ed64f489bab494218fc5c5942b95d5b6'
         '8acb314f30d6e531ac9efa8655f68ccb'
         'c80e253756446d59a90c4c21b1cb8740'
         '7cba3dd3544436c3c1f05e36c0018e92'
         '3398271c3e0a054e64b06b99aa2dc63d'
         '4bc84c48ed1bb9659698853acb5cfd76'
         'c40befe5932f1b42efaced6b46809303')
