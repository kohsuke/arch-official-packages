# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=audacity
pkgver=1.3.6
pkgrel=1
pkgdesc="Audacity is a program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'libsamplerate' \
         'lame' 'shared-mime-info' 'redland' 'jack-audio-connection-kit')
makedepends=('zip' 'python' 'pkgconfig')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/audacity/${pkgname}-src-${pkgver}.tar.bz2 \
        audacity.xpm
        audacity16.xpm)
md5sums=('a59d30bcedd017c5b3ac7bcecf7a1628' 'c8f7f7851638eedf84d05a1afde797b2'\
         '5e94ccbfb665e8356dc4172a3673b4d4')
sha1sums=('d02edac1dafb0c6f6fb12d29505c63b72fb1b116'
          'a2b6626cd1cdcd9facb262116804d9349e1e6144'
          'a208a4ed129c73f97347bab929c7758a345e8e9a')

build() {
  cd ${startdir}/src/${pkgname}-src-${pkgver}

  WX_CONFIG=/usr/bin/wx-config \
    ./configure --prefix=/usr \
                --with-portaudio=v19 \
                --with-libsamplerate=system \
                --without-libresample \
                --with-libmad=system \
                --with-id3tag=system \
                --with-flac=system \
                --with-vorbis=system \
                --with-libexpat=system \
                --with-libsndfile=system \
                --enable-unicode
  make || return 1
  make DESTDIR=${startdir}/pkg INSTALL_PATH=${startdir}/pkg install

  install -m644 ${startdir}/src/audacity.xpm \
                ${startdir}/pkg/usr/share/${pkgname}/audacity.xpm
  install -m644 ${startdir}/src/audacity16.xpm \
                ${startdir}/pkg/usr/share/${pkgname}/audacity16.xpm
}
