# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer0.10-ffmpeg
pkgver=0.10.13
pkgrel=1
pkgdesc="Gstreamer FFMpeg Plugin"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gstreamer0.10-base' 'bzip2')
makedepends=('pkgconfig' 'yasm' 'sdl')
url="http://www.gstreamer.net"
groups=('gstreamer0.10-plugins')
options=('!libtool')
source=(http://gstreamer.freedesktop.org/src/gst-ffmpeg/gst-ffmpeg-${pkgver}.tar.bz2)
sha256sums=('76fca05b08e00134e3cb92fa347507f42cbd48ddb08ed3343a912def187fbb62')

build() {
  cd "${srcdir}/gst-ffmpeg-${pkgver}"
  unset CFLAGS
  unset CXXFLAGS
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static --with-ffmpeg-extra-configure="--enable-runtime-cpudetect"
  make
}

package() {
  cd "${srcdir}/gst-ffmpeg-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
