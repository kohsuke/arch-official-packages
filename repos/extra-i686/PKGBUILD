# $Id$
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kaffeine
pkgver=1.2.2
pkgrel=1
pkgdesc='KDE media player'
license=('GPL')
arch=('i686' 'x86_64')
url="http://kaffeine.kde.org"
depends=('kdelibs' 'kdebase-runtime' 'xine-lib')
makedepends=('pkgconfig' 'cmake' 'automoc4')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
        cd $srcdir/build
	make DESTDIR=$pkgdir install
}
md5sums=('690e48d2e5fe123887109aa9b1bc1c31')
