# $Id$
# Contributor: Ben <contrasutra@myrealbox.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gweled
pkgver=0.7
pkgrel=5
pkgdesc="Bejewled Game (aka Diamond Mine)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://sebdelestaing.free.fr/gweled/"
depends=('libgnomeui' 'librsvg' 'libmikmod' 'filesystem')
install=gweled.install
source=(http://sebdelestaing.free.fr/gweled/Release/${pkgname}-${pkgver}.tar.gz
        invalid_free.patch)
md5sums=('730fe1737e0b0e9940575aa573d63d84' '7b467d82e42183c36b25bcf5f9bf4407')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np0 -i ${srcdir}/invalid_free.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --with-scores-user=root --with-scores-group=games
  make LDFLAGS+=-Wl,--export-dynamic || return 1
  make DESTDIR=${pkgdir} install

  #We generate these files on postinstall to prevent score resets on upgrade
  rm -rf ${pkgdir}/var
}

