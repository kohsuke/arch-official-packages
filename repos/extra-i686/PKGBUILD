# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=amarok
replaces=('amarok-base' 'amarok-engine-xine' 'amarok-base-mysqlfree')
pkgver=2.2.2
pkgrel=2
pkgdesc="A media player for KDE"
arch=("i686" "x86_64")
url="http://amarok.kde.org"
license=('GPL2' 'LGPL2' 'FDL')
depends=('kdebase-runtime' 'mysql' 'qtscriptgenerator' 'taglib-extras' 'liblastfm' 'qca')
makedepends=('pkgconfig' 'automoc4' 'cmake' 'libgpod' 'libmtp' 'loudmouth')
optdepends=("libgpod: support Apple iPod audio devices"
	"libmtp: support for portable media devices"
	"loudmouth: backend needed by mp3tunes for syncing")
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
	'fix-photo-widget.patch')
md5sums=('c2c4ae2f2ff7154a064b554cdee557a9'
         '43f029e83500eb2edb773ad82d302a11')

build() {
   cd ${srcdir}/${pkgname}-${pkgver}
   # already fixed in 2.2.3
   patch -Np1 -i ${srcdir}/fix-photo-widget.patch || return 1

   cd ${srcdir}
   mkdir build
   cd build
   cmake ../${pkgname}-${pkgver} \
           -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_SKIP_RPATH=ON \
           -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
           -DCMAKE_INSTALL_PREFIX=/usr
   make || return 1
   make DESTDIR=${pkgdir} install || return 1
}
