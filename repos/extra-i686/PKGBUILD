# $Id$
# Maintainer: Ray Rashif <schivmeister@gmail.com>
# Contributor: Orivej Desh <smpuj@bk.ru>

pkgname=vamp-plugin-sdk
pkgver=2.1
pkgrel=1
pkgdesc="The Vamp audio analysis plugin system"
arch=('i686' 'x86_64')
url="http://www.vamp-plugins.org/"
license=('BSD')
depends=('gcc-libs' 'libsndfile')
#options=('!libtool')
source=(http://downloads.sourceforge.net/vamp/$pkgname-$pkgver.tar.gz
        gcc44.patch)
md5sums=('13252077a73987dae72a9174e529b6b9'
         'a07e3ef9c657a56cf2e8c4afc0ba1cec')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # gcc 4.4 compatibility
  patch -Np1 -i ../gcc44.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
