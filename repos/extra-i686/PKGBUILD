# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=racket
pkgver=5.2.1
pkgrel=3
pkgdesc="A programming language environment (formerly known as PLT Scheme) suitable for tasks ranging from scripting to application development"
arch=('i686' 'x86_64')
url="http://racket-lang.org/"
license=('LGPL')
depends=('gtk2' 'desktop-file-utils')
makedepends=('gsfonts')
options=('!libtool' '!strip')
install=racket.install
source=(http://download.racket-lang.org/installers/${pkgver}/racket/${pkgname}-${pkgver}-src-unix.tgz \
        drracket.desktop drracket-normal.rkt)
sha1sums=('b51cdd8b9825edb60e5c2c80472cec8220918cd1'
          'a20808f6b250225704856f82a544681a962a299d'
          '6699e80e424479d45f427ba341003235e0d475cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  [ "$CARCH" == "x86_64" ] && export CFLAGS+="-fPIC"
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install

  # FS#30245
  install -D -m644 "${srcdir}/drracket-normal.rkt" "${pkgdir}/usr/lib/racket/collects/drracket/private/drracket-normal.rkt"

  install -D -m644 ../collects/icons/plt.xpm "${pkgdir}/usr/share/pixmaps/drracket.xpm"
  install -D -m644 "${srcdir}/drracket.desktop" "${pkgdir}/usr/share/applications/drracket.desktop"
}
