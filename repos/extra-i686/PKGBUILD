# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=quodlibet
pkgver=2.3.2
pkgrel=1
pkgdesc="An audio player written in pygtk"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://code.google.com/p/quodlibet/"
depends=('gstreamer0.10-python' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 
         'gstreamer0.10-ugly-plugins' 'mutagen' 'pygtk')
makedepends=('intltool')
optdepends=('gstreamer0.10-ffmpeg: for ffmpeg (ASF/WMA) support ' 
            'gstreamer0.10-bad-plugins: for MPEG-4 (AAC) and Musepack support' 
            'dbus-python: for dbus support' 
            'notification-daemon: for notification support' 
            'libgpod: for ipod support' 
            'python-feedparser: for audio feeds (podcast) support' 
            'udisks: for media devices support' 
            'media-player-info: for media devices support')
options=('!makeflags')
source=(http://quodlibet.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
sha1sums=('21bc5fb988ca6c09959fc7212c05272a126a5be5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's#env python#env python2#' *.py quodlibet/util/fmps.py
  ./setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./setup.py install --prefix="${pkgdir}/usr"
  install -D -m644 quodlibet/images/exfalso.png "${pkgdir}/usr/share/pixmaps/exfalso.png"
  install -D -m644 quodlibet/images/quodlibet.png "${pkgdir}/usr/share/pixmaps/quodlibet.png"
}
