# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-search-tool
pkgver=3.4.0
pkgrel=1
pkgdesc="Utility to search for files"
arch=(i686 x86_64)
url="http://gnome.org"
license=('GPL2')
depends=('gconf' 'gtk3' 'libsm')
makedepends=('intltool' 'gnome-doc-utils')
install=$pkgname.install
options=('!emptydirs')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('6c4ed39ab1196d118e5e0d2a7faa07fbbb257831454ee92ff0c6f28c0942e69c')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-install
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" --domain $pkgname "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
