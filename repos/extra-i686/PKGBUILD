# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-games
pkgver=2.32.0
pkgrel=3
pkgdesc="Some Games for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=('pygtk' 'libsm' 'gconf>=2.32.0' 'guile>=1.8.7' 'librsvg>=2.32.0' 'filesystem>=2010.09' 'desktop-file-utils' 'libcanberra>=0.25' 'clutter-gtk>=0.10.8' 'hicolor-icon-theme' 'python-rsvg>=2.31.0')
makedepends=('pkg-config' 'gnome-doc-utils>=0.20.1' 'intltool' 'glproto' 'gobject-introspection')
optdepends=('python-gtkglext: glchess 3D mode')
provides=('glchess' 'gnome-sudoku')
conflicts=('glchess' 'gnome-sudoku')
options=('!emptydirs' '!libtool')
install=gnome-games.install
url="http://www.gnome.org"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('7cd2a282071b1b9cdff8a7102abddc26e253af7b3094551220addf2b3a2850c2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper \
      --disable-static \
      --with-scores-user=root --with-scores-group=games \
      --enable-introspection=yes \
      --enable-omitgames=swell-foop,lightsoff
  make
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  # Remove all scores, we generate them from postinstall
  rm -rf "${pkgdir}/var"

  sed -i "s|#!/usr/bin/python$|#!/usr/bin/python2|" \
    $pkgdir/usr/lib/python2.7/site-packages/gnome_sudoku/{tracker_info,number_box}.py

  sed -i "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $pkgdir/usr/bin/{gnome-sudoku,glchess}

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-games ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
