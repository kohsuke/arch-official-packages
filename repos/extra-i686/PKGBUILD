# $Id$
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Maintainer: roberto <roberto@archlinux.org>

pkgname=transcode
_sripver=0.3-4
pkgver=1.1.0
pkgrel=6
pkgdesc="Transcode is a video/DVD ripper and encoder for the terminal/console."
arch=(i686 x86_64)
license=('GPL2')
depends=('imagemagick>=6.5.1.9' 'lzo2' 'libdv' 'libdvdread>=4.1.3' 'ffmpeg>=0.5' 'mjpegtools' 'xvidcore' 'libmpeg2' 'libtheora' 'libxaw' 'x264>=20090416' 'libxml2' 'libxv' 'a52dec' 'alsa-lib')
makedepends=('nasm')
url="http://transcoding.org"
options=(!libtool !makeflags)
source=(http://fromani.exit1.org/${pkgname}-${pkgver}.tar.bz2 \
        http://downloads.sourceforge.net/sourceforge/subtitleripper/subtitleripper-${_sripver}.tgz)
md5sums=('5ca205e32b546402a48ea8004a7b3232'
         'c0bd49a88f667c68c4430ad25bbed510')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|ffmpeg/avcodec.h|libavcodec/avcodec.h|' import/{import_ffmpeg.c,decode_lavc.c} || return 1
  sed -i 's|ffmpeg/avcodec.h|libavcodec/avcodec.h|' export/{aud_aux.c,export_ffmpeg.c,ffmpeg_cfg.h} || return 1
  sed -i 's|ffmpeg/avcodec.h|libavcodec/avcodec.h|' filter/filter_resample.c || return 1
  autoconf || return 1

 # building from here
  ./configure --prefix=/usr \
    --enable-sse=no --enable-sse2=no --enable-altivec=no --enable-mmx \
    --enable-lame --enable-ogg --enable-vorbis --enable-theora \
    --enable-libdv --enable-libxml2 --enable-v4l \
    --enable-imagemagick --enable-libjpeg --enable-lzo --enable-mjpegtools \
    --enable-sdl --enable-freetype2 --enable-a52 --enable-libpostproc \
    --enable-xvid --enable-x264 --enable-alsa \
    --with-libxml2-prefix=/usr \
    --with-mjpegtools-prefix=/usr \
    --with-imagemagick-prefix=/usr \
    --with-libdv-includes=/usr \
    --with-libpostproc-includes=/usr/include/libpostproc \
    --with-ogg-prefix=/usr \
    --with-lame-libs=/usr \
    --with-lzo-libs=/usr/lib \
    --with-libavcodec-prefix=/usr \
    --with-libavcodec-includes=/usr/include \
    --with-libavcodec-libs=/usr/lib || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
 ##############################################################################
 # this imports the latest subtitleripper sources and replaces the internal
  mkdir contrib
  mv ${srcdir}/subtitleripper contrib/subrip
 # removes dependency on libppm, make it dependend on netpbm just failed, argh...
  cd contrib/subrip || return 1
  sed -e 's|^\(.*lppm.*\)$|#\1|' \
      -e 's|^\(.*D_HAVE_LIB_PPM.*\)$|#\1|' \
      -e 's/DEFINES :=/DEFINES = -DHAVE_GETLINE/' \
      -i Makefile || return 1
  make || return 1
  install -m 755 pgm2txt ${pkgdir}/usr/bin/ || return 1
  install -m 755 srttool ${pkgdir}/usr/bin/ || return 1
  install -m 755 subtitle2pgm ${pkgdir}/usr/bin/ || return 1
  install -m 755 subtitle2vobsub ${pkgdir}/usr/bin/ || return 1
  install -m 755 vobsub2pgm ${pkgdir}/usr/bin/ || return 1
}
