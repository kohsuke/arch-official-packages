# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer0.10
pkgver=0.10.21
pkgrel=2
pkgdesc="GStreamer Multimedia Framework"
arch=(i686 x86_64)
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2>=2.6.32' 'glib2>=2.18.1')
makedepends=('perlxml' 'pkgconfig' 'gtk-doc')
options=('!libtool')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.bz2
	bgo555631.patch)
md5sums=('7bad90af3fd81a1535363cf85359125c'
         '71eb301845184638e6556dacd1ca7d55')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
  patch -Np0 -i "${srcdir}/bgo555631.patch" || return 1
  ./configure --prefix=/usr \
	--sysconfdir=/etc --localstatedir=/var \
	--disable-docs-build || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  cd "${pkgdir}/usr/bin"

  #Remove unversioned gst-* binaries to get rid of conflicts
  for bins in `ls *-0.10`; do
    rm -f ${bins/-0.10/}
  done
}
