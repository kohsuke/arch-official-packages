# $Id: PKGBUILD 41669 2009-06-06 10:21:13Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=konq-plugins
pkgver=svn1217499
pkgrel=1
pkgdesc="Extra plugins for Konqueror"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('kdebase-konqueror' 'tidyhtml' 'kwebkitpart')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'docbook-xsl')
install=${pkgname}.install
# svn co -q svn://websvn.kde.org:443/home/kde/trunk/KDE/kdebase/apps/konq-plugins
source=("ftp://ftp.archlinux.org/other/kde/${pkgname}-${pkgver}.tar.bz2"
        'CMakeLists.patch')
md5sums=('1abdef7d120d8caf6f6709a3db032893'
         '4652be501c5b81370010f3c28fd02bc3')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np0 -i ${srcdir}/CMakeLists.patch

  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
