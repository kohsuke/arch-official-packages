# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.0.1
pkgrel=5
pkgdesc="Driver for FireWire audio devices"
arch=('i686' 'x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('libiec61883' 'libavc1394' 'libsigc++'
          'libxml++' 'alsa-lib' 'dbus')
makedepends=('scons' 'python2-qt')
optdepends=('python2: ffado-diag'
            'python2-qt: mixer applet')
provides=('ffado')
source=("http://www.ffado.org/files/$pkgname-$pkgver.tar.gz"
        '60-ffado.rules')
md5sums=('786f31facd417e6207e429f50af0e15e'
         'f03a3e950801c4405b8934084651bca8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # hack to use CFLAGS without optimising
  sed -i 's/-O2//g' SConstruct

  if [ "$CARCH" = "x86_64" ]; then
    sed -i "s/-m64/-m64 $CFLAGS/g" SConstruct
  else
    sed -i "s/-m32/-m32 $CFLAGS/g" SConstruct
  fi

  # python2 fix
  sed -i 's:python $SOURCE:python2 $SOURCE:' SConstruct
  sed -i 's:python support/tools:python2 support/tools:' \
    support/tools/SConscript

  scons PREFIX=/usr #ENABLE_OPTIMIZATIONS=True
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  scons DESTDIR="$pkgdir" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # add audio device rules
  # see https://bugs.archlinux.org/task/26342
  install -Dm644 "$srcdir/60-ffado.rules" \
    "$pkgdir/lib/udev/rules.d/60-ffado.rules"

  # python2 fix
  for i in $(grep '^#!.*bin.*python' -R "$pkgdir" | sed 's/:.*//'); do
    sed -i 's:^#!.*bin.*python:#!/usr/bin/python2:' "$i"
  done

  # desktop files & icons
  install -Dm644 support/xdg/ffado.org-ffadomixer.desktop \
    "$pkgdir/usr/share/applications/ffadomixer.desktop"

  install -Dm644 support/xdg/hi64-apps-ffado.png \
	      "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
