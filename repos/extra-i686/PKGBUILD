# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=racket
pkgver=5.0.2
pkgrel=2
pkgdesc="PLT Racket (a variant of Scheme) Environment"
arch=('i686' 'x86_64')
url="http://racket-lang.org/"
license=('LGPL')
depends=('gcc-libs' 'cairo' 'desktop-file-utils' 'libjpeg' 'libgl' 'libxmu' 'libxft')
makedepends=('mesa' 'libxaw')
provides=('drscheme')
conflicts=('drscheme')
replaces=('drscheme')
options=('!strip')
install=racket.install
source=(http://download.racket-lang.org/installers/${pkgver}/racket/${pkgname}-${pkgver}-src-unix.tgz \
        http://downloads.sourceforge.net/sourceforge/libpng/libpng-1.4.5.tar.xz drracket.desktop)
md5sums=('21df9a2f9709026d8b3d040ba72deafc' '37d420c7d8294aac438c150bb4335901'\
         '9223c9736ead677431542b41271c7b3c')
sha1sums=('e741630f6b524ef32a13b7df842592ccfe8d39c9' 'c31578e005090280203cb3a6080e44f38c3f1a51'\
         'a20808f6b250225704856f82a544681a962a299d')

build() {
# Updating libpng source included in racket source tarball
  cd "${srcdir}/libpng-1.4.5/"
  cp pngmem.c pngpriv.h png.h pngconf.h pngwutil.c pngpread.c pngread.c pngrutil.c png.c \
     pngwrite.c pngwtran.c pngrtran.c pngset.c pngtrans.c pngget.c pngrio.c \
     "${srcdir}/${pkgname}-${pkgver}/collects/plot/src/all"
  rm "${srcdir}/${pkgname}-${pkgver}/collects/plot/src/all/"{pnggccrd.c,pngvcrd.c}

  cd "${srcdir}/${pkgname}-${pkgver}/src"
  rm -r wxcommon/{jpeg,libpng,zlib}
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../collects/icons/plt.xpm "${pkgdir}/usr/share/pixmaps/drracket.xpm"
  install -D -m644 "${srcdir}/drracket.desktop" "${pkgdir}/usr/share/applications/drracket.desktop"
}
