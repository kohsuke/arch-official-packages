# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=2.2.7
pkgrel=2
pkgdesc="A personal and small-business financial-accounting application"
arch=(i686 x86_64)
url="http://www.gnucash.org"
license=("GPL")
depends=('gtkhtml' 'slib' 'goffice>=0.6.5' 'guile')
makedepends=('texinfo' 'intltool' 'iso-codes') #remove iso-codes when FS#12081 is closed
optdepends=('evince: for print preview')
options=('!libtool' '!makeflags')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('6ffb49df7b1a2a83538ccf5bb543b5ae')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man --sysconfdir=/etc
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  install -d -m755 ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas

  rm $pkgdir/usr/share/info/dir
}
