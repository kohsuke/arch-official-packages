# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=minicom
pkgver=2.5
pkgrel=2
pkgdesc="A serial communication program"
arch=('i686'  'x86_64')
url="http://alioth.debian.org/projects/minicom/"
license=('GPL')
depends=('bash' 'ncurses')
backup=('etc/minirc.dfl')
source=("http://alioth.debian.org/frs/download.php/3487/${pkgname}-${pkgver}.tar.gz")
md5sums=('a5117d4d21e2c9e825edb586ee2fe8d2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
  install -Dm644 doc/minirc.dfl ${pkgdir}/etc/minirc.dfl
}
