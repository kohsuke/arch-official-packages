# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
pkgname=libunique3
pkgver=3.0.2
pkgrel=1
pkgdesc="Library for writing single instance applications for GTK3"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3')
makedepends=('gtk-doc' 'gobject-introspection')
options=('!libtool')
url="http://live.gnome.org/LibUnique"
source=(http://ftp.gnome.org/pub/gnome/sources/libunique/${pkgver%.*}/libunique-${pkgver}.tar.xz)
sha256sums=('a8f02ce073e2b920cca8ac45d418e7cb64438ad0814780c5912c6d63f8a4e038')

build() {
  cd "${srcdir}/libunique-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-dbus
  make
}

package() {
  cd "${srcdir}/libunique-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
