# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=watchdog
pkgver=5.12
pkgrel=2
pkgdesc="Watchdog daemon"
arch=(i686 x86_64)
url="http://sourceforge.net/projects/watchdog"
license=('GPL')
depends=('glibc')
backup=(etc/watchdog.conf
        etc/conf.d/watchdog
        etc/conf.d/wd_keepalive)
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        watchdog.sh
        watchdog.cf
        watchdog-5.12-fix-oom-in-wd_keepalive.patch
        watchdog.systemd)
md5sums=('cea28bea70e54f3625062bc808aef9af'
         'f57d33967eb9404dd52e3c4e73795a20'
         '01c1e2e13f515131180b4ebe9b8b7cc3'
         'c90c2686975b5254f82b3de683f1f442'
         'a835ae5cbdbb21c19a468dedb5d734e4')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -p1 -i "${srcdir}"/watchdog-5.12-fix-oom-in-wd_keepalive.patch
  ./configure \
	--prefix=/usr \
	--mandir=/usr/share/man \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-pidfile=/run/watchdog.pid \
	--with-ka_pidfile=/run/wd_keepalive.pid
  
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make install DESTDIR="${pkgdir}"
  for fil in watchdog wd_keepalive ; do
	install -D -m755 "${srcdir}"/watchdog.sh "${pkgdir}"/etc/rc.d/${fil}
	install -D -m644 "${srcdir}"/watchdog.cf "${pkgdir}"/etc/conf.d/${fil}
  done
  install -D -m644 "${srcdir}"/watchdog.systemd "${pkgdir}"/usr/lib/systemd/system/watchdog.service

  sed -i 's/watchdog/wd_keepalive/g' "${pkgdir}"/etc/rc.d/wd_keepalive
  sed -i 's/watchdog/wd_keepalive/g' "${pkgdir}"/etc/conf.d/wd_keepalive
  sed -i 's/Watchdog/Simple Watchdog/' "${pkgdir}"/etc/rc.d/wd_keepalive
}
