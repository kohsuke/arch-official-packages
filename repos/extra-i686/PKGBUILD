# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=sox
pkgver=14.4.0
pkgrel=2
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('i686' 'x86_64')
url="http://sox.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('libltdl' 'file' 'libsndfile' 'libpng' 'lame' 'opencore-amr' 'gsm')
makedepends=('ffmpeg' 'libao' 'libmad' 'libid3tag' 'wavpack' 'libpulse')
optdepends=('libao: for ao plugin'
            'ffmpeg: for ffmpeg plugin'
            'libmad: for mp3 plugin'
            'libid3tag: for mp3 plugin'
            'wavpack: for wavpack plugin'
            'libpulse: for pulse plugin')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        sox_default_audio_driver_fallback.patch)
sha1sums=('12dfc57e503ed4e18cedcac292cdae7982281978'
          'ab304f1fc17269262dbc4977aa9b7e97ec805ae4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "${srcdir}/sox_default_audio_driver_fallback.patch"
  sed -i 's|man1/sox.1 soxeffect.7|man1/sox.1.gz soxeffect.7.gz|' Makefile.in
  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-dyn-default --with-distro="Arch Linux"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
