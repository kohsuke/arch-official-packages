# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=2.7.9
pkgrel=2
pkgdesc='Input method server supporting various input methods'
url='http://hyperrate.com/dir.php?eid=67'
license=('LGPL')
arch=('i686' 'x86_64')
depends=('gtk2' 'libxtst')
makedepends=('qt' 'gtk3' 'anthy' 'libchewing')
optdepends=('qt: support for qt4 input method'
            'gtk3: support for gtk3 input method'
            'anthy: support for anthy input method'
            'libchewing: support for chewing input method')
source=("http://www.csie.nctu.edu.tw/~cp76/gcin/download/${pkgname}-${pkgver}.tar.xz"
        'qt4.patch')
sha1sums=('bd0375188a0d8a74efece0e23f30c16cd4a2f1df'
          '1c6cdcaa218615e53869bb9d67d7f9d2141e86fe')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../qt4.patch
	sed '/include suffixes-rule/a \
		CFLAGS+='"${CFLAGS}"' \
		LDFLAGS+='"${LDFLAGS}"' \
		OPTFLAGS=' \
		-i Makefile

	./configure --prefix=/usr --use_i18n=Y
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
