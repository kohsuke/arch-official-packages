# $Id: PKGBUILD 73980 2012-07-18 00:34:26Z speps $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=suil
pkgver=0.6.10
pkgrel=2
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=(i686 x86_64)
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('lv2')
makedepends=('python2' 'qt4' 'gtk2')
optdepends=('qt4: Qt UIs wrapping support'
            'gtk2: Gtk2 UIs wrapping support')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('735e3cc4d539059cf3948d3dba75807e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  python2 ./waf configure --prefix=/usr \
                          --mandir=/usr/share/man
  python2 ./waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir" python2 ./waf install

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
