# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=rdesktop
pkgver=1.7.0
pkgrel=2
depends=('libx11' 'openssl' 'libao' 'libsamplerate')
pkgdesc="An open source client for Windows Remote Desktop Services"
url="http://www.rdesktop.org/"
license=('GPL')
arch=('i686' 'x86_64')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'rdesktop-send_physical_buttons.diff' 'rdesktop-libao.patch')
md5sums=('77fa93b21aa38837a85e81c00e757228'
         '880d3aeac67b901e6bf44d1323374768'
         'bd2c9bc68bddcc2652c668753d787df7')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # FS#15113
  patch -i ${srcdir}/rdesktop-send_physical_buttons.diff

  # Fix libao segfault, from Fedora
  patch -i ${srcdir}/rdesktop-libao.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
