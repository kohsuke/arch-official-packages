# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=monodevelop
pkgver=2.4
pkgrel=1
pkgdesc="An IDE primarily designed for C# and other .NET languages"
arch=('i686' 'x86_64')
url="http://www.monodevelop.org"
license=('GPL')
depends=('mono>=2.6.3' 'mono-addins>=0.4' 'gnome-sharp' 'gecko-sharp-2.0>=0.10' 'gnome-desktop-sharp')
options=(!makeflags force)
install=monodevelop.install
source=(http://ftp.novell.com/pub/mono/sources/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('14deccd526d640cd38482f7ce7c0cb41')

build() {
  export MONO_SHARED_DIR=$srcdir/src/.wabi
  mkdir -p $MONO_SHARED_DIR

  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --disable-update-mimedb --disable-update-desktopdb
  make || return 1
  make DESTDIR=$pkgdir install

  rm -r $MONO_SHARED_DIR
}

