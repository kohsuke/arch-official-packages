# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qtiplot
pkgver=0.9.7
pkgrel=1
pkgdesc="Data analysis and scientific plotting - free clone of Origin"
arch=("i686" "x86_64")
url="http://soft.proindependent.com/qtiplot.html"
depends=('qwtplot3d>=0.2.7' 'muparser>=1.28' 'gsl' 'liborigin>=20070926' 'python>=2.5.1' 'pyqt' 'sip')
license=("GPL2")
source=("http://soft.proindependent.com/src/qtiplot-$pkgver.tar.bz2" \
	    "$pkgname.desktop" \
        "$pkgname.png" \
        "qtiplot-archlinux.pro"
	"qtiplot-gcc4.3.patch")
md5sums=('1a5c13bcaa81b15c2d152f2d47c04a5f'
         '56bd53f4f1367c285086acb969f13348'
         'ad8affbd6f0d5cbdcde46c923ee2668a'
         '7a2965c3c1a8e6c10ff1d5ba3e9326b6'
         '741f81202c63055e824e4bd51895827a')

build() {
  cd $srcdir
  patch -Np0 < $srcdir/qtiplot-gcc4.3.patch

  cp -f qtiplot-archlinux.pro \
        $srcdir/$pkgname-$pkgver/$pkgname/qtiplot.pro || return 1

  cd $srcdir/$pkgname-$pkgver/$pkgname
  export QMAKESPEC=linux-g++

  qmake qtiplot.pro || return 1
  make QTDIR=/usr/ QMAKESPEC=linux-g++ || return 1
  install -D $srcdir/$pkgname-$pkgver/$pkgname/$pkgname \
	$pkgdir/usr/bin/$pkgname || return 1

  # make it nice:
  install -D -m644 $srcdir/$pkgname.png \
  	$pkgdir/usr/share/pixmaps/$pkgname.png
  install -D -m644 $srcdir/$pkgname.desktop \
  	$pkgdir/usr/share/applications/$pkgname.desktop

# install wordlist for autocompletion
 install -Dm644 $srcdir/$pkgname-$pkgver/$pkgname/qti_wordlist.txt \
	$pkgdir/usr/bin/qti_wordlist.txt
}

