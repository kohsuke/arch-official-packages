#$Id$
#Maintainer: Thayer Williams <thayer@archlinux.org>

pkgname=pm-utils
pkgver=1.2.2.1
pkgrel=2
pkgdesc="Utilities and scripts for power management"
arch=('i686' 'x86_64')
url="http://pm-utils.freedesktop.org"
license=('GPL')
depends=('glibc' 'vbetool' 'procps')
makedepends=('xmlto')
source=(http://pm-utils.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
	service.patch
	11netcfg)
md5sums=('dcc7bde623d0bae207158e153b7394a8'
         'bf6560934c4efa79143c91dcca5a07b8'
         '9839687c8787d99fd4ff36aa3238c27b')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np0 -i $srcdir/service.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  # install additional hooks
  install -m755 $srcdir/11netcfg $pkgdir/usr/lib/pm-utils/sleep.d/11netcfg || return 1
}

