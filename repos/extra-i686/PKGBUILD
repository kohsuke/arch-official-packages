# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=transfig
pkgver=3.2.5d
pkgrel=1
pkgdesc="Format conversion utility that can be used with xfig"
arch=('i686' 'x86_64')
url="http://www.xfig.org"
license=('custom')
depends=('libpng' 'libxpm')
makedepends=('imake')
source=(http://downloads.sourceforge.net/mcj/${pkgname}.${pkgver}.tar.gz LICENSE \
        transfig-3.2.5d-libpng-1.5.patch)
sha1sums=('90ff277cc9b3fa0d0313052fcf5e3ffad8652abc'
          'b8b712871615308b8b6add92f86d218437d652f2'
          '9f9f332b0b31e58e59542bc1568df9617b71710a')

build() {
  cd "${srcdir}/${pkgname}.${pkgver}"
  sed -i 's/XCOMM USELATEX2E = -DLATEX2E/USELATEX2E = -DLATEX2E/' transfig/Imakefile
  sed -i 's/XCOMM USEINLINE = -DUSE_INLINE/USEINLINE = -DUSE_INLINE/' fig2dev/Imakefile
  patch -p1 -i ../transfig-3.2.5d-libpng-1.5.patch

  xmkmf
  make FIG2DEV_LIBDIR=/usr/share/fig2dev Makefiles
  make FIG2DEV_LIBDIR=/usr/share/fig2dev XFIGLIBDIR=/usr/share/xfig
}

package() {
  cd "${srcdir}/${pkgname}.${pkgver}"

  make DESTDIR="${pkgdir}" XFIGLIBDIR=/usr/share/xfig \
    FIG2DEV_LIBDIR=/usr/share/fig2dev MANPATH=/usr/share/man \
    install install.man
  install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
