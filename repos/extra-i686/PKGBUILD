# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.3.3
pkgrel=2
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('qt' 'shared-mime-info' 'hal' 'xz-utils' 'heimdal'
	 'enchant' 'jasper' 'openexr' 'strigi>=0.7.0' 'libxtst'
	 'giflib' 'soprano>=2.3.0' 'ca-certificates' 'xdg-utils')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl' 'hspell' 'libxslt')
replaces=('arts')
install='kdelibs.install'
source=(http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2
        'kde-applications-menu.patch' 'archlinux-menu.patch' 'abs-syntax-highlight.patch'
        'CVE-2009-0689.patch'
	'fix-glibc-2.11.patch')
md5sums=('20fd3793d9d23088ecb1d5aed0254216'
         '280f34ee159845f8902c31bd499254fc'
         '0f214b222bfb0327e7a2b6fb13756895'
         '18ea42696a7f41332a092d6ead7efc6a'
         'ec70af24f769f17082a9ab69dc1e24e9'
	 'ed737b0b9d14f6f13d150121f088290c')

build() {
	cd $srcdir/${pkgname}-${pkgver}

	# see http://securityreason.com/it_news/0/0x31
	# will be fixed in 4.3.4
	patch -p0 -i $srcdir/CVE-2009-0689.patch || return 1
	# see https://bugs.kde.org/show_bug.cgi?id=213223
	patch -p0 -i ${srcdir}/fix-glibc-2.11.patch || return 1
	
	# avoid file conflict with gnome-menu
	patch -p1 -i $srcdir/kde-applications-menu.patch
	# add Archlinux menu entry
	patch -p1 -i $srcdir/archlinux-menu.patch
	# add syntax highlightning for PKGBUILD and .install files
	patch -p1 -i $srcdir/abs-syntax-highlight.patch

	cd ..
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DKDE_DISTRIBUTION_TEXT='Arch Linux' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install

	# cert bundle seems to be hardcoded
	# link it to the one from ca-certificates
	rm -f $pkgdir/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/usr/share/apps/kssl/ca-bundle.crt
}
