# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-exchange
pkgver=3.4.3
pkgrel=1
pkgdesc="Ximian Connector Exchange plugin for Evolution"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.ximian.com"
depends=('evolution-data-server' 'gtkhtml4' 'gnome-desktop')
makedepends=('intltool' 'evolution')
options=('!libtool' '!emptydirs')
install=evolution-exchange.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('28b2b47bfdae097b13722dfd0aae6e1335de8d98bd10534a3c7675489352f7b7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/evolution --disable-static \
      --with-libdb=/usr --with-krb5=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" --domain evolution-exchange-3.2 "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f $pkgdir/etc/gconf/schemas/*.schemas
}
