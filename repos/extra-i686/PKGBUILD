# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=xfig
pkgver=3.2.5
pkgrel=3
pkgdesc="Xfig drawing tool"
arch=('i686' 'x86_64')
url="http://www.xfig.org"
license=('custom')
depends=('libpng' 'libjpeg>=7' 'xaw3d>=1.5E' 'libxi' 'desktop-file-utils')
makedepends=('imake')
optdepends=('transfig: to use the frontend to convert fig files')
options=('!makeflags' '!emptydirs')
install=xfig.install
source=(http://downloads.sourceforge.net/mcj/${pkgname}.${pkgver}.full.tar.gz \
	xfig-3.2.4-redhat.patch xfig-3.2.5-color-resources.patch \
        xfig-3.2.5-enable-Xaw3d.patch \
        xfig-3.2.5-fhs.patch xfig-3.2.5-missing-protos.patch \
        xfig-3.2.5-mkstemp.diff xfig-3.2.5-modepanel.patch \
        xfig-3.2.5-quiet.patch xfig-3.2.5-urwfonts.patch \
        xfig-3.2.5-zoom-crash.patch xfig.3.2.5-Xaw3d.patch \
        xfig.3.2.5-modularX.patch LICENSE xfig.desktop xfig.png)
md5sums=('fae0c67a3951bd41c057deb63b6aa47a' 'eddf54ba411da92c2b58aef8169ffddd'\
         'af171656320c7d30d381c5ceb799b0f6' '00985b3ef840171a1910a48ed1acf7e2'\
         'b17069a48c157b6afbd74cfae21e7f73' '29fe2a11f3520e8c5c3330c87eb52e74'\
         'e8ed46711a875701037c2c83d56efb6e' 'ed8ff51f215d99046d5c1e83505fbf24'\
         'a77e18752385fb9e637c1d18bf6c33bc' 'c5b7dc054063f02c6bfcd132dae82b6f'\
         '21afcf387f76f756abbb11e6544a2d2f' '8689364e6b18c7f6b1afcb3afb7b3d30'\
         'a9e001daf43022e9453504933ad28d49' 'f405b7a96f3401c12153fe6170a7d8f1'\
         '8ac2de637c9148aa4a32978910822230' '8c99ca12404ed300c77106458c7340c0')
sha1sums=('ce09d1a74dbddf0bc6b20fe9c558db584fbfed17'
          'cfe1c3c7864045966792d81fbcb03dd6dfc62ccb'
          'bb33f4ded24ab6167fc253bc1add9d8cd2da43bb'
          '7555cda41f7b14958be6fa6402c48b0489271864'
          '95413aa929341349786561aa91f57b15d06c4257'
          '1fb2efd7cb3a00e57c6b9614ac77dc8d04f50ab2'
          '7af8464d143e35f9fac7254fe6a18717d6cfd24a'
          'fd18fad3d8768a6d9736a9fac88de4abaaa5b79d'
          'a9a8e5fe88d8235e66adfc6e27a6e1d72f40d6f3'
          'e595ccdbe293ea0fdecbf59a2192ae57ced2c8eb'
          'a5ad69bb7b03f023ef502bdad18971b565a55ddd'
          'c82e6acab6af3d68d8421657aec889acb733e68e'
          '41640f6fe0ef90bf5ec417e84f3a041e3b51cffb'
          '31edf4cfab708820ea3f114d095dfef5aa88e5aa'
          '9011b8fa6143e37525b28cc9800a3bf2d6b008d2'
          '8b7554cfac560186e4cc6318e078c988bf228ab1')

build() {
  cd "${srcdir}/${pkgname}.${pkgver}"
  patch -p1 < ../xfig-3.2.4-redhat.patch || return 1
  patch -p1 < ../xfig-3.2.5-fhs.patch || return 1
  patch -p1 < ../xfig-3.2.5-mkstemp.diff || return 1
  patch -p1 < ../xfig.3.2.5-modularX.patch || return 1
  patch -p1 < ../xfig.3.2.5-Xaw3d.patch || return 1
  patch -p1 < ../xfig-3.2.5-enable-Xaw3d.patch || return 1
  patch -p1 < ../xfig-3.2.5-color-resources.patch || return 1
  patch -p1 < ../xfig-3.2.5-quiet.patch || return 1
  patch -p1 < ../xfig-3.2.5-urwfonts.patch || return 1
  patch -p1 < ../xfig-3.2.5-zoom-crash.patch || return 1
  patch -p1 < ../xfig-3.2.5-missing-protos.patch || return 1
  patch -p1 < ../xfig-3.2.5-modepanel.patch || return 1

  xmkmf || return 1
  make DOCDIR=/usr/share/doc || return 1
  make DESTDIR="${pkgdir}" DOCDIR=/usr/share/doc install.all || return 1

  rm "${pkgdir}/usr/lib/X11/app-defaults"
  chmod 755  "${pkgdir}"/usr/share/xfig/Libraries/Fasteners/{,Bolts,Machinescrews,Springs,Woodscrews} || return 1

  install -D -m644 ../xfig.desktop "${pkgdir}/usr/share/applications/xfig.desktop" || return 1
  install -D -m644 ../xfig.png  "${pkgdir}/usr/share/pixmaps/xfig.png" || return 1
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1	
}
