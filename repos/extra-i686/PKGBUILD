# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=amarok
replaces=('amarok-base' 'amarok-engine-xine' 'amarok-base-mysqlfree')
pkgver=2.2.1
pkgrel=2
pkgdesc="A media player for KDE"
arch=("i686" "x86_64")
url="http://amarok.kde.org"
license=('GPL2' 'LGPL2' 'FDL')
depends=('mysql>=5.1.41' 'kdebase-runtime>=4.3' 'libmtp>=0.3.7' 'libgpod>=0.7.2'
	'qtscriptgenerator>=0.1.0' 'taglib-extras>=1.0.1' 'liblastfm>=0.3.0'
	'loudmouth>=1.4.3' 'qca')
makedepends=('pkgconfig' 'automoc4' 'cmake')
install=amarok.install
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
	'fix-random-with-repeat.patch')
md5sums=('4f6f45f0c25f85f2a2a1fff169ac7193'
         '4932796051747035e67a6ff68b4ea561')

build() {
   cd ${srcdir}/${pkgname}-${pkgver}
   patch -Np1 -i ${srcdir}/fix-random-with-repeat.patch || return 1

   cd ${srcdir}
   mkdir build
   cd build
   cmake ../${pkgname}-${pkgver} \
           -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_SKIP_RPATH=ON \
           -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
           -DCMAKE_INSTALL_PREFIX=/usr
   make || return 1
   make DESTDIR=${pkgdir} install || return 1
}
