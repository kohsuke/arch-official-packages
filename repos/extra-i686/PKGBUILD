# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gstreamer0.10-bad
pkgname=('gstreamer0.10-bad' 'gstreamer0.10-bad-plugins')
pkgver=0.10.19
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL' 'GPL')
makedepends=('pkgconfig' 'gstreamer0.10-base>=0.10.29' 'xvidcore>=1.2.2' 'libdca>=0.0.5' 'bzip2' 'libdc1394>=2.1.2' 'exempi>=2.1.1' 'jack>=0.118.0' 'neon>=0.29.3' 'faac>=1.28' 'libexif>=0.6.19' 'musicbrainz>=2.1.5' 'faad2>=2.7' 'libmms>=0.6' 'libcdaudio>=0.99.12' 'libmpcdec>=1.2.6' 'mjpegtools>=1.9.0' 'libdvdnav>=4.1.3' 'libmodplug>=0.8.8.1' 'jasper>=1.900.1' 'liblrdf>=0.4.0' 'libofa>=0.9.3' 'soundtouch>=1.5.0' 'libvdpau>=0.4' 'schroedinger>=1.0.9' 'libass>=0.9.9' 'libvpx>=0.9.1')
url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-bad/gst-plugins-bad-${pkgver}.tar.bz2)
md5sums=('368f72e2a1c7a6e8fe60394e4315259b')

build() {
  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Bad Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" || return 1

  make || return 1
  sed -e 's/gst sys ext/gst/' -i Makefile
}

package_gstreamer0.10-bad() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugin libraries (gst-plugins-bad)"
  depends=('gstreamer0.10-base>=0.10.29')
  groups=('gstreamer0.10-plugins')

  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_gstreamer0.10-bad-plugins() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugins (gst-plugins-bad)"
  depends=("gstreamer0.10-bad=${pkgver}" 'xvidcore>=1.2.2' 'libdca>=0.0.5' 'bzip2' 'libdc1394>=2.1.2' 'exempi>=2.1.1' 'jack>=0.118.0' 'neon>=0.29.3' 'faac>=1.28' 'libexif>=0.6.19' 'musicbrainz>=2.1.5' 'faad2>=2.7' 'libmms>=0.6' 'libcdaudio>=0.99.12' 'libmpcdec>=1.2.6' 'mjpegtools>=1.9.0' 'libdvdnav>=4.1.3' 'libmodplug>=0.8.8.1' 'jasper>=1.900.1' 'liblrdf>=0.4.0' 'libofa>=0.9.3' 'libvdpau>=0.4' 'soundtouch>=1.5.0' 'libass>=0.9.9' 'schroedinger>=1.0.9' 'libvpx>=0.9.1')
  groups=('gstreamer0.10-plugins')

  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  make -C gst-libs DESTDIR="${pkgdir}" install
  make -C ext DESTDIR="${pkgdir}" install
  make -C sys DESTDIR="${pkgdir}" install
  make -C gst-libs DESTDIR="${pkgdir}" uninstall
}
