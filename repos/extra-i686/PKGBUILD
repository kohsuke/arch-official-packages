# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor : shining <shiningxc.at.gmail.com>
# Contributor : cyberdune <cyberdune@gmail.com>

pkgname=octave
pkgver=3.4.0
pkgrel=2
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('i686' 'x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw' 'pcre' 'curl' 'lapack' 'libx11' 'graphicsmagick' 'glpk' 'hdf5' 'gcc-libs' 'qhull')
makedepends=('texinfo' 'graphicsmagick' 'gcc-fortran' 'umfpack')
optdepends=('texinfo: for help-support in octave'
            'gnuplot: alternative plotting'
	    'umfpack: LU decomposition of some large sparse matrices')
source=("ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.bz2"
	octave-3.4.0-gcc46.patch)
options=('!emptydirs')
#install=octave.install
sha1sums=('936a8fc962abd96e7568fb5909ec2a4d7997a1a8'
          '791c905a80510783e5f9c556c12f02400887fbec')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fedora gcc46 patch
  patch -Np1 -i ${srcdir}/octave-3.4.0-gcc46.patch

  # http://www.nabble.com/Random-rounding-errors-td16010966.html
  FFLAGS="-O -ffloat-store" \

  ./configure --prefix=/usr --libexecdir=/usr/lib \
  --enable-shared --disable-static --disable-docs

  make
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
