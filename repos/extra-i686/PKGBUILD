# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fbida
pkgver=2.08
pkgrel=1
pkgdesc="Few applications to display and elementary edit images: fbi, fbgs, ida, exiftran"
arch=('i686' 'x86_64')
url="http://www.kraxel.org/blog/linux/fbida/"
license=('GPL2')
depends=('giflib' 'libtiff' 'libexif' 'lesstif' 'libpng' 'fontconfig')
makedepends=('libxpm')
optdepends=('ghostscript: to use fbgs')
source=(http://www.kraxel.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('9b3693ab26a58194e36b479bffb61ed0')
sha1sums=('597e0953c68112bf7c363b1ccaa94e72100b116a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/ungif/gif/' GNUmakefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" prefix=/usr install
}
