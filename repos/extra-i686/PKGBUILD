# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem-plparser
pkgver=3.4.3
pkgrel=1
url="http://www.gnome.org"
pkgdesc="Totem playlist parser library"
license=('LGPL')
arch=(i686 x86_64)
depends=('gmime' 'libsoup-gnome' 'libarchive' 'libquvi')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/totem-pl-parser/${pkgver%.*}/totem-pl-parser-$pkgver.tar.xz)
sha256sums=('ab8beed4dd5d3a5d7ed5dc8b357e94dc03a8553fef6e33c2e7fb63a76e44c42b')

build() {
  cd totem-pl-parser-$pkgver
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static
  make
}

package(){
  cd totem-pl-parser-$pkgver
  make DESTDIR="$pkgdir" install
}
