# $Id$
# Maintainer: damir <damir@archlinux.org>

pkgname=dasher
pkgver=4.11
pkgrel=1
pkgdesc="Information-efficient text-entry interface, driven by natural continuous pointing gestures"
arch=('i686' 'x86_64')
license=('GPL')
depends=('at-spi' 'gnome-speech' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils' 'libgnomeui')
url="http://www.gnome.org"
options=('!emptydirs')
install=dasher.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/4.11/${pkgname}-${pkgver}.tar.bz2
        error-format-security.patch
        fix-binutils-gold.patch)
sha256sums=('fb21cc4ef606e10a8e5deffebe8695a376eabb0766d7d1bdf80abc5f03f4fed2'
            '7aeba2667972e408f38b8337f972ff589f05cdf244dec5674f138fc5110405df'
            'f9e2a661e6d0b873e9a5d491b1b70f3a05ea4edda693e078e2e206451c265daf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/error-format-security.patch"
  patch -Np1 -i "${srcdir}/fix-binutils-gold.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper --enable-speech
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain dasher ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
