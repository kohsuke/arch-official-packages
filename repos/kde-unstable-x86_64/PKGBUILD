# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-svgpart
pkgver=4.9.0
pkgrel=1
pkgdesc="A KPart for viewving SVGs"
url="https://projects.kde.org/projects/kde/kdegraphics/svgpart"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/stable/${pkgver}/src/svgpart-${pkgver}.tar.xz")
sha1sums=('e66323aef77e08d3d7faee0fab8a7a69a1708489')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../svgpart-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
