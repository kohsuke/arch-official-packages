# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kgamma
pkgver=4.7.97
pkgrel=1
pkgdesc="A monitor calibration tool"
url="http://kde.org/applications/graphics/kgamma/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
# note on libxxf86vm:
#       not detected by namcap because libgl depends on it
#       but nvidia providing libgl does not depend on libxxf86vm
depends=('kdebase-runtime' 'libxxf86vm')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/kgamma-${pkgver}.tar.bz2")
sha1sums=('0c96b1ede6e52fae1984958e41e1b66756a1d08e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kgamma-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
