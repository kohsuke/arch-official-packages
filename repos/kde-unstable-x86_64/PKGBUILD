# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kopete
pkgver=4.10.80
pkgrel=1
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-runtime' 'kdepimlibs' 'qca-ossl' 'libotr3' 'libmsn' 'libidn'
         'qimageblitz' 'libgadu' 'mediastreamer')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kopete-${pkgver}.tar.xz"
        'libotr3.patch')
sha1sums=('ee9fccbd612f821f5c1a02a0ec107b4faf4cc94c'
          '9c3b0ee15538fbfa36aa0a4748b1f6b5a7905384')

prepare() {
  cd kopete-${pkgver}
  patch -p2 -i "${srcdir}"/libotr3.patch
}

build() {
  mkdir build
  cd build
  cmake ../kopete-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
