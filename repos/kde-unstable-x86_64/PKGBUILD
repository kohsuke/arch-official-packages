# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-killbots
pkgver=4.9.90
pkgrel=1
pkgdesc="A simple game of evading killer robots"
url="http://kde.org/applications/games/killbots/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/killbots-${pkgver}.tar.xz")
sha1sums=('82f2c9e294b371a439f6e550cdc3e9eeffc28993')

build() {
  mkdir build
  cd build
  cmake ../killbots-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
