# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kolf
pkgver=4.9.80
pkgrel=1
pkgdesc="A miniature golf game with 2d top-down view"
url="http://kde.org/applications/games/kolf/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kolf-${pkgver}.tar.xz")
sha1sums=('4083457d9d3331e554b3ea391bcd37ce7eea973f')

build() {
  mkdir build
  cd build
  cmake ../kolf-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
