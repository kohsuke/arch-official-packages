# $Id: PKGBUILD 17562 2008-10-31 03:05:49Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.1.85
pkgrel=1
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('libxcursor' 'phonon' 'shared-mime-info' 'qt>=4.4' 'libxpm' 'hal' 'perl'
	 'enchant' 'jasper' 'openexr' 'strigi' 'bzip2' 'libxslt' 'libxtst'
	 'giflib' 'soprano' 'ca-certificates' 'heimdal' 'xdg-utils' 'libxft')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl' 'hspell')
replaces=('arts')
options=('docs')
install='kdelibs.install'
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kde-applications-menu.patch' 'archlinux-menu.patch')
md5sums=('312597e801622e4b0e9208d466ec1b98'
         'e94450ba5430ea9c1e33bad9ae38ca2d'
         '0ad42200df2d2b8aab01d2ef4b21e02f')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	# avoid file conflict with gnome-menu
	patch -p1 -i $srcdir/kde-applications-menu.patch || return 1
	# add Archlinux menu entry
	patch -p0 -i $srcdir/archlinux-menu.patch || return 1
	cd ..
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_DISTRIBUTION_TEXT='Arch Linux' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF
	make
	make DESTDIR=$pkgdir install

	# cert bundle seems to be hardcoded
	# link it to the one from ca-certificates
	rm -f $pkgdir/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/usr/share/apps/kssl/ca-bundle.crt
}
