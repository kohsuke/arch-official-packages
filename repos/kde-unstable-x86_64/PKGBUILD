# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kstars
pkgver=4.9.95
pkgrel=1
pkgdesc="Desktop Planetarium"
url="http://kde.org/applications/education/kstars/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'wcslib' 'libindi')
makedepends=('cmake' 'automoc4' 'eigen' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kstars-${pkgver}.tar.xz")
sha1sums=('46eed98749a15b29b4a18bc93c2c1c71f1a6ca7d')

build() {
  mkdir build
  cd build
  cmake ../kstars-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Xplanet=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
