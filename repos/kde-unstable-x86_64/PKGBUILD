# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-print-manager
pkgver=4.9.90
pkgrel=1
pkgdesc="A tool for managing print jobs and printers"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdeutils/print-manager'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-workspace' 'libcups' 'system-config-printer')
makedepends=('cmake' 'automoc4')
replaces=('print-manager' 'kdeutils-printer-applet' 'kdeadmin-system-config-printer-kde')
conflicts=('print-manager')
groups=('kde' 'kdeutils')
source=("http://download.kde.org/unstable/${pkgver}/src/print-manager-${pkgver}.tar.xz")
sha1sums=('9a50b3713f8b80693bf54a01403083a721aab95a')

build() {
  mkdir build
  cd build
  cmake ../print-manager-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
