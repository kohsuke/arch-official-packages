# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=nepomuk-core
pkgver=4.9.95
pkgrel=2
pkgdesc="Contains the central Nepomuk services like file indexing, file system monitoring, query, storage, client libraries"
url="https://projects.kde.org/projects/kde/kdelibs/nepomuk-core"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'poppler-qt' 'taglib' 'ffmpeg')
makedepends=('cmake' 'automoc4' 'doxygen')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'fix-segfault.patch')
sha1sums=('98bee83019e469e28772da24cbec05d7744e6e44'
          'de0588164a4ae3ea89278675564a55e7e78ae4aa')

build() {
  cd ${pkgname}-${pkgver}
  # KDEBUG#312148
  patch -p1 -i "${srcdir}"/fix-segfault.patch
  cd ../

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  # Fix the python shebang
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/nepomuk-simpleresource-rcgen
}
sha1sums=('98bee83019e469e28772da24cbec05d7744e6e44'
          'cfb1420dee81922e13952f81c0dde3450e99cf02')
