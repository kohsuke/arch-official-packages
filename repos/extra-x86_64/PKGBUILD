# $Id $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=polkit-kde
pkgver=0.95.1
pkgrel=2
pkgdesc='KDE PolicyKit Authentication Agent'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
source=("ftp://ftp.kde.org/pub/kde/stable/apps/KDE4.x/admin/${pkgname}-1-${pkgver}.tar.bz2"
        'xdg-path.patch'
	'gcc4.5.patch')
md5sums=('68e40d2a43335cb5876a1c8bc51c461b'
         '84d4873e60c1b628bcbaaf38c7ab2f44'
         '06caf1645510367eb253509240d3c8bd')

build() {
	cd ${srcdir}/${pkgname}-1-${pkgver}
	patch -p0 -i ${srcdir}/xdg-path.patch
	patch -Np4 -i ${srcdir}/gcc4.5.patch

	cd ${srcdir}
	mkdir build
	cd build
	export XDG_CONFIG_DIRS=/etc/xdg
	cmake ../${pkgname}-1-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_SKIP_RPATH=ON
	make
}

package() {
	cd ${srcdir}/build
	make DESTDIR=${pkgdir} install
}
