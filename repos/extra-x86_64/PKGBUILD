# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>

pkgname=virtuoso
pkgver=6.1.5.20120630
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A scalable cross-platform server that combines SQL/RDF/XML Data Management with Web Application Server and Web Services Platform functionality'
url='http://virtuoso.openlinksw.com/wiki/main/Main/'
license=('GPL')
depends=('libldap')
makedepends=('bison' 'flex' 'gperf')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-opensource-${pkgver}.tar.gz")
md5sums=('1364679ffdbc6b0c16eadf6dee0e2676')

build() {
  cd ${srcdir}/${pkgname}-opensource-${pkgver}

  ./autogen.sh
  ./configure --prefix=/usr \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-rendezvous \
    --disable-hslookup \
    --disable-all-vads
  make
}

package() {
  # install server
  cd ${srcdir}/${pkgname}-opensource-${pkgver}/binsrc/virtuoso
  make DESTDIR=${pkgdir} install

  # install driver
  cd ${srcdir}/${pkgname}-opensource-${pkgver}/binsrc/driver
  make DESTDIR=${pkgdir} install

  # Install some useful tools; rename to avoid conflicts
  for bin in isql isqlw; do
    install -Dm755 ${srcdir}/${pkgname}-opensource-${pkgver}/binsrc/tests/$bin \
      ${pkgdir}/usr/bin/$bin-vt
  done
}
