# $Id$
#Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=libcl
pkgver=1.1
_pkgver=290.10
pkgrel=2
pkgdesc="OpenCL library and ICD loader from NVIDIA"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('gcc-libs')
license=('custom')
options=('!strip')

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${_pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${_pkgver}/${_pkg}.run")
    md5sums=('50319a4b3818c12c9c7243525e0e6316')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${_pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${_pkgver}/${_pkg}.run")
    md5sums=('cebfba9a7e91716a06c66bb5b38d9661')
fi
build() {
    cd $srcdir
    sh ${_pkg}.run --extract-only
}

package() {
    cd $srcdir/${_pkg}
    install -D -m755 libOpenCL.so.1.0.0 $pkgdir/usr/lib/libOpenCL.so.1.0.0
    ln -s /usr/lib/libOpenCL.so.1.0.0 $pkgdir/usr/lib/libOpenCL.so.1
    ln -s /usr/lib/libOpenCL.so.1 $pkgdir/usr/lib/libOpenCL.so
    install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
