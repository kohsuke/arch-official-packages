# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=qtchooser
pkgver=26
pkgrel=4
pkgdesc='Wrap the other Qt tools by searching for different instances of Qt on the system'
arch=('i686' 'x86_64')
url='http://qt.gitorious.org/qt/qtchooser'
license=('GPL')
depends=('gcc-libs')
source=("http://macieira.org/${pkgname}/${pkgname}-${pkgver}-g97962d2.tar.gz"
        {3,4,5}.conf
        'give-home-configs-precedence.patch')
md5sums=('a188a5cb63113130e0d05fe4d5bf4e95'
         '7c5da2b7911d1c4fabc63d6ef00f0020'
         'aa4d49b269e1b806f5eea170801f0aa6'
         'cf83f09a9a5a44d898b93f624d74dd11'
         '13478e4685804c9f79e824b4220bbc2c')

prepare() {
  cd ${pkgname}-${pkgver}-g97962d2

  patch -p1 -i "${srcdir}"/give-home-configs-precedence.patch
}

build() {
  cd ${pkgname}-${pkgver}-g97962d2
  make
}

package() {
  cd ${pkgname}-${pkgver}-g97962d2
  make INSTALL_ROOT="${pkgdir}" install

  install -d "${pkgdir}"/etc/xdg/qtchooser
  install -m644 "${srcdir}"/{3,4,5}.conf \
    "${pkgdir}"/etc/xdg/qtchooser/
  
  # Set the default Qt
  ln -s /etc/xdg/qtchooser/5.conf \
    "${pkgdir}"/etc/xdg/qtchooser/default.conf
}
