# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-games
pkgver=2.30.2
pkgrel=1
pkgdesc="Some Games for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsm' 'gconf>=2.28.1' 'guile>=1.8.7' 'librsvg>=2.26.3' 'filesystem>=2010.02' 'desktop-file-utils' 'gnome-python-desktop>=2.30.2' 'libcanberra>=0.23' 'clutter-gtk>=0.10.4' 'hicolor-icon-theme' 'seed>=2.30.0' 'gir-repository')
makedepends=('pkg-config' 'gnome-doc-utils>=0.20.1' 'intltool' 'glproto')
optdepends=('python-gtkglext: glchess 3D mode')
provides=('glchess' 'gnome-sudoku')
conflicts=('glchess' 'gnome-sudoku')
options=('!emptydirs' '!libtool')
install=gnome-games.install
url="http://www.gnome.org"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('57c3f3685cdd49016dfbf236f4a1d98c9d669d456fabe0aeb6f5a4128dba3586')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper \
      --disable-static \
      --with-scores-user=root --with-scores-group=games \
      --enable-introspection=yes
  make
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  # Remove all scores, we generate them from postinstall
  rm -rf "${pkgdir}/var"

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-games ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
