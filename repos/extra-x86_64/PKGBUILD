# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity
pkgver=1.3.13
pkgrel=2
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'wxgtk' 'lame' 'hicolor-icon-theme'
	 'desktop-file-utils' 'jack' 'soundtouch' 'ffmpeg')
options=('!makeflags')
install=audacity.install
source=(http://audacity.googlecode.com/files/${pkgname}-minsrc-${pkgver}-beta.tar.bz2 \
        audacity-1.3.13-ffmpeg.patch)
sha1sums=('47a1340699958c8e1804f374209835be865d7027'
          '26c2ad8e611705b2762a318e66312169f59cc218')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}-beta"
  patch -p1 -i "${srcdir}/audacity-1.3.13-ffmpeg.patch"
  WX_CONFIG=/usr/bin/wx-config ./configure --prefix=/usr \
    --with-portaudio --with-libsamplerate \
    --without-libresample --with-libmad \
    --with-ffmpeg --with-id3tag --with-libflac \
    --with-vorbis --with-libexpat \
    --with-libsndfile --with-soundtouch \
    --enable-unicode --without-taglib
  make
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}-beta"
  make DESTDIR="${pkgdir}" install
}
