# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=telepathy-qt4
pkgver=0.5.15
pkgrel=1
pkgdesc="A library for Qt-based Telepathy clients."
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
license=('LGPL')
options=('!libtool')
depends=('qt' 'telepathy-farsight')
makedepends=('libxslt' 'python2' 'cmake')
source=(http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('2066d447a46ca3d767ff82ca562244a0')

build() {
    cd "${srcdir}"

    mkdir build
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${srcdir}"/build
    make DESTDIR="$pkgdir" install
}
