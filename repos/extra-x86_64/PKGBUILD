# $Id$
# Contributor: damir <damir@archlinux.org>
# Maintainer: Thayer Williams <thayer@archlinux.org>

pkgname=gtk2-themes-collection
pkgver=0.2.0
pkgrel=2
pkgdesc="Additional GTK2 themes and their respective Metacity themes"
url="http://art.gnome.org/themes/gtk2/"
license=('custom:gtk-themes-collection')
arch=('i686' 'x86_64')
depends=('gtk2')
source=(http://art.gnome.org/download/themes/gtk2/1364/GTK2-ClearlooksDarkLime.tar.gz
        http://art.gnome.org/download/themes/gtk2/1065/GTK2-ClearlooksDarkOrange.tar.gz
        http://art.gnome.org/download/themes/gtk2/1285/GTK2-Darkilouche.tar.bz2
        http://art.gnome.org/download/themes/gtk2/1345/GTK2-Foresight.tar.bz2
        http://art.gnome.org/download/themes/gtk2/1284/GTK2-Gilouche.tar.gz
        http://art.gnome.org/download/themes/gtk2/571/GTK2-Glossy_P.tar.gz
        http://art.gnome.org/download/themes/gtk2/213/GTK2-H2O-default-2.0.tar.gz
        http://art.gnome.org/download/themes/gtk2/734/GTK2-industrial-myst.tar.bz2
        http://art.gnome.org/download/themes/gtk2/702/GTK2-Litoral.tar.gz
        http://art.gnome.org/download/themes/gtk2/567/GTK2-Milk-2.0.tar.bz2
        http://art.gnome.org/download/themes/gtk2/733/GTK2-SnowLight.tar.bz2
        http://art.gnome.org/download/themes/gtk2/712/GTK2-Tenebrific.tar.gz
        http://art.gnome.org/download/themes/gtk2/728/GTK2-toolbox-2.0.tar.gz
        http://art.gnome.org/download/themes/gtk2/598/GTK2-Yattacier3.tar.gz
        LICENSE)
md5sums=('200e6a847b0b54aef3f8e15bec1bc026' '6676ce2e5e106abb29347030c586457b'\
         '9f00f2419c6bf36966af29862b2345c4' '81ebf09ed6a672695f12070c759b02e1'\
         '1927bab8eb8e059523675b68ef11eee8' '6abac5e23af652db613fb76f6a8acaa5'\
         'e10b726e4bdb8e69d32d0c2b432fabb9' 'a42d486927c26f717d3c907914292526'\
         '094dffbc797e2d3e3a2c33dae52ef938' 'dd638825e8d2a0ff7ababebbb59619e6'\
         '0c0cd466a59a025f403d4b7f6e9c962f' 'f44a8b00620eee82150410cbb10f488f'\
         '0bfe75796b04fa6a8d76a6a4fc686fef' '9697ef94d36747f9ee3503ef59ca044c'\
         '352bf86d1bdd230d932fa9c555f7c037')

build() {
    rm $srcdir/*.tar.{gz,bz2}
    mkdir -p $pkgdir/usr/share/themes
    cd $srcdir/

    # make sure whitespaces are seen as filename parts
    shopt -s dotglob

    # start of loop
    for i in * ; do

        # make sure, the var is in fact a dir
        if [ -d "$i" ]; then
            echo "==> $i "
            cd "$i"
            
            # install the gtk2 theme
            mkdir -p                "$pkgdir/usr/share/themes/$i/"
            cp -r gtk-2.0/          "$pkgdir/usr/share/themes/$i/"

            # install the metacity theme if present
            if [ -d metacity-1 ]; then
                echo        "      has metacity theme ..."
                mkdir -p            "$pkgdir/usr/share/themes/$i/"
                cp -r metacity-1/   "$pkgdir/usr/share/themes/$i/"
            fi

            # install the theme file if present
            if [ -e index.theme ]; then
                echo        "      has index.theme ..."
                cp index.theme      "$pkgdir/usr/share/themes/$i/index.theme"
            fi

            echo "==> [done] "
            cd $srcdir/
        fi
    done

    # reset shell option to avoid unwanted behaviour
    shopt -u dotglob

    # clean up the H2O themes
    rm $pkgdir/usr/share/themes/H2O-gtk2-*/gtk-2.0/.gtkrc.swp

    # copy license file
    mkdir -p $pkgdir/usr/share/licenses/$pkgname
    cp LICENSE $pkgdir/usr/share/licenses/$pkgname/
}

