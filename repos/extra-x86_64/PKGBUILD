# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gstreamer0.10-ugly
pkgname=('gstreamer0.10-ugly' 'gstreamer0.10-ugly-plugins')
pkgver=0.10.15
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconfig' 'gstreamer0.10-base>=0.10.29' 'libdvdread>=4.1.3' 'lame>=3.98.4' 'libmpeg2>=0.5.1' 'a52dec>=0.7.4' 'libid3tag>=0.15.1b' 'libmad>=0.15.1b' 'libsidplay>=1.36.59' 'libcdio>=0.82' 'x264=20100607' 'opencore-amr>=0.1.2')
url="http://gstreamer.freedesktop.org/"
options=(!libtool)
source=(${url}/src/gst-plugins-ugly/gst-plugins-ugly-${pkgver}.tar.bz2)
md5sums=('21c034a762a5da252f91640e53bfe457')

build() {
  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Ugly Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"
  make
  sed -e 's/gst-libs gst ext/gst-libs gst/' -i Makefile
}

package_gstreamer0.10-ugly() {
  pkgdesc="GStreamer Multimedia Framework Ugly plugin libraries"
  depends=('gstreamer0.10-base>=0.10.29')
  
  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_gstreamer0.10-ugly-plugins() {
  pkgdesc="GStreamer Multimedia Framework Ugly Plugins (gst-plugins-ugly)"
  depends=("gstreamer0.10-ugly=${pkgver}" 'libdvdread>=4.1.3' 'lame>=3.98.4' 'libmpeg2>=0.5.1' 'a52dec>=0.7.4' 'libid3tag>=0.15.1b' 'libmad>=0.15.1b' 'libsidplay>=1.36.59' 'libcdio>=0.82' 'x264=20100607' 'opencore-amr>=0.1.2')
  groups=('gstreamer0.10-plugins')
  replaces=('gstreamer0.10-dvdread' 'gstreamer0.10-mpeg2dec' 'gstreamer0.10-mad' 'gstreamer0.10-lame' 'gstreamer0.10-sidplay' 'gstreamer0.10-a52dec')
  conflicts=('gstreamer0.10-dvdread' 'gstreamer0.10-mpeg2dec' 'gstreamer0.10-mad' 'gstreamer0.10-lame' 'gstreamer0.10-sidplay' 'gstreamer0.10-a52dec')

  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  make -C ext DESTDIR="${pkgdir}" install
}
