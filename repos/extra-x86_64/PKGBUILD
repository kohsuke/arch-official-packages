# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=2.8.0
pkgrel=1
pkgdesc='Input method server supporting various input methods'
url='http://hyperrate.com/dir.php?eid=67'
license=('LGPL')
arch=('i686' 'x86_64')
depends=('gtk2' 'libxtst')
makedepends=('qt' 'gtk3' 'anthy' 'libchewing')
optdepends=('qt: support for qt4 input method'
            'gtk3: support for gtk3 input method'
            'anthy: support for anthy input method'
            'libchewing: support for chewing input method')
source=("http://www.csie.nctu.edu.tw/~cp76/gcin/download/${pkgname}-${pkgver}.tar.xz")
sha1sums=('69fe604939bc013f043ff0b4baa63b7951b26c95')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed 's/QT=qt4/QT=qt/' -i qt4-im/Makefile
	sed 's/lib64/lib/g' -i configure
	sed '/include suffixes-rule/a \
		CFLAGS+='"${CFLAGS}"' \
		LDFLAGS+='"${LDFLAGS}"' \
		OPTFLAGS=' \
		-i Makefile

	./configure --prefix=/usr --use_i18n=Y
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
