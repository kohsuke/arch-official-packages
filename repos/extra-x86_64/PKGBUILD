# $Id$
# Maintainer:Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-nv
pkgver=2.1.18
pkgrel=6
pkgdesc="X.org nv video driver"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorg-server-devel>=1.11.99.903')
conflicts=('xorg-server<1.11.99.903')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        git_fixes.patch)
sha1sums=('d35b2fa5a26a507a9cc95b69243d9fd0c0f32aa2'
          '07469140e20751a7366017b116a8c4d7d39eaac0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/git_fixes.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
