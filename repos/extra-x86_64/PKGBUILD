# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity
pkgver=1.3.13
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'libsamplerate' 'lame' 'hicolor-icon-theme'
	'shared-mime-info' 'desktop-file-utils' 'redland' 'jack' 'soundtouch')
makedepends=('zip')
options=('!makeflags')
install=audacity.install
source=(http://audacity.googlecode.com/files/${pkgname}-minsrc-${pkgver}-beta.tar.bz2)
md5sums=('57be7fadb8c8dd17b8462c7ac1561d03')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}-beta"
  WX_CONFIG=/usr/bin/wx-config ./configure --prefix=/usr \
    --with-portaudio --with-libsamplerate \
    --without-libresample --with-libmad \
    --with-id3tag --with-libflac \
    --with-vorbis --with-libexpat \
    --with-libsndfile --with-soundtouch \
    --enable-unicode --without-taglib
  make
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}-beta"
  make DESTDIR="${pkgdir}" install
}
