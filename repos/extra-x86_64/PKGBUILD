# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=bijiben
pkgver=3.8.3
pkgrel=1
pkgdesc="Write out notes, every detail matters"
arch=(i686 x86_64)
url="https://live.gnome.org/Bijiben"
license=(GPL)
depends=(tracker clutter-gtk libzeitgeist webkitgtk3 hicolor-icon-theme desktop-file-utils)
makedepends=(intltool itstool)
options=(!emptydirs)
install=bijiben.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver::3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('287742267c7484a92ead8c896091ceb0b2603ba8b5dccf304ea534456735beec')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
