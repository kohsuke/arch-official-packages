# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
# Package originally generated by cabal2arch 0.7.6
pkgname=haskell-zlib
pkgver=0.5.2.0
pkgrel=5
pkgdesc="Compression and decompression in the gzip and zlib formats"
url="http://hackage.haskell.org/package/zlib"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=(ghc=6.12.3 'zlib' sh)
options=('strip')
source=(http://hackage.haskell.org/packages/archive/zlib/${pkgver}/zlib-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('19859e241dc18ef1501a5d44d8523507')

build() {
    cd ${srcdir}/zlib-$pkgver
    runhaskell Setup configure -p --enable-split-objs --enable-shared --prefix=/usr --docdir=/usr/share/doc/${pkgname}
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
}

package() {
    cd ${srcdir}/zlib-$pkgver
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/zlib
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
