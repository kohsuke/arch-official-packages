# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=iptraf
pkgver=3.0.0
pkgrel=2
pkgdesc="An IP network monitor."
arch=('i686' 'x86_64')
url="http://iptraf.seul.org/"
depends=('ncurses>=5.6-7')
license=('GPL')
options=('!makeflags')
source=(ftp://iptraf.seul.org/pub/$pkgname/$pkgname-$pkgver.tar.gz
	iptraf-$pkgver-headerfix.patch)
md5sums=('377371c28ee3c21a76f7024920649ea8'
	'1d7f4a5c81086463452e73dae1799acf')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  patch -Np1 -i ../$pkgname-$pkgver-headerfix.patch || return 1
  cd src/
  sed -i -e s:/var/local/iptraf:/var/lib/iptraf: \
      -e s:/usr/local/bin:/usr/sbin: dirs.h
  make CFLAGS="$CFLAGS" DEBUG="" TARGET="/usr/sbin" \
      WORKDIR="/var/lib/iptraf" clean all || return 1

  for sbin in iptraf rvnamed; do
    install -D -m755 $sbin ${pkgdir}/usr/sbin/$sbin
  done

  cd ../Documentation
  for man in *.8; do
    install -D -m644 $man ${pkgdir}/usr/share/man/man8/$man
  done
  mkdir -p ${pkgdir}/var/{lib,log,run}/iptraf
}
# vim: ts=2: ft=sh
