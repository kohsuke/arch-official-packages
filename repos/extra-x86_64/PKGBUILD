# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-server-utils
pkgver=7.4
pkgrel=3
pkgdesc="X.Org utilities required by xorg-server"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxfontcache' 'libxi' 'libxmu' 'libxxf86misc' 'libxrandr' 'libxxf86vm' 'mcpp>=2.6')
makedepends=('pkgconfig' 'xbitmaps' 'bigreqsproto' 'xtrans')
source=(${url}/releases/individual/app/iceauth-1.0.2.tar.bz2
        ${url}/releases/individual/app/rgb-1.0.3.tar.bz2
        ${url}/releases/individual/app/sessreg-1.0.4.tar.bz2
        ${url}/releases/individual/app/xcmsdb-1.0.1.tar.bz2
        ${url}/releases/individual/app/xbacklight-1.1.tar.bz2
	${url}/releases/individual/app/xgamma-1.0.2.tar.bz2
        ${url}/releases/individual/app/xhost-1.0.2.tar.bz2
        ${url}/releases/individual/app/xmodmap-1.0.3.tar.bz2
        ${url}/releases/individual/app/xrandr-1.3.0.tar.bz2
        ${url}/releases/individual/app/xrdb-1.0.5.tar.bz2
        ${url}/releases/individual/app/xrefresh-1.0.2.tar.bz2
        ${url}/releases/individual/app/xset-1.0.4.tar.bz2
        ${url}/releases/individual/app/xsetmode-1.0.0.tar.bz2
        ${url}/releases/individual/app/xsetroot-1.0.2.tar.bz2)
md5sums=('7ab8b64edf0212a9d9a3c8129901a450'
         '44ea16cc3104de6401bc74035f642357'
         '839e968d7197b9563f0fd8a5a3aac2e3'
         '8579d5f50ba7f0c4a5bf16b9670fea01'
         '51b4a1c0ae2b3bd77417306fd78a3e94'
         'f13ddedaa63a608d3b025d326f4f5b5d'
         'f746aba36f075ae4cae313d849a94f4e'
         '626731003ec22a74ecf9e44ed098bbdf'
         '616612a15711a4422e71be6acc42ca3c'
         '9c30b8e1709d5367beb7706146640c07'
         '1228f890f86148e4e6ae22aa73118cbb'
         '2f41fd983ba4d89419204854936025e2'
         'd074e79d380b031d2f60e4cd56538c93'
         '9af7db9f3052aef0b11636720b3101dd')

build() {
  cd ${startdir}/src
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      case "${i}" in
        xrdb*)
	  ./configure --prefix=/usr --mandir=/usr/share/man --with-cpp=/usr/bin/mcpp\ -@old || return 1
	;;
	*)
	  ./configure --prefix=/usr --mandir=/usr/share/man || return 1
	;;
      esac
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      popd
    fi
  done
}
