# $Id: PKGBUILD 72422 2012-06-14 07:51:14Z speps $
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=sratom
pkgver=0.2.0
pkgrel=1
pkgdesc="An LV2 Atom RDF serialisation library"
arch=(i686 x86_64)
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('lv2' 'sord')
makedepends=('python2')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('c03cf2849186818610ffe889be4f5b55')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove ldconfig
  sed -i '/ldconfig/d' wscript

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir/" python2 waf install

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
