# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=13.06
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('i686' 'x86_64')
url="http://www.rosegardenmusic.com/"
license=('GPL')
depends=('liblrdf' 'dssi' 'fftw' 'lirc-utils'
         'perl' 'qt4' 'shared-mime-info')
makedepends=('imake')
optdepends=('lilypond: notation display'
            'cups: printing support'
            'okular: print preview, or any other PDF viewer'
            'timidity++: MIDI playback, or any other softsynth'
            'flac'
            'wavpack')
[ "$CARCH" = "i686" ] && optdepends+=('dssi-vst: win32 VST support')
install=$pkgname.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('67b110277718b6ef2ec098a8fc452340')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # lrdf + raptor2 compatibility
  lrdf_CFLAGS="$(pkg-config --cflags raptor2)"

  # qt4 compatibility (qt->qt5)
  LRELEASE=/usr/lib/qt4/bin/lrelease
  LUPDATE=/usr/lib/qt4/bin/lupdate

  export lrdf_CFLAGS LRELEASE LUPDATE

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
