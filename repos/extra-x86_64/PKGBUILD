# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Charly Ghislain <charlyghislain@gmail.com>

pkgname=libmsn
pkgver=4.2
pkgrel=2
pkgdesc="A reusable, open-source, fully documented library for connecting to Microsoft's MSN"
url="http://sourceforge.net/projects/libmsn/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('openssl')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
        'fix-header.patch')
md5sums=('8887196051d2960a9279a5e00c8416a4'
         '918b4fc89910118c63b2147eacc2c3f3')

build() {
  cd "${srcdir}"

  patch -p0 -i "${srcdir}"/fix-header.patch

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}/build"
  make "DESTDIR=${pkgdir}" install
}
