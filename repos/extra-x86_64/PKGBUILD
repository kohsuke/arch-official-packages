# $Id$
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libftdi
pkgver=0.20
pkgrel=1
pkgdesc="A library to talk to FTDI chips"
arch=('i686' 'x86_64')
url="http://www.intra2net.com/en/developer/libftdi/download.php"
license=('GPL2' 'LGPL2.1')
depends=('libusb-compat' 'gcc-libs')
makedepends=('boost')
options=('!libtool')
source=(http://www.intra2net.com/en/developer/libftdi/download/$pkgname-$pkgver.tar.gz{,.sig})
sha1sums=('4bc6ce70c98a170ada303fbd00b8428d8a2c1aa2'
          '807d145147c9bee08b4cea4d7ca82ccaf1330f02')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '/no-install/d' examples/Makefile.in
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
