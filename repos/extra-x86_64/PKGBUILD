# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-marble
pkgver=4.8.0
pkgrel=2
pkgdesc="Desktop Globe"
url="http://kde.org/applications/education/marble/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'gpsd')
optdepends=('gpsd: gps support')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/xz/marble-${pkgver}.tar.xz"
        'fix-with-qt48.patch')
sha1sums=('6c3c6b03c9d8e92730d6955adcbdb5de42fda43d'
          '3ec130564aa1b7a61a0c3cd51533c69c6c35bd0e')

build() {
  cd "${srcdir}"/marble-${pkgver}
  patch -p1 -i "${srcdir}"/fix-with-qt48.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../marble-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
