# $Id$
# Maintainer: Thayer Williams <thayer@archlinux.org>
# Contributor: Eric Johnson <eric@coding-zone.com>

pkgname=i8kutils
pkgver=1.33
pkgrel=3
pkgdesc="Dell Inspiron/Latitude kernel driver and utilities"
arch=('i686' 'x86_64')
url="http://people.debian.org/~dz/i8k/"
license=('GPL2')
depends=('glibc')
optdepends=('tk: for i8kmon deamon')
conflicts=('i8kmon')
provides=('i8kmon')
replaces=('i8kmon')
backup=('etc/i8kutils/i8kmon.conf')
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}_${pkgver}.tar.gz
        i8kmon.service)
md5sums=('1ab077b301dbb0f4be8ef52eb3b97628'
         '32c159616f72ab65969db95b9e824ced')

prepare() {
  cd $pkgname-$pkgver

  # conform to Arch Linux guidelines
  sed -i 's|/etc/i8kmon|/etc/i8kutils/i8kmon.conf|g' i8kmon.1
  sed -i 's|/etc/i8kmon.conf|/etc/i8kutils/i8kmon.conf|g' i8kmon
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  install -d "$pkgdir"/usr/{bin,share/man/man1}
  make DESTDIR="$pkgdir" install install-man
  install -D -m644 i8kmon.conf "$pkgdir/etc/i8kutils/i8kmon.conf"
  install -D -m644 ../i8kmon.service "$pkgdir/usr/lib/systemd/system/i8kmon.service"
}
