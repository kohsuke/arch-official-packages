# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>

pkgname=pycrypto
pkgver=2.5
pkgrel=1
pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Python."
arch=('i686' 'x86_64')
depends=('python2')
url="http://www.dlitz.net/software/pycrypto/"
license=('custom')
source=("http://ftp.dlitz.net/pub/dlitz/crypto/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e950a78184e2a7defccf5d45e0c29c1e9edeb29984433f0d110a21e9631e38de')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"
  cp -r LEGAL "${pkgdir}/usr/share/licenses/${pkgname}/"
}
