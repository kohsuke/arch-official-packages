# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=0.4.0
pkgrel=3
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('i686' 'x86_64')
url="http://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('cmake' 'pkgconfig' 'perl' 'python2' 'lua' 'tcl>=8.6.0' 'ruby' 'aspell' 'guile')
optdepends=('perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell' 'guile')
options=('!libtool')
source=("http://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.bz2"
        'ruby-2.0.0-crash-fix.diff')
md5sums=('b2e10ca1e3a72142fcc8d58c7c774b1b'
         '8bafb0ec603af018e2d66155640685b4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix: https://bugs.archlinux.org/task/34342
  # original commit: http://git.savannah.gnu.org/gitweb/?p=weechat.git;a=commit;h=4fdbb83a079f24a742633fd4d02084911580c19c
  patch -Np1 -i ${srcdir}/ruby-2.0.0-crash-fix.diff

  mkdir build
  cd build
  cmake .. -DPREFIX=/usr \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}/" install
}
