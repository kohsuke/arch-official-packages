# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=bin86
pkgver=0.16.18
pkgrel=2
pkgdesc="A complete 8086 assembler and loader"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.debath.co.uk/"
depends=('glibc')
source=("http://www.debath.co.uk/dev86//$pkgname-$pkgver.tar.gz"
        bin86-0.16.17-x86_64-1.patch)
md5sums=('914068fb649b93642d1cf21857b75699'
         '92bdce7b0655cd2e9f83c83fc56d128e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if [[ $CARCH = x86_64 ]]; then
     patch -Np1 < "$srcdir/bin86-0.16.17-x86_64-1.patch"
  fi

  make PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man1"
  make "PREFIX=$pkgdir/usr" "MANDIR=$pkgdir/usr/share/man/man1" install
}
