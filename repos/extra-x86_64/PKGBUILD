# $Id$
# Maintainer:
# Contributor: Ben <contrasutra@myrealbox.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gweled
pkgver=0.8
pkgrel=1
pkgdesc="A GTK+ version of Bejeweled game (aka Diamond Mine)"
arch=('i686' 'x86_64')
license=('GPL')
url="https://launchpad.net/gweled/"
depends=('filesystem' 'librsvg' 'libmikmod' 'xdg-utils' 'hicolor-icon-theme')
makedepends=('intltool')
install=${pkgname}.install
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
md5sums=('2b52636abdd6b71e537ede7410db4cfa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-scores-user=root \
    --with-scores-group=games
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  #We generate these files on postinstall to prevent score resets on upgrade
  rm -rf "${pkgdir}/var"
}
