# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>

pkgbase=obexd
pkgname=('obexd-client' 'obexd-server')
pkgver=0.29
pkgrel=2
url="http://www.bluez.org/"
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('openobex' 'dbus-glib' 'libical')
source=(http://www.kernel.org/pub/linux/bluetooth/${pkgbase}-${pkgver}.tar.bz2)
md5sums=('3e90cff18f1339a44adde2c920f95393')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package_obexd-client() {
  pkgdesc="D-Bus service providing high-level OBEX client side functionality"
  depends=('openobex' 'glib2')
  provides=("obexd=${pkgver}")
  replaces=('obexd')
  conflicts=('obexd')

  cd "${srcdir}/${pkgbase}-${pkgver}/client"
  install -Dm755 obex-client ${pkgdir}/usr/lib/obex-client
  install -Dm644 obex-client.service ${pkgdir}/usr/share/dbus-1/services/obex-client.service
}

package_obexd-server() {
  pkgdesc="D-Bus service providing high-level OBEX server side functionality"
  depends=('openobex' 'glib2' 'libical')

  cd "${srcdir}/${pkgbase}-${pkgver}/src"
  install -Dm755 obexd ${pkgdir}/usr/lib/obexd 
  install -Dm644 obexd.service ${pkgdir}/usr/share/dbus-1/services/obexd.service
}
