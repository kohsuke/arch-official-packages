# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-exchange
pkgver=3.0.1
pkgrel=2
pkgdesc="Ximian Connector Exchange plugin for Evolution"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.ximian.com"
depends=('evolution-data-server' 'gtkhtml4' 'gnome-desktop' 'libunique3')
makedepends=('intltool' 'evolution')
options=('!libtool' '!emptydirs')
install=evolution-exchange.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('4c020f7946f534e3245806f29f48dbc9ccb98afdbc92d7bb5b14a5410668ad02')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/evolution --disable-static \
      --with-libdb=/usr --with-krb5=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain evolution-exchange-3.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
