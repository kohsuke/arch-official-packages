# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer0.10-bad
pkgver=0.10.8
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugin libraries (gst-plugins-bad)"
arch=(i686 x86_64)
license=('LGPL' 'GPL')
depends=('gcc-libs>=4.3.2' 'gstreamer0.10-base>=0.10.21')
makedepends=('pkgconfig')
url="http://gstreamer.freedesktop.org/"
options=(!libtool)
_relname=gst-plugins-bad
source=(${url}/src/${_relname}/${_relname}-${pkgver}.tar.bz2
	configure.patch)
md5sums=('ddc8f92dab0081f131650ad6c95908b8'
	 '897af7df21ec32724c1aa5f79d1296cb')

build() {
  cd "${srcdir}/${_relname}-${pkgver}"
  patch -Np0 -i "${srcdir}/configure.patch" || return 1
  autoconf || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental --disable-external \
    --disable-docs-build --disable-plugin-docs \
    --with-package-name="GStreamer Bad Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
