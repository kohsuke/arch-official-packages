# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepimlibs
pkgver=4.2.4
pkgrel=2
pkgdesc="KDE PIM Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
groups=('kde')
depends=('kdelibs' 'akonadi' 'libical' 'gpgme')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'cyrus-sasl' 'openldap')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        "gpgme-1.2.0.patch")

build() {
	cd $srcdir/$pkgname-$pkgver
        patch -Np0 -i ../gpgme-1.2.0.patch || return 1
        cd ..
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
}
md5sums=('ee1d783140be0bb67652784f6fa93f70'
         '049ee12f62c3020866f046e41f21dc79')
