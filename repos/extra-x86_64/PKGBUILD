# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Andrew Rose <ody@netrux.com>

pkgname=gif2png
pkgver=2.5.5
pkgrel=1
pkgdesc="A GIF to PNG image format converter"
arch=('i686' 'x86_64')
url="http://www.catb.org/~esr/gif2png/"
license=('ZLIB')
depends=('libpng')
optdepends=('python2: for using web2png')
source=("http://www.catb.org/~esr/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'gif2png-2.5.5-libpng15.patch')
md5sums=('cfb4c003966866b6069d3707bfb12435'
         '5b3467b97157f9a7978a230b934bc488')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's#env python#env python2#' web2png
  patch -Np1 -i ${srcdir}/gif2png-2.5.5-libpng15.patch

  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
