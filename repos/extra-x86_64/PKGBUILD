# $Id: PKGBUILD 41669 2009-06-06 10:21:13Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=konq-plugins
pkgver=svn1179246
pkgrel=1
pkgdesc="Extra plugins for Konqueror"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('kdebase-konqueror' 'tidyhtml' 'kwebkitpart')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'docbook-xsl')
install=${pkgname}.install
# svn co -q svn://websvn.kde.org:443/home/kde/trunk/extragear/base/konq-plugins
source=("ftp://ftp.archlinux.org/other/kde/${pkgname}-${pkgver}.tar.bz2")
md5sums=('647bdd4cc1adc66ff2faef6cc187202b')
options=('force')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
