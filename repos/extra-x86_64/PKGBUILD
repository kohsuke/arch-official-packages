# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=2.2.4
pkgrel=2
pkgdesc="A personal and small-business financial-accounting application"
arch=(i686 x86_64)
url="http://www.gnucash.org"
license=("GPL")
depends=('gtkhtml>=3.18.0' 'swig' 'slib' 'goffice>=0.6.2' 'libgnomeprintui>=2.18.2'
        'guile>=1.8.4' 'desktop-file-utils>=0.14' 'gconf>=2.18.0.1-4')
makedepends=('texinfo' 'perlxml' 'gnome-doc-utils>=0.11.2')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('27e660297dc5b8ce574515779d05a5a5')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --sysconfdir=/etc --libexecdir=/usr/lib/$pkgname

  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
    DESTDIR=${startdir}/pkg install || return 1

  mkdir -p ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas \
    ${startdir}/pkg/etc/gconf/schemas/*.schemas
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
