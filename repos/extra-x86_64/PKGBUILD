# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=bluefish
pkgver=2.0.1
pkgrel=1
pkgdesc="A powerful HTML editor for experienced web designers and programmers"
arch=('i686' 'x86_64')
url="http://bluefish.openoffice.nl/"
license=('GPL')
depends=('pcre' 'gtk2' 'enchant' 'desktop-file-utils' 'shared-mime-info')
makedepends=('perlxml' 'gucharmap' 'intltool')
optdepends=('gucharmap: for using the charmap plugin')
options=('!libtool')
install=bluefish.install
source=(http://www.bennewitz.com/bluefish/stable/source/bluefish-${pkgver}.tar.bz2)
md5sums=('9b672411369bd580421f832214bf65b3')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	    --enable-spell-check --disable-update-databases --disable-xml-catalog-update || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install || return 1
}
