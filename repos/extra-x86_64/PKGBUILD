# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=racket
pkgver=5.3
pkgrel=1
pkgdesc="A programming language environment (formerly known as PLT Scheme) suitable for tasks ranging from scripting to application development"
arch=('i686' 'x86_64')
url="http://racket-lang.org/"
license=('LGPL')
depends=('gtk2' 'desktop-file-utils')
makedepends=('gsfonts')
options=('!libtool' '!strip')
install=racket.install
source=(http://download.racket-lang.org/installers/${pkgver}/racket/${pkgname}-${pkgver}-src-unix.tgz \
        drracket.desktop)
sha1sums=('16883898c0cc418ba44732e3f62a54cf043a1de8'
          'a20808f6b250225704856f82a544681a962a299d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  [ "$CARCH" == "x86_64" ] && export CFLAGS+="-fPIC"
  sed -i 's/struct siginfo/siginfo_t/' racket/gc2/sighand.c
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install

  install -D -m644 ../collects/icons/plt.xpm "${pkgdir}/usr/share/pixmaps/drracket.xpm"
  install -D -m644 "${srcdir}/drracket.desktop" "${pkgdir}/usr/share/applications/drracket.desktop"
}
