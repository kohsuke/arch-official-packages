# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-cantor
pkgver=4.7.3
pkgrel=1
pkgdesc="KDE Frontend to Mathematical Software"
url="http://kde.org/applications/education/cantor/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libspectre')
makedepends=('cmake' 'automoc4' 'r')
optdepends=('maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/cantor-${pkgver}.tar.bz2")
sha1sums=('ce6240d44cbf47b3dbfd1dbc03196c916e4394e9')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../cantor-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
