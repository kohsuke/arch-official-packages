# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgbase=libofx
pkgname=('libofx' 'libofx-doc')
pkgver=0.9.5
pkgrel=1
pkgdesc="API for the OFX banking standard"
arch=('i686' 'x86_64')
url="http://libofx.sourceforge.net"
license=('GPL')
depends=('opensp' 'curl' 'libxml++')
checkdepends=('gnupg')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz libofx-gcc47.patch)
sha1sums=('7e5245d68a0f3f7efad2fd809b2afbbff6ba0e73'
          '6c327e24543cb8ba901bfb204343a85c68264654')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  patch -p1 -i "$srcdir/libofx-gcc47.patch"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgbase-$pkgver"
  make check
}

package_libofx() {
  options=('!libtool' '!docs')

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install
}

package_libofx-doc() {
  pkgdesc="Documention of the OFX banking standard API"
  depends=()

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install-docDATA
  make -C doc DESTDIR="$pkgdir" install
}
