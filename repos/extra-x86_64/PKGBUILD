# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=rdesktop
pkgver=1.8.0
pkgrel=2
pkgdesc="An open source client for Windows Remote Desktop Services"
arch=('i686' 'x86_64')
url="http://www.rdesktop.org/"
license=('GPL3')
depends=('libao' 'libsamplerate' 'xorg-xrandr' 'pcsclite')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'rdesktop-send_physical_buttons.diff' 'rdesktop-libao.patch' 'rdesktop-tcp.patch')
md5sums=('203d662ac20b22250bbbd525a9f29f3a'
         'cbfb12729e7f28e497afb883cc42022b'
         '6514f31dc879a0a66c955e4e3348c143'
         'f204fd8a40b16ca10d4962d4eaeca4db')

prepare() {
  cd ${pkgname}-${pkgver}

  # FS#15113
  patch -i "${srcdir}/rdesktop-send_physical_buttons.diff"

  # Fix libao segfault, from Fedora
  patch -i "${srcdir}/rdesktop-libao.patch"

  # FS#36488
  patch -i "${srcdir}/rdesktop-tcp.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-smartcard \
    --with-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
