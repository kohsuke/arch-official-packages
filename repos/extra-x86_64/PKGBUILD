# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer0.10
pkgver=0.10.23
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=(i686 x86_64)
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2>=2.7.3' 'glib2>=2.20.1')
makedepends=('intltool' 'pkgconfig' 'gtk-doc')
options=('!libtool')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.bz2)
md5sums=('f7b2e300d2d85756407ec529424ab237')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
  ./configure --prefix=/usr \
	--sysconfdir=/etc --localstatedir=/var \
	--disable-docs-build || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  cd "${pkgdir}/usr/bin"

  #Remove unversioned gst-* binaries to get rid of conflicts
  for bins in `ls *-0.10`; do
    rm -f ${bins/-0.10/}
  done
}
