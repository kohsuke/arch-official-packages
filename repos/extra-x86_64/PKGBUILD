# $Id$

# Contributor: FUBAR <mrfubar@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=bitlbee
pkgver=3.2
pkgrel=3
pkgdesc='Brings instant messaging (XMPP, MSN, Yahoo!, AIM, ICQ, Twitter) to IRC'
url='http://www.bitlbee.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gnutls' 'glib2')
makedepends=('asciidoc' 'libotr3')
optdepends=('skype4py: to use skyped'
            'libotr3: for OTR encryption support'
            'xinetd: to run bitlbee through xinetd')
source=("http://get.bitlbee.org/src/${pkgname}-${pkgver}.tar.gz"
        'xinetd'
        'bitlbee.tmpfiles')
sha1sums=('21e17f082c776566429603b1e8c966983a75ac9e'
          '5e0af27ba9cc4fe455e3381c75fc49a9326e2f17'
          '3695ed2fe22436c4d0fc3ead829f7d1f89bc491c')
backup=('etc/bitlbee/bitlbee.conf'
        'etc/bitlbee/motd.txt'
        'etc/xinetd.d/bitlbee')
install=bitlbee.install

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --etcdir=/etc/bitlbee \
    --sbindir=/usr/bin \
    --pidfile=/run/bitlbee/bitlbee.pid \
    --ipcsocket=/run/bitlbee/bitlbee.sock \
    --systemdsystemunitdir=/usr/lib/systemd/system \
    --ssl=gnutls \
    --strip=0 \
    --otr=plugin \
    --skype=plugin

  # hacky: build against libotr3
  sed -i 's,^OTRFLAGS=.*,OTRFLAGS=-lotr3,' Makefile.settings
  sed -i 's,#include.*libotr,&3,' otr.h

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install{,-etc,-dev}

  install -o65 -g65 -dm770 "$pkgdir/var/lib/bitlbee"
  install -Dm644 "$srcdir/xinetd" "$pkgdir/etc/xinetd.d/bitlbee"
  install -Dm644 "$srcdir/bitlbee.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/bitlbee.conf"
}
