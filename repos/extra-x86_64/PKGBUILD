# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libiodbc
pkgver=3.52.8
pkgrel=2
pkgdesc='Independent Open DataBase Connectivity for Linux'
arch=('i686' 'x86_64')
url="http://www.iodbc.org/dataspace/iodbc/wiki/iODBC/"
license=('LGPL')
depends=('sh')
makedepends=('chrpath')
options=('!libtool')
source=("http://downloads.sourceforge.net/iodbc/${pkgname}-${pkgver}.tar.gz")
md5sums=('9889f95f632f10895ff53b8a22162810')

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr \
		--disable-static \
		--includedir=/usr/include/libiodbc \
		--disable-gui \
        --disable-libodbc
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install

	chrpath -d ${pkgdir}/usr/bin/iodbctest{,w}
	rm -rf ${pkgdir}/usr/share/libiodbc/
}
