# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: J. Santiago Hirschfeld <jsantiagoh@yahoo.com.ar>

pkgname=transfig
pkgver=3.2.5
pkgrel=2
pkgdesc="Format conversion utility that can be used with xfig"
arch=('i686' 'x86_64')
url="http://www.xfig.org"
license=('custom')
depends=('libpng' 'libxpm')
makedepends=('imake')
source=(http://downloads.sourceforge.net/mcj/${pkgname}.${pkgver}.tar.gz LICENSE remove-stderr-msg.patch)
md5sums=('3b2cffdd25529ae5877e305f29aae6d1' 'a0b2ea903821c46dbc77afefc06d1aa4'\
         '38f8c8701191c2b54fc9f3a3baee8ee2')
sha1sums=('2657c104d0099dcb4565a8762a9543310e55e767' 'b8b712871615308b8b6add92f86d218437d652f2'\
          '1c35e72455fe899a7a232d3c2a5ff98f33973be6')

build() {
  cd ${srcdir}/${pkgname}.${pkgver}
  patch -p1 < ../remove-stderr-msg.patch || return 1
  sed -i 's/XCOMM USELATEX2E = -DLATEX2E/USELATEX2E = -DLATEX2E/' transfig/Imakefile || return 1
  sed -i 's/XCOMM USEINLINE = -DUSE_INLINE/USEINLINE = -DUSE_INLINE/' fig2dev/Imakefile || return 1

  xmkmf || return 1
  make FIG2DEV_LIBDIR=/usr/share/fig2dev Makefiles || return 1
  make FIG2DEV_LIBDIR=/usr/share/fig2dev XFIGLIBDIR=/usr/share/xfig || return 1
  make DESTDIR=${pkgdir} XFIGLIBDIR=/usr/share/xfig FIG2DEV_LIBDIR=/usr/share/fig2dev MANPATH=/usr/share/man install install.man || return 1
  install -D -m644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}
