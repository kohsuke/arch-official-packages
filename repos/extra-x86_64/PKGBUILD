# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xf86-video-s3virge
pkgver=1.10.4
pkgrel=7
pkgdesc="X.org S3 Virge video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('pkgconfig' 'xorg-server-devel>=1.11.99.903')
conflicts=('xorg-server<1.11.99.903')
options=(!libtool)
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        copyright.patch
        s3virge-1.10.4-vga.patch)
md5sums=('6517bbbf808c700502d51acdc44662f8'
         'f91fb259e3632fb9130fcbe69ecd9d6a'
         '45c7d912734c9f7dfc1f42077827cb8e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/copyright.patch"
  patch -Np1 -i ${srcdir}/s3virge-1.10.4-vga.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
