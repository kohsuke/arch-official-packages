# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=sox
pkgver=14.4.1
pkgrel=3
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('i686' 'x86_64')
url="http://sox.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('libltdl' 'file' 'libsndfile' 'libpng' 'lame' 'opencore-amr' 'gsm')
makedepends=('libao' 'libmad' 'libid3tag' 'wavpack' 'libpulse')
checkdepends=('time')
optdepends=('libao: for ao plugin'
            'libmad: for mp3 plugin'
            'libid3tag: for mp3 plugin'
            'wavpack: for wavpack plugin'
            'libpulse: for pulse plugin')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('4775f9e9eebb5a2fe79df548908a7a24088118d9')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's|man1/sox.1 soxeffect.7|man1/sox.1.gz soxeffect.7.gz|' Makefile.in
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-dyn-default --with-distro="Arch Linux"
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make bindir=. installcheck
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
