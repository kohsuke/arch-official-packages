# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kgpg
pkgver=4.8.0
pkgrel=1
pkgdesc='A GnuPG frontend'
url='http://kde.org/applications/utilities/kgpg/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdepim-runtime' 'kde-agent')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/xz/kgpg-${pkgver}.tar.xz")
sha1sums=('4edbfa73c62fdd99f87b5b42dd7f1330dc8f5986')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kgpg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
  cd $srcdir/build/doc
  make DESTDIR=$pkgdir install
}
