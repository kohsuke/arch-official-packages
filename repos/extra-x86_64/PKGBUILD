# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kdf
pkgver=4.8.0
pkgrel=1
url="http://kde.org/applications/system/kdiskfree/"
arch=('i686' 'x86_64')
pkgdesc='View Disk Usage'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/xz/kdf-${pkgver}.tar.xz")
sha1sums=('cc7b1450624b14ca740b64edd5b8fb54faca491e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kdf-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
  cd $srcdir/build/doc
  make DESTDIR=$pkgdir install
}
