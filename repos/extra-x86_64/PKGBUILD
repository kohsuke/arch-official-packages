# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Henrik Ronellenfitsch <searinox@web.de>
# Contributor: Alessio Sergi <sergi.alessio {at} gmail.com>
# Contributor: Dario 'Dax' Vilardi <dax [at] deelab [dot] org>

pkgname=amule
pkgver=10771
pkgrel=1
pkgdesc="An eMule-like client for ed2k p2p network"
arch=('i686' 'x86_64')
url="http://www.amule.org"
license=('GPL')
depends=('wxgtk' 'gd' 'geoip' 'libupnp' 'crypto++')
source=("http://amule.sourceforge.net/tarballs/aMule-SVN-r${pkgver}.tar.bz2"
        'gcc4.7.patch')
md5sums=('0439a71048c7dbdf912d19979dc0faad'
         '0498a6795f4ac3a3e8bbcf51a5026820')

build() {
  cd "${srcdir}/aMule-SVN-r${pkgver}"

  # Fix gcc-4.7 build
  patch -Np1 -i "${srcdir}/gcc4.7.patch"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-cas \
              --enable-wxcas \
              --enable-amule-daemon \
              --enable-amulecmd \
              --enable-amule-gui \
              --enable-alc \
              --enable-alcc \
              --enable-webserver \
              --disable-debug \
              --enable-optimize \
              --enable-ccache \
              --enable-geoip \
              --enable-upnp

  make
}

package() {
  cd "${srcdir}/aMule-SVN-r${pkgver}"

  make DESTDIR=${pkgdir}/ install
}
