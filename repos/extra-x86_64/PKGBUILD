# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgbase=libofx
pkgname=('libofx' 'libofx-doc')
pkgver=0.9.7
pkgrel=1
pkgdesc="API for the OFX banking standard"
arch=('i686' 'x86_64')
url="http://libofx.sourceforge.net"
license=('GPL')
depends=('opensp' 'curl' 'libxml++')
checkdepends=('gnupg')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha1sums=('9c6ac208dcc6a89525281827c3ab56c08923a02c')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgbase-$pkgver"
  make check
}

package_libofx() {
  options=('!libtool' '!docs')

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install
}

package_libofx-doc() {
  pkgdesc="Documention of the OFX banking standard API"
  depends=()

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install-docDATA
  make -C doc DESTDIR="$pkgdir" install
}
