# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=seamonkey
pkgver=2.0.5
pkgrel=1
pkgdesc="SeaMonkey internet suite"
arch=('i686' 'x86_64')
license=('MPL')
depends=('gtk2>=2.20.1' 'mozilla-common>=1.3' 'nss>=3.12.4' 'libxt' 'gcc-libs>=4.5' 'alsa-lib' 'dbus-glib>=0.86')
makedepends=('zip' 'pkg-config' 'diffutils' 'gcc' 'libgnomeui>=2.24.2' 'python' 'libidl2')
replaces=('mozilla')
url="http://www.mozilla.org/projects/seamonkey"
source=(http://releases.mozilla.org/pub/mozilla.org/${pkgname}/releases/${pkgver}/source/${pkgname}-${pkgver}.source.tar.bz2
        mozconfig
        seamonkey.desktop
        seamonkey-appversion.patch
        seamonkey-2.0-lang.patch)
md5sums=('9da657e86397bc1d4069d05beb6a98c6' '6f25f3af4506a3dfdd9da2d79181c0fd'
         '448848e65efcbf1b2be2873b66b6cd48' 'a4275fc48361c80f676e96fdb28bd74e'
         '25b6fe16ac24cd5c852213e5c1adb272')

build() {
  cd "${srcdir}/comm-1.9.1"
  patch -Np1 -i "${srcdir}/seamonkey-appversion.patch"
  patch -Np1 -i "${srcdir}/seamonkey-2.0-lang.patch"

  cp "${srcdir}/mozconfig" .mozconfig
  unset CFLAGS
  unset CXXFLAGS

  export LDFLAGS="-Wl,-rpath,/usr/lib/seamonkey-2.0"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  make -j1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m644 "${srcdir}/comm-1.9.1/suite/branding/icons/gtk/seamonkey.png" \
                "${pkgdir}/usr/share/pixmaps/" || return 1
  install -m644 "${srcdir}/seamonkey.desktop" "${pkgdir}/usr/share/applications/"

  rm -f "${pkgdir}/usr/lib/pkgconfig/"seamonkey-ns{s,pr}.pc
}
