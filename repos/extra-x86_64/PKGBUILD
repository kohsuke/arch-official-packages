# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=1.4.4
pkgrel=2
pkgdesc="Traditional Chinese Input Method Server for X window system (XIM)"
arch=("i686" "x86_64")
license=('LGPL')
url="http://www.csie.nctu.edu.tw/~cp76/gcin/"
depends=('gtk2>=2.16.1' 'libxtst' 'bash')
makedepends=('anthy')
install=gcin.install
source=(http://cle.linux.org.tw/gcin/download/${pkgname}-${pkgver}.tar.bz2
	fix_x86_64_libdir.patch
	fix_rpath.patch)
options=('!makeflags')
md5sums=('917874510ebdb8b28bf6a63251600493'
         '2018737a07a0a7a9e0c28e11245dde14'
	 'e6d2c05cec749c4920255f2a46401e28')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np0 -i "${srcdir}/fix_x86_64_libdir.patch" || return 1
    patch -Np1 -i "${srcdir}/fix_rpath.patch" || return 1
    ./configure --prefix=/usr || return 1
    make || return 1
    make DESTDIR="${pkgdir}" install || return 1
}

