# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=vice
pkgver=2.3
pkgrel=6
pkgdesc="The Versatile Commodore 8-bit Emulator"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.viceteam.org/"
depends=('alsa-lib' 'giflib' 'libxxf86vm' 'gtk2')
makedepends=('xorg-font-utils')
options=('!makeflags')
install=vice.install
source=("http://downloads.sourceforge.net/project/vice-emu/releases/${pkgname}-${pkgver}.tar.gz"
        'vice-2.3-x11video.patch'
        'vice-2.3-no-fc-cache.patch'
        'vice-2.3-notexi-notxt.patch'
        'vice-2.3-zlib-1.2.7.patch')
md5sums=('b48d137874daad50c087a0686cbdde34'
         '088292ce9efe785edfb2b0c7c670785e'
         '873042d07f3768819b589a4c8fe24607'
         '05159b6ef78e3b7dd5d362f766da4cd4'
         '6d8736974a21c3862a4bf0fe0ee6e266')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/vice-2.3-x11video.patch"
  patch -Np1 -i "${srcdir}/vice-2.3-no-fc-cache.patch"
  patch -Np1 -i "${srcdir}/vice-2.3-notexi-notxt.patch"
  patch -Np1 -i "${srcdir}/vice-2.3-zlib-1.2.7.patch"

  autoreconf
  ./configure --prefix=/usr \
              --with-alsa \
              --enable-fullscreen \
              --enable-gnomeui
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" realdocdir="/usr/share/doc/vice" install
  ln -s /usr/share/doc/vice "${pkgdir}/usr/lib/vice/doc"
}
