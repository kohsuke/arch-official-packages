# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-cantor
pkgver=4.8.0
pkgrel=1
pkgdesc="KDE Frontend to Mathematical Software"
url="http://kde.org/applications/education/cantor/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libspectre' 'kdeedu-analitza')
makedepends=('cmake' 'automoc4' 'r')
optdepends=('maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/xz/cantor-${pkgver}.tar.xz")
sha1sums=('b0ef9cb3c25e4909b6855317425e9d9f12697dff')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../cantor-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
