# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdewebdev
pkgver=4.2.1
pkgrel=1
pkgdesc="KDE Web Development Applications"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdepimlibs' 'kdebase-runtime' 'tidyhtml')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'ruby')
options=('docs')
install='kdewebdev.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('38e6f1343cf1024e52059146609a6a85')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
}
