# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=strigi
pkgver=git20110405
pkgrel=1
pkgdesc="Fast crawling desktop search engine with Qt4 GUI"
arch=('i686' 'x86_64')
url="http://www.vandenoever.info/software/strigi/"
license=('GPL2')
depends=('qt' 'clucene' 'bzip2' 'exiv2' 'libxml2')
makedepends=('cmake' 'pkg-config')
source=("ftp://ftp.archlinux.org/other/kde/${pkgname}-${pkgver}.tar.bz2")
md5sums=('dcc88c925c5ad264429ba841cab19b65')
options=('!libtool')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_INOTIFY=ON \
    -DENABLE_LOG4CXX=OFF \
    -DENABLE_FAM=OFF
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
