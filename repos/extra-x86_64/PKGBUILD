# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-python
pkgver=4.7.4
pkgrel=2
pkgdesc="KDE bindings for Python"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdepim-runtime' 'python2-pyqt' 'qscintilla')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/stable/${pkgver}/src/pykde4-${pkgver}.tar.bz2")
sha1sums=('cac4b94098dba73d58fb4f7d73d7c65c306517d1')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  # Use the python2 executable
  find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
