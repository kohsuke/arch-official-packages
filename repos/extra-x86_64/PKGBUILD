# $Id$
# Maintainer:
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=jade
pkgver=1.2.1
pkgrel=5
_debpatch=47
pkgdesc="James Clark's DSSSL Engine"
url='http://www.jclark.com/jade/'
arch=('i686' 'x86_64')
license=('custom')
depends=('gcc-libs')
replaces=('openjade')
options=('!libtool' '!makeflags')
source=(ftp://ftp.jclark.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz
        http://ftp.debian.org/debian/pool/main/j/jade/jade_${pkgver}-${_debpatch}.diff.gz)
md5sums=('4239670ca6b578bff68b8c2e7cd1225f'
         'feb3889e2a5fa86ac6bdb742a25d0001')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/jade_${pkgver}-${_debpatch}.diff
  chmod 755 configure
  ./configure --prefix=/usr --enable-default-catalog=/usr/share/sgml/catalog
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -d -m755 ${pkgdir}/usr/lib
  make prefix=${pkgdir}/usr install
  mv ${pkgdir}/usr/bin/sx ${pkgdir}/usr/bin/sgml2xml
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
