# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=1.6.9
pkgrel=1
pkgdesc='Input method server supporting various input methods'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://hyperrate.com/dir.php?eid=67'
depends=('gtk2' 'libxtst')
makedepends=('qt' 'gtk3' 'anthy' 'libchewing')
optdepends=('qt: support for qt4 input method'
            'gtk3: support for gtk3 input method'
            'anthy: support for anthy input method'
            'libchewing: support for chewing input method')
source=("http://www.csie.nctu.edu.tw/~cp76/gcin/download/${pkgname}-${pkgver}.tar.xz"
        'qt4-lib64.patch')
sha1sums=('61949c4f1e6691ff7d668576dbd56b85e0f0ba06'
          '4307b0d7d6dc10f15909c94973c4357caf31de7e')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../qt4-lib64.patch
	sed '/include suffixes-rule/a \
		CFLAGS+='"${CFLAGS}"' \
		LDFLAGS+='"${LDFLAGS}"' \
		OPTFLAGS=' \
		-i Makefile

	./configure --prefix=/usr --use_i18n=Y
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
