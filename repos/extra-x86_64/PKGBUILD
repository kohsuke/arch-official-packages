# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ardour, liblrdf and raptor are heavily dependent on each other. Updating of
# one package mostly will require an update of all the other packages. I vote
# for fixed dependencies speaking in terms of versions

pkgname=ardour
pkgver=2.8.12
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=('i686' 'x86_64')
url="http://ardour.org/"
license=('GPL')
depends=('liblrdf>=0.4.0-8' 'liblo' 'libusb-compat'
         'libgnomecanvas' 'aubio' 'slv2>=0.6.6-6')
         # -libgnomecanvas(+libgnomecanvasmm)
         # +soundtouch (these +/- changes are for SYSLIBS)
         # liblrdf versioned for https://bugs.archlinux.org/task/25005
         # slv2 versioned for https://bugs.archlinux.org/task/25060
makedepends=('scons' 'boost' 'cwiid')
optdepends=('cwiid: Wiimote support')
changelog=${pkgname}.changelog
source=(ftp://ftp.archlinux.org/other/ardour/${pkgname}-${pkgver}.tar.bz2
        ${pkgname}.desktop)
md5sums=('7c031892f53aeacf08a750fc320c79c5'
         '8aeaf433ebf781733db48e5a16b0c4da')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '/-O3/d' SConstruct

  scons ${MAKEFLAGS} ARCH="${CFLAGS} -O3" \
                     PREFIX="/usr" \
                     DIST_LIBDIR="lib" \
                     FREEDESKTOP=0 \
                     FREESOUND=1 \
                     WIIMOTE=1 \
                     SYSLIBS=0 \
                     DESTDIR="${pkgdir}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  scons DESTDIR="${pkgdir}" install

  # install some freedesktop.org compatibility
  install -Dm644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
