# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=misdnuser
pkgver=2.0.13_20120524
pkgrel=1
pkgdesc="Tools and library for mISDN"
arch=('i686' 'x86_64')
url="http://www.misdn.org"
license=('GPL')
depends=('isdn4k-utils' 'spandsp')
backup=('etc/capi20.conf') 
options=('!makeflags' '!libtool' '!strip')
source=(ftp://ftp.archlinux.org/other/misdnuser/${pkgname}-${pkgver}.tar.gz
        misdncapid.rc
        mISDNcapid.service
        mISDNcapid.conf)

build() {
  # only enable for debugging!
  #export CFLAGS+=" -g -O0"
  #export CXXFLAGS+=" -g -O0"
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-capi --enable-softdsp --with-mISDN_group=uucp
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m 755 ${srcdir}/misdncapid.rc ${pkgdir}/etc/rc.d/misdncapid
  # fix udev rule
  mkdir -p ${pkgdir}/usr/lib/udev/rules.d
  mv ${pkgdir}/etc/udev/rules.d/45-misdn.rules ${pkgdir}/usr/lib/udev/rules.d
  rm -r ${pkgdir}/etc/udev/
  # remove temporary dir /var/run/mISDNcapid
  rm -r ${pkgdir}/var
  # add systemd files
  install -D -m644 ${srcdir}/mISDNcapid.service ${pkgdir}/usr/lib/systemd/system/mISDNcapid.service
  install -D -m644 ${srcdir}/mISDNcapid.conf ${pkgdir}/usr/lib/tmpfiles.d/mISDNcapid.conf
}

md5sums=('d9b400e9888042e75c14fb3a8c2885cb'
         '2009563b144de037ecb20f8d727a772e'
         '638b399f7e0937639960c4dfb3d5138e'
         '42c3b46880a68c3883ee1ed00af34b45')
