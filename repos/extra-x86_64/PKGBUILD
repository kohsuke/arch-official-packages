# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fbida
pkgver=2.09
pkgrel=2
pkgdesc="Few applications to display and elementary edit images: fbi, fbgs, ida, exiftran"
arch=('i686' 'x86_64')
url="http://www.kraxel.org/blog/linux/fbida/"
license=('GPL2')
depends=('giflib' 'libtiff' 'libexif' 'lesstif' 'libpng' 'fontconfig' 'curl')
makedepends=('libxpm')
optdepends=('ghostscript: to use fbgs')
source=(http://www.kraxel.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha1sums=('5838b2cb7188481fece6159fccb3b121acd906e7'
          '18f5660301201326bd9426ce55e9fc217bf720d8')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/ungif/gif/' GNUmakefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make STRIP="" DESTDIR="${pkgdir}" prefix=/usr install
}
