# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=rdesktop
pkgver=1.8.0
pkgrel=3
pkgdesc="An open source client for Windows Remote Desktop Services"
arch=('i686' 'x86_64')
url="http://www.rdesktop.org/"
license=('GPL3')
depends=('libao' 'libsamplerate' 'xorg-xrandr' 'pcsclite')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'rdesktop-send_physical_buttons.diff' 'rdesktop-libao.patch' 'rdesktop-tcp.patch'
	rdesktop-r1729.patch rdesktop-r1731.patch rdesktop-r1732.patch)
sha1sums=('2d39a41d29ad1ad2509d1e343a2817a3c7d666de'
          '809a9dc11ad4f704f79fc4bc2a70b76a4f42596d'
          '68a48061e5ed76150c1444891a192f7884fd056e'
          'a14793c72194bed74725bcf75fdb8b93c7577399'
          '972d6ec59cf140383880464e3ef43e03969f3bfe'
          '9a417aba27ded0ab595cbb07fccd452bdc0af20c'
          '1bc92e04058a2f5c02f42a5c97da7ee672a71698')

prepare() {
  cd ${pkgname}-${pkgver}

  # FS#15113
  patch -i "${srcdir}/rdesktop-send_physical_buttons.diff"

  # Fix libao segfault, from Fedora
  patch -i "${srcdir}/rdesktop-libao.patch"

  # FS#36488
  patch -i "${srcdir}/rdesktop-tcp.patch"
  patch -i "${srcdir}/rdesktop-r1731.patch"
  patch -i "${srcdir}/rdesktop-r1732.patch"

  # FS#36505
  patch -i "${srcdir}/rdesktop-r1729.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-smartcard \
    --with-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
