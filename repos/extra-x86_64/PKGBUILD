# $Id$
# Maintainer: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=wicd
pkgver=1.5.4
pkgrel=3
pkgdesc="New and alternative wireless/wired network management utility"
arch=('i686' 'x86_64')
url="http://wicd.sourceforge.net/"
license=('GPL2')
depends=('dbus-python' 'pygtk' 'dhcpcd' 'wpa_supplicant' 'ethtool')
optdepends=("hicolor-icon-theme" "wireless_tools")
conflicts=('wicd-svn')
install=wicd.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz wicd-daemon wicd.desktop)
options=(emptydirs)
md5sums=('2a2d120aecded75d4c07fcc4426098f6' 'f40e5f59998d0829707a7c9976afa8f8' \
         'eee9b10d9c860e1c7d195f115dc603f9')
  
build() {
  cd $srcdir/$pkgname-$pkgver
  python setup.py configure --no-install-init --resume=/usr/share/wicd/scripts/ --suspend=/usr/share/wicd/scripts/ --verbose
  python setup.py install --root=$pkgdir

  # Add custom rc.d script
  install -Dm755 ../wicd-daemon $pkgdir/etc/rc.d/wicd
  install -Dm644 ../wicd.desktop $pkgdir/usr/share/applications/wicd.desktop

  cd build/lib/wicd
  for i in *.py; do
    install -Dm 755 $i $pkgdir/usr/lib/wicd/$i
  done
  
  # Remove unneeded files, this breaks wicd supposedly
  #rm -rf  $pkgdir/usr/{share/autostart,lib/wicd/{__init__,gui,misc,networking,wnettools,wpath}.py}
}
