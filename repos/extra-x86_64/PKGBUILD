# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-smokeqt
pkgver=4.8.0
pkgrel=1
pkgdesc="Language independent library for Qt bindings"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebase-runtime' 'qimageblitz' 'qscintilla' 'kdebindings-smokegen')
makedepends=('cmake' 'automoc4')
conflicts=('kdebindings-smoke')
source=("http://download.kde.org/stable/${pkgver}/src/xz/smokeqt-${pkgver}.tar.xz")
sha1sums=('9ad56f1441c92b6d24b7e9ec19fe62da34820731')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../smokeqt-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Qwt5=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
