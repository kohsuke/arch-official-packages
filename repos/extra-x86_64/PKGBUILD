# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Woody Gilk <woody@archlinux.org>

pkgname=gimp-devel
pkgver=2.6.1
pkgrel=1
pkgdesc="GNU Image Manipulation Program (Development Version)"
arch=('i686' 'x86_64')
url="http://www.gimp.org/"
license=('GPL')
depends=('gtk2>=2.12.11' 'lcms>=1.17' 'libxpm>=3.5.7' 'libwmf>=0.2.8.4' \
         'libxmu>=1.0.4' 'librsvg>=2.22.2' 'libmng>=1.0.10' 'dbus-glib>=0.7.6' \
         'libexif>=0.6.16' 'pygtk>=2.12.1' 'desktop-file-utils' 'gegl>=0.0.18')
makedepends=('gutenprint>=5.0.2' 'intltool>=0.35.5' 'libwebkit' \
             'gnome-python>=2.16.2' 'poppler-glib>=0.8.6' 'pkgconfig>=0.21' \
             'alsa-lib>=1.0.16' 'libgnomeui>=2.22.1' 'gtk-doc')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support' \
            'libwebkit: for the help browser' 'poppler-glib: for pdf support' \
            'hal: for Linux input event controller module' \
            'alsa-lib: for MIDI event controller module')
options=('!libtool' '!makeflags')
conflicts=('gimp')
provides=("gimp=${pkgver}")
install=${pkgname}.install
source=(ftp://ftp.gimp.org/pub/gimp/v${pkgver%.*}/gimp-${pkgver}.tar.bz2 linux.gpl)
md5sums=('2fa176d1da75575c88b01019d014744d' 'bb27bc214261d36484093e857f015f38')
sha1sums=('c03c737496444c811c73772d356db3dbf886fd68' '110ce9798173b19a662d086ed7b882b4729f06cf')

build() {
  cd ${srcdir}/gimp-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-mp --enable-gimp-console --enable-gimp-remote \
    --enable-python --with-gif-compression=lzw \
    --without-aa || return 1
  make || return 1
  make DESTDIR=${pkgdir} install-strip || return 1

  install -D -m644 ${srcdir}/linux.gpl \
    ${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl || return 1

#  ln -s gimp-2.5 ${pkgdir}/usr/bin/gimp || return 1
#  ln -s gimp-console-2.5 ${pkgdir}/usr/bin/gimp-console || return 1
#  ln -s gimp-remote-2.5 ${pkgdir}/usr/bin/gimp-remote || return 1
}
