# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-klickety
pkgver=4.10.2
pkgrel=1
pkgdesc="An adaptation of the Clickomania game"
url="http://kde.org/applications/games/klickety/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/klickety-${pkgver}.tar.xz")
sha1sums=('54548dd1ebad1481ecda5a50df7e72a43a6a8cbb')

build() {
  mkdir build
  cd build
  cmake ../klickety-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
