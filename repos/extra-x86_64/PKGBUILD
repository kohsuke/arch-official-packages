# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-kimono
pkgver=4.7.2
pkgrel=1
pkgdesc=".NET/Mono bindings for the KDE libraries"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-qyoto' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost' 'kdepimlibs')
conflicts=('kdebindings-csharp')
source=("http://download.kde.org/stable/${pkgver}/src/kimono-${pkgver}.tar.bz2")
sha1sums=('2c2c331991e2b42101957ecfd13a18fbd4f86a68')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kimono-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Soprano=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
