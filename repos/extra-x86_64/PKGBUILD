# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=xaos
pkgver=3.4
pkgrel=1
pkgdesc="XaoS is a fast portable real-time interactive fractal zoomer"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/xaos/"
license=('GPL')
depends=('slang' 'gsl' 'ncurses' 'libpng' 'zlib' 'aalib>=1.4rc5-5' 'gpm>=1.20.4' 'libxxf86vm' 'libxxf86dga')
install=xaos.install
source=(http://downloads.sourceforge.net/sourceforge/xaos/XaoS-${pkgver}.tar.gz)
md5sums=('366fd8151e9642a0d9afce889912e388')
sha1sums=('4e419715ae0479fa3a20ed5f48ede7ad222f2d10')

build() {
  cd ${srcdir}/XaoS-${pkgver}
  ./configure --prefix=/usr || return 1
  make || return 1
  make prefix=${pkgdir}/usr \
    LOCALEDIR=${pkgdir}/usr/share/locale install || return 1
  
  rm ${pkgdir}/usr/share/info/dir || return 1
  gzip ${pkgdir}/usr/share/info/* || return 1
}
