# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=gnupg2
pkgver=2.0.16
pkgrel=2
pkgdesc="GNU Privacy Guard 2 - a PGP replacement tool"
arch=('i686' 'x86_64')
depends=('libldap' 'curl' 'bzip2' 'zlib' 'libksba' 'libgcrypt'
	'pth' 'libusb' 'libassuan' 'texinfo' 'readline' 'pinentry' 'dirmngr')
license=('GPL')
url="http://www.gnupg.org/"
install=${pkgname}.install
source=(ftp://ftp.gnupg.org/gcrypt/gnupg/gnupg-$pkgver.tar.bz2
       gnupg2-2.0.16-security.patch)

build() {
  cd ${srcdir}/gnupg-$pkgver
  patch -Np0 -i ../gnupg2-2.0.16-security.patch
  ./configure --prefix=/usr --libexecdir=/usr/lib/gnupg2 #$EXTRAOPTS
  make
}

package() {
  cd ${srcdir}/gnupg-$pkgver
  make DESTDIR=${pkgdir} install
  # move conflicting files
  mv ${pkgdir}/usr/share/gnupg{,2}
  rm -f ${pkgdir}/usr/share/info/dir
}
md5sums=('88a4d46deca63d2eca29b2b611304afb'
         'a2c570dcc439283dcad76539a7d6e5a2')
