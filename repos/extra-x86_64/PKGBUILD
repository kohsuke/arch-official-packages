# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=racket
pkgver=5.0
pkgrel=2
pkgdesc="PLT Racket (a variant of Scheme) Environment"
arch=('i686' 'x86_64')
url="http://racket-lang.org/"
license=('LGPL')
depends=('gcc-libs' 'cairo' 'desktop-file-utils' 'libjpeg' 'libgl' 'libxmu' 'libxft')
makedepends=('mesa' 'libxaw')
provides=('drscheme')
conflicts=('drscheme')
replaces=('drscheme')
options=('!makeflags' '!strip')
install=racket.install
source=(http://download.racket-lang.org/installers/${pkgver}/racket/${pkgname}-${pkgver}-src-unix.tgz \
	drracket.desktop libpng-1.4.patch)
md5sums=('369af9d3728ab636e8234d8ee039658c' '9223c9736ead677431542b41271c7b3c'\
         'ec18faef466c1f18047d14a50be0413c')
sha1sums=('9b76bd19da9af1fd684abdebb68ca72a7599541a' 'a20808f6b250225704856f82a544681a962a299d'\
         '2280da2a2ce28b13530c9e749939b895c25fd22e')

build() {
  cd "${srcdir}/plt-${pkgver}/src"
  patch -p2 < ../../libpng-1.4.patch || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "${srcdir}/plt-${pkgver}/src"
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 ../collects/icons/plt.xpm "${pkgdir}/usr/share/pixmaps/drracket.xpm" || return 1
  install -D -m644 "${srcdir}/drracket.desktop" "${pkgdir}/usr/share/applications/drracket.desktop" || return 1
}
