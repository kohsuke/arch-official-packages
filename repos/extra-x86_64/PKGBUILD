# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=clutter-gtk2
_pkgbasename=clutter-gtk
pkgver=0.10.8
pkgrel=3
pkgdesc="GTK clutter widget"
arch=('i686' 'x86_64')
url="http://clutter-project.org/"
options=('!libtool')
license=('LGPL')
depends=('clutter' 'gtk2')
makedepends=('gobject-introspection' 'gtk-doc')
source=(http://www.clutter-project.org/sources/${_pkgbasename}/${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.bz2
        gir.patch
        libm.patch)
sha256sums=('7867f951568871d9f68cbe8a3644fd53d2138f725b7ba61ffc0053eb64e80814'
            'a7bb2e41b155a293fef3a58f9d0486788564a5da9373b00cc5209bdbdcb1ec66'
            '7d5532702eb3c18fff2144f8d501f77d3d9c9c23e6904374f05634fea2d7a8ef')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -i "$srcdir/gir.patch"
  patch -Np1 -i "$srcdir/libm.patch"
  autoreconf -i
  export CFLAGS+=" -lgthread-2.0"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir/usr/share/gtk-doc"
}
