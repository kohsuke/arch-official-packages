# $Id$

pkgbase=fltk
pkgname=('fltk' 'fltk-docs')
pkgver=1.3.0
pkgrel=1
arch=('i686' 'x86_64')
license=('custom:LGPL')
url="http://www.fltk.org/"
depends=('sh' 'libjpeg' 'libpng' 'libxft' 'libxinerama' 'alsa-lib' 'hicolor-icon-theme' 'desktop-file-utils' 'xdg-utils')
makedepends=('mesa' 'doxygen')
source=(http://ftp.easysw.com/pub/$pkgbase/$pkgver/$pkgbase-$pkgver-source.tar.gz)
md5sums=('44d5d7ba06afdd36ea17da6b4b703ca3')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr --enable-threads --enable-xft --enable-shared
  make
  cd documentation
  make html
}

package_fltk() {
  pkgdesc="Graphical user interface toolkit for X"
  options=('!docs')
  install=fltk.install

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install install-desktop
  chmod 644 "$pkgdir"/usr/lib/*.a
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_fltk-docs() {
  pkgdesc="Graphical user interface toolkit for X (documentation)"
  depends=('')

  cd "$srcdir/$pkgbase-$pkgver/documentation"
  install -d "$pkgdir"/usr/share/doc/fltk/
  for _file in html/* ; do
    install -m644 $_file "$pkgdir"/usr/share/doc/fltk/
  done
  cd ../test
  make DESTDIR="$pkgdir" install
  install -D -m644 ../COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
