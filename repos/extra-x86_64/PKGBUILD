# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=amarok
replaces=('amarok-base' 'amarok-engine-xine' 'amarok-base-mysqlfree')
pkgver=2.3.2
pkgrel=3
pkgdesc="A media player for KDE"
arch=("i686" "x86_64")
url="http://amarok.kde.org"
license=('GPL2' 'LGPL2' 'FDL')
depends=('kdebase-runtime' 'mysql' 'qtscriptgenerator' 'taglib-extras' 'liblastfm')
makedepends=('pkgconfig' 'automoc4' 'cmake' 'libgpod' 'libmtp' 'loudmouth' 'docbook-xsl')
optdepends=("libgpod: support Apple iPod audio devices"
	"libmtp: support for portable media devices"
	"loudmouth: backend needed by mp3tunes for syncing")
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
	'scanning-qt-regression.patch'
	'fix-applet-size-on-startup.patch')
md5sums=('4e03dc009f8b44d9b8dfb5f6d1034081'
	'4026759a6c753a5209e655cd4cc36bd8'
	'fdfe4947405fcfa58f273d34b41ddb32')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Already fixed upstream
  patch -Np1 -i ${srcdir}/fix-applet-size-on-startup.patch
  # https://bugs.kde.org/show_bug.cgi?id=251762
  patch -Np1 -i ${srcdir}/scanning-qt-regression.patch

  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
