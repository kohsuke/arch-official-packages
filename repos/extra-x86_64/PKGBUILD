# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=timidity++
pkgver=2.13.2
pkgrel=10
pkgdesc="A MIDI to WAVE converter and player"
arch=('i686' 'x86_64')
url="http://timidity.sourceforge.net"
license=('GPL')
depends=('libao' 'libvorbis' 'jack' 'ncurses')
makedepends=('xaw3d' 'gtk2' 'tk' 'libxaw')
optdepends=('gtk2: for using the GTK+ interface' 'tk: for using the Tk interface' \
            'xaw3d: for using the Xaw interface')
backup=('etc/timidity++/timidity.cfg')
source=(http://downloads.sourceforge.net/timidity/TiMidity++-${pkgver}.tar.bz2 \
        timidity.cfg timidity++.sh 2.13.2-gcc4.patch TiMidity++-2.13.2+flac-1.1.3.patch \
        timidity++-2.13.2-exiterror.patch timidity++-2.13.2-gtk26.patch \
        timidity++-2.13.2-polling.patch)
md5sums=('a82ceeb2245e22f4de2b41da21eaee32' '3fcc8f065c959205113fa7e9ab2be3c6'\
         '35606c12af9c6be9361d64fc536f046f' '0868926c5290817cacb9a1849bc043c2'\
         'fcbd27ff83066f69a3f8bd2442a3b3e2' '31bdaea612f18e2c3d45a8a73ab44c81'\
         '16a4adec164836d4390dc6b0f9a69ce9' '3f0034ead451cdd1078868e9e9b0494c')
sha1sums=('8e603146e1164335303a5ac5dff291f369241b4e' '660b3afbb720d26e8f008034cee66dd8da082d6e'\
         '9f3e732a7ca1e97119a76df62ecf154df04d4f77' '8d50618a2379c078b07e8d0fc59457855c16106c'\
         '59b0dbe3cde8f7b41d83676d8c37809255d3fb36' '7cd697b4f7cb1ce45cec46c2e0f6e5e8bf1d9d60'\
         'ce115e84d99708f0bdb5b57bd98b0c3514fa2bf4' '6abaedaecdecabf9dc76c062f7157571aed53984')

build() {
  cd "${srcdir}/TiMidity++-${pkgver}"
  patch -p1 < ../2.13.2-gcc4.patch
  patch -p1 < ../TiMidity++-2.13.2+flac-1.1.3.patch
  patch -p1 < ../timidity++-2.13.2-exiterror.patch
  patch -p0 < ../timidity++-2.13.2-gtk26.patch
  patch -p0 < ../timidity++-2.13.2-polling.patch
  sed -i 's/tcl8.4/tcl8.5/' configure
  sed -i 's/tk8.4/tk8.5/' configure

  ./configure --prefix=/usr --mandir=/usr/share/man --with-default-path=/etc/timidity++/ \
	--enable-server --enable-alsaseq --enable-spectrogram \
	--enable-audio=alsa,oss,ao,vorbis,flac,esd,jack \
	--enable-dynamic=ncurses,tcltk,vt100,xaw,gtk \
	--disable-gtktest
  make
}

package() {
  cd "${srcdir}/TiMidity++-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../timidity.cfg "${pkgdir}/etc/timidity++/timidity.cfg"
  install -D -m755 ../timidity++.sh "${pkgdir}/etc/rc.d/timidity++"
}
