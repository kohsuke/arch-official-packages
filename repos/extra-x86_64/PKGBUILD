# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
pkgname=telepathy-sofiasip
pkgver=0.7.1
pkgrel=1
pkgdesc="a SIP-protocol connection manager for the Telepathy framework based on SofiaSIP-stack."
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
license=('LGPL')
depends=('telepathy-glib' 'sofia-sip')
makedepends=('libxslt' 'python2')
install=telepathy-sofiasip.install
groups=('telepathy')
source=(http://telepathy.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('4139027790aae97d8a18260e09a747c6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --libexecdir=/usr/lib/telepathy
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
