# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=transfig
pkgver=3.2.5e
pkgrel=1
pkgdesc="Format conversion utility that can be used with xfig"
arch=('i686' 'x86_64')
url="http://www.xfig.org"
license=('custom')
depends=('libpng' 'libxpm' 'bc')
makedepends=('imake')
source=(http://downloads.sourceforge.net/mcj/${pkgname}.${pkgver}.tar.gz LICENSE)
sha1sums=('27aa9691bf84f8775db9be39c453a8132148bad1'
          'b8b712871615308b8b6add92f86d218437d652f2')

prepare() {
  cd ${pkgname}.${pkgver}
  sed -i 's/XCOMM USELATEX2E/USELATEX2E/' transfig/Imakefile
  sed -i 's/XCOMM USEINLINE/USEINLINE/' fig2dev/Imakefile
}

build() {
  cd ${pkgname}.${pkgver}
  xmkmf
  make FIG2DEV_LIBDIR=/usr/share/fig2dev Makefiles
  make FIG2DEV_LIBDIR=/usr/share/fig2dev XFIGLIBDIR=/usr/share/xfig
}

package() {
  cd ${pkgname}.${pkgver}
  make DESTDIR="${pkgdir}" XFIGLIBDIR=/usr/share/xfig \
    FIG2DEV_LIBDIR=/usr/share/fig2dev MANPATH=/usr/share/man \
    install install.man
  mv "${pkgdir}/usr/bin/fig2ps2tex.sh" "${pkgdir}/usr/bin/fig2ps2tex"
  install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
