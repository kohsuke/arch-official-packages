# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gconf-perl
_realname=Gnome2-GConf
pkgver=1.044
pkgrel=5
pkgdesc="Gnome2-GConf perl bindings for gconf"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends')
depends=('glib-perl' 'gconf' 'perl>=5.14')
options=(!emptydirs)
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/${_realname}-${pkgver}.tar.gz)
md5sums=('ea386003b18f067524833b0eeb271330')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  dbus-launch --exit-with-session make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
