# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-step
pkgver=4.8.4
pkgrel=1
pkgdesc="Interactive Physical Simulator"
url="http://kde.org/applications/education/step/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libqalculate' 'gsl')
makedepends=('cmake' 'automoc4' 'eigen')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/step-${pkgver}.tar.xz")
sha1sums=('498d8243309388fa8b59cd968c0c92fc588eda11')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../step-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
