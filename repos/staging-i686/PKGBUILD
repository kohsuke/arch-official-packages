# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=qtwebkit
pkgver=2.2.0
pkgrel=3
arch=('i686' 'x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
depends=('qt' 'gperf' 'bison' 'phonon' 'gstreamer0.10-base')
makedepends=('python2' 'mesa' 'chrpath')
conflicts=('qt<4.8')
source=("http://get.qt.nokia.com/${pkgname}/QtWebKit-${pkgver}.tar.gz"
        'python2-path.patch')
sha1sums=('3582f3a8b593140af004a19479f5ec19693b3df7'
          'b0ef3d5596171e3900a685df9bcfac3068ad6330')

build() {
  cd "${srcdir}"/QtWebKit-${pkgver}

  patch -p1 -i "${srcdir}"/python2-path.patch

  cd Tools/Scripts
  ./build-webkit --qt \
    --prefix=/usr \
    --makeargs="${MAKEFLAGS}" \
    --release \
    --3d-canvas
}

package() {
  cd "${srcdir}"/QtWebKit-${pkgver}
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  # Fix RPATH
  chrpath -r /usr/lib/ "${pkgdir}"/usr/lib/qt/imports/QtWebKit/libqmlwebkitplugin.so
}
