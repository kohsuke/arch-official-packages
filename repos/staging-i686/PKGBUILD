# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-klettres
pkgver=4.7.1
pkgrel=1
pkgdesc="Learn The Alphabet"
url="http://kde.org/applications/education/klettres/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/klettres-${pkgver}.tar.bz2")
sha1sums=('e7fba352257c44d9ee84439dee4d3dfb58d4fd8d')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../klettres-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
