# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
pkgname=xulrunner
pkgver=2.0.1
_ffoxver=4.0.1
pkgrel=2
pkgdesc="Mozilla Runtime Environment"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'gcc-libs' 'libidl2' 'mozilla-common' 'nss' 'libxt' 'libxrender' 'hunspell' 'startup-notification' 'mime-types' 'dbus-glib' 'alsa-lib' 'libevent' 'sqlite3>=3.7.4' 'libnotify')
makedepends=('zip' 'pkg-config' 'diffutils' 'python2' 'wireless_tools' 'yasm' 'mesa')
url="http://wiki.mozilla.org/XUL:Xul_Runner"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${_ffoxver}/source/firefox-${_ffoxver}.source.tar.bz2
        mozconfig
        mozilla-pkgconfig.patch
        xulrunner-version.patch
        xulrunner-omnijar.patch
        port_gnomevfs_to_gio.patch)
options=('!emptydirs')
md5sums=('9abda7d23151e97913c8555a64c13f34'
         '2358a2ddd35bcdd62ff42442dfe548d9'
         '639ea80e823543dd415b90c0ee804186'
         'a0236f6c3e55f60b7888d8cf137ff209'
         '0bf82bc6677e3ce57fd20a147fe8d7b1'
         '42f83468b296452fb754a81a4317ca64')
build() {
  cd "${srcdir}/mozilla-2.0"
  cp "${srcdir}/mozconfig" .mozconfig

  #fix libdir/sdkdir - fedora
  patch -Np1 -i "${srcdir}/mozilla-pkgconfig.patch"

  #Force installation to the same path for every version
  patch -Np1 -i "${srcdir}/xulrunner-version.patch"

  #https://bugzilla.mozilla.org/show_bug.cgi?id=620931
  patch -Np1 -i "${srcdir}/xulrunner-omnijar.patch"

  #https://bugzilla.mozilla.org/show_bug.cgi?id=494163
  patch -Np1 -i "${srcdir}/port_gnomevfs_to_gio.patch"

  unset CFLAGS
  unset CXXFLAGS

  export CXXFLAGS="-fpermissive"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd "${srcdir}/mozilla-2.0"
  make -j1 -f client.mk DESTDIR="${pkgdir}" install

  #Remove included dictionaries, add symlink to system myspell path.
  #Note: this will cause file conflicts when users have installed dictionaries in the old location
  rm -rf "${pkgdir}/usr/lib/xulrunner-2.0/dictionaries"
  ln -sf /usr/share/myspell/dicts "${pkgdir}/usr/lib/xulrunner-2.0/dictionaries"

  # add xulrunner library path to ld.so.conf
  install -d ${pkgdir}/etc/ld.so.conf.d
  echo "/usr/lib/xulrunner-2.0" > ${pkgdir}/etc/ld.so.conf.d/xulrunner.conf
}
