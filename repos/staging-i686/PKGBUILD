# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-acecad
_gitdate=20110318
pkgver=1.4.99_git${_gitdate} # see configure.ac
pkgrel=1
pkgdesc="X.Org acecad tablet input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('sysfsutils')
makedepends=('xorg-server-devel')
conflicts=('xorg-server<1.10.0')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=(#${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	ftp://ftp.archlinux.org/other/$pkgname/${pkgname}-${_gitdate}.tar.bz2)
sha1sums=('f1aa0c13542242316ebd401369959f6c55ae8222')

# source PKGBUILD && mksource
mksource() {
	mkdir /tmp/${pkgname}-${_gitdate}
	pushd /tmp/${pkgname}-${_gitdate}
	git clone -v --depth 1 git://anongit.freedesktop.org/xorg/driver/xf86-input-acecad
	cd xf86-input-acecad
	git archive --prefix=xf86-video-acecad-${_gitdate}/ --format=tar HEAD | bzip2 > /tmp/${pkgname}-${_gitdate}/${pkgname}-${_gitdate}.tar.bz2
	popd
}

build() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
#  ./configure --prefix=/usr
  cd ${srcdir}/xf86-video-acecad-${_gitdate}
  ./autogen.sh --prefix=/usr
  make
}

package() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
  cd ${srcdir}/xf86-video-acecad-${_gitdate}

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
