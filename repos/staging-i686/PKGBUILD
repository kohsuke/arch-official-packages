# $Id$
# Maintainer: Jeff Mickey <jeff@archlinux.org>

pkgname=liquidwar
pkgver=5.6.4
pkgrel=5
pkgdesc="A unique multiplayer wargame"
arch=('i686' 'x86_64')
url="http://www.ufoot.org/liquidwar/"
license=('GPL')
depends=('allegro4')
makedepends=('python2')
install=liquidwar.install
source=(http://www.ufoot.org/download/$pkgname/v5/$pkgver/$pkgname-$pkgver.tar.gz
        http://www.ufoot.org/sig/$pkgname-$pkgver.tar.gz.sig)
sha1sums=('a3006c4da9517c3af3b7bf165b2c86fa369a71c6'
          'a262967d60e0f04ac5b99693144ef6b0038bb6c4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/python/python2/' doc/Makefile.in
  [ "$CARCH" == "x86_64" ] && EXTRAOPTS="--disable-asm"
  LIBS='-lm' PYTHON=yes ./configure --prefix=/usr $EXTRAOPTS
  make GAMEDIR=/usr/bin DATADIR=/usr/share/liquidwar
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" GAMEDIR=/usr/bin DATADIR=/usr/share/liquidwar install_nolink
}
