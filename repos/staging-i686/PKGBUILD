# $Id: PKGBUILD 356 2008-04-18 22:56:27Z aaron $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=ilmbase
pkgver=1.0.3
pkgrel=1
depends=('gcc-libs')
pkgdesc="Base libraries from ILM for OpenEXR"
arch=(i686 x86_64)
url="http://www.openexr.com"
license=('custom')
options=('!libtool')
source=(https://github.com/downloads/openexr/openexr/$pkgname-$pkgver.tar.gz 
        ilmbase-IexMath.patch)
md5sums=('d99962ec7e2bfac0647aaabf6b91d5de'
         '9c8efaf15f60bccf79749ec879657962')

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  patch -p0 -i "${srcdir}/ilmbase-IexMath.patch"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  # one of the tests fails randomly for an unknown reason
  make check || true
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
