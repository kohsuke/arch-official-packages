# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=ocfs2-tools
pkgver=1.4.3
pkgrel=2
pkgdesc="Oracle Cluster File System 2 utilities"
arch=(i686 x86_64)
url="http://oss.oracle.com/projects/ocfs2-tools/"
depends=('e2fsprogs' 'glib2' 'readline>=6.0.00')
license=('GPL2')
source=(http://oss.oracle.com/projects/ocfs2-tools/dist/files/source/v1.4/$pkgname-$pkgver.tar.gz
gcc45-ftbfs.patch)
options=(!makeflags)
optdepends=('python2: for using ocfs2console')
md5sums=('7d8895ea41999ce8f468e2bc7dc55ddc'
         '5dba18ca5596b1de0f2eeb30f5ed1656')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's#/usr/bin/python#/usr/bin/python2#' ocfs2console/ocfs2console
  # Fix build with gcc 4.5
  # http://svn.debian.org/viewsvn/collab-maint?view=rev&revision=15814
  patch -p1 -i ${srcdir}/gcc45-ftbfs.patch
  ./configure --prefix=/usr --mandir=/usr/share/man
  make LDFLAGS=-L/usr/lib
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -m 755 -D $srcdir/$pkgname-$pkgver/vendor/common/ocfs2.init \
	$pkgdir/etc/rc.d/ocfs2
  install -m 755 -D $srcdir/$pkgname-$pkgver/vendor/common/o2cb.init \
	$pkgdir/etc/rc.d/o2cb
}
