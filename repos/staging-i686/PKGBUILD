# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=timidity++
pkgver=2.14.0
pkgrel=4
pkgdesc="A MIDI to WAVE converter and player"
arch=('i686' 'x86_64')
url="http://timidity.sourceforge.net"
license=('GPL')
depends=('libao' 'jack')
makedepends=('xaw3d' 'gtk2' 'tk' 'libxaw')
optdepends=('gtk2: for using the GTK+ interface' 'tk: for using the Tk interface' \
            'xaw3d: for using the Xaw interface')
backup=('etc/timidity++/timidity.cfg')
source=(http://downloads.sourceforge.net/timidity/TiMidity++-${pkgver}.tar.xz \
        timidity.cfg timidity++.sh timidity.service)
sha1sums=('15ec27f1ea3e718a8d61603521fc16df5c0dd24b'
          '660b3afbb720d26e8f008034cee66dd8da082d6e'
          '9f3e732a7ca1e97119a76df62ecf154df04d4f77'
          'f0351b9eb8928d3d82c380107654a7dbe7cd2d54')

build() {
  cd "${srcdir}/TiMidity++-${pkgver}"
  sed -i -e 's/tcl8.5/tcl8.6 tcl8.5/' -e 's/tk8.5/tk8.6 tk8.5/' configure
  sed -i 's/my_interp->result/(char *) Tcl_GetObjResult(my_interp)/' interface/tk_c.c
  ./configure --prefix=/usr --mandir=/usr/share/man --with-default-path=/etc/timidity++/ \
    --enable-server --enable-alsaseq --enable-spectrogram --enable-network --enable-xft \
    --enable-audio=alsa,oss,ao,vorbis,flac,jack \
    --enable-dynamic=ncurses,tcltk,vt100,xaw,gtk
  make
}

package() {
  cd "${srcdir}/TiMidity++-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../timidity.cfg "${pkgdir}/etc/timidity++/timidity.cfg"
  install -D -m755 ../timidity++.sh "${pkgdir}/etc/rc.d/timidity++"
  install -D -m644 ../timidity.service "${pkgdir}/usr/lib/systemd/system/timidity.service"
}
