# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgbase=liborigin2
pkgname=('liborigin2' 'liborigin2-docs')
pkgver=20100913
pkgrel=3
arch=('i686' 'x86_64')
url="http://soft.proindependent.com/liborigin2/"
license=('GPL3')
makedepends=('doxygen' 'qt' 'boost')
replaces=('liborigin')
provides=('liborigin')
source=(http://download.berlios.de/qtiplot/${pkgbase}-13092010.zip
	liborigin.pro.archlinux
	http://tree.phi-sci.com/tree.hh)
md5sums=('e2b41c4d8a1e2d357dd2b5b331d31375' '2b19f7a4bd120f6d689edc327468e48e'\
         'd24a1c3bdd811354ea7f5885d86ddacb')
sha1sums=('3f17d5a8c484ddb864a29a91a94d677b0b3b18d7' '1664b00929d6fba6e5ebcf7cb422e1da1b3897a5'\
         'd10344d969e0b892c21e1bd5bd3541d838526b24')

build() {
	cd "${srcdir}/${pkgbase}"

	install -Dm644 "${srcdir}/liborigin.pro.archlinux" ./liborigin.pro
        install -Dm644 "${srcdir}/tree.hh" ./tree.hh # get the header back like it was in previous releases
	qmake liborigin.pro QMAKESPEC=linux-g++
  	make QTDIR=/usr/ QMAKESPEC=linux-g++

	 # create documentation
        cd doc
        doxygen
}

package_liborigin2() {
        pkgdesc="A library for reading OriginLab OPJ project files"
        depends=('gcc-libs' 'boost-libs')

        cd "${srcdir}/${pkgbase}"

  	INSTALL_ROOT="${pkgdir}" make install

        # install missing includes
        install -Dm644 tree.hh "${pkgdir}/usr/include/liborigin2/tree.hh"
        for i in *.h; do
          install -Dm644 ${i} "${pkgdir}/usr/include/liborigin2/${i}"
        done
}
package_liborigin2-docs() {
        pkgdesc="Documentation for liborigin2: a library for reading OriginLab OPJ project files"

        cd "${srcdir}/${pkgbase}/doc"
	install -d "${pkgdir}/usr/share/doc/${pkgbase}"
	cp -rf html/* "${pkgdir}/usr/share/doc/${pkgbase}"
	chmod 755 "${pkgdir}/usr/share/doc/liborigin2/images"
	chmod 644 "${pkgdir}/usr/share/doc/liborigin2/images/origin_import.png"
}
