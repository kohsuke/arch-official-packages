# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fbida
pkgver=2.07
pkgrel=4
pkgdesc="Few applications to display and elementary edit images: fbi, fbgs, ida, exiftran"
arch=('i686' 'x86_64')
url="http://linux.bytesex.org/fbida/"
license=('GPL2')
depends=('giflib' 'libtiff' 'libjpeg' 'libexif' 'lesstif' 'libxpm' 'libpng' 'libx11' 'libxext' 'fontconfig')
optdepends=('ghostscript: to use fbgs')
replaces=('fbi')
provides=('fbi')
backup=('etc/X11/app-defaults/Ida')
source=(http://dl.bytesex.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz libpng-1.4.patch)
md5sums=('3e05910fb7c1d9b2bd3e272d96db069c' '31b5a5318c16808d55403e06110ff5a5')
sha1sums=('4758178299e09d5251b9cf20337a81cc20553d45' 'dcb39c82e7e6f4d7d6e8d0e95c661f83a2655c7f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../libpng-1.4.patch || return 1
  rm jpeg/jpeg*
  make Make.config || return 1
  for config in HAVE_LIB{SANE,CURL,LIRC}; do
     sed -i "s/$config.*/$config := no/" Make.config || return 1
  done
  make CC=gcc || return 1
  make DESTDIR="${pkgdir}" prefix=/usr install || return 1
}

