# $Id$ 
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgnome
pkgver=2.27.5
pkgrel=1
pkgdesc="Common libraries for GNOME"
arch=(i686 x86_64)
license=('LGPL')
depends=('gnome-vfs>=2.24.1-2' 'libbonobo>=2.24.1' 'gconf>=2.26.2-3')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=libgnome.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('f989f7c5c263bd3dc7454b0b4ddb154bbd39b81a11138ccca57d72404b01a546')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --disable-esd || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain libgnome-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
