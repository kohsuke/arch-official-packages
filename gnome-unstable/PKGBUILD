# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=seahorse-plugins
pkgver=2.27.1
pkgrel=1
pkgdesc="Seahorse plugins"
arch=(i686 x86_64)
license=('GPL')
depends=('gnome-panel>=2.27.91' 'seahorse>=2.27.90' 'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils>=0.17.4' 'pkgconfig' 'gedit>=2.27.4' 'nautilus>=2.27.91')
groups=('gnome-extra')
url="http://www.gnome.org"
options=(!libtool !emptydirs)
install=seahorse-plugins.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.27/${pkgname}-${pkgver}.tar.bz2
        seahorse-plugins-2.26.2-gpgme-lfs.patch)
sha256sums=('b4c8ba73844bc2a95a10ccfc149c3edea49fc8aadecd658e764d5caf562389c5'
            'c757c40d705174df3e01b572b64a97fe0599555b943658adbc4278eedf79e807')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/seahorse-plugins-2.26.2-gpgme-lfs.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper \
      --disable-update-mime-database \
      --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain seahorse-plugins ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
