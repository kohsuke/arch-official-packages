# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=devicekit-disks
pkgver=005
pkgrel=1
pkgdesc="Disk Management Service"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/DeviceKit"
license=('GPL')
depends=('udev>=145' 'zlib' 'sg3_utils' 'sqlite3' 'glib2>=2.21.4' 'dbus-glib>=0.80' 'polkit>=0.93' 'parted>=1.8.8' 'device-mapper>=1.02.33' 'libatasmart>=0.5')
options=(!libtool)
makedepends=('intltool')
source=(http://hal.freedesktop.org/releases/DeviceKit-disks-${pkgver}.tar.gz
        DeviceKit-disks-005-port-to-new-libatasmart.patch)
md5sums=('8c0e5daa492e044a5a4f65b9412c0b34'
         'ae14b0d242a6f675a735d191b3e7823a')

build() {
  cd "${srcdir}/DeviceKit-disks-${pkgver}"
  patch -Np1 -i "${srcdir}/DeviceKit-disks-005-port-to-new-libatasmart.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/DeviceKit --disable-static || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
