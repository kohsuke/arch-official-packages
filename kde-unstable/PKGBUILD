# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.2.90
pkgrel=1
pkgdesc="KDE Base Runtime Environment"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
groups=('kde')
depends=('kdelibs' 'kdelibs-experimental' 'oxygen-icons' 'openslp'
         'hicolor-icon-theme' 'smbclient' 'rarian' 'xorg-xauth')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs')
optdepends=('htdig: to build the search index'
            'gdb: drkonq crash handler')
install='kdebase-runtime.install'
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('c710e2eef51349c09cce0f523ab7b543')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_PulseAudio=OFF
	make
	make DESTDIR=$pkgdir install
	rm -f ${pkgdir}/usr/share/icons/hicolor/index.theme
	ln -sf /usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/
}
options=('!strip')
