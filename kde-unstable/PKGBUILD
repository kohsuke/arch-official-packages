# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-python
pkgver=4.6.80
pkgrel=1
pkgdesc="KDE bindings for python"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebindings')
depends=('kdepim-runtime' 'python2-pyqt' 'qscintilla' 'boost-libs')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/unstable/${pkgver}/src/pykde4-${pkgver}.tar.bz2"
        'fix-build.patch')
sha1sums=('f45e40298046ecea382f9b85c34b3e21d856a9da'
          '3d2329981ea571d38f0354c6d1300a2743a775db')

build() {
  cd "${srcdir}"/pykde4-${pkgver}
  patch -p3 -i "${srcdir}"/fix-build.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  # Use the python2 executable
  find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
sha1sums=('f45e40298046ecea382f9b85c34b3e21d856a9da'
          '4eb8917ad922819bbea038d79a9786cf5d0e2767')
