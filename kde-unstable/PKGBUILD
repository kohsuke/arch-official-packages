# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-killbots
pkgver=4.9.80
pkgrel=1
pkgdesc="A simple game of evading killer robots"
url="http://kde.org/applications/games/killbots/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/killbots-${pkgver}.tar.xz")
sha1sums=('403e36ec16722d985bc5f58ecb2e28dcb83007f3')

build() {
  mkdir build
  cd build
  cmake ../killbots-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
