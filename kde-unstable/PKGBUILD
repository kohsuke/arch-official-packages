# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-smokekde
pkgver=4.8.80
pkgrel=1
pkgdesc="Language independent library for KDE bindings"
url="https://projects.kde.org/projects/kde/kdebindings/smoke/smokekde"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost'
             'kdepimlibs' 'kdegraphics-okular' 'kdesdk-kate')
optdepends=('kdesdk-kate: Kate bindings'
            'kdegraphics-okular: Okular bindings')
conflicts=('kdebindings-smoke')
source=("http://download.kde.org/unstable/${pkgver}/src/smokekde-${pkgver}.tar.xz")
sha1sums=('d349b9fe6ef29c0717d3faa831d2f2d1f0281290')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../smokekde-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Soprano=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
