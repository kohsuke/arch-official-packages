# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase
pkgver=4.2.90
pkgrel=1
pkgdesc="KDE Base Applications"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdebase-runtime' 'libraw1394')
makedepends=('kdebase-workspace' 'pkgconfig' 'cmake' 'automoc4')
optdepends=('kdebase-workspace')
options=('docs')
install='kdebase.install'
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('295467ac1181c1dc09c6f7dc61bece1e')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
}
