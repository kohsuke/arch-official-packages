# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kspaceduel
pkgver=4.9.80
pkgrel=1
pkgdesc="Each of two possible players controls a satellite spaceship orbiting the sun"
url="http://kde.org/applications/games/kspaceduel/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kspaceduel-${pkgver}.tar.xz")
sha1sums=('44ed8e2d47db3e6eebd3e843a33ef484fe8f69c6')

build() {
  mkdir build
  cd build
  cmake ../kspaceduel-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
