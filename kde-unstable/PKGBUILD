# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebase
pkgname=('kdebase-dolphin'
         'kdebase-kappfinder'
         'kdebase-kdepasswd'
         'kdebase-kdialog'
         'kdebase-keditbookmarks'
         'kdebase-kfind'
         'kdebase-kinfocenter'
         'kdebase-konqueror'
         'kdebase-konsole'
         'kdebase-kwrite'
         'kdebase-lib'
         'kdebase-nsplugins'
         'kdebase-plasma')
pkgver=4.2.90
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
makedepends=('kdebase-workspace' 'pkgconfig' 'cmake' 'automoc4' 'kdebase-runtime' 'libraw1394')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('295467ac1181c1dc09c6f7dc61bece1e')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdebase-dolphin() {
	pkgdesc='a file manager'
	depends=('kdebase-runtime' 'kdebase-lib')
	install='kdebase.install'
	cd $srcdir/build/apps/dolphin
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/dolphin
	make DESTDIR=$pkgdir install
}

package_kdebase-kappfinder() {
	pkgdesc='a utility to search for non-KDE applications and create menu items for them'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdebase.install'
	cd $srcdir/build/apps/kappfinder
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kappfinder
	make DESTDIR=$pkgdir install
}

package_kdebase-kdepasswd() {
	pkgdesc='a utility for changing your password (on UNIX systems)'
	depends=('kdebase-runtime' 'kdebase-lib')
	install='kdebase.install'
	cd $srcdir/build/apps/kdepasswd
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kdepasswd
	make DESTDIR=$pkgdir install
}

package_kdebase-kdialog() {
	pkgdesc='a utility for displaying dialog boxes from shell scripts'
	depends=('kdelibs')
	install='kdebase.install'
	cd $srcdir/build/apps/kdialog
	make DESTDIR=$pkgdir install
}

package_kdebase-keditbookmarks() {
	pkgdesc='a utility for editing the KDE bookmark store'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdebase.install'
	cd $srcdir/build/apps/keditbookmarks
	make DESTDIR=$pkgdir install
}

package_kdebase-kfind() {
	pkgdesc='a utility for searching for files and directories'
	depends=('kdebase-runtime' 'kdebase-lib')
	install='kdebase.install'
	cd $srcdir/build/apps/kfind
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kfind
	make DESTDIR=$pkgdir install
}

package_kdebase-kinfocenter() {
	pkgdesc=''
	depends=('kdebase-runtime' 'libraw1394' 'kdelibs')
	install='kdebase.install'
	cd $srcdir/build/apps/kinfocenter
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kinfocenter
	make DESTDIR=$pkgdir install
}

package_kdebase-konqueror() {
	pkgdesc='a web browser and general frame for embedding KParts'
	depends=('kdebase-runtime' 'kdebase-lib' 'kdebase-nsplugins' 'kdebase-keditbookmarks')
	install='kdebase.install'
	cd $srcdir/build/apps/konqueror
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/konqueror
	make DESTDIR=$pkgdir install
}

package_kdebase-konsole() {
	pkgdesc='a terminal emulator'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdebase.install'
	cd $srcdir/build/apps/konsole
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/konsole
	make DESTDIR=$pkgdir install
}

package_kdebase-kwrite() {
	pkgdesc='a text editor with several advanced features, including syntax highlighting and spell-checking'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdebase.install'
	cd $srcdir/build/apps/kwrite
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kwrite
	make DESTDIR=$pkgdir install
}

package_kdebase-lib() {
	pkgdesc=''
	groups=()
	depends=('kdelibs')
	install='kdebase.install'
	cd $srcdir/build/apps/lib
	make DESTDIR=$pkgdir install
}

package_kdebase-nsplugins() {
	pkgdesc=''
	depends=('kdelibs')
	install='kdebase.install'
	cd $srcdir/build/apps/nsplugins
	make DESTDIR=$pkgdir install
}

package_kdebase-plasma() {
	pkgdesc=''
	depends=('kdebase-workspace' 'kdebase-lib')
	install='kdebase.install'
	cd $srcdir/build/apps/plasma
	make DESTDIR=$pkgdir install
}

