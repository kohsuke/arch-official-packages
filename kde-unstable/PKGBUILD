# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-kimono
pkgver=4.6.80
pkgrel=1
pkgdesc=".NET/Mono bindings for the KDE libraries"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebindings')
depends=('kdebindings-qyoto' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen')
replaces=('kdebindings-csharp')
source=("http://download.kde.org/unstable/${pkgver}/src/kimono-${pkgver}.tar.bz2")
sha1sums=('911dfbee3141ca0f72c13e15bafc549ccd9ab33a')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kimono-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
