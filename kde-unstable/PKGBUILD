# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>

pkgname=virtuoso
pkgver=5.0.12
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A scalable cross-platform server that combines SQL/RDF/XML Data Management with Web Application Server and Web Services Platform functionality'
url='http://virtuoso.openlinksw.com/wiki/main/Main/'
license=('GPL')
depends=('openssl')
makedepends=('bison' 'flex')
# downgrade from 6.0.0-3
options=('!libtool' 'force' '!makeflags')
source=("http://downloads.sourceforge.net/virtuoso/${pkgname}-opensource-${pkgver}.tar.gz")
md5sums=('64949660e3f053ec43d00519a19751aa')

build() {
  cd $srcdir/${pkgname}-opensource-${pkgver}

  ./configure --prefix=/usr \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-rendezvous \
    --disable-hslookup \
    --disable-all-vads || return 1
  make || return 1
}

package() {
  # install server
  cd ${srcdir}/${pkgname}-opensource-${pkgver}/binsrc/virtuoso
  make DESTDIR=${pkgdir} install || return 1

  # install driver
  cd ${srcdir}/${pkgname}-opensource-${pkgver}/binsrc/driver
  make DESTDIR=${pkgdir} install || return 1
}
