# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-marble
pkgver=4.6.95
pkgrel=1
pkgdesc="Desktop Globe"
url="http://kde.org/applications/education/marble/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'gpsd')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/marble-${pkgver}.tar.bz2"
        'gpsd-2.96.patch')
sha1sums=('f70645d42f246e38e912d84d4009ec0795be4c31'
          'f44dfd3bb384e631d59b93d7dda3413795da8183')

build() {
  cd "${srcdir}"/marble-${pkgver}
  patch -p1 -i "${srcdir}"/gpsd-2.96.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../marble-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
