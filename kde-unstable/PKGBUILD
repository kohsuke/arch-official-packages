# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.2.90
pkgrel=2
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('phonon' 'shared-mime-info' 'hal' 'xz-utils'
	 'enchant' 'jasper' 'openexr' 'strigi' 'libxtst'
	 'giflib' 'soprano>2.2.3' 'ca-certificates' 'xdg-utils')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl' 'hspell')
replaces=('arts')
install='kdelibs.install'
source=(http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2
        'kde-applications-menu.patch' 'archlinux-menu.patch')
md5sums=('49ddb0f7a20fb2141c2f284df84cb1e2'
         'e94450ba5430ea9c1e33bad9ae38ca2d'
         'a6b31e1e5812340f010ef4cdd77e356c')

build() {
	cd $srcdir/${pkgname}-${pkgver}

	# avoid file conflict with gnome-menu
	patch -p1 -i $srcdir/kde-applications-menu.patch
	# add Archlinux menu entry
	patch -p1 -i $srcdir/archlinux-menu.patch
	cd ..
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DKDE_DISTRIBUTION_TEXT='Arch Linux' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF
	make
	make DESTDIR=$pkgdir install

	# cert bundle seems to be hardcoded
	# link it to the one from ca-certificates
	rm -f $pkgdir/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/usr/share/apps/kssl/ca-bundle.crt
}
