# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kiten
pkgver=4.6.95
pkgrel=1
pkgdesc="Japanese Reference/Study Tool"
url="http://kde.org/applications/education/kiten/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kiten-${pkgver}.tar.bz2")
sha1sums=('c83f9078141fe2868be1b0040cbe204228aacb1c')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kiten-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
