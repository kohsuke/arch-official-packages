# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=nepomuk-core
pkgver=4.10.97
pkgrel=2
pkgdesc="Contains the central Nepomuk services like file indexing, file system monitoring, query, storage, client libraries"
url="https://projects.kde.org/projects/kde/kdelibs/nepomuk-core"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'poppler-qt' 'taglib' 'ffmpeg' 'ebook-tools')
makedepends=('cmake' 'automoc4' 'doxygen')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'virtuoso617.patch')
sha1sums=('b35fc166c07c1372d3ba73513780fc41c2005a8f'
          '2643c311027c758dff635159c6b8a0ebb6088c3e')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/virtuoso617.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # Fix the python shebang
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/nepomuk-simpleresource-rcgen
}
