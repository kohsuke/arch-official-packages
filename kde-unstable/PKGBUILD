# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-audiocd-kio
pkgver=4.9.95
pkgrel=2
pkgdesc="Kioslave for accessing audio CDs"
url='https://projects.kde.org/projects/kde/kdemultimedia/audiocd-kio'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkcddb' 'libkcompactdisc' 'lame' 'cdparanoia')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdemultimedia')
replaces=('kdemultimedia-kioslave')
conflicts=('kdemultimedia-kioslave')
source=("http://download.kde.org/unstable/${pkgver}/src/audiocd-kio-${pkgver}.tar.xz")
sha1sums=('bf116156ff1f671aa398de0f4d3641ce321d1a06')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../audiocd-kio-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
