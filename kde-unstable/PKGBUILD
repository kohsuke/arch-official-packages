# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kstars
pkgver=4.10.80
pkgrel=1
pkgdesc="Desktop Planetarium"
url="http://kde.org/applications/education/kstars/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'wcslib' 'libindi')
makedepends=('cmake' 'automoc4' 'eigen')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kstars-${pkgver}.tar.xz")
sha1sums=('0c8e16398799270ffbd936b02d2d9b09869af6c5')

build() {
  mkdir build
  cd build
  cmake ../kstars-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Xplanet=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
