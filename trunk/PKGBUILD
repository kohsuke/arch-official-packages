# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Felipe Machado aka arch_audio <machado.felipe@gmail.com>

pkgname=rubberband
pkgver=1.6.0
pkgrel=1
pkgdesc="Time-stretching and pitch-shifting audio library and utility"
arch=('i686' 'x86_64')
url="http://www.breakfastquay.com/rubberband/"
license=('GPL')
depends=('libsamplerate' 'fftw' 'vamp-plugin-sdk')
makedepends=('ladspa')
source=("http://code.breakfastquay.com/attachments/download/16/$pkgname-$pkgver.tar.bz2"
        'gcc46.patch'
        'vectorops.patch')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix gcc 4.6 compatibility
  patch -Np1 -i "$srcdir/gcc46.patch"

  # fix bug exposed by gcc 4.6
  # see https://bugs.archlinux.org/task/26140
  patch -Np1 -i "$srcdir/vectorops.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
md5sums=('28e3dc1f5ae694d6846bcb0ef3d597fc'
         '7c2f404975da6052f6b80eac12efbeb4'
         '4bdc9689179d3a2cf64172f0f911701d')
