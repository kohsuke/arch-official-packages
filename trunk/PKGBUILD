# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Todd Musall <tmusall@comcast.net>

pkgname=grip
pkgver=3.3.1
pkgrel=9
pkgdesc="A cd-player and ripper for the Gnome desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://nostatic.org/grip/"
depends=('vte' 'curl' 'libgnomeui' 'id3lib' 'cdparanoia')
optdepends=('yelp: to display the help and notices')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz \
        grip-3.3.1-invalid-genre-size.patch)
md5sums=('4b4233999b9f2bc85c711092553ea9aa'
         '046448be7d1580e86a2211cd8661d855')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/grip-3.3.1-invalid-genre-size.patch"
  sed -i '/[.] conftest[.]id3/s: c: ./c:' configure 
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
