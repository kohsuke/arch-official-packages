# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
pkgname=haskell-network
pkgver=2.2.1.7
pkgrel=4
pkgdesc="Networking-related facilities"
url="http://hackage.haskell.org/cgi-bin/hackage-scripts/package/network"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=(ghc=6.12.3 sh 'haskell-parsec=2.1.0.1')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/network/$pkgver/network-$pkgver.tar.gz)
install=haskell-network.install

build() {
    cd ${srcdir}/network-$pkgver
    runhaskell Setup configure -p --enable-split-objs --enable-shared --prefix=/usr
    runhaskell Setup build
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
md5sums=('566cfeef09ff4d2e52110ec4a9a9879b')
