# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor : shining <shiningxc.at.gmail.com>
# Contributor : cyberdune <cyberdune@gmail.com>

pkgname=octave
pkgver=3.6.2
pkgrel=2
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('i686' 'x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw' 'pcre' 'curl' 'lapack' 'libx11' 'graphicsmagick' 'glpk' 'hdf5' 'gcc-libs' 'qhull' 'fltk' 'suitesparse')
makedepends=('texinfo' 'graphicsmagick' 'gcc-fortran' 'suitesparse' 'texlive-core')
optdepends=('texinfo: for help-support in octave'
            'gnuplot: alternative plotting')
source=("ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.bz2"
        buildfix.diff
        octave-gethelp.patch)
options=('!emptydirs')
install=octave.install
sha1sums=('145fef0122268086727a60e1c33e29d56fd546d7'
          'a7cdba1e05fbdd182facdd804aeec37729255551'
          'd39d54763ac86114029f8bc5d60763d2adce27bb')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # patch by Gentoo http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/sci-mathematics/octave/files/octave-3.4.3-gets.patch?view=log
  patch -Np0 -i ${srcdir}/buildfix.diff
  # http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/sci-mathematics/octave/files/octave-3.4.3-help.patch?view=log
  patch -Np0 -i ${srcdir}/octave-gethelp.patch

  autoreconf -vfi

  # http://www.nabble.com/Random-rounding-errors-td16010966.html
  FFLAGS="-O -ffloat-store" \

  ./configure --prefix=/usr --libexecdir=/usr/lib \
  --enable-shared --disable-static \
  --with-umfpack="-lumfpack -lsuitesparseconfig" # https://mailman.cae.wisc.edu/pipermail/help-octave/2012-September/053991.html

  LANG=C make
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  # add octave library path to ld.so.conf.d
  install -d ${pkgdir}/etc/ld.so.conf.d
  echo "/usr/lib/${pkgname}/${pkgver}" > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf
}
