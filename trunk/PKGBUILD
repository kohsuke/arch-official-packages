# $Id$
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=eclipse
pkgver=3.6
_internal_pkgver=3.6
pkgrel=1
_date=201006080911
pkgdesc="An IDE for Java and other languages"
arch=('i686' 'x86_64')
url="http://eclipse.org"
depends=('java-environment' 'gtk2' 'unzip' 'xulrunner')
install=${pkgname}.install
makedepends=('zip')
license=("EPL/1.1")
source=("ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops/R-${pkgver}-${_date}/eclipse-SDK-$pkgver-linux-gtk.tar.gz"
	'http://www.bearfruit.org/files/eclipse-icon-clean.svg'
	'eclipse.sh'
	'eclipse.desktop'
	'eclipse.ini.patch')
md5sums=('5f14c2de0dd9d9af614a0be53121a326'
	'77cff7543ccf7e177cb6667f5a20ce19'
	'7ea99a30fbaf06ec29261541b8eb1e23'
	'ba8a37e30a7ebd69774cec87c69e8c44'
	'017b2a68edc48a752a7e27c7aceef797')
[ "$CARCH" = "x86_64" ] && source[0]="ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops/R-${pkgver}-${_date}/eclipse-SDK-$pkgver-linux-gtk-x86_64.tar.gz"
[ "$CARCH" = "x86_64" ] && md5sums[0]='8cefbf42bb8f08b814e32726bb207f25'

package() {
  cd "${srcdir}/eclipse"

  # patch to increase default memory limits
  patch -Np1 -i "${srcdir}/eclipse.ini.patch"

  # install eclipse
  install -m755 -d "${pkgdir}/usr/share"
  mv "${srcdir}/eclipse" "${pkgdir}/usr/share/"

  # install misc
  install -d ${pkgdir}/usr/bin ${pkgdir}/usr/share/applications \
    ${pkgdir}/usr/share/icons/hicolor/{16x16,32x32,48x48}/apps
  install -m755 "${srcdir}/eclipse.sh" "${pkgdir}/usr/bin/eclipse"
  install -m644 "${srcdir}/eclipse.desktop" "${pkgdir}/usr/share/applications/"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_${_internal_pkgver}.v${_date}/eclipse.png \
    "${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse.png"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_${_internal_pkgver}.v${_date}/eclipse32.png \
    "${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse.png"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_${_internal_pkgver}.v${_date}/eclipse48.png \
    "${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse.png"

  # install icon
  install -Dm644 ${srcdir}/eclipse-icon-clean.svg \
    ${pkgdir}/usr/share/icons/hicolor/scalable/apps/eclipse.svg
}
