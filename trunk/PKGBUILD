# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-intel-legacy
pkgver=2.3.2
pkgrel=1
_mesaver=7.0.4
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ legacy video drivers"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libdrm>=2.4.5-3' 'libxvmc' 'expat>=2.0.1')
makedepends=('pkgconfig' 'xorg-server>=1.6.0' 'xf86driproto>=2.0.4' 'glproto>=1.4.9' 'mesa>=7.4' 'imake')
conflicts=('xorg-server<1.6.0' 'xf86-video-i810' 'xf86-video-intel' 'intel-dri')
options=('!libtool')
source=(${url}/releases/individual/driver/xf86-video-intel-${pkgver}.tar.bz2
	http://downloads.sourceforge.net/sourceforge/mesa3d/MesaLib-${_mesaver}.tar.bz2
	intel-newserver.patch)

build() {
  cd "${srcdir}/xf86-video-intel-${pkgver}"
  patch -Np1 -i "${srcdir}/intel-newserver.patch" || return 1
  ./configure --prefix=/usr --enable-dri || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  cd "${srcdir}/Mesa-${_mesaver}/configs"
  CONFIG="linux-dri-x86"
  [ "$CARCH" = "x86_64" ] && CONFIG="linux-dri-x86-64"
  echo "EXTRA_LIB_PATH =" >> ${CONFIG}
  echo "OPT_FLAGS = ${CFLAGS}" >> ${CONFIG}
  echo "SRC_DIRS = glx/x11 mesa" >> ${CONFIG}
  echo "USING_EGL = 0" >> ${CONFIG}
  echo "PROGRAM_DIRS =" >> ${CONFIG}
  echo "MKDEP = makedepend" >> ${CONFIG}
  echo "DRI_DIRS = i810 i915 i965" >> ${CONFIG}
  echo "DRI_DRIVER_SEARCH_DIR = /usr/lib/xorg/modules/dri" >> ${CONFIG}
  echo "DRI_DRIVER_INSTALL_DIR = /usr/lib/xorg/modules/dri" >> ${CONFIG}
  echo "ARCH_FLAGS += -DGLX_USE_TLS" >> ${CONFIG}
  echo "X11_INCLUDES = `pkg-config --cflags-only-I x11`" >> ${CONFIG}

  ln -s ${CONFIG} current
  cd "${srcdir}/Mesa-${_mesaver}/src/mesa"
  make linux-solo || return 1
  cd drivers/dri
  make DESTDIR="${pkgdir}" install || return 1

}
