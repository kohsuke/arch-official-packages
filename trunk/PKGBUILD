# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=cagibi
pkgver=0.2.0
pkgrel=2
pkgdesc='A cache/proxy daemon for SSDP (the discovery part of UPnP)'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/cagibi/'
license=('LGPL')
depends=('qt')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('af66d89337ef80e8a868d3bb9dd92328')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
