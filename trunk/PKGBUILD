# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=sonata
pkgver=1.6.2.1
pkgrel=7
pkgdesc="Elegant GTK+ music client for MPD"
arch=('i686' 'x86_64')
url="http://sonata.berlios.de/"
license=('GPL3')
depends=('pygtk' 'python-mpd')
optdepends=('gnome-python-extras: Enhanced system tray support'
            'tagpy: Metadata editing support'
            'dbus-python: Various extra functionality (e.g. multimedia keys support)')
source=(http://download.berlios.de/${pkgname}/${pkgname}-$pkgver.tar.gz
        info.py.patch)
changelog=ChangeLog
md5sums=('0b912325e7175abad3bf6c0edc071e05'
         '595fcf7615035829b264afecb5a1beeb')

build() {
  cd "$srcdir/${pkgname}-$pkgver"
  patch -Np0 -i $srcdir/info.py.patch
}

package() {
  cd "$srcdir/${pkgname}-$pkgver"
  python2 setup.py install --prefix=/usr --optimize 1 --root="$pkgdir"
}
