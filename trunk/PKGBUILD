# $ Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-workspace
pkgver=4.1.0
pkgrel=4
pkgdesc="KDE Base Workspace"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdelibs' 'kdebase-runtime' 'libcaptury' 'libxxf86misc' 'libxcomposite' 'libxres'
	 'libusb' 'libxss' 'lm_sensors' 'libxinerama' 'libxklavier' 'qimageblitz')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'networkmanager' 'kdemultimedia')
optdepends=('kdemultimedia')
replaces=('kdmtheme' 'kde-common')
conflicts=('kde-common')
options=('docs')
backup=('usr/share/config/kdm/kdmrc')
source=("ftp://ftp.archlinux.org/other/kde/${pkgname}-${pkgver}.tar.bz2"
        'kdm-zsh-profile.patch' 'kdm' 'kde.pam' 'kde-np.pam' 'kscreensaver.pam')
md5sums=('d8dbe16f5807536a429bbf9fd33e82a5'
         '40f5556820d52a14e9d9722a5a8a491e'
         '5d80164932e0d44d8b802d3929a004ab'
         '10a490653b002e6f9e7476ff9d37c011'
         '552337fd9a3982d809ea16c7f0033d42'
         '367a3538f54db71f108b34cfa31088ac')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	patch -Np1 -i ../kdm-zsh-profile.patch || return 1
	cd ../
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF
	make
	make DESTDIR=$pkgdir install

	install -D -m755 ${srcdir}/kdm ${pkgdir}/etc/rc.d/kdm
	install -D -m644 ${srcdir}/kde.pam ${pkgdir}/etc/pam.d/kde
	install -D -m644 ${srcdir}/kde-np.pam ${pkgdir}/etc/pam.d/kde-np
	install -D -m644 ${srcdir}/kscreensaver.pam ${pkgdir}/etc/pam.d/kscreensaver
	install -d -m755 ${pkgdir}/etc/X11/sessions/
	ln -sf /usr/share/apps/kdm/sessions/kde{,-safe}.desktop ${pkgdir}/etc/X11/sessions/
}
