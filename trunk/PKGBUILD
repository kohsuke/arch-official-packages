# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=racket
pkgver=5.1
pkgrel=1
pkgdesc="PLT Racket (a variant of Scheme) Environment"
arch=('i686' 'x86_64')
url="http://racket-lang.org/"
license=('LGPL')
depends=('gtk2' 'libjpeg' 'desktop-file-utils')
provides=('drscheme')
conflicts=('drscheme')
replaces=('drscheme')
options=('!strip')
install=racket.install
source=(http://download.racket-lang.org/installers/${pkgver}/racket/${pkgname}-${pkgver}-src-unix.tgz \
        drracket.desktop jpeg-version.patch)
md5sums=('c3b0bffb998ad530c06512d045a59f15' '9223c9736ead677431542b41271c7b3c'\
         '0aae72b6f19963c569d20fa8793482e1')
sha1sums=('6c314a71bab77eb48efad8f6c9fa597b6a5a305d' 'a20808f6b250225704856f82a544681a962a299d'\
         'afa13f3ef66f5434b209f545f82545a869ada2b2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  (cd ..; patch -p1 -i "${srcdir}/jpeg-version.patch")
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../collects/icons/plt.xpm "${pkgdir}/usr/share/pixmaps/drracket.xpm"
  install -D -m644 "${srcdir}/drracket.desktop" "${pkgdir}/usr/share/applications/drracket.desktop"
}
