# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepimlibs
pkgver=4.8.3
pkgrel=1
pkgdesc="KDE PIM Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
depends=('kdelibs' 'gpgme' 'akonadi' 'libical' 'prison')
makedepends=('cmake' 'automoc4' 'boost' 'cyrus-sasl')
install='kdepimlibs.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('453badec44fa2fd8b551fc9f8df2d51c88930dca')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
}
