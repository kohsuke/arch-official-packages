# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Andrew Rose <ody@netrux.com>

pkgname=gif2png
pkgver=2.5.8
pkgrel=1
pkgdesc="A GIF to PNG image format converter"
arch=('i686' 'x86_64')
url="http://www.catb.org/~esr/gif2png/"
license=('ZLIB')
depends=('libpng')
optdepends=('python2: for using web2png')
source=("http://www.catb.org/~esr/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('0f5caeeca561ade7f267ef3f2a9944b4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's#env python#env python2#' web2png

  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
