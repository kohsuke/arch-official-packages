# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=rdesktop
pkgver=1.7.0
pkgrel=1
depends=('libx11' 'openssl' 'libao' 'libsamplerate')
pkgdesc="An open source client for Windows Remote Desktop Services"
url="http://www.rdesktop.org/"
license=('GPL')
arch=('i686' 'x86_64')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'rdesktop-send_physical_buttons.diff')
md5sums=('77fa93b21aa38837a85e81c00e757228'
         '880d3aeac67b901e6bf44d1323374768')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # FS#15113
  patch -i ${srcdir}/rdesktop-send_physical_buttons.diff

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
