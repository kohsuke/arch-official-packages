# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-trident
pkgver=1.3.1
pkgrel=3
pkgdesc="X.org Trident video driver"
_mesaver="7.3"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libgl>=7.3' 'expat')
makedepends=('pkgconfig' 'xorg-server>=1.6.0' 'mesa>=7.3' 'xf86driproto' 'dri2proto>=1.99.3' 'xf86dgaproto' 'glproto')
conflicts=('xorg-server<1.6.0')
groups=('xorg-video-drivers')
options=(!libtool)
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        http://downloads.sourceforge.net/sourceforge/mesa3d/MesaLib-${_mesaver}.tar.bz2
	mesa-7.1-link-shared.patch LICENSE)
md5sums=('9f280bef235426394013cb5e1854b823' '781e7811a6ed5c97b2b8defefc8ffbc9'\
         'f0baa948d9810f268413111ee439d24b' '92c153c192ee248572ec5fe8232a3d4d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  cd "${srcdir}/Mesa-${_mesaver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=trident \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
