# $ Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.1.0
pkgrel=1
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('libxcursor' 'phonon' 'shared-mime-info' 'qt>=4.4'
	 'enchant' 'jasper' 'openexr' 'strigi' 'bzip2' 'libxslt'
	 'giflib' 'soprano' 'ca-certificates' 'heimdal')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl')
replaces=('arts')
conflicts=('gnome-menus')
options=('docs')
source=("ftp://ftp.archlinux.org/other/kde/${pkgname}-${pkgver}.tar.bz2")
md5sums=('a72fd8653b86f73860d0e7ebf0f2d606')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_DISTRIBUTION_TEXT='Arch Linux' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF
	make
	make DESTDIR=$pkgdir install

	# cert bundle seems to be hardcoded
	# link it to the one from ca-certificates
	rm -f $pkgdir/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/usr/share/apps/kssl/ca-bundle.crt
}