# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=avogadro
pkgver=1.0.1
pkgrel=3
pkgdesc="An advanced molecular editor based on Qt"
arch=('i686' 'x86_64')
url="http://avogadro.openmolecules.net/wiki/Main_Page"
license=('GPL2')
depends=('openbabel' 'pyqt' 'boost-libs' 'glew' 'python-numpy')
makedepends=('cmake' 'boost')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'sip-4.11.patch')
md5sums=('0d5c391197101f0aab7be6b59f81e6fd'
        '5202204b1381cf298a583aab06d9cca4')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np0 -i ${srcdir}/sip-4.11.patch

  cd ${srcdir}
  mkdir build
  cd build

  # use the cmake module in system to enable python 2.7 support
  rm ../${pkgname}-${pkgver}/cmake/modules/FindPythonLibs.cmake

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
