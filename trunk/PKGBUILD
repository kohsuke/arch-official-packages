# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevplatform
pkgver=1.2.90
_pkgver=4.2.90
pkgrel=1
pkgdesc="A C/C++ development platform for KDE"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdelibs' 'subversion' 'qjson')
optdepends=("kdesdk-kompare: difference checking")
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/unstable/kdevelop/${_pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('1049eb9c723225c24a38cf2e34381feb370013fc')

build() {
  cd "${srcdir}"
  mkdir build
  cd build 
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
