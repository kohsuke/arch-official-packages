# $Id: PKGBUILD 105813 2011-01-12 00:25:58Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub-at-gmail.com>

pkgname=kdeplasma-applets-networkmanagement
epoch=1
pkgver=git20110323
_commit=986cae8974998213496a8678ad03523dba83e69d
pkgrel=1
pkgdesc="KDE control panel and widget network connections"
arch=('i686' 'x86_64')
url="http://www.kde.org/"
license=('GPL')
depends=('kdebase-workspace' 'networkmanager')
makedepends=('cmake' 'python2' 'automoc4' 'mobile-broadband-provider-info')
optdepends=('mobile-broadband-provider-info: allow to add new mobile connection')
install=${pkgname}.install
source=("${pkgname}-${pkgver}.tar.gz"::"http://quickgit.kde.org/?p=networkmanagement.git&a=snapshot&h=${_commit}"
        'nm-09.patch')
md5sums=('8373cd729b12a821b28b483e3e9d253f'
         'c46839bc0503f2dbec76282b8ab163bd')

build() {
  cd "${srcdir}/networkmanagement"
  patch -p1 -i "${srcdir}/nm-09.patch"

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../networkmanagement \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDBUS_SYSTEM_POLICY_DIR=/etc/dbus-1/system.d
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
