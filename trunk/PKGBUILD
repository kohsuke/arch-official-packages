# $Id: PKGBUILD,v 1.9 2009/02/06 16:41:19 abhidg Exp $
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgbase=libofx
pkgname=('libofx' 'libofx-doc')
pkgver=0.9.1
pkgrel=1
pkgdesc="API for the OFX banking standard"
arch=('i686' 'x86_64')
url="http://libofx.sourceforge.net"
license=('GPL')
depends=('opensp' 'curl' 'gcc-libs')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz \
        fix_cpp_includes.patch)
md5sums=('e634f47ca12e82d860912d48c93b8e50' '17b65fe122dbb2df222aeee698bdf049')
sha1sums=('a12110ddcbff596e26496865570fb4dcb75c08d0' 'adbab270bbd2ea938a5392217c87a268a43b4991')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i "$srcdir/fix_cpp_includes.patch"
  ./configure --prefix=/usr
  make
}

package_libofx() {
  options=('!libtool' '!docs')

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

package_libofx-doc() {
  pkgdesc="Documention of the OFX banking standard API"
  depends=()

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-docDATA
  cd doc
  make DESTDIR="$pkgdir" install
}
