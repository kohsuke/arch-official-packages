# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
pkgver=0.15
pkgrel=3
pkgdesc="A full featured and easy to use Jabber client"
arch=('any')
url="http://www.gajim.org/"
license=('GPL3')
depends=('pygtk' 'python2-pyopenssl' 'hicolor-icon-theme' 'dnsutils' 'python2-pyasn1')
makedepends=('gettext' 'intltool')
optdepends=('dbus-python: dbus support'
            'farstream: for video/voice support'
            'gstreamer0.10-bad: for video/voice support'
            'gtkspell: for spelling support'
            'libxss: for idle module'
            'notification-daemon: for desktop notification'
            'python-gnomekeyring: for GnomeKeyring support'
            'pycrypto: support for E2E encryption'
            'python2-docutils: for RST generator support'
            'gupnp-igd: for UPnP-IGD support')
options=('!libtool')
install=gajim.install
source=(http://www.gajim.org/downloads/${pkgver}/gajim-${pkgver}.tar.bz2 \
        gajim-port-to-farstream.patch)
sha1sums=('68934aa40090d21b2937df551373e454f5aa6bb6'
          '50fbda69e481d6d2b239adfe4c50ac4415b2e93d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i ../gajim-port-to-farstream.patch
  PYTHON=python2 ./configure --prefix=/usr --enable-site-packages
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
