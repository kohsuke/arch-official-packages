# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepimlibs
pkgver=4.5.0
pkgrel=1
pkgdesc="KDE PIM Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
depends=('kdelibs' 'akonadi' 'libical' 'gpgme')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'cyrus-sasl' 'openldap' 'docbook-xsl')
install='kdepimlibs.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
	'contactsearch-speed-fix.diff')
sha1sums=('b49afbdad6a906b9e2affdf8a77d4bf312677b69'
	'ff31718b0963585e730207352e2d3eb4d5c9d04f')

build() {
	# Already fixed in 4.5.1
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -Np0 -i ${srcdir}/contactsearch-speed-fix.diff

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
}
