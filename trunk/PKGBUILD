# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>

pkgbase=obexd
pkgname=('obexd-client' 'obexd-server')
pkgver=0.29
pkgrel=3
url="http://www.bluez.org/"
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('openobex' 'libical')
source=(http://www.kernel.org/pub/linux/bluetooth/${pkgbase}-${pkgver}.tar.bz2)
md5sums=('3e90cff18f1339a44adde2c920f95393')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --libexecdir=/usr/lib/obexd
  make
}

package_obexd-client() {
  pkgdesc="D-Bus service providing high-level OBEX client side functionality"
  depends=('openobex' 'glib2')
  provides=("obexd=${pkgver}")
  replaces=('obexd')
  conflicts=('obexd')

  cd "${srcdir}/${pkgbase}-${pkgver}/client"
  install -m755 -d "${pkgdir}/usr/lib/obexd"
  install -m755 -d "${pkgdir}/usr/share/dbus-1/services"
  install -m755 obex-client "${pkgdir}/usr/lib/obexd/"
  install -m644 obex-client.service "${pkgdir}/usr/share/dbus-1/services/"
}

package_obexd-server() {
  pkgdesc="D-Bus service providing high-level OBEX server side functionality"
  depends=('openobex' 'glib2' 'libical')

  cd "${srcdir}/${pkgbase}-${pkgver}/src"
  install -m755 -d "${pkgdir}/usr/lib/obexd"
  install -m755 -d "${pkgdir}/usr/share/dbus-1/services"
  install -m755 obexd "${pkgdir}/usr/lib/obexd/"
  install -m644 obexd.service "${pkgdir}/usr/share/dbus-1/services/"
}
