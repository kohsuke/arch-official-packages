# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-games
pkgver=2.29.6
pkgrel=2
pkgdesc="Some Games for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libsm' 'gconf>=2.28.0' 'guile>=1.8.7' 'librsvg>=2.26.0' 'filesystem>=2009.11' 'desktop-file-utils' 'gnome-python-desktop>=2.29.1' 'libcanberra>=0.21' 'clutter-gtk>=0.10.2' 'hicolor-icon-theme' 'seed>=2.29.90')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.18.1' 'intltool')
provides=('glchess' 'gnome-sudoku')
conflicts=('glchess' 'gnome-sudoku')
options=('!emptydirs' '!libtool')
install=gnome-games.install
url="http://www.gnome.org"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.29/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('c80f14099ae9d1b6a525386a26cb8e60de1580cde3710bc7116d4b8e96c61a41')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper \
      --disable-static \
      --with-scores-user=root --with-scores-group=games \
      --enable-introspection=yes || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  # Remove all scores, we generate them from postinstall
  rm -rf "${pkgdir}/var"

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-games ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
