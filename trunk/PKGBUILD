# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xaos
pkgver=3.5
pkgrel=4
pkgdesc="A fast portable real-time interactive fractal zoomer"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/xaos/"
license=('GPL')
depends=('gsl' 'libpng' 'aalib')
options=('!makeflags')
install=xaos.install
source=(http://downloads.sourceforge.net/sourceforge/xaos/${pkgname}-${pkgver}.tar.gz)
md5sums=('465d1469b276501a58bf325e493ea591')
sha1sums=('6d16a58187fba7276e6bd0547cc2fd6bb073b801')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-gsl --with-aa-driver
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
