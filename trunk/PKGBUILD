# $Id$
#  Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=glsof
pkgver=1.5
_fmver=1.5
_qver=1.0.0
pkgrel=1
pkgdesc="Two separate GUI utilities (Queries and Filemonitor) to the lsof command line utility"
arch=('i686' 'x86_64')
url="http://glsof.sourceforge.net/"
license=('GPL3')
depends=('sh' 'java-runtime' 'lsof')
source=(http://downloads.sourceforge.net/sourceforge/glsof/filemonitor-${_fmver}.tar.gz)
if [ "$CARCH" = "i686" ]; then
  source=(${source[@]} http://downloads.sourceforge.net/sourceforge/glsof/queries-${_qver}/queries_linux32.tar.gz \
          glsof-filemonitor glsof-queries)
  md5sums=('6ca86f9f356e9cba02e4f0861eb1c464'
           '895bcec79edea97ebde36d7f5eaa3ee9'
           '7e43c1120e15e65bd1a12d6f188be4a0'
           '980da4b39e1e3f4fbed08018dfd3be7b')
elif [ "$CARCH" = "x86_64" ]; then
  source=(${source[@]} http://downloads.sourceforge.net/sourceforge/glsof/queries-${_qver}/queries_linux64.tar.gz \
          glsof-filemonitor glsof-queries)
  md5sums=('6ca86f9f356e9cba02e4f0861eb1c464'
           'ed96ee2105428aa8b038f5fe13b8cd1d'
           '7e43c1120e15e65bd1a12d6f188be4a0'
           '980da4b39e1e3f4fbed08018dfd3be7b')
fi

package() {
 cd "${srcdir}"
 install -D -m755 glsof-filemonitor "${pkgdir}/usr/bin/glsof-filemonitor"
 install -D -m755 glsof-queries "${pkgdir}/usr/bin/glsof-queries"
 install -D -m644 filemonitor/filemonitor.jar "${pkgdir}/usr/share/java/glsof/filemonitor.jar"
 install -D -m644 queries_linux??/queries.jar "${pkgdir}/usr/share/java/glsof/queries.jar"
 install -D -m644 queries_linux??/queries.pdf "${pkgdir}/usr/share/doc/glsof/queries.pdf"
}
