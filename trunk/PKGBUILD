# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgname=miro
pkgver=1.2.7
pkgrel=2
pkgdesc="The free and open source internet TV platform"
arch=('i686' 'x86_64')
url="http://www.getmiro.com"
license=('GPL2')
depends=('xine-lib>=1.1.12' 'boost>=1.36.0' 'gnome-python-extras>=2.19.1'
	 'dbus-python>=0.82.4' 'pyrex' 'xulrunner>=1.9.0.1' 'gtk2' 'nspr')
makedepends=('pkgconfig' 'qt3')
install=miro.install
conflicts=('democracy')
replaces=('democracy')
provides=('democracy')

#use our own checkout, the official one is missing files
#http://ftp.osuosl.org/pub/pculture.org/miro/src/Miro-${pkgver}.tar.gz
source=(http://dev.archlinux.org/~ronald/Miro-1.2.7.tar.gz
        missing-headers.patch
	Miro-xulrunner.patch
	boost_1.36.0_fix.patch)
md5sums=('e55150642de55fc52eb2599e3dc6fe21'
         'b5482eac565a26817af0105b061a8ad5'
         '3c06b40cd13c2fb202f58dcf64d4b524'
         '3b4e1eac01155b9de381b0744ba9d725')

build() {
  cd ${srcdir}/Miro-${pkgver}
  patch -Np1 -i ${srcdir}/missing-headers.patch || return 1
  patch -Np1 -i ${srcdir}/Miro-xulrunner.patch || return 1
  patch -Np1 -i ${srcdir}/boost_1.36.0_fix.patch || return 1

  cd platform/gtk-x11
  python setup.py install --root=${pkgdir} || return 1
}
