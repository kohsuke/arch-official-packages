# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=xfig
pkgver=3.2.5b
pkgrel=5
pkgdesc="An interactive drawing tool"
arch=('i686' 'x86_64')
url="http://www.xfig.org/userman/"
license=('custom')
depends=('libpng' 'libjpeg' 'xaw3d' 'libxi' 'desktop-file-utils' 'gsfonts')
makedepends=('imake')
optdepends=('transfig: to use the frontend to convert fig files')
options=('!makeflags' '!emptydirs')
install=xfig.install
source=(http://downloads.sourceforge.net/mcj/${pkgname}.${pkgver}.full.tar.gz \
	xfig-3.2.4-redhat.patch xfig-3.2.5-color-resources.patch \
        xfig-3.2.5-enable-Xaw3d.patch xfig-3.2.5-urwfonts.patch \
        xfig-3.2.5-fhs.patch xfig-3.2.5-missing-protos.patch \
        xfig.3.2.5-modularX.patch xfig-3.2.5b-fix-eps-reading.patch LICENSE xfig.desktop)
md5sums=('499b0ce103a6b353453bf7e327f9a3b9' '5c2bc208c8ee852bc9ffa00dcbfd8241'\
         'a4b91e4aaf48ed07f4a579a1f692ef82' '46c6fb15d26c9fe03e558bdacc0b0a53'\
         'c5b7dc054063f02c6bfcd132dae82b6f' 'b532871c792716465e3b05967bcfba10'\
         'de059a6b8ce287942c7da35d19ee46f3' '7347389f7fdecd582579845df0c0b664'\
         'fa14e85b5365d07ca8deca9ea47d4e6a' 'f405b7a96f3401c12153fe6170a7d8f1'\
         '8ac2de637c9148aa4a32978910822230')
sha1sums=('0730d7e6bc217c0de02682efb0078821512bb542' '926ad99c7221baa4aa9e9737722958860b2700fc'\
         'b7c03fb70066ff476a4039a8c0f8930c15edef68' '33bff26b42ce164e79de11808b5b34b363f0d59f'\
         'e595ccdbe293ea0fdecbf59a2192ae57ced2c8eb' '39bec6bac9746b643b05fd1286826113ede07ff6'\
         'bd76cd50be6a5789cfd6f182309cfd7056be04f3' 'a1285f4e61ad2eec040460ab03e8022098e7f78e'\
         '12a64ddaf65a73e23ef74f31483515a0e262768c' '31edf4cfab708820ea3f114d095dfef5aa88e5aa'\
         '9011b8fa6143e37525b28cc9800a3bf2d6b008d2')

build() {
  cd "${srcdir}/${pkgname}.${pkgver}"
  patch -p1 < ../xfig-3.2.4-redhat.patch
  patch -p1 < ../xfig-3.2.5-fhs.patch
  patch -p1 < ../xfig.3.2.5-modularX.patch
  patch -p1 < ../xfig-3.2.5-enable-Xaw3d.patch
  patch -p1 < ../xfig-3.2.5-color-resources.patch
  patch -p1 < ../xfig-3.2.5-urwfonts.patch
  patch -p1 < ../xfig-3.2.5-missing-protos.patch
  patch -p1 < ../xfig-3.2.5b-fix-eps-reading.patch
  chmod 755 Libraries

  xmkmf
  make DOCDIR=/usr/share/doc
}

package() {
  cd "${srcdir}/${pkgname}.${pkgver}"
  make DESTDIR="${pkgdir}" DOCDIR=/usr/share/doc install.all
  rm "${pkgdir}/usr/lib/X11/app-defaults"

  find "${pkgdir}/usr/share/xfig/Libraries" -type f -exec chmod 0644 {} \;
  find "${pkgdir}/usr/share/xfig/Libraries" -type d -exec chmod 0755 {} \;
  find "${pkgdir}/usr/share/doc/xfig" -type f -exec chmod 0644 {} \;
  find "${pkgdir}/usr/share/doc/xfig" -type d -exec chmod 0755 {} \;

  install -D -m644 ../xfig.desktop "${pkgdir}/usr/share/applications/xfig.desktop"
  install -D -m644 xfig.png  "${pkgdir}/usr/share/pixmaps/xfig.png"
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"	
}
