# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=sox
pkgver=14.3.2
pkgrel=1
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('i686' 'x86_64')
url="http://sox.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('libtool' 'file' 'libsndfile' 'libpng' 'lame' 'opencore-amr')
makedepends=('ffmpeg' 'libao' 'libmad' 'libid3tag')
optdepends=('libao: for ao plugin' 'ffmpeg: for ffmpeg plugin' 'libmad: for mp3 plugin' \
            'libid3tag: for mp3 plugin')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f6e1999b05d9b0cc9bb47ce8d3208dee')
sha1sums=('026636c90d7accba76225a2821aaa2ffa6fe41a3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-dyn-default --with-distro="Arch Linux"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  ln -sf play "${pkgdir}/usr/bin/rec"
  ln -sf ../man1/sox.1.gz "${pkgdir}/usr/share/man/man7/soxeffect.7"
}
