# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-doc-utils
pkgver=0.20.2
pkgrel=2
pkgdesc="Documentation utilities for Gnome"
arch=('any')
license=('GPL' 'LGPL')
depends=('libxslt>=1.1.26' 'python2' 'docbook-xml' 'rarian>=0.8.1')
makedepends=('pkgconfig' 'intltool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.20/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('f6c0124f61b9400c63da630dd3a9ac1b738eac0058da245661ff699fad267aca')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
