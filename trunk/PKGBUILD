# $Id$
#
# Contributor: Paul Mattal <paul@mattal.com>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: GARETTE Emmanuel <gnunux at laposte dot net>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=dovecot
pkgver=2.0.0
pkgrel=1
pkgdesc="An IMAP and POP3 server written with security primarily in mind"
arch=('i686' 'x86_64')
url="http://dovecot.org/"
license=("LGPL")
depends=("pam>=1.0.4" "libmysqlclient>=5.1.38" "postgresql-libs>=8.4.1-1" "sqlite3>=3.6.18" "db>=4.8"
	 "heimdal>=1.3.1" 'libcap>=2.17' 'bzip2' 'curl')
optdepends=('libldap: ldap plugin')
makedepends=('libldap>=2.4.18-1')
provides=('imap-server' 'pop3-server')
options=('!libtool')
backup=(etc/dovecot/dovecot.conf
	etc/dovecot/conf.d/{10-auth,10-director,10-logging,10-mail,10-master,10-ssl}.conf
	etc/dovecot/conf.d/{15-lda.conf,20-imap,20-lmtp,20-pop3}.conf
	etc/dovecot/conf.d/{90-acl,90-plugin,90-quota}.conf
	etc/dovecot/conf.d/auth-{checkpassword,deny,ldap,master,passwdfile,sql,static,system,vpopmail}.conf.ext
	etc/ssl/dovecot-openssl.cnf)
install=$pkgname.install
source=(http://dovecot.org/releases/2.0/${pkgname}-${pkgver}.tar.gz dovecot.sh)
md5sums=('2ca3cbd523cac1cbd53a904255dcd4f8'
         'd020d43eab4ded6fb379dadc570a9490')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  # configure with openssl, mysql, and postgresql support
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
  	--libexecdir=/usr/lib  --with-moduledir=/usr/lib/dovecot/modules \
	--with-nss \
	--with-pam \
	--with-mysql \
	--with-pgsql \
	--with-sqlite \
	--with-ssl=openssl --with-ssldir=/etc/dovecot/ssl \
	--with-gssapi \
	--with-ldap=plugin \
	--with-zlib --with-bzlib \
	--with-libcap \
	--with-solr \
	--with-docs
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install

  # install the launch script
  install -D -m755 ${srcdir}/$pkgname.sh ${pkgdir}/etc/rc.d/$pkgname

  # install example conf files and ssl.conf
  install -d -m755 ${pkgdir}/etc/dovecot/conf.d
  install -m 644 ${pkgdir}/usr/share/doc/dovecot/example-config/conf.d/*.conf ${pkgdir}/etc/dovecot/conf.d
  install -m 644 ${pkgdir}/usr/share/doc/dovecot/example-config/conf.d/*.conf.ext ${pkgdir}/etc/dovecot/conf.d
  install -m 644 ${pkgdir}/usr/share/doc/dovecot/example-config/dovecot.conf ${pkgdir}/etc/dovecot/
  install -d -m755 ${pkgdir}/etc/ssl
  install -m 644  ${srcdir}/$pkgname-$pkgver/doc/dovecot-openssl.cnf ${pkgdir}/etc/ssl/

  rm ${pkgdir}/etc/dovecot/README
  
  # fix paths in the config example file where we have a different layout
#  sed -i 's|/usr/lib/dovecot|/usr/lib/dovecot/modules|g;
#	  s|libexec|lib|g;
#	  s|lib/sendmail|sbin/sendmail|g' $pkgdir/etc/dovecot/dovecot.conf || return 1
}
