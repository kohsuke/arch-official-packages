# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-desktop-sharp
pkgver=2.26.0
pkgrel=6
pkgdesc="GNOME desktop bindings for C#"
arch=('i686' 'x86_64')
license=(LGPL)
url="http://gtk-sharp.sourceforge.net"
depends=('gnome-sharp' 'gnome-desktop2' 'vte' 'librsvg' 'libwnck' 'gtkhtml' 'gtksourceview2')
makedepends=('monodoc' 'libgnomeprintui')
optdepends=('libgnomeprintui')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
	gnome-desktop-sharp-lib-target.patch)
md5sums=('4bc990900bb318b2ba0b0e7998bb47d1'
         '980cd3adf5e745f4caee0a172a51dcc3')

build() {
 # get rid of that .wapi errors; thanks to brice
  export MONO_SHARED_DIR="$srcdir/wapi"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i $srcdir/gnome-desktop-sharp-lib-target.patch
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make GACUTIL="/usr/bin/gacutil /root ${pkgdir}/usr/lib" \
      DESTDIR="$pkgdir" install
}
