# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-server-utils
pkgver=7.4
pkgrel=1
pkgdesc="X.Org utilities required by xorg-server"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxfontcache' 'libxi' 'libxmu' 'libxxf86misc' 'libxrandr' 'libxxf86vm' 'mcpp>=2.6')
makedepends=('pkgconfig' 'xbitmaps' 'bigreqsproto' 'xtrans')
source=(${url}/releases/individual/app/iceauth-1.0.2.tar.bz2
        ${url}/releases/individual/app/rgb-1.0.3.tar.bz2
        ${url}/releases/individual/app/sessreg-1.0.4.tar.bz2
        ${url}/releases/individual/app/xcmsdb-1.0.1.tar.bz2
        ${url}/releases/individual/app/xbacklight-1.1.tar.bz2
	${url}/releases/individual/app/xgamma-1.0.2.tar.bz2
        ${url}/releases/individual/app/xhost-1.0.2.tar.bz2
        ${url}/releases/individual/app/xmodmap-1.0.3.tar.bz2
        ${url}/releases/individual/app/xrandr-1.2.3.tar.bz2
        ${url}/releases/individual/app/xrdb-1.0.5.tar.bz2
        ${url}/releases/individual/app/xrefresh-1.0.2.tar.bz2
        ${url}/releases/individual/app/xset-1.0.4.tar.bz2
        ${url}/releases/individual/app/xsetmode-1.0.0.tar.bz2
        ${url}/releases/individual/app/xsetroot-1.0.2.tar.bz2)

build() {
  cd ${startdir}/src
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      case "${i}" in
        xrdb*)
	  ./configure --prefix=/usr --with-cpp=/usr/bin/mcpp\ -@old || return 1
	;;
	*)
	  ./configure --prefix=/usr || return 1
	;;
      esac
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      popd
    fi
  done
}
