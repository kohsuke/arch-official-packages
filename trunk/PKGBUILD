# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca> 
# Contributor: Jaroslaw Swierczynski <swiergot@juvepoland.com>
# Contributor: Nicolai Lissner <nlissne@linux01.gwdg.de>

pkgname=dvdauthor
pkgver=0.7.0
pkgrel=4
pkgdesc="DVD authoring tools"
arch=('i686' 'x86_64')
url="http://dvdauthor.sourceforge.net/"
license=('GPL')
depends=('libdvdread' 'imagemagick' 'libxml2' 'libpng' 'fribidi')
options=('!makeflags')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'dvdauthor-0.7.0-libpng15.patch')
md5sums=('33a447fb98ab3293ac40f869eedc17ff'
         '9ea90bbea6adb0469c8c1b2f7d033f11')

build() {
  cd "${srcdir}/${pkgname}"

  patch -Np0 -i "${srcdir}/dvdauthor-0.7.0-libpng15.patch"

  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install
}
