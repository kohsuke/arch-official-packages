# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
# Package originally generated by cabal2arch 0.4.1
pkgname=haskell-zlib
pkgrel=4
pkgver=0.5.2.0
pkgdesc="Compression and decompression in the gzip and zlib formats"
url="http://hackage.haskell.org/cgi-bin/hackage-scripts/package/zlib"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=(ghc=6.12.3 'zlib' sh)
options=('strip')
source=(http://hackage.haskell.org/packages/archive/zlib/$pkgver/zlib-$pkgver.tar.gz)
install=haskell-zlib.install

build() {
    cd ${srcdir}/zlib-$pkgver
    runhaskell Setup configure -p --enable-split-objs --enable-shared --prefix=/usr
    runhaskell Setup build
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
}

package() {
    cd ${srcdir}/zlib-$pkgver
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
md5sums=('19859e241dc18ef1501a5d44d8523507')
