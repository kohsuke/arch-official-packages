# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=libwebkit
pkgname=(libwebkit libwebkit3)
pkgver=1.8.3
pkgrel=1
pkgdesc="An opensource web content engine"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite' 'icu' 'gstreamer0.10-base' 'libsoup' 'enchant' 'libgl'
         'geoclue')
makedepends=('gperf' 'gobject-introspection' 'python2' 'gtk2' 'gtk3' 'mesa')
options=('!libtool' '!emptydirs')
source=(http://webkitgtk.org/releases/webkit-$pkgver.tar.xz
        bison26.diff)
sha256sums=('ada02d636af61aed38f142d3cded662d141ce71264f624c4eb873621a74cc9e7'
            '2a9c0c1b9d5259fd1254ebea228fc83313267fbe8cfec1bb43356de1f355b93e')

build() {
  cd "$srcdir/webkit-$pkgver"
  # https://bugs.webkit.org/show_bug.cgi?id=92264 / http://trac.webkit.org/changeset/124099
  patch -Np1 -i ${srcdir}/bison26.diff

  mkdir build-gtk{2,3}

  # clear makeflags: race during introspection.
  # WebKit*.gir should be created before WebKit*.typelib, but isn't
  #MAKEFLAGS=

  ( cd build-gtk2 && _build --with-gtk=2.0 )
  ( cd build-gtk3 && _build --with-gtk=3.0 )
}

_build() {
  PYTHON=/usr/bin/python2 ../configure --prefix=/usr \
    --enable-introspection \
    --with-font-backend=freetype \
    --with-unicode-backend=icu \
    --enable-spellcheck "$@"
  #make all stamp-po
  
  # crazy smp build fix by LFS - http://www.linuxfromscratch.org/blfs/view/svn/x/webkitgtk.html
  # !!! safes a lot of time but may cover other bugs you won't notice anymore !!!
  error_count=0
  until make all stamp-po #-j$(getconf _NPROCESSORS_ONLN)
  do
    if (( $((error_count++)) > 4 ))
    then
      echo Too Many Errors
      break
    fi
  done
  unset error_count
}

package_libwebkit() {
  pkgdesc+=" (for GTK2)"
  depends+=(gtk2)

  cd "$srcdir/webkit-$pkgver/build-gtk2"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/libwebkit/LICENSE"
}

package_libwebkit3() {
  pkgdesc+=" (for GTK3)"
  depends+=(gtk3)

  cd "$srcdir/webkit-$pkgver/build-gtk3"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/libwebkit3/LICENSE"
}



