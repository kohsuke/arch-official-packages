# $Id$
# Maintainer:
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Michel Brabants <michel.brabants@euphonynet.be>

pkgname=libzrtpcpp
pkgver=2.1.2
pkgrel=1
pkgdesc="A C++ implementation of Phil Zimmermann's ZRTP specification"
arch=('i686' 'x86_64')
url='http://www.gnutelephony.org/index.php/GNU_ZRTP'
license=('GPL3')
depends=('ccrtp')
makedepends=('cmake')
source=("https://github.com/downloads/wernerd/ZRTPCPP/${pkgname}-${pkgver}.tar.bz2")
md5sums=('2f78cffbdb0f3e0ee92609fc01a1b6c5')

build() {
  sed 's/lib64/lib/g' -i "${srcdir}"/${pkgname}-${pkgver}/CMakeLists.txt

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
