# $Id: PKGBUILD 2058 2009-09-07 21:22:13Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: William DÃ­az <wdiaz@archlinux.us>

pkgname=farsight2
pkgver=0.0.21
pkgrel=2
pkgdesc="Audio/Video conference software for Instant Messengers"
arch=('i686' 'x86_64')
url="http://farsight.freedesktop.org"
license=('LGPL')
depends=('libnice>=0.0.13' 'gstreamer0.10-base-plugins>=0.10.29' 'gstreamer0.10-python>=0.10.18' 'python2' 'pygobject>=2.21.3')
optdepends=('gstreamer0.10-good: for RTP plugin'
            'gstreamer0.10-bad: for RTP plugin')
options=('!libtool' '!emptydirs')
source=(http://farsight.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('5898612c389f3538a4160e677c03087a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # we do not use pygtk
  sed -i -e 's#.*pygtk.*##' python/Makefile.in
  sed -i -e 's#python-config#python2-config#' python/Makefile.in
  # this makefile is broken
  sed -i -e 's#        #	#' docs/libs/Makefile.in
  ./configure --prefix=/usr \
      --with-package-name='Arch Linux farsight2 package' \
      --with-package-origin='http://archlinux.org' \
      --disable-static -disable-valgrind --disable-debug

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
