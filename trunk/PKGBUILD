# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=windowmaker-crm-git
pkgver=20090914
pkgrel=1
pkgdesc="An X11 window manager with a NEXTSTEP look and feel - git version from Carlos R. Mafra"
arch=(i686 x86_64)
url="http://www.windowmaker.info/"
license=('GPL' 'custom')
depends=('libxinerama' 'libpng' 'libxpm' 'libxft' 'libtiff>=3.8.2-5' 'giflib')
options=('!libtool')
provides=('windowmaker')
conflicts=('windowmaker')
source=(ftp://ftp.archlinux.org/other/windowmaker/windowmaker-$pkgver.tar.gz)
md5sums=('0b8d53d33c7537fae8fc2162c0738128')

build() {
  cd $srcdir/windowmaker-$pkgver
  [ -z "$LINGUAS" ] && export LINGUAS="`ls po/*.po | sed 's:po/\(.*\)\.po$:\1:'`"
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-gnustepdir=/usr/share/GNUstep --with-nlsdir=/usr/share/locale \
    --enable-xinerama  || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1
  install -D -m644 COPYING.WTFPL $pkgdir/usr/share/licenses/$pkgname/COPYING.WTFPL
}
