# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=watchdog
pkgver=5.9
pkgrel=2
pkgdesc="Watchdog daemon"
arch=(i686 x86_64)
url="http://sourceforge.net/projects/watchdog"
license=('GPL')
depends=('glibc')
backup=(etc/watchdog.conf
        etc/conf.d/watchdog
        etc/conf.d/wd_keepalive)
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        watchdog.sh
        watchdog.cf
        watchdog-5.9-use-oom_score_adj.patch)
md5sums=('d7cae3c9829f5d9a680764f314234867'
         '854451f554718b55186599faaa8d85f6'
         '01c1e2e13f515131180b4ebe9b8b7cc3'
         '9a1ab454e42a7f2969ef6bf2da8a4543')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -p1 -i "${srcdir}"/watchdog-5.9-use-oom_score_adj.patch
  ./configure \
	--prefix=/usr \
	--mandir=/usr/share/man \
	--sysconfdir=/etc \
	--localstatedir=/var
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make install DESTDIR="${pkgdir}"
  for fil in watchdog wd_keepalive ; do
	install -D -m755 "${srcdir}"/watchdog.sh "${pkgdir}"/etc/rc.d/${fil}
	install -D -m644 "${srcdir}"/watchdog.cf "${pkgdir}"/etc/conf.d/${fil}
  done

  sed -i 's/watchdog/wd_keepalive/g' "${pkgdir}"/etc/rc.d/wd_keepalive
  sed -i 's/watchdog/wd_keepalive/g' "${pkgdir}"/etc/conf.d/wd_keepalive
  sed -i 's/Watchdog/Simple Watchdog/' "${pkgdir}"/etc/rc.d/wd_keepalive
}
