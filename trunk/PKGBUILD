# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=qtwebkit
pkgver=2.3.beta2
_pkgver=2.3-beta2
pkgrel=2
arch=('i686' 'x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
depends=('qt4' 'systemd' 'gstreamer0.10-base')
makedepends=('gperf' 'python2' 'ruby' 'git')
conflicts=('qt<4.8')
source=("${pkgname}-${pkgver}.tar.gz"::"http://gitorious.org/webkit/qtwebkit-23/archive-tarball/${pkgname}-${_pkgver}"
        'use-python2.patch')
sha1sums=('b1bfa9e3303ce9c95eec0a1e66b2b812b844bf08'
          '315b6ff603f35e5492a036f7082f6aa075dfb607')

build() {
  cd webkit-qtwebkit-23

  patch -p1 -i "${srcdir}"/use-python2.patch

  OPTS="--no-webkit2"
  if [ "${CARCH}" = "i686" ]; then
      # FS#33418
      OPTS="${OPTS} --no-sse2"
  fi

  export QTDIR=/usr
  Tools/Scripts/build-webkit --qt \
    --makeargs="${MAKEFLAGS}" \
    --prefix=/usr \
    ${OPTS}
}

package() {
  cd webkit-qtwebkit-23
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install
}
