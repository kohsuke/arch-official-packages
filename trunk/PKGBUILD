# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: mario <mario_vazq@hotmail.com>

pkgname=dnsutils
_pkgver=9.9.2-P2
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc='DNS utilities: dig host nslookup'
url='http://www.isc.org/software/bind/'
license=('custom:ISC')
arch=('i686' 'x86_64')
options=('!makeflags')
depends=('openssl' 'krb5' 'idnkit' 'dnssec-anchors')
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz"
        'remove-bind.patch')
sha1sums=('3aadeaf64164a31a1cf22737ff2eea706792b68d'
          'bb13bd54134e4d787469be25461a3cc3f5cb57f4')

replaces=('bind-tools' 'host')

build() {
	cd "${srcdir}/bind-${_pkgver}"

	patch -p1 -i ../remove-bind.patch
	export STD_CDEFINES='-DDIG_SIGCHASE'

	# hack to remove unused bloat from the binaries
	CFLAGS+=' -fdata-sections -ffunction-sections'
	LDFLAGS+=' -Wl,--gc-sections'

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--disable-linux-caps \
		--with-openssl \
		--with-idn \

	make
}

package() {
	cd "${srcdir}/bind-${_pkgver}"
	install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cd bin; make DESTDIR="${pkgdir}" install
}
