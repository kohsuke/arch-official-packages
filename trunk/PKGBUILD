# $Id$
# Maintainer: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=openobex
pkgver=1.7
pkgrel=1
pkgdesc="Implementation of the OBject EXchange (OBEX) protocol"
url="http://dev.zuckschwerdt.org/openobex/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('bluez' 'libusb-compat')
makedepends=('cmake')
options=('!libtool' '!makeflags')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-Source.tar.gz)
md5sums=('11031f6f0b876bb6259bd27106491528')

build() {
  [ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver}-Source \
    -DCMAKE_BUILD_TYPE=Release -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DCMAKE_INSTALL_SBINDIR=bin 
  make all openobex-apps
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
