# $Id$
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.
#   Also, due to dependency loop problems, haskell-ghc-paths and haddock
#   do not carry version dependencies. You will have to remember to build them too.
pkgname=ghc
pkgver=7.2.2
pkgrel=2
pkgdesc="The Glasgow Haskell Compiler"
arch=(i686 x86_64)
url="http://www.haskell.org/ghc/"
license=("custom")
depends=('perl' 'gmp>=5.0' gcc)
makedepends=(ghc happy perl libxslt docbook-xsl)
install=ghc.install
groups=(haskell)
source=("http://www.haskell.org/ghc/dist/$pkgver/ghc-$pkgver-src.tar.bz2"
        build.mk)

build() {
  cd $srcdir/$pkgname-$pkgver

  cp $srcdir/build.mk mk/build.mk

  ./configure --prefix=/usr 
  make -j1 
}

package() {
  cd $srcdir/$pkgname-$pkgver
  
  make DESTDIR=$pkgdir install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/ghc/license
  install -d $pkgdir/usr/share/haskell
}

md5sums=('fa19d9fe5049b7a1352b0b7c9880c141'
         '5a3e0880c80b1dcecf779ff2e63a9c0d')
