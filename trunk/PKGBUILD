# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-server-utils
pkgver=7.5
pkgrel=1
pkgdesc="X.Org utilities required by xorg-server"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfontcache>=1.0.5' 'libxi>=1.3' 'libxaw>=1.0.6' 'libxxf86misc>=1.0.2' 'libxrandr>=1.3.0' 'libxxf86vm>=1.1.0' 'mcpp>=2.7.2' 'libxp>=1.0.0')
makedepends=('pkgconfig' 'xbitmaps' 'bigreqsproto' 'xtrans')
groups=('xorg')
source=(${url}/releases/individual/app/iceauth-1.0.3.tar.bz2
        ${url}/releases/individual/app/rgb-1.0.3.tar.bz2
        ${url}/releases/individual/app/sessreg-1.0.4.tar.bz2
        ${url}/releases/individual/app/xcmsdb-1.0.1.tar.bz2
        ${url}/releases/individual/app/xbacklight-1.1.1.tar.bz2
        ${url}/releases/individual/app/xgamma-1.0.2.tar.bz2
        ${url}/releases/individual/app/xhost-1.0.2.tar.bz2
        ${url}/releases/individual/app/xinput-1.4.99.3.tar.bz2
        ${url}/releases/individual/app/xmodmap-1.0.4.tar.bz2
        ${url}/releases/individual/app/xrandr-1.3.2.tar.bz2
        ${url}/releases/individual/app/xrdb-1.0.5.tar.bz2
        ${url}/releases/individual/app/xrefresh-1.0.2.tar.bz2
        ${url}/releases/individual/app/xset-1.1.0.tar.bz2
        ${url}/releases/individual/app/xsetmode-1.0.0.tar.bz2
        ${url}/releases/individual/app/xsetroot-1.0.2.tar.bz2
        ${url}/releases/individual/app/xvidtune-1.0.1.tar.bz2
        LICENSE.iceauth
        LICENSE.xcmsdb
        LICENSE.xrdb
        LICENSE.xrefresh 
        LICENSE.xset
        LICENSE.xsetroot
        LICENSE.xvidtune)
md5sums=('975ade3f238c1eb10705da0a91e6e8e7'
         '44ea16cc3104de6401bc74035f642357'
         '839e968d7197b9563f0fd8a5a3aac2e3'
         '8579d5f50ba7f0c4a5bf16b9670fea01'
         '3e39eec6d0fd5c587ca6d55aa7bb8fe1'
         'f13ddedaa63a608d3b025d326f4f5b5d'
         'f746aba36f075ae4cae313d849a94f4e'
         'cc2b98b561b6431bd1e06d13ad2c7b5e'
         'bbe021f812e0014a8ee3692317788119'
         '2cb19bb1c19ccf77c40032b03dbe06f0'
         '9c30b8e1709d5367beb7706146640c07'
         '1228f890f86148e4e6ae22aa73118cbb'
         'eeb0d02f69c76bd40470dede99b4bd49'
         'd074e79d380b031d2f60e4cd56538c93'
         '9af7db9f3052aef0b11636720b3101dd'
         'e0744594f4e5969b20df28d897781318'
         '65695dc24c24958190724a23655ffda7'
         'b6235416dca2205f0ba83c0c4b3ddeb8'
         'd0a9bf9f958d238583af763819d64b5f'
         '008d85623476ccad16e056809ed43807'
         '19fa75322c9c3a40c20787c07ed3f8af'
         '3485f0bcea219af946adf6492f55b54c'
         '4e6babde6334e3c5cb4a3136526aa564')

build() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      case "${i}" in
        xrdb*)
          ./configure --prefix=/usr --mandir=/usr/share/man --with-cpp=/usr/bin/mcpp\ -@old || return 1
          ;;
        *)
          ./configure --prefix=/usr --mandir=/usr/share/man || return 1
          ;;
      esac
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      install -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*} || return 1
      popd
    fi
  done
# Replacing stub license files
  install -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/"  || return 1
}
