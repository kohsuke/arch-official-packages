# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gcalctool
pkgver=6.0.1
pkgrel=1
pkgdesc="GNOME Scientific calculator"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'dconf')
makedepends=('intltool' 'gnome-doc-utils')
groups=('gnome-extra')
options=(!emptydirs)
url="http://www.gnome.org"
install=gcalctool.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('be11a7c36a1ec20bd0b190e572aefedea13fb2cb8479b06673e182c7461cadfe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
