# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=racket
pkgver=5.0.1
pkgrel=1
pkgdesc="PLT Racket (a variant of Scheme) Environment"
arch=('i686' 'x86_64')
url="http://racket-lang.org/"
license=('LGPL')
depends=('gcc-libs' 'cairo' 'desktop-file-utils' 'libjpeg' 'libgl' 'libxmu' 'libxft')
makedepends=('mesa' 'libxaw')
provides=('drscheme')
conflicts=('drscheme')
replaces=('drscheme')
options=('!makeflags' '!strip')
install=racket.install
changelog=ChangeLog
source=(http://download.racket-lang.org/installers/${pkgver}/racket/${pkgname}-${pkgver}-src-unix.tgz \
	drracket.desktop libpng-1.4.patch)
md5sums=('52449ea8866c3c149fe4a45ef04ef73a' '9223c9736ead677431542b41271c7b3c'\
         'ec18faef466c1f18047d14a50be0413c')
sha1sums=('3109294115184c96d260fef4458a866975a7c847' 'a20808f6b250225704856f82a544681a962a299d'\
         '2280da2a2ce28b13530c9e749939b895c25fd22e')

build() {
  cd "${srcdir}/plt-${pkgver}/src"
  patch -p2 < ../../libpng-1.4.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/plt-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../collects/icons/plt.xpm "${pkgdir}/usr/share/pixmaps/drracket.xpm"
  install -D -m644 "${srcdir}/drracket.desktop" "${pkgdir}/usr/share/applications/drracket.desktop"
}
