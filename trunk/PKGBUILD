# $Id: PKGBUILD 105813 2011-01-12 00:25:58Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub-at-gmail.com>

pkgname=kdeplasma-applets-networkmanagement
epoch=1
pkgver=git20110524
_commit=f112886575a9c1fc8986f098db9ef84a6fb2e98e
pkgrel=1
pkgdesc="KDE control panel and widget network connections"
arch=('i686' 'x86_64')
url="http://www.kde.org/"
license=('GPL')
depends=('kdebase-workspace' 'networkmanager')
makedepends=('cmake' 'python2' 'automoc4' 'mobile-broadband-provider-info')
optdepends=('mobile-broadband-provider-info: allow to add new mobile connection')
install=${pkgname}.install
source=("${pkgname}-${pkgver}.tar.gz"::"http://quickgit.kde.org/?p=networkmanagement.git&a=snapshot&h=${_commit}")
md5sums=('19d139aa4e80b5829b8a898b315a0a0a')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../networkmanagement \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDBUS_SYSTEM_POLICY_DIR=/etc/dbus-1/system.d
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
