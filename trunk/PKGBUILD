# $Id: PKGBUILD 136836 2011-09-02 07:59:01Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=icedtea-web-java7
pkgname=('icedtea-web-java7' 'icedtea-web-java7-doc')
pkgver=1.1.3
_date=20110925
pkgrel=0.${_date}.1
arch=('i686' 'x86_64')
url="http://icedtea.classpath.org/wiki/IcedTea-Web"
license=('GPL2')
makedepends=('jdk7-openjdk' 'zip' 'xulrunner')
source=(${pkgbase}-${_date}.tar.xz)
#http://icedtea.classpath.org/download/source/${pkgname}-${pkgver}.tar.gz
md5sums=('5da8a5aa74fb92fcb84cb9921c909ab1')

_javaver=7
_jvmdir=/usr/lib/jvm/java-${_javaver}-openjdk

mksource() {
  mkdir /tmp/${pkgbase}-${_date}
  pushd /tmp/${pkgbase}-${_date}
  hg clone http://icedtea.classpath.org/hg/icedtea-web ${pkgbase}-${_date}
  rm -rf ${pkgbase}-${_date}/.hg*
  tar -cvJf /tmp/${pkgbase}-${_date}/${pkgbase}-${_date}.tar.xz *
  popd
}

build() {
  cd "${srcdir}/${pkgname}-${_date}"

  #. /etc/profile.d/jre.sh
  . /etc/profile.d/jre7-openjdk.sh
  #. /etc/profile.d/jdk.sh
  . /etc/profile.d/jdk7-openjdk.sh
  
  autoreconf --force --install
  
  ./configure --prefix=${_jvmdir} \
      --datarootdir=/usr/share \
      --with-jdk-home=${_jvmdir}
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_date}"
  make -k check
}


package_icedtea-web-java7() {

  pkgdesc="provides a Free Software web browser plugin running applets written in the Java programming language and an implementation of Java Web Start, originally based on the NetX project"
  depends=('jre7-openjdk' 'gtk2' 'desktop-file-utils')
  install=${pkgname}.install

  cd "${srcdir}/${pkgname}-${_date}"
  # possible make target (see bottom of Makefile.am: install-exec-local install-data-local
  make DESTDIR="${pkgdir}" install-exec-local install-data-local

  # Install desktop files.
  install -m755 -d ${pkgdir}/usr/share/{applications,pixmaps}
  install -m644 javaws.png ${pkgdir}/usr/share/pixmaps
  install -m644 {javaws,itweb-settings}.desktop ${pkgdir}/usr/share/applications
  # remove splitted doc files
  rm -rf ${pkgdir}/usr/share/doc

  # link binaries into /usr/bin + jre/bin
  install -m755 -d ${pkgdir}/usr/bin
  install -m755 -d ${pkgdir}/${_jvmdir}/jre/bin
  pushd ${pkgdir}/${_jvmdir}/bin
  for file in *; do
    ln -sf ${_jvmdir}/bin/${file} ${pkgdir}/usr/bin
    ln -sf ${_jvmdir}/bin/${file} ${pkgdir}/${_jvmdir}/jre/bin
  done
  popd

  # link the mozilla-plugin - test it here http://www.java.com/en/download/help/testvm.xml
  install -m755 -d ${pkgdir}/usr/lib/mozilla/plugins/
  ln -sf ${_jvmdir}/lib/IcedTeaPlugin.so ${pkgdir}/usr/lib/mozilla/plugins/
}

package_icedtea-web-java7-doc() {

  pkgdesc="icedtea-web browser plugin + Java WebStart - documentation files"

  cd "${srcdir}/${pkgbase}-${_date}"
#  install -m755 -d ${pkgdir}/${_jvmdir}/jre/lib
  make DESTDIR="${pkgdir}" install-data-local
  # remove javaws about and man page
  rm -rf ${pkgdir}/usr/lib
  rm -rf ${pkgdir}/usr/share/man
  rm -rf ${pkgdir}/usr/share/icedtea-web # conflicting and unneeded file it seems
}
