# $Id$
# Maintainer: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Maintainer: roberto <roberto@archlinux.org>
pkgname=xconq
pkgver=7.5.0pre
_rpmver=7.5.0-0pre.0.20040523
pkgrel=1
pkgdesc="Xconq is a general strategy game system."
arch=(i686 x86_64)
url="http://sources.redhat.com/xconq/"
license=('GPL')
makedepends=('rpmextract')
depends=('tk>=8.5.5' 'libxmu')
options=(force)
install=xconq.install
source=(ftp://sources.redhat.com/pub/xconq/xconq-7.5.pre/Unix/Sources/${pkgname}-${_rpmver}.src.rpm)
md5sums=('f675fbc766290682f74b643845546898')

build() {
  cd "${srcdir}"
  rpmextract.sh ${pkgname}-${_rpmver}.src.rpm || return 1
  bsdtar xf xconq-7.5.0.tar.gz || return 1
  cd xconq-7.5.0
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info --enable-alternate-scoresdir=/var/games --enable-alternate-x11-app-defaults-dir=/etc/X11/app-defaults --enable-alternate-gameuser=root --enable-alternate-gamegroup=root || return 1
  make X11_LDFLAGS="" || return 1
  install -m755 -d "${pkgdir}/var/games"
  make X11_LDFLAGS="" \
    prefix="${pkgdir}/usr" \
    mandir="${pkgdir}/usr/share/man" \
    infodir="${pkgdir}/usr/share/info" \
    scoresdir="${pkgdir}/var/games" install || return 1
  chown -R root:games "${pkgdir}/var/games" || return 1
  chown root:games "${pkgdir}/usr/bin/xconq" || return 1
  chmod 775 "${pkgdir}/var/games" || return 1
  chmod 2755 "${pkgdir}/usr/bin/xconq" || return 1
  rmdir "${pkgdir}/usr/share/info"
}
