# $Id$
# Maintainer: Thomas Baechler <thomas@archlinux.org>

pkgname="v86d"
pkgver=0.1.9
pkgrel=11
pkgdesc="userspace helper for uvesafb that runs x86 code in an emulated environment"
url="http://dev.gentoo.org/~spock/projects/uvesafb/"
license=('GPL2')
arch=("i686" "x86_64")
depends=('glibc')
conflicts=('mkinitcpio<0.5.99')
backup=(etc/modprobe.d/uvesafb.conf)
source=(http://dev.gentoo.org/~spock/projects/uvesafb/archive/v86d-$pkgver.tar.bz2
        v86d
        v86d_hook
        modprobe.uvesafb)
md5sums=('ebbbc8e7013c9544b6ba6981add43831'
         '33f3131e7bf041ed6a67e92bff272f4c'
         '813958b8f6c75a7063e4858f31509694'
         '2d7cc8dc6a41916a13869212d0191147')
options=(!makeflags)

build() {
  cd $srcdir/v86d-$pkgver
  ./configure --with-x86emu
  # we only need /usr/include/video/uvesafb.h
  make KDIR=/usr
}

package() {
  cd $srcdir/v86d-$pkgver
  make DESTDIR=$pkgdir install

  install -D -m644 $srcdir/v86d $pkgdir/lib/initcpio/install/v86d
  install -D -m644 $srcdir/v86d_hook $pkgdir/lib/initcpio/hooks/v86d
  install -D -m644 $srcdir/modprobe.uvesafb $pkgdir/etc/modprobe.d/uvesafb.conf
}
