# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>

pkgname=gnome-python-desktop
pkgver=2.29.1
pkgrel=1
pkgdesc="Gnome Python interfaces for libraries not part of the core platform"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('gnome-desktop>=2.29.90' 'gnome-python>=2.28.0')
options=('!libtool')
makedepends=('intltool' 'pkgconfig' 'gnome-media>=2.28.5' 'gnome-panel>=2.29.6' 'gtksourceview>=1.8.5' 'libgnomeprintui>=2.18.4' 'libgtop>=2.28.0' 'totem-plparser>=2.29.1' 'evince>=2.29.5' 'bug-buddy>=2.28.0' 'brasero>=2.29.90')
optdepends=('gnome-media' 'gnome-panel' 'gtksourceview' 'libgnomeprintui' 'libgtop' 'totem-plparser' 'evince' 'bug-buddy' 'brasero')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.29/${pkgname}-${pkgver}.tar.bz2
        totem-plparser-api.patch)
sha256sums=('7cc853f9d7cc7fdc895006abbe7a8c5bb3b27d6d4dc14d2bb09afc63fd397618'
            '2657e937801103b009a4c84777bfaa764ca847293a2ae9fcf6d946070c7f8309')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/totem-plparser-api.patch" || return 1
  ./waf configure --prefix=/usr || return 1
  ./waf build ${MAKEFLAGS} || return 1
  ./waf install --destdir="${pkgdir}" || return 1
  install -m644 gnomeprint/art.defs "${pkgdir}/usr/share/pygtk/2.0/defs/" || return 1
}
