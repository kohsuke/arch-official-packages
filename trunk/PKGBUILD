# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=evas
pkgver=1.7.1
pkgrel=1
pkgdesc="A hardware-accelerated canvas API for X-Windows"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('eet' 'giflib' 'libpng' 'libjpeg>=7' 'libxrender' 'mesa'
  'fribidi' 'fontconfig' 'freetype2')
optdepends=("evas_generic_loaders")
options=('!libtool' '!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('86e9bb6813eb707ccd162c08f06ca1f5ed78ee75')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/evas \
    --enable-strict \
    --disable-static \
    --enable-fontconfig \
    --enable-fribidi \
    --disable-harfbuzz \
    --disable-pixman \
    --enable-buffer \
    --disable-gl-sdl \
    --enable-fb \
    --disable-directfb \
    --disable-wayland-shm \
    --disable-wayland-egl \
    --disable-sdl-primitive \
    --disable-software-xcb \
    --enable-software-xlib \
    --disable-gl-xcb \
    --enable-gl-xlib \
    --disable-image-loader-edb \
    --enable-image-loader-gif \
    --disable-image-loader-tiff \
    --disable-image-loader-svg \
    --disable-install-examples \
    --disable-tests \
    --disable-coverage \
    --disable-doc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license files
  install -Dm644 "$srcdir/$pkgname-$pkgver/COPYING" \
        "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
