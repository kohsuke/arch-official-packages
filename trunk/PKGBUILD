# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=soprano
pkgver=2.6.50git20110728
pkgrel=1
pkgdesc='A library which provides a highly usable object-oriented C++/Qt4 framework for RDF data'
arch=('i686' 'x86_64')
url='http://soprano.sourceforge.net/'
license=('GPL' 'LGPL')
depends=('qt' 'redland-storage-virtuoso' 'libiodbc' 'virtuoso')
makedepends=('cmake' 'openjdk6' 'doxygen')
#source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
source=("${pkgname}-${pkgver}.tar.bz2"::"http://quickgit.kde.org/?p=soprano.git&a=snapshot&h=a2f8207cc83fb33253d548eaca8029c8f836addf&fmt=tbz2")
md5sums=('1d7f99d7d5954fc88e4f7653aa89862e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  . /etc/profile.d/openjdk6.sh
  # we need the rpath
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSOPRANO_DISABLE_CLUCENE_INDEX=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
