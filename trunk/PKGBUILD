# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Maintainer: Ray Rashif <schivmeister@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ardour, liblrdf and raptor are heavily dependent on each other. Updating of
# one package mostly will require an update of all the other packages. I vote
# for fixed dependencies speaking in terms of versions

pkgname=ardour
pkgver=2.8.11
pkgrel=2
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=('i686' 'x86_64')
url="http://ardour.org"
license=('GPL')
depends=('liblrdf' 'liblo' 'aubio' 'libusb-compat'
         'slv2' 'rubberband' 'libgnomecanvas')
makedepends=('scons' 'boost' 'pkg-config')
changelog=${pkgname}.changelog
source=(ftp://ftp.archlinux.org/other/ardour/${pkgname}-${pkgver}.tar.bz2
        ${pkgname}.desktop)
md5sums=('f451a8d0abc133a1700c3932e07a5612'
         '8aeaf433ebf781733db48e5a16b0c4da')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '/-O3/d' SConstruct

  scons ${MAKEFLAGS} ARCH="${CFLAGS}" \
                     PREFIX="/usr" \
                     DIST_LIBDIR="lib" \
                     FREEDESKTOP=0 \
                     FREESOUND=1 \
                     DESTDIR="${pkgdir}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  scons PREFIX="/usr" \
        FREEDESKTOP=0 \
        FREESOUND=1 \
        DESTDIR="${pkgdir}" install

  # install some freedesktop.org compatibility
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
