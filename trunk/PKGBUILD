# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: aurelien <aurelien@archlinux.org>
# Contributor: Herb Rose (hrose56@yahoo.com)

pkgname=indent
pkgver=2.2.11
pkgrel=1
pkgdesc="A tool to change the appearance of a C program by inserting or deleting whitespace"
arch=('i686' 'x86_64')
url="http://indent.isidore-it.eu/beautify.html"
depends=('glibc')
makedepends=('texi2html')
license=('GPL3')
install=indent.install
source=(http://indent.isidore-it.eu/$pkgname-$pkgver.tar.gz indent-2.2.11-segfault.patch)
md5sums=('98beafca62472805a3739d3867d5d70f'
         'e5ded8513547ff4555dec2bb805dd2ab')
sha1sums=('9b242528bbc4914464117c69ffae9df24d3fa4a7'
          '31a6dcd5edcac4decac81dbb52bc9415b8b148e4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i ../indent-2.2.11-segfault.patch
  sed -i 's/-number/-number-sections/' doc/Makefile.in
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" docdir=/usr/share/doc/indent install
}
