# $Id$
# Maintainer: Varun Acharya <varun@archlinux.org>
# Contributor: Rashif "Don Ray" Rahman <rayrashif@gmail.com>

pkgname=wicd
pkgver=1.5.8
pkgrel=2
pkgdesc="New and alternative wireless/wired network management utility"
arch=('i686' 'x86_64')
url="http://wicd.sourceforge.net/"
license=('GPL2')
depends=(python dbus-python pygtk dhcpcd wpa_supplicant ethtool shared-mime-info)
optdepends=(wireless_tools hicolor-icon-theme)
conflicts=('wicd-svn')
install=wicd.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz wicd-daemon wicd.desktop)
options=(emptydirs)
backup=(etc/wicd/encryption/templates/active)
md5sums=('93df47de53bbe172ab09affe94fe18f6'
         'f40e5f59998d0829707a7c9976afa8f8'
         'eee9b10d9c860e1c7d195f115dc603f9')

build() {
  cd $startdir/src/$pkgname-$pkgver
  python setup.py configure --no-install-init --resume=/usr/share/wicd/scripts/ --suspend=/usr/share/wicd/scripts/ --verbose
  python setup.py install --root=$startdir/pkg

  # Add custom rc.d script
  install -Dm755 ../wicd-daemon $startdir/pkg/etc/rc.d/wicd
  install -Dm644 ../wicd.desktop ${startdir}/pkg/usr/share/applications/wicd.desktop

  cd build/lib/wicd
  for i in *.py; do
    install -Dm 755 $i $startdir/pkg/usr/lib/wicd/$i
  done
  
  rm -rf  ${startdir}/pkg/usr/share/autostart
}

