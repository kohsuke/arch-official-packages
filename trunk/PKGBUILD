# $Id$
# Maintainer: Firmicus <francois.archlinux.org>
# Contributor: Tom K <tomk@runbox.com>

pkgname=mod_perl
pkgver=2.0.5
pkgrel=8
pkgdesc="Apache module that embeds the Perl interpreter within the server"
url="http://search.cpan.org/dist/${pkgname}/"
depends=('perl' 'apache' 'db' 'apr-util' 'perl-linux-pid')
license=('APACHE')
arch=('i686' 'x86_64')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/P/PH/PHRED/${pkgname}-${pkgver}.tar.gz
       nolfs.patch)
md5sums=('03d01d135a122bd8cebd0cd5b185d674'
         '088e082afef57f92f234a01269e24ce5')

build() {
  cd  ${srcdir}/${pkgname}-${pkgver}

  # Patch from openSUSE to fix segfaults on i686 (FS#25008):
  #   avoid a conflict between apache's LARGEFILE64_SOURCE and perl's
  #   FILE_OFFSET_BITS=64 because the conflict isn't real and causes the perl
  #   structures to be invalid if only the apache flags are used
  patch -Np1 -i "$srcdir/nolfs.patch"

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor MP_APXS=/usr/sbin/apxs
  make
}

package() {
  cd  ${srcdir}/${pkgname}-${pkgver}
  make install DESTDIR=${pkgdir}
}
