# $Id: PKGBUILD,v 1.9 2009/02/06 16:41:19 abhidg Exp $
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgbase=libofx
pkgname=('libofx' 'libofx-doc')
pkgver=0.9.2
pkgrel=1
pkgdesc="API for the OFX banking standard"
arch=('i686' 'x86_64')
url="http://libofx.sourceforge.net"
license=('GPL')
depends=('opensp' 'curl' 'gcc-libs')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('cb72fce95e0d59bef78ba0601fbc91f3')
sha1sums=('f11e873a50f5bd16749a7c0700acbf5d565bc859')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package_libofx() {
  options=('!libtool' '!docs')

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

package_libofx-doc() {
  pkgdesc="Documention of the OFX banking standard API"
  depends=()

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-docDATA
  cd doc
  make DESTDIR="$pkgdir" install
}
