# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qtiplot
pkgver=0.9.7.2
pkgrel=1
pkgdesc="Data analysis and scientific plotting - free clone of Origin"
arch=("i686" "x86_64")
url="http://soft.proindependent.com/qtiplot.html"
depends=('qwtplot3d>=0.2.7' 'muparser>=1.28' 'gsl' 'python>=2.5.1' 'pyqt' 'sip')
# build against static liborigin provided in the package ...
# as systemwide one doesn't provide all needed functions
makedepends=('boost>=1.36')
license=("GPL2")
source=("http://soft.proindependent.com/src/qtiplot-$pkgver.tar.bz2" \
	"$pkgname.desktop" \
        "$pkgname.png" \
        "qtiplot-gcc4.3.patch" \
	"liborigin-gcc4.3.patch"
	"qtiplot.archlinux.patch")
md5sums=('ff1bea2f8b3e7722ae75820c3dc33b78'
         '56bd53f4f1367c285086acb969f13348'
         'ad8affbd6f0d5cbdcde46c923ee2668a'
         '927149a0c3623209d4aaef867031ba76'
         'ad2333cfb8df37d33a6449c2c49ec945'
         'c8a8024230e01c518f6bb60c01d3663e')

build() {
  cd $srcdir/$pkgname-$pkgver/$pkgname

  # patching
  patch -Np0 -i $srcdir/qtiplot-gcc4.3.patch || return 1
  patch -Np0 -i $srcdir/qtiplot.archlinux.patch || return 1
  patch -Np0 -i $srcdir/liborigin-gcc4.3.patch || return 1

  export QMAKESPEC=linux-g++

  qmake qtiplot.pro || return 1
  make QTDIR=/usr/ QMAKESPEC=linux-g++ || return 1
  install -D $srcdir/$pkgname-$pkgver/$pkgname/$pkgname \
	$pkgdir/usr/bin/$pkgname || return 1

  # make it nice:
  install -D -m644 $srcdir/$pkgname.png \
  	$pkgdir/usr/share/pixmaps/$pkgname.png
  install -D -m644 $srcdir/$pkgname.desktop \
  	$pkgdir/usr/share/applications/$pkgname.desktop

# install wordlist for autocompletion
 install -Dm644 $srcdir/$pkgname-$pkgver/$pkgname/qti_wordlist.txt \
	$pkgdir/usr/bin/qti_wordlist.txt
}

