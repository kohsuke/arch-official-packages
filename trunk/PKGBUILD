# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=totem-plparser
pkgver=2.32.3
pkgrel=1
url="http://www.hadess.net/totem.php3"
pkgdesc="Totem playlist parser library"
license=('LGPL')
arch=(i686 x86_64)
depends=('gmime' 'libsoup-gnome')
makedepends=('intltool' 'pkgconfig' 'gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/totem-pl-parser/2.32/totem-pl-parser-${pkgver}.tar.bz2)
sha256sums=('c15149650c5420426e7becd6eb4298e898d1975cb7c9ff534cc0785d350916e2')

build() {
  cd "${srcdir}/totem-pl-parser-${pkgver}"
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static
  make
}

package(){
  cd "${srcdir}/totem-pl-parser-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
