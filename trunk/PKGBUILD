# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=lilo
pkgver=23.1
pkgrel=1
pkgdesc="A bootloader for Linux"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/lilo/"
license=('BSD')
# While lilo should stay in the base category,
# it usually makes no sense if it is installed
# with pacman -S base, therefore, don't add
# the base group here.
#groups=('base')
backup=('etc/lilo.conf')
depends=('device-mapper' 'coreutils')
makedepends=('bin86')
install=lilo.install
options=('!makeflags')
source=(https://alioth.debian.org/frs/download.php/3315/${pkgname}-${pkgver}.src.tar.gz
        'lilo.conf')
md5sums=('72f9b547393c0d0fb48b4b1c23f85f50'
        'a3a4c90bead3f9b8672bd384ff9f8db1')

build() {
  cd "${srcdir}/${pkgname}-23.0"
  export LC_ALL=C
  make all
}

package() {
  cd "${srcdir}/${pkgname}-23.0"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/lilo.conf" "${pkgdir}/etc/lilo.conf"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
