# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Bastien Bouclet <bastien.bouclet@gmail.com>

pkgname=gtkglextmm
pkgver=1.2.0
pkgrel=6
pkgdesc="Cpp C++ bindings for gtkglext"
arch=('i686' 'x86_64')
url="http://gtkglext.sourceforge.net/"
depends=('gtkmm' 'gtkglext')
makedepends=('pkgconfig')
options=('!libtool')
license=('GPL')
source=(http://downloads.sourceforge.net/sourceforge/gtkglext/${pkgname}-${pkgver}.tar.bz2
        gdkspanfunc.patch)
md5sums=('27c05f4d45c5fd07b6fb0f044add3056'
         'd7c8ea33c748a6857ee629c16b2d9bff')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#24706
  patch -Np1 -i "${srcdir}/gdkspanfunc.patch"

  ./configure --prefix=/usr \
      --disable-gtkglext-test --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
