# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgname=miro
pkgver=5.0.2
pkgrel=1
pkgdesc="The free and open source internet TV platform"
arch=('i686' 'x86_64')
url="http://www.getmiro.com"
license=('GPL2')
depends=('dbus-python' 'pyrex' 'pygtk' 'gstreamer0.10' 'python2-gconf'
         'python-pysqlite' 'pywebkitgtk' 'shared-mime-info'
         'desktop-file-utils' 'gstreamer0.10' 'hicolor-icon-theme'
         'gstreamer0.10-python' 'python-notify' 'libtorrent-rasterbar'
         'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'python-pycurl' 
         'gstreamer0.10-ffmpeg' 'ffmpeg' 'mutagen' 'xdg-utils')
makedepends=('pkg-config' 'boost')
install=miro.install
source=("http://ftp.osuosl.org/pub/pculture.org/${pkgname}/src/${pkgname}-${pkgver}.tar.gz"
        'ffmpeg.patch')
sha1sums=('67babcf76cc08b72b8f55d425b1372ae0a979805'
          'ce0f42be2a0a3ac99f07b5abfabf0975928c841c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cd linux
  patch -p2 -i "${srcdir}"/ffmpeg.patch

  python2 setup.py install --root="${pkgdir}"

  # fix miro startup script so --debug works with python2
  sed -i "s|which python|which python2|" "${pkgdir}/usr/bin/miro"
  sed -i "s|./miro.real|/usr/bin/miro.real|" "${pkgdir}/usr/bin/miro"

  # fix python scripts for python2
  sed -i "s|/usr/bin/env python|/usr/bin/env python2|" ${pkgdir}/usr/share/miro/resources/searchengines/update-icons.py
  for i in usr/share/miro/resources/testdata/echonest-replies/generate.py usr/share/miro/resources/searchengines/update-icons.py \
    usr/share/miro/resources/testdata/7digital-replies/generate.py; do
    sed -i "s|/usr/bin/python|/usr/bin/python2|" ${pkgdir}/${i} 
  done

  # Fakeroot segfaults on these; remove
  if [ ${arch} == "i686" ]; then
    rm -f ${pkgdir}/usr/bin/codegen.Linux-x86_64 || true
  else
    rm -f ${pkgdir}/usr/bin/codegen.Linux-i686 || true
  fi
}
