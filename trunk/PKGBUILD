# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=0.13.3
pkgrel=1
pkgdesc="The command-line interface for Cabal and Hackage."
url="http://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('haskell-http=4000.2.2-1' 'haskell-zlib=0.5.3.3-3' 'haskell-mtl=2.0.1.0-4' 'haskell-network=2.3.0.11-1')
depends=('ghc' 'gmp' 'zlib')
#source=(http://hackage.haskell.org/packages/archive/cabal-install/$pkgver/cabal-install-$pkgver.tar.gz)
# use cabal-install from ghc since hackage cabal-install hasn't been updated
source=("http://www.haskell.org/ghc/dist/7.4.1/ghc-7.4.1-src.tar.bz2")
md5sums=('5d86c420978b49cc60edea9bd4c36703')

build() {
    #cd $srcdir/cabal-install-$pkgver
    cd ghc-7.4.1/libraries/Cabal/cabal-install
    runhaskell Setup configure --enable-shared --enable-split-objs --enable-executable-stripping --prefix=/usr
    runhaskell Setup build
}

package() {
    #cd $srcdir/cabal-install-$pkgver
    cd ghc-7.4.1/libraries/Cabal/cabal-install
    runhaskell Setup copy --destdir=$pkgdir
    install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
