# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gtkhtml4
_pkgbasename=gtkhtml
pkgver=3.91.90
pkgrel=1
pkgdesc="A lightweight HTML renderer/editor widget for GTK3"
arch=(i686 x86_64)
license=('GPL')
depends=('gconf' 'enchant' 'iso-codes' 'gnome-icon-theme' 'libsoup')
makedepends=('intltool')
url="http://www.gnome.org"
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/3.91/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('b7a458d73652e5ea947d9f3704aa5816df82d90d6d379593fd36d9cf4623d5aa')

build() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/gtkhtml3 \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
