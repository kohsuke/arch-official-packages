# $Id: PKGBUILD 16363 2010-04-29 09:59:57Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: iKevin <kellwood at ameritech dot net>

pkgname=umfpack
pkgver=5.5.2
_ufcfgver=3.7.0
_amdver=2.2.3
_cholmodver=1.7.4
_colamdver=2.7.4
_ccolamdver=2.7.4
pkgrel=1
pkgdesc="A multifrontal direct solver for unsymmetric linear systems (AMD/${_amdver}, UFConfig/${_ufcgfgver})"
arch=('i686' 'x86_64')
url="http://www.cise.ufl.edu/research/sparse/umfpack"
license=('LGPL')
makedepends=('lapack' 'blas')
noextract=(CHOLMOD-${_cholmodver}.tar.gz
	COLAMD-${_colamdver}.tar.gz
	CCOLAMD-${_ccolamdver}.tar.gz)
source=(http://www.cise.ufl.edu/research/sparse/umfpack/UMFPACK-$pkgver.tar.gz
	http://www.cise.ufl.edu/research/sparse/UFconfig/UFconfig-${_ufcfgver}.tar.gz
	http://www.cise.ufl.edu/research/sparse/amd/AMD-${_amdver}.tar.gz
	http://www.cise.ufl.edu/research/sparse/cholmod/CHOLMOD-${_cholmodver}.tar.gz
	http://www.cise.ufl.edu/research/sparse/colamd/COLAMD-${_colamdver}.tar.gz
	http://www.cise.ufl.edu/research/sparse/ccolamd/CCOLAMD-${_ccolamdver}.tar.gz)
md5sums=('07eaa6ae3de176e5b3681032c10c76be'
         'ab8c355d683e8c5597a0824b32704c70'
         '4d2a7db82406f880e7686fbd2df5a3bf'
         'c2088078a86ca1a88e64037f80ae6540'
         '186d692ab7211e79f9b727d8192635f7'
         'bb55f6b4b9358160eaba1a57f863c65e')

build() {
  CFLAGS+=" -DNCHOLMOD"
  cd "$srcdir"
  cp -rf UFconfig/xerbla UMFPACK
  make -C UMFPACK library
  make -C AMD library  
}

package() {
  cd "$srcdir"
  
  install -dm755 "$pkgdir"/usr/{include,lib}

  install -m644 $srcdir/UMFPACK/Lib/libumfpack.a "$pkgdir/usr/lib/"
  install -m644 $srcdir/UMFPACK/Include/*.h $pkgdir/usr/include

  install -m644 "$srcdir/AMD/Lib/libamd.a" "$pkgdir/usr/lib/"
  install -m644 $srcdir/AMD/Include/*.h $pkgdir/usr/include

  install -m644 "$srcdir/UFconfig/UFconfig.h" "$pkgdir/usr/include"
}
