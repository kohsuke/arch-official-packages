# $Id$
# Maintainer: François Charette <francois.archlinux.org>
# Contributor: Damir Perisa <damir.archlinux.org>
# Contributor: Björn Martensen <bjoern.martensen@gmail.com>

pkgname=thinkfinger
pkgver=0.3
pkgrel=4
pkgdesc="ThinkFinger is a driver for the SGS Thomson Microelectronics fingerprint reader found in most IBM/Lenovo ThinkPads"
url="http://thinkfinger.sourceforge.net/"
arch=('i686' 'x86_64')
license=("GPL")
depends=('pam' 'libusb-compat')
install=thinkfinger.install
options=('!libtool')
source=(http://downloads.sourceforge.net/thinkfinger/$pkgname-$pkgver.tar.gz thinkfinger-uinput-hack.patch)
md5sums=('588565233bcbea5ff0a7f5314361c380'
         '71dc334282d19e6db4f6254542ba563c')

build() {
  cd $srcdir/$pkgname-$pkgver
  # Patch from Ubuntu: see http://bugs.archlinux.org/task/12580
  patch -p0 < ../thinkfinger-uinput-hack.patch
  ./configure 	--prefix=/usr \
  		--with-securedir=/lib/security \
		--with-birdir=/etc/pam_thinkfinger
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  # create dir to store fingerprints
  install -d $pkgdir/etc/pam_thinkfinger
  
  make DESTDIR=$pkgdir install
}

