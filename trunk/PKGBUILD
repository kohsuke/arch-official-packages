# $Id$
# Maintainer:
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=netkit-bsd-finger
pkgver=0.17
pkgrel=8
pkgdesc="BSD-finger ported to Linux"
arch=('i686' 'x86_64')
url='ftp://ftp.uk.linux.org/pub/linux/Networking/netkit'
license=('BSD')
depends=('glibc' 'xinetd')
backup=('etc/xinetd.d/finger')
source=("ftp://ftp.archlinux.org/other/${pkgname}/bsd-finger-${pkgver}.tar.gz"
        'finger.xinetd' 'LICENSE'
        'fix-manpage-typo.patch')
md5sums=('52bf281aac8814bf56cdc92f7661ee75'
         'a71333c0f3a6ac2ca9e37a6425abfc52'
         '5fc568418a0379fc3a358ed1c2523b44'
         '9c4ad79a8df81ade6d1938955f1debbd')

prepare() {
   cd bsd-finger-${pkgver}
   # FS#28232
   patch -p1 -i "${srcdir}"/fix-manpage-typo.patch
   sed -i 's@include <sys/time.h>@include <time.h>@' finger/lprint.c
   sed -i 's@include <sys/time.h>@include <time.h>@' finger/sprint.c
}

build() {
   cd bsd-finger-${pkgver}
   ./configure --prefix=/usr
   make
}

package() {
   cd bsd-finger-${pkgver}
   install -d "${pkgdir}"/usr/{bin,share/man/{man1,man8}}
   make INSTALLROOT="${pkgdir}" SBINDIR=/usr/bin MANDIR=/usr/share/man install

   install -D -m644 "${srcdir}/finger.xinetd" "${pkgdir}/etc/xinetd.d/finger"
   install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
