# $Id$
# Maintainer:
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: roberto <roberto@archlinux.org>

pkgname=transcode
_sripver=0.3-4
pkgver=1.1.5
pkgrel=4
pkgdesc="A video/DVD ripper and encoder for the terminal/console."
arch=('i686' 'x86_64')
url="http://tcforge.berlios.de/"
license=('GPL')
depends=('imagemagick' 'lzo2' 'libdv' 'libdvdread' 'ffmpeg' 'mjpegtools'
	'xvidcore' 'libmpeg2' 'libtheora' 'libxaw' 'x264' 'libxml2'
	'libxv' 'a52dec' 'alsa-lib')
makedepends=('nasm' 'pkg-config')
options=('!libtool' '!makeflags')
source=(http://download2.berlios.de/tcforge/${pkgname}-${pkgver}.tar.bz2
	http://downloads.sourceforge.net/sourceforge/subtitleripper/subtitleripper-${_sripver}.tgz)
md5sums=('41ac6b1c0fe30f3aab286e771fc31b9e' 'c0bd49a88f667c68c4430ad25bbed510')
sha1sums=('48c36e06ca0cd1cdc1a9c6a241e231f896eefd26' 'd93ff3578dd5f722c8f4ef16bc0903eec5781a0d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
 # building from here
  ./configure --prefix=/usr \
    --enable-sse=no --enable-sse2=no --enable-altivec=no --enable-mmx \
    --enable-lame --enable-ogg --enable-vorbis --enable-theora \
    --enable-libdv --enable-libxml2 --enable-v4l \
    --enable-imagemagick --enable-libjpeg --enable-lzo --enable-mjpegtools \
    --enable-sdl --enable-freetype2 --enable-a52 --enable-libpostproc \
    --enable-xvid --enable-x264 --enable-alsa \
    --enable-libmpeg2 --enable-libmpeg2convert \
    --with-libxml2-prefix=/usr \
    --with-mjpegtools-prefix=/usr \
    --with-imagemagick-prefix=/usr \
    --with-libdv-includes=/usr \
    --with-libpostproc-includes=/usr/include/libpostproc \
    --with-ogg-prefix=/usr \
    --with-lame-libs=/usr \
    --with-lzo-libs=/usr/lib \
    --with-libavcodec-prefix=/usr \
    --with-libavcodec-includes=/usr/include \
    --with-libavcodec-libs=/usr/lib

  make
 ##############################################################################
 # this imports the latest subtitleripper sources and replaces the internal
  mkdir contrib
  mv "${srcdir}/subtitleripper" contrib/subrip
 # removes dependency on libppm, make it dependend on netpbm just failed, argh...
  cd contrib/subrip
  sed -e 's|^\(.*lppm.*\)$|#\1|' \
      -e 's|^\(.*D_HAVE_LIB_PPM.*\)$|#\1|' \
      -e 's/DEFINES :=/DEFINES = -DHAVE_GETLINE/' \
      -i Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  cd contrib/subrip
  install -m 755 pgm2txt "${pkgdir}/usr/bin/"
  install -m 755 srttool "${pkgdir}/usr/bin/"
  install -m 755 subtitle2pgm "${pkgdir}/usr/bin/"
  install -m 755 subtitle2vobsub "${pkgdir}/usr/bin/"
  install -m 755 vobsub2pgm "${pkgdir}/usr/bin/"
}
