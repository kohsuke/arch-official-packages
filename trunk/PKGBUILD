# $Id$
# Contributor: FUBAR <mrfubar@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=bitlbee
pkgver=3.0.1
pkgrel=2
pkgdesc='Brings instant messaging (XMPP, MSN, Yahoo!, AIM, ICQ, Twitter) to IRC'
arch=('i686' 'x86_64')
url='http://www.bitlbee.org/'
license=('GPL')
depends=('gnutls' 'glib2' 'libotr')
optdepends=('xinetd: to run bitlbee through xinetd')
source=("http://get.bitlbee.org/src/${pkgname}-${pkgver}.tar.gz"
	'bitlbee.xinetd'
	'bitlbee.rcd')
sha1sums=('f3a0651d723b71b08f0dad3b5cb2843608d5c265'
          '5e0af27ba9cc4fe455e3381c75fc49a9326e2f17'
          '299776783902cc2e20be49e2fe729572ca656383')

backup=('etc/bitlbee/bitlbee.conf' 'etc/bitlbee/motd.txt' 'etc/xinetd.d/bitlbee')
install=install

build() { 
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	./configure --prefix=/usr \
		--etcdir=/etc/bitlbee \
		--pidfile=/var/run/bitlbee/bitlbee.pid \
		--ipcsocket=/var/run/bitlbee/bitlbee.sock \
		--strip=0 \
		--otr=1 \
		--ssl=gnutls
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -dm755 "${pkgdir}"/var/{lib,run}
	install -o65 -g65 -dm770 "${pkgdir}"/var/lib/bitlbee
	install -o65 -g65 -dm755 "${pkgdir}"/var/run/bitlbee
	install -Dm644 "${srcdir}"/bitlbee.xinetd "${pkgdir}"/etc/xinetd.d/bitlbee
	install -Dm755 "${srcdir}"/bitlbee.rcd "${pkgdir}"/etc/rc.d/bitlbee

	make DESTDIR="${pkgdir}" install
	make DESTDIR="${pkgdir}" install-etc
	make DESTDIR="${pkgdir}" install-dev
}
