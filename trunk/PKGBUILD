# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gcalctool
pkgver=6.3.5
pkgrel=1
pkgdesc="GNOME Scientific calculator"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'dconf')
makedepends=('intltool' 'yelp-tools')
groups=('gnome-extra')
options=(!emptydirs)
url="http://www.gnome.org"
install=gcalctool.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('03dabdb4c3841aff6e75a5d7a57cb3884445b66b9680c7675cd83f08c8cac4e7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile \
      --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
