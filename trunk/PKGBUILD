# $Id: PKGBUILD 105813 2011-01-12 00:25:58Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub-at-gmail.com>

pkgname=kdeplasma-applets-networkmanagement
pkgver=git20110407
_commit=a2d35d4ec55fe6a5db209974a928482c662ac830
pkgrel=1
pkgdesc="KDE control panel and widget network connections"
arch=('i686' 'x86_64')
url="http://www.kde.org/"
license=('GPL')
depends=('kdebase-workspace' 'networkmanager')
makedepends=('cmake' 'python2' 'automoc4' 'mobile-broadband-provider-info')
optdepends=('mobile-broadband-provider-info: allow to add new mobile connection')
install=${pkgname}.install
#source=("${pkgname}-${pkgver}.tar.gz"::"http://quickgit.kde.org/?p=networkmanagement.git&a=snapshot&h=${_commit}")
source=('ftp://ftp.archlinux.org/other/kde/networkmanagement-nm09.tar.bz2')
md5sums=('a853338f79bc75837e343c3820d1bb39')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../networkmanagement \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDBUS_SYSTEM_POLICY_DIR=/etc/dbus-1/system.d
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
