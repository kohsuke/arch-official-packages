# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub-at-gmail.com>

pkgname=kdeplasma-applets-networkmanagement
pkgver=svn1192135
pkgrel=1
pkgdesc="KDE4 Network Management and plasmoid"
arch=('i686' 'x86_64')
url="http://www.kde.org/"
license=('GPL')
depends=('kdebase-workspace' 'networkmanager')
makedepends=('cmake' 'python2' 'automoc4')
provides=('knetworkmanagement')
conflicts=('networkmanager-plasmoid-svn' 'kdeplasma-applets-networkmanager')
replaces=('networkmanager-plasmoid-svn' 'kdeplasma-applets-networkmanager')
install=${pkgname}.install
#svn co -q svn://websvn.kde.org:443/home/kde/trunk/extragear/base/networkmanagement
source=("http://ftp.archlinux.org/other/kde/${pkgname}-${pkgver}.tar.bz2")
md5sums=('513c16a77a6aee562b014d8c0226baa9')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../networkmanagement \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDBUS_SYSTEM_POLICY_DIR=/etc/dbus-1/system.d
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
