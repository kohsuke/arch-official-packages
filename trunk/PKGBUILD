# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-soap-lite
pkgver=0.712
pkgrel=1
pkgdesc="Client and server side SOAP implementation"
arch=('any')
url="http://search.cpan.org/dist/SOAP-Lite"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-crypt-ssleay' 'perl-mime-lite' 'perl-libwww' 'perl-uri' \
         'perl-mime-tools' 'perl-io-socket-ssl' 'perlxml' 'perl-fcgi')
options=('!emptydirs')
provides=(
    perl-apache-soap
    perl-apache-xmlrpc-lite
    perl-io-sessiondata=1.20.0
    perl-io-sessionset
    perl-soap-constants
    perl-soap-lite-deserializer-xmlschema1999
    perl-soap-lite-deserializer-xmlschema2001
    perl-soap-lite-deserializer-xmlschemasoap1_1
    perl-soap-lite-deserializer-xmlschemasoap1_2
    perl-soap-lite-packager
    perl-soap-lite-utils
    perl-soap-packager=0.712.0
    perl-soap-test
    perl-soap-transport-http
    perl-soap-transport-io
    perl-soap-transport-local
    perl-soap-transport-loopback
    perl-soap-transport-mailto
    perl-soap-transport-pop3=0.712.0
    perl-soap-transport-tcp=0.712.0
    perl-uddi-lite=0.712.0
    perl-xml-parser-lite
    perl-xmlrpc-lite
    perl-xmlrpc-test
    perl-xmlrpc-transport-http
    perl-xmlrpc-transport-pop3
    perl-xmlrpc-transport-tcp
)
source=("http://www.cpan.org/authors/id/M/MK/MKUTTER/SOAP-Lite-${pkgver}.tar.gz")
md5sums=('fd71a0c0feff85f670ab4b2e571652a8')

build() {
  cd  "${srcdir}/SOAP-Lite-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor || return 1
  make || return 1
}

package() {
  cd  "${srcdir}/SOAP-Lite-${pkgver}"
  make install DESTDIR="${pkgdir}" || return 1

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
