# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=setserial
pkgver=2.17
pkgrel=4
pkgdesc="Allows to change various attributes of a serial device"
arch=('i686' 'x86_64')
url="http://setserial.sourceforge.net/"
license=('GPL')
depends=('glibc')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "$pkgname.patch")
md5sums=('c4867d72c41564318e0107745eb7a0f2'
         '99919d3be7c1550721494070a7ace66a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i ../$pkgname.patch
  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d ${pkgdir}/usr/{bin,share/man/man8}
  make DESTDIR="${pkgdir}" install
}
