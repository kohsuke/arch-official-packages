# $Id$

pkgname=fetchmail
pkgver=6.3.25
pkgrel=1
pkgdesc="A remote-mail retrieval utility"
arch=('i686' 'x86_64')
url="http://fetchmail.berlios.de/"
license=('GPL')
depends=('openssl')
makedepends=('python2')
optdepends=('tk: for using fetchmailconf'
            'python2: for using fetchmailconf')
backup=('etc/conf.d/fetchmail')
options=('!makeflags')
install='fetchmail.install'
source=("http://download.berlios.de/$pkgname/${pkgname}-${pkgver}.tar.xz"
        "http://download.berlios.de/$pkgname/${pkgname}-${pkgver}.tar.xz.asc"
        'fetchmail.tmpfiles' 'fetchmail.service')
sha1sums=('a246a6a3caf90e1106448c9dde4463e87d816031'
          '9a7f76c9fd9f85fc4b421e34fa8c15d2838d61fc'
          '199ba749c829f22286c34aabcf8b7dd5bbd7c0e6'
          '69caf3fd4d4446db3bf53144e4fa8edbdbbfcd10')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' fetchmailconf.py
  PYTHON=python2 ./configure --prefix=/usr --with-ssl=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -o 90 -g nobody "${pkgdir}/var/lib/fetchmail"
  install -D -m644 ${srcdir}/fetchmail.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/fetchmail.conf
  install -D -m644 ${srcdir}/fetchmail.service ${pkgdir}/usr/lib/systemd/system/fetchmail.service
}
