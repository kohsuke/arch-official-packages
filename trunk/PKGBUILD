# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=mono-zeroconf
pkgver=0.9.0
pkgrel=3
pkgdesc="A cross platform Zero Configuration Networking library for Mono and .NET."
url="http://www.mono-project.com/Mono.Zeroconf"
license=('GPL')
arch=('any')
depends=('mono' 'avahi')
options=('!makeflags')
source=("http://download.banshee-project.org/mono-zeroconf/${pkgname}-${pkgver}.tar.bz2")
md5sums=('cb25d50898442a7766c324dcb52ef9bf')

build() {
    export MONO_SHARED_DIR=${srcdir}/.wabi
    mkdir -p "${MONO_SHARED_DIR}"

    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure --prefix=/usr --disable-docs --disable-mdnsresponder
    make hicolordir=/usr/share/icons/hicolor
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make hicolordir=/usr/share/icons/hicolor \
    GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
        DESTDIR=${pkgdir} install
    rm -rf "${MONO_SHARED_DIR}"
}
