# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=antlr
pkgname=java-${_libname}3
pkgver=1.4.3
pkgrel=1
pkgdesc="Language tool that provides a framework for constructing grammatical parsers"
arch=('any')
url="http://www.antlr.org/"
license=('BSD')
depends=('java-runtime')
makedepends=('apache-ant')
# Thanks Mageia:
# http://svnweb.mageia.org/packages/cauldron/antlrworks/current/SOURCES/antlrworks-1.4.3-jdk7.patch
# Patch proposed upstream on 2012-09-15:
# http://www.antlr.org/pipermail/antlr-interest/2012-September/thread.html
source=(http://www.antlr.org/download/${_libname}works-${pkgver}-src.zip
        antlrworks-1.4.3-jdk7.patch
        LICENSE)
sha256sums=('ec76aa4f2aa06283bd8acff418d5a0517edff024c10dfab978f0023cf877fec7'
            '39cf6629d7a779af5434d207fe467bde9c4bf2258c5aee2b11b376a1ffbf960c'
            'a13d8ca68ed79457f0e1c4465bbfa8a0704ccad75e5a909ed2fb7632f3fd77b6')

build() {
  cd "${srcdir}"

  patch -p1 < antlrworks-1.4.3-jdk7.patch
  ant build
}

package() {
  install -Dm644 ${srcdir}/dist/${_libname}works-${pkgver}.jar \
                 ${pkgdir}/usr/share/java/${_libname}works-${pkgver}.jar
  ln -s ${_libname}works-${pkgver}.jar \
        ${pkgdir}/usr/share/java/${_libname}works.jar

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
