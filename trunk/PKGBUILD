# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rfkill
pkgver=0.5
pkgrel=1
pkgdesc='Tool for enabling and disabling wireless devices'
url='http://wireless.kernel.org/en/users/Documentation/rfkill'
license=('custom')
arch=('i686' 'x86_64')
source=("https://www.kernel.org/pub/software/network/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'service.unblock'
        'service.block'
        'rules.d')
sha1sums=('03025d4ae285c40d5c19ec99ef7b317afda9d900'
          '5f4a04a434df2ca28ca6687326c67c1f125aa3d4'
          '6cc294af37232469cd5ba63455b571fe258e1cc6'
          '6390eaa793b473098aaa11b13c5254d611a4c13a')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make SBINDIR=/usr/bin DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../rules.d "${pkgdir}/usr/lib/udev/rules.d/60-rfkill.rules"
	install -Dm644 ../service.block "${pkgdir}/usr/lib/systemd/system/rfkill-block@.service"
	install -Dm644 ../service.unblock "${pkgdir}/usr/lib/systemd/system/rfkill-unblock@.service"
}
