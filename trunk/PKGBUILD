# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=eeze
pkgver=1.7.1
pkgrel=1
pkgdesc="Manipulate devices using udev and sysfs"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('ecore' 'eina' 'udev')
options=('!libtool' '!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('74c7f85a112bb80e16419a30244c144ebc8b20d7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static \
    --disable-tests \
    --disable-doc
  make
}

package(){
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # install license files
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING
}
