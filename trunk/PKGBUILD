# $Id$ 
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgnome
pkgver=2.22.0
pkgrel=1
pkgdesc="Common libraries for GNOME"
arch=(i686 x86_64)
license=('LGPL')
depends=('gnome-vfs>=2.22.0' 'esd>=0.2.38' 'libbonobo>=2.22.0')
makedepends=('pkgconfig' 'perlxml')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=libgnome.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.22/${pkgname}-${pkgver}.tar.bz2)
md5sums=('52db42762589826c66d27def66112807')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  mkdir -p ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
