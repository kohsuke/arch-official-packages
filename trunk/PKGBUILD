# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andries Radu <admiral0@live.it>

pkgname=opengtl
pkgver=0.9.16
pkgrel=2
pkgdesc="A set of library for using and integrating transformation algorithms (such as filter or color conversion) in graphics applications"
url="http://www.opengtl.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gcc-libs' 'llvm')
makedepends=('cmake' 'libpng')
optdepends=('libpng: for using the png extension')
source=(http://download.opengtl.org/OpenGTL-${pkgver}.tar.bz2)
sha1sums=('3cfe4a08e5778c13f7e74dfc822adb68f97ad048')

build() {
  cd "${srcdir}"
  # Fix build with LLVM 3.1
  sed -i 's/LLVM_VERSION STREQUAL "3.0"/LLVM_VERSION STREQUAL "3.1"/' \
    OpenGTL-${pkgver}/CMakeLists.txt
  sed -i 's/ConstantArray::get/ConstantDataArray::getString/' \
    OpenGTL-${pkgver}/OpenGTL/GTLCore/AST/Expression.cpp
  mkdir build
  cd build
  cmake ../OpenGTL-${pkgver} \
                -DCMAKE_BUILD_TYPE=Release \
                -DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
