# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.7.3
pkgrel=1
pkgdesc="KDE Base Runtime Environment"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
depends=('kdelibs' 'ntrack' 'smbclient' 'libssh' 'libcanberra' 'oxygen-icons'
        'xorg-xauth' 'hicolor-icon-theme')
makedepends=('pkg-config' 'cmake' 'automoc4' 'kdepimlibs' 'openslp' 'xine-lib')
optdepends=('htdig: to build the search index in khelpcenter'
            'rarian: needed by khelpcenter'
            'gdb: drkonq crash handler')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/kde-runtime-${pkgver}.tar.bz2"
        'libqzeitgeist08.patch')
sha1sums=('293dec26e0d7278396d2ad0240d14aeea6060970'
          '164c9e4305029cb68a2101cfeeb76c7066c2fd39')

build() {
    cd "${srcdir}"/kde-runtime-${pkgver}
    patch -p1 -i "${srcdir}"/libqzeitgeist08.patch

    cd "${srcdir}"
	mkdir build
	cd build
	cmake ../kde-runtime-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$srcdir/build"
	make DESTDIR="$pkgdir" install
	rm -f "${pkgdir}/usr/share/icons/hicolor/index.theme"
	ln -sf /usr/lib/kde4/libexec/kdesu "${pkgdir}/usr/bin/"
}
