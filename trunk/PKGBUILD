# $Id$
# Maintainer : Tobias Powalowski <tpowa@archlinux.org>

_kernver=2.6.38-ARCH;

pkgname=intel-537
pkgver=2.60.80.0
pkgrel=73
pkgdesc="Intel Modem Drivers for 537 chipsets. For stock arch 2.6 kernel"
arch=(i686)
license=('custom:"INTEL537"')
url="http://linmodems.technion.ac.il/packages/Intel/"
depends=('intel-537-utils' 'kernel26>=2.6.38' 'kernel26<2.6.39')
makedepends=('kernel26-headers>=2.6.38' 'kernel26-headers<2.6.39')
source=(http://linmodems.technion.ac.il/packages/intel/Philippe.Vouters/intel-536EP-537EP_2009_07_04.tar.bz2
        kernel-2.6.31.patch
        kernel-2.6.33.patch
        kernel-2.6.37.patch)
install=intel-537.install
options=(!strip)

build() {
  cd $srcdir/intel-536-537
  patch -Np0 -i ../kernel-2.6.31.patch
  patch -Np0 -i ../kernel-2.6.33.patch
  patch -Np0 -i ../kernel-2.6.37.patch
  # fix makefile
  sed -i -e "s/\`uname -r\`/${_kernver}/g" makefile
  sed -i -e "s/\`uname -r\`/${_kernver}/g" config_check
  make KDIR=/lib/modules/${_kernver}/build KERNEL_SOURCE_PATH=/lib/modules/${_kernver}/build MODEM_TYPE=537 537
}

package() {
  cd $srcdir/intel-536-537
  # Install kernel module
  install -D -m 644 Intel537.ko $pkgdir/lib/modules/${_kernver}/kernel/drivers/net/intel537.ko
  sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" $startdir/intel-537.install
  # gzip -9 modules
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

}
md5sums=('dfea7456fd1b7a6cbf18cdd8ff69e1e7'
         '0de9e6359610264a63754b39846aa178'
         'c23dbe78cc43064628d2b6599a0827e8'
         'e75ac5426300ab97e6752d0bc3ad6c25')
