# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ypbind-mt
pkgver=1.33
pkgrel=4
pkgdesc='Linux NIS daemon'
url='http://www.linux-nis.org/nis/ypbind-mt/'
license=('GPL2')
arch=('i686' 'x86_64')
makedepends=('networkmanager')
depends=('rpcbind' 'openslp' 'dbus-glib')
optdepends=('yp-tools: to set a domain name')
backup=('etc/yp.conf' 'etc/conf.d/ypbind' 'etc/conf.d/nisdomainname')
#source=("ftp://ftp.kernel.org/pub/linux/utils/net/NIS/${pkgname}-${pkgver}.tar.gz"
source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'nisdomainname.conf'
        'ypbind.conf'
        'ypbind')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--enable-dbus-nm \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -D -m644 etc/yp.conf "${pkgdir}"/etc/yp.conf
	install -D -m755 ../ypbind "${pkgdir}"/etc/rc.d/ypbind
	install -D -m644 ../ypbind.conf "${pkgdir}"/etc/conf.d/ypbind
	install -D -m644 ../nisdomainname.conf "${pkgdir}"/etc/conf.d/nisdomainname
	install -d -m755 "${pkgdir}"/var/yp/binding
}
md5sums=('692f68ed0476762be4228ce543a5b791'
         'e83a85291ea2ee152a78f2c16b0b1185'
         '229309a989abad27703ac2e6d07dc7ae'
         '1f7a4c80414d580c9afb3a08267beed0')
