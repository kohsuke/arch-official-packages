# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=gcc4.6
pkgver=4.6.3
pkgrel=1
pkgdesc="The GNU Compiler Collection (${pkgver:0:3}; C and C++)"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
url="http://gcc.gnu.org"
depends=(gcc)
checkdepends=('dejagnu')
options=('!libtool' '!emptydirs')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        gcc_pure64.patch
        gcc-hash-style-both.patch
        gcc-4.6.2-cloog-0.17.patch)
md5sums=('773092fe5194353b02bb0110052a972e'
         '4030ee1c08dd1e843c0225b772360e76'
         '4df25b623799b148a0703eaeec8fdf3f'
         '6d9939a2e667376031679ac9f9c49263')

build() {
  cd gcc-$pkgver

  # Do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  if [ "${CARCH}" = "x86_64" ]; then
    patch -p1 -i ${srcdir}/gcc_pure64.patch
  fi
  patch -p0 -i ${srcdir}/gcc-hash-style-both.patch

  # compatibility with latest cloog
  patch -p1 -i ${srcdir}/gcc-4.6.2-cloog-0.17.patch

  echo ${pkgver} > gcc/BASE-VER

  cd ..
  mkdir gcc-build
  cd gcc-build

  ../gcc-$pkgver/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --mandir=/usr/share/man --infodir=/usr/share/info \
      --with-bugurl=https://bugs.archlinux.org/ \
      --enable-languages=c,c++,lto \
      --enable-shared --enable-threads=posix \
      --with-system-zlib --enable-__cxa_atexit \
      --disable-libunwind-exceptions --enable-clocale=gnu \
      --disable-libstdcxx-pch --enable-libstdcxx-time \
      --enable-gnu-unique-object --enable-linker-build-id \
      --with-ppl --enable-cloog-backend=isl \
      --enable-lto --enable-gold --enable-ld=default \
      --enable-plugin --with-plugin-ld=ld.gold \
      --disable-multilib --disable-libssp \
      --enable-checking=release \
      --program-suffix=-${pkgver:0:3} \
      --enable-version-specific-runtime-libs
  make
}

check() {
  # increase stack size to prevent test failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=31827
  ulimit -s 32768

  # do not abort on error as some are "expected"
  make -C gcc-build -k check || true
  gcc-$pkgver/contrib/test_summary
}

package()
{
  make -j1 -C gcc-build DESTDIR=${pkgdir} install

  # Install Runtime Library Exception
  install -Dm644 gcc-$pkgver/COPYING.RUNTIME \
    $pkgdir/usr/share/licenses/$pkgname/RUNTIME.LIBRARY.EXCEPTION

  # Remove files already in gcc
  pacman -Qql gcc gcc-libs | while read; do
    [[ $REPLY == */ ]] && continue
    rm -fv "${pkgdir}${REPLY}" "${pkgdir}${REPLY%.gz}"
  done
}
