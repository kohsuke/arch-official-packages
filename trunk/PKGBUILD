# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepimlibs
pkgver=4.6.0
pkgrel=1
pkgdesc="KDE PIM Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
depends=('kdelibs' 'gpgme' 'akonadi' 'libical' )
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'cyrus-sasl' 'openldap'
        'docbook-xsl')
install='kdepimlibs.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'glibc-2.13.patch')
sha1sums=('14624960410d270077df530d70d34e270260b3c4'
        '0a3888f121b313daaf10c1f48e6fc7d058a89a57')

build() {
    # Already fixed upstream
    cd ${srcdir}/${pkgname}-${pkgver}
    patch -Np0 -i ${srcdir}/glibc-2.13.patch

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
}
