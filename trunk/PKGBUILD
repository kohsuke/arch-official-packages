# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-mach64
pkgver=6.8.1
pkgrel=1
_mesaver="7.4.1"
pkgdesc="X.org mach64 video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libdrm>=2.4.9' "libgl>=${_mesaver}" 'expat>=2.0.1')
makedepends=('pkgconfig' 'xorg-server>=1.6.1' "mesa>=${_mesaver}"
             'dri2proto>=2.0' 'glproto>=1.4.9' 'xf86driproto' 'diffutils' 'xineramaproto')
conflicts=('xorg-server<1.6.0')
groups=('xorg-video-drivers')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
       http://downloads.sourceforge.net/sourceforge/mesa3d/MesaLib-${_mesaver}.tar.bz2
       7.4-branch.patch
       mesa-7.1-link-shared.patch
       LICENSE)
md5sums=('0856d9c8435cf4350b68fbd57ca6d4fc'
         '423260578b653818ba66c2fcbde6d7ad'
         'd9dbc4717b6867d2ed7abba0a84ee577'
         'f0baa948d9810f268413111ee439d24b'
         '901824dae8053c63ef9d313cdaa129a0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-dri || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  cd "${srcdir}/Mesa-${_mesaver}"
  patch -Np1 -i "${srcdir}/7.4-branch.patch" || return 1
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=mach64 \
    --enable-glx-tls \
    --disable-ttm-api \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
