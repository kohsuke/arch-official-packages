# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>

pkgname=lv2core
pkgver=6.0
pkgrel=1
pkgdesc="LV2: Successor to the LADSPA audio plug-in standard"
url="http://lv2plug.in/"
license=('LGPL' 'custom')
arch=('any')
makedepends=('python2')
provides=('lv2')
source=("http://lv2plug.in/spec/$pkgname-$pkgver.tar.bz2")
md5sums=('433b195a13b230e302d9e2e2fea37383')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	python2 waf configure --prefix=/usr
	python2 waf build $MAKEFLAGS
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	python2 waf install --destdir="$pkgdir"

	install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
