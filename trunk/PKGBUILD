# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=fvwm-crystal
pkgver=3.0.6
pkgrel=3
pkgdesc="Transparent Themes for fvwm"
arch=('any')
url="http://gna.org/projects/fvwm-crystal/"
license=('GPL')
depends=('fvwm-devel' 'imagemagick' 'python2' 'habak' 'trayer')
source=(http://download.gna.org/fvwm-crystal/${pkgver}/${pkgname}-${pkgver}.tar.gz python2.6.patch)
md5sums=('82e2800882abb2c822519f1aa4dc0c72' 'c0baa3da5e188f7e0f7a01d6481b6a53')
sha1sums=('965e521f64b8a89994f5afee25c8b1c88239bff0' 'cfac2165ca8311fc446410cfed3b1d1f92b8ae61')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 < ../python2.6.patch
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install install-doc
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' "${pkgdir}"/usr/bin/fvwm-crystal.{apps,wallpaper} \
    "${pkgdir}"/usr/share/fvwm-crystal/fvwm/scripts/FvwmMPD/{getprevdir.py,stripnames.py}
  install -D -m644 addons/fvwm-crystal.desktop "${pkgdir}/etc/X11/sessions/fvwm-crystal.desktop"

  # Cleaning up files from svn tree
  find "${pkgdir}" -depth -type d -name .svn -exec rm -rf {} \;
}
