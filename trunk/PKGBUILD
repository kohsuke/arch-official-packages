# $Id$
# Maintainer: Tobias Kieslich <tobias funnychar archlinux.org>

pkgname=xfce4-mixer
pkgver=4.8.0
pkgrel=3
pkgdesc="The volume control plugin for the Xfce panel"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('xfce4-panel' 'gstreamer0.10-base' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('gstreamer0.10-base-plugins: to support basic audio hardware'
            'gstreamer0.10-good-plugins: well supported hardware'
            'gstreamer0.10-bad-plugins: not so well supported hardware'
            'gstreamer0.10-ugly-plugins: might contain questionable license hardware')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/apps/$pkgname/4.8/$pkgname-$pkgver.tar.bz2)
sha256sums=('9f9f48fdd0d3f6b1e46694b93a44d15b800db72a96d831b5111d9ac976970fab')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --with-sound=alsa \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
