# $Id$
# Contributor: Dennis "Gyroplast" Herbrich <dennis@archlinux.org>
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=isdn4k-utils
pkgver=3.2p1
pkgrel=6
pkgdesc="User space administration programs and tools for ISDN"
arch=('i686' 'x86_64')
url="http://www.isdn4linux.de/"
license=('GPL')
depends=('ncurses') 
backup=(etc/isdn/{callerid.conf,isdn.conf,rate.conf,isdnlog.isdnctrl0.options})
options=('!makeflags')
source=(ftp://ftp.isdn4linux.de/pub/isdn4linux/utils/${pkgname}.v${pkgver}.tar.bz2
        isdn4k-utils-3.2p1-build.patch
        config)
md5sums=('d347afa462e46eccfd1284aebae227b6'
         '7f056ddac093c77d8be4e076d0e9421c'
         '3429894039f33c114fef5ef9f866920e')

build() {
  cd $srcdir/$pkgname
  patch -Np1 -i $srcdir/isdn4k-utils-3.2p1-build.patch
  cp $srcdir/config ./.config
  make subconfig
  make
}

package() {
  cd $srcdir/$pkgname
  install -dm755 $pkgdir/sbin
  make DESTDIR=$pkgdir install
}
