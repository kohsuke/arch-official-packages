# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgname=gecko-sharp-2.0
pkgver=0.13
pkgrel=4
pkgdesc="C# bindings for gtkmozembed"
arch=('i686' 'x86_64')
license=('LGPL' 'MPL')
url="http://www.mono-project.com/"
depends=('xulrunner' 'gtk-sharp-2')
makedepends=('monodoc' 'pkgconfig')
source=("http://go-mono.com/sources/gecko-sharp-2.0/gecko-sharp-2.0-${pkgver}.tar.bz2"
        'gecko-sharp-2.0-0.12-xulrunner.patch')
md5sums=('f88eaa06e71f8d8fa34cf59a3e034a6b'
         '4f2c07c3fb0c1a94adaa4e4c8d57608b')

build() {
  # get rid of that .wapi errors; thanks to brice
  export MONO_SHARED_DIR="${srcdir}/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  cd ${srcdir}/gecko-sharp-2.0-${pkgver}
  patch -Np1 -i ${srcdir}/gecko-sharp-2.0-0.12-xulrunner.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/gecko-sharp-2.0-${pkgver}
  make GACUTIL="/usr/bin/gacutil /root ${pkgdir}/usr/lib" \
    DESTDIR=${pkgdir} install
}
