# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=wimax-tools
pkgver=1.4.4
pkgrel=1
pkgdesc="WiMAX Tools"
arch=(i686 x86_64)
license=('custom:BSD')
url="http://www.linuxwimax.org/"
depends=(libnl glib2 sh)
options=('!libtool')
source=("$pkgname-$pkgver.tar.gz::http://www.linuxwimax.org/Download?action=AttachFile&do=get&target=$pkgname-$pkgver.tar.gz"
        LICENSE)
sha256sums=('63ea9796704ae2f0e324b655398d103cdd4261d9a2e58d6a41c5574719dec4bd'
            '00337c026ccf1ac2c624c325801378129e7daa5294e98a5b79ae4ceb60b14624')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # --disable-static doesn't work
  rm "$pkgdir"/usr/lib/*.a

  install -Dm 644 "$srcdir/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
