# $Id$
# Maintainer: damir <damir@archlinux.org>

pkgname=dasher
pkgver=4.10.1
pkgrel=3
pkgdesc="Information-efficient text-entry interface, driven by natural continuous pointing gestures"
arch=('i686' 'x86_64')
license=('GPL')
depends=('at-spi' 'libgnomeui' 'gnome-speech' 'gconf' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils')
url="http://www.gnome.org"
groups=('gnome-extra')
options=('!emptydirs')
install=dasher.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/4.10/${pkgname}-${pkgver}.tar.bz2")
md5sums=('83e556690ac54c4bb8c49c050510259e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper --enable-speech
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain dasher ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
