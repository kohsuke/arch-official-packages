# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rfkill
pkgver=0.4
pkgrel=2
pkgdesc="A tool for enabling and disabling wireless devices"
arch=('i686' 'x86_64')
url="http://linuxwireless.org/en/users/Documentation/rfkill"
license=('custom')
depends=('glibc' 'filesystem>=2010.01')
source=(http://wireless.kernel.org/download/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        60-rfkill.rules
        rfkill.conf.d
        rfkill.rc.d)
backup=(etc/conf.d/rfkill)
md5sums=('727892c0fb35c80ee3849fbe89b45350'
         '63f9bf9264911242e430867a41e8918c'
         '398e7cadf023e05e3e0c323aa33575b9'
         '793f0510e26547eb2632efce7d16e50e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # License
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Udev rule
  install -Dm644 "${srcdir}/60-rfkill.rules" "${pkgdir}/lib/udev/rules.d/60-rfkill.rules"
  # Init script
  install -Dm644 "${srcdir}/rfkill.conf.d" "${pkgdir}/etc/conf.d/rfkill"
  install -Dm755 "${srcdir}/rfkill.rc.d" "${pkgdir}/etc/rc.d/rfkill"
}
