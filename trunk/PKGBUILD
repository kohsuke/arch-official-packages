# $Id$

pkgname=fetchmail
pkgver=6.3.21
pkgrel=4
pkgdesc="A remote-mail retrieval utility"
arch=('i686' 'x86_64')
url="http://fetchmail.berlios.de/"
license=('GPL')
depends=('openssl')
makedepends=('python2')
optdepends=('tk: for using fetchmailconf'
            'python2: for using fetchmailconf')
backup=('etc/conf.d/fetchmail')
options=('!makeflags')
install='fetchmail.install'
source=("http://downloads.sourceforge.net/$pkgname.berlios/${pkgname}-${pkgver}.tar.xz"
        "http://downloads.sourceforge.net/$pkgname.berlios/${pkgname}-${pkgver}.tar.xz.asc"
        'fetchmail.rc' 'fetchmail.conf' 'fetchmail.tmpfiles' 'fetchmail.service')
sha1sums=('4f1e5f2e1a616d4007da1de9c976c2c1fb5dcb81'
          'bc8c68ec0d71b97d3168981cbaa4b04ab8e56059'
          'fc25180f1add26df0a3bdaac03fd9d2e3473ff88'
          '30401729386d6f774c6c36ab8530842166de54a8'
          '199ba749c829f22286c34aabcf8b7dd5bbd7c0e6'
          '11ff81fc8363a54099880da18634b0d1ecf9fa82')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' fetchmailconf.py
  PYTHON=python2 ./configure --prefix=/usr --with-ssl=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm755 "${srcdir}/fetchmail.rc" "${pkgdir}/etc/rc.d/fetchmail"
  install -Dm644 "${srcdir}/fetchmail.conf" "${pkgdir}/etc/conf.d/fetchmail"
  install -d -o 90 -g nobody "${pkgdir}/var/lib/fetchmail"
  install -D -m644 ${srcdir}/fetchmail.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/fetchmail.conf
  install -D -m644 ${srcdir}/fetchmail.service ${pkgdir}/usr/lib/systemd/system/fetchmail.service
}
