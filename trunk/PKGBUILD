# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase
pkgver=4.2.1
pkgrel=2
pkgdesc="KDE Base Applications"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdelibs' 'kdebase-runtime' 'qimageblitz' 'libraw1394')
makedepends=('kdebase-workspace' 'pkgconfig' 'cmake' 'automoc4')
optdepends=('kdebase-workspace')
options=('docs')
install='kdebase.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2" 'konsole-layout.patch')
md5sums=('a8f391db7b98feaa6ae81b8f1e99ed5a'
         '9e87d93baef801397cf7e90a82ae8894')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	# fix konsole layout; see https://bugs.kde.org/show_bug.cgi?id=171544
	patch -p1 -i $srcdir/konsole-layout.patch || return 1
	cd ../
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
}
