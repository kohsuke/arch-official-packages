# $ Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-workspace
pkgver=4.1.0
pkgrel=1
pkgdesc="KDE Base Workspace"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdelibs' 'kdebase-runtime' 'libcaptury' 'libxxf86misc' 'libxcomposite' 'qimageblitz'
	 'libusb' 'libxss' 'lm_sensors' 'libxinerama' 'libxklavier' 'libxdamage')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'networkmanager' 'kdemultimedia')
options=('docs')
backup=('usr/share/config/kdm/kdmrc')
source=("ftp://ftp.archlinux.org/other/kde/${pkgname}-${pkgver}.tar.bz2"
        'kdm-zsh-profile.patch')
md5sums=('73850c1bc24454a5770b28fdb1dd53d4' '40f5556820d52a14e9d9722a5a8a491e')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	patch -Np1 -i ../kdm-zsh-profile.patch || return 1
	cd ../
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF
	make
	make DESTDIR=$pkgdir install
}
