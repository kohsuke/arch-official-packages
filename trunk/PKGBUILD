# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=shared-desktop-ontologies
pkgver=0.8.0
pkgrel=1
pkgdesc="Ontologies necessary for the Nepomuk semantic desktop"
arch=('any')
url="http://sourceforge.net/apps/trac/oscaf/"
license=('GPL')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/oscaf/$pkgname-$pkgver.tar.bz2")
md5sums=('3abbd4ddc683aaa0504ed8499b57285e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
