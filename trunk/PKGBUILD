# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity
pkgver=2.0.0
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'wxgtk' 'lame' 'hicolor-icon-theme'
	 'desktop-file-utils' 'jack' 'soundtouch' 'ffmpeg')
options=('!makeflags')
install=audacity.install
source=(http://audacity.googlecode.com/files/${pkgname}-minsrc-${pkgver}.tar.bz2)
sha1sums=('0175474e63c51aaa97df9b45574f7f307f1d3bc8')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"
  WX_CONFIG=/usr/bin/wx-config ./configure --prefix=/usr \
    --with-portaudio --with-libsamplerate \
    --without-libresample --with-libmad \
    --with-ffmpeg --with-id3tag --with-libflac \
    --with-vorbis --with-libexpat \
    --with-libsndfile --with-soundtouch \
    --enable-unicode --without-taglib
  make
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
