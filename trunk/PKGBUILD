# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=fetchmail
pkgver=6.3.19
pkgrel=1
pkgdesc="A remote-mail retrieval utility"
url="http://fetchmail.berlios.de/"
arch=('i686' 'x86_64')
depends=('glibc' 'openssl' 'python2')
optdepends=('tk: fetchmailconf')
options=('!makeflags')
license=('GPL')
install=fetchmail.install
source=(http://download2.berlios.de/fetchmail/${pkgname}-${pkgver}.tar.bz2
        fetchmail.rc fetchmail.conf)
md5sums=('64519711c8533f5a34d20c9ff620d880' '6742e99d65a827bc2b9d6bc8efe269e6'\
         '8fcd6a22dfbc247755fdd050c5c5c4d3')
sha1sums=('fcc9b9299fe147d8f522cff93f8f619e5e1372b7' '13c7e3c538211c79c5b403f2bfa6712a2ad0ac30'\
         '30401729386d6f774c6c36ab8530842166de54a8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-ssl=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm755 "${srcdir}/fetchmail.rc" "${pkgdir}/etc/rc.d/fetchmail"
  install -Dm644 "${srcdir}/fetchmail.conf" "${pkgdir}/etc/conf.d/fetchmail"

  # "home" directory for pid file.
  install -d -o 90 -g nobody -m700 "${pkgdir}/var/run/fetchmail"
}
