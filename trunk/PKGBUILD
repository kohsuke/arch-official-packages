# $Id: PKGBUILD 105813 2011-01-12 00:25:58Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub-at-gmail.com>

pkgname=kdeplasma-applets-networkmanagement
pkgver=git20110407
_commit=fd1a410d508652d149cdbe83823dae208222b181
pkgrel=1
pkgdesc="KDE control panel and widget network connections"
arch=('i686' 'x86_64')
url="http://www.kde.org/"
license=('GPL')
depends=('kdebase-workspace' 'networkmanager')
makedepends=('cmake' 'python2' 'automoc4' 'mobile-broadband-provider-info')
optdepends=('mobile-broadband-provider-info: allow to add new mobile connection')
install=${pkgname}.install
source=("${pkgname}-${pkgver}.tar.gz"::"http://quickgit.kde.org/?p=networkmanagement.git&a=snapshot&h=${_commit}"
        'nm-09-compat.patch')
md5sums=('ba0c541331d3d6412351a9ac2fb60a97'
         'cd32651d8c31a9286047f82974069984')

build() {
  cd ${srcdir}/networkmanagement
  patch -Np1 -i ${srcdir}/nm-09-compat.patch

  cd ${srcdir}
  mkdir build
  cd build
  cmake ../networkmanagement \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDBUS_SYSTEM_POLICY_DIR=/etc/dbus-1/system.d
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
