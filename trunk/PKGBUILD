# $Id$
# Maintainer:  Jan de Groot <jan@archlinux.org>

pkgname=gconf
pkgver=2.26.0
pkgrel=1
pkgdesc="A configuration database system"
arch=(i686 x86_64)
license=('LGPL')
depends=('orbit2>=2.14.17' 'gtk2>=2.16.0' 'libxml2>=2.7.3' 'policykit>=0.9' 'libldap>=2.3.43')
makedepends=('pkgconfig' 'intltool>=0.40.6' 'gtk-doc>=1.10')
options=('!libtool')
install=gconf.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/GConf/2.26/GConf-${pkgver}.tar.bz2
	gconf-merge-schema
	gconfpkg
	gconf-reload.patch)
md5sums=('b010f0de356ea093c6a73778b13de956'
         '1412bafb06f7d8a9601c8f1c4d72cc06'
         'b745f04b4121d8724c002b14a4c110b6'
         'cfcc8e15be7b8a48de4aa34336ff6090')

build() {
  cd "${srcdir}/GConf-${pkgver}"
  patch -Np1 -i "${srcdir}/gconf-reload.patch" || return 1
  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --libexecdir=/usr/lib/GConf \
	      --disable-static --enable-defaults-service || return 1
  make pkglibdir=/usr/lib/GConf || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 "${srcdir}/gconf-merge-schema" "${pkgdir}/usr/bin/" || return 1
  install -d -m755 "${pkgdir}/usr/sbin"
  install -m755 "${srcdir}/gconfpkg" "${pkgdir}/usr/sbin/" || return 1
}
