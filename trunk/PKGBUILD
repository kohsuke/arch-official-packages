# $Id$
# Maintainer: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=xbill
pkgver=2.1
pkgrel=5
pkgdesc="Xbill is a game that tests you reflexes as you seek and destroy all forms of Bill"
arch=('i686' 'x86_64')
url="http://www.xbill.org/"
license=('GPL')
depends=('gtk' 'xaw3d>=1.5E' 'lesstif')
makedepends=('libxaw')
install=xbill.install
source=(http://www.xbill.org/download/$pkgname-$pkgver.tar.gz xbill-wrapper.c)
md5sums=('585e4866b15255a24203db9959407b2f' '9f299e482623999b03ce7585413bc182')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --localstatedir=/var/games || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1

# Build wrapper script from Slackware to use setgid for score file
  mv $pkgdir/usr/bin/xbill $pkgdir/usr/bin/xbill-bin
  gcc -Wall ../xbill-wrapper.c -o $pkgdir/usr/bin/xbill || return 1

# set appropriate permissions and destinations
  chown root:games $pkgdir/usr/bin/xbill
  chmod 4755 $pkgdir/usr/bin/xbill
  chown root:games $pkgdir/var/games
  chmod 775 $pkgdir/var/games
  chown root:games $pkgdir/var/games/xbill
  chmod 775 $pkgdir/var/games/xbill

# Handle the scores file creation in the install script
  rm $pkgdir/var/games/xbill/scores
}
