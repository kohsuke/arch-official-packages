# $Id$
# Maintainer: damir <damir@archlinux.org>

pkgname=scim-uim
pkgver=0.2.0
pkgrel=4
pkgdesc='UIM bridging input engine for SCIM'
arch=('i686' 'x86_64')
url='http://www.scim-im.org/'
license=('GPL')
depends=('scim>=1.4.8' 'uim>=1.5.5')
options=(!libtool)
source=("http://downloads.sourceforge.net/sourceforge/scim/$pkgname-$pkgver.tar.gz"
        'scim-uim-0.2.0-fix-gcc43-build.patch'
        'uim-1.5.5-header.diff')
sha1sums=('456621d5969b6552c3c33c83cf295aa4134fff0a'
          'a9d67946f956d0572c73065e2b4b268bfdf149e3'
          '3b083260910f7855b5446960ab1203c052f4b56c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < ../scim-uim-0.2.0-fix-gcc43-build.patch
  patch -p1 < ../uim-1.5.5-header.diff
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
