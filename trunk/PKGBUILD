# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=ortp
pkgver=0.20.0
pkgrel=2
pkgdesc="A Real-time Transport Protocol (RTP) library"
arch=('i686' 'x86_64')
url="http://www.linphone.org/index.php/eng/code_review/ortp"
license=('LGPL')
depends=('libzrtpcpp' 'libsrtp')
options=('!libtool')
source=("http://download-mirror.savannah.gnu.org/releases/linphone/${pkgname}/sources/${pkgname}-${pkgver}.tar.gz"{,.sig}
        'libzrtpcpp2.patch')
md5sums=('b3f2f02fc5b7ba7e294806c81d419f3c'
         'c5d39f9ad14fe252db9a96f616308b0b'
         'a37392fd9889711ac2f0fed2e388fbd8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}"/libzrtpcpp2.patch

  ./configure --prefix=/usr \
    --enable-zrtp \
    --disable-strict
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
