# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=1.5.8
pkgrel=1
pkgdesc='Input method server supporting various input methods'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://www.csie.nctu.edu.tw/~cp76/gcin/'
depends=('gtk2' 'libxtst')
makedepends=('anthy' 'qt')
optdepends=('qt: support for qt4 input method')
source=("http://cle.linux.org.tw/gcin/download/${pkgname}-${pkgver}.tar.bz2"
        'fix_x86_64_libdir.patch'
        'fix_qt4_paths.patch')
sha1sums=('b4f58c68ce5b690c3ddde2d4f90e0e128dc0e10d'
          'e5c113e50420bf4750f5547641df6ec4b7e437e0'
          '16d004424caed541d5baaa6874f708b1c138ab08')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i "${srcdir}/fix_x86_64_libdir.patch"
	patch -p1 -i "${srcdir}/fix_qt4_paths.patch"
	./configure --prefix=/usr --use_i18n=Y
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
