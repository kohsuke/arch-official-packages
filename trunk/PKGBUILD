# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=kvm
pkgver=84
pkgrel=1
pkgdesc="Latest KVM QEMU is a generic and open source processor emulator which achieves a good emulation speed by using dynamic translation."
arch=(i686 x86_64)
license=('GPL2')
url="http://www.nongnu.org/qemu/"
depends=('sdl' 'alsa-lib' 'esound' 'zlib' 'e2fsprogs' 'gnutls>=2.4.1' 'bluez')
install=kvm.install
conflicts=('qemu')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        65-kvm.rules
        70-kqemu.rules)

build()
{
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr --audio-drv-list=oss,alsa,sdl,esd
    # fix sdl compilation, JON: and extboot
    sed -i -e 's#-rpath,/usr/lib#-rpath,/usr/lib,-rpath,/lib#g' qemu/config-host.mak
    for dir in libkvm user qemu extboot; do
        cd ${srcdir}/kvm-${pkgver}/${dir}
        make || return 1
    done
    # install kvm-qemu
    install -D -m 755 ${srcdir}/${pkgname}-${pkgver}/qemu/x86_64-softmmu/qemu-system-x86_64 \
        ${pkgdir}/usr/bin/qemu-kvm
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/scripts/65-kvm.rules \
        ${pkgdir}/etc/udev/rules.d/65-kvm.rules || return 1
    # install kvm bios files
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/qemu/pc-bios/bios.bin \
        ${pkgdir}/usr/share/qemu/bios.bin
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/qemu/pc-bios/vgabios.bin \
        ${pkgdir}/usr/share/qemu/vgabios.bin
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/qemu/pc-bios/vgabios-cirrus.bin \
        ${pkgdir}/usr/share/qemu/vgabios-cirrus.bin
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/extboot/extboot.bin \
         ${pkgdir}/usr/share/qemu/extboot.bin
    # install udev rules
    install -D -m644 ${srcdir}/70-kqemu.rules \
                     ${pkgdir}/lib/udev/rules.d/70-kqemu.rules
    install -D -m644 ${srcdir}/65-kvm.rules \
                     ${pkgdir}/lib/udev/rules.d/65-kvm.rules
}
md5sums=('39b7206ef400845800f081a5b901f757'
         'cead7ba387159d360f892281499975df'
         'ec06591830b8fcf53913f05f3d66f7e5')
