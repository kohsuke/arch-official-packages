# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libtextcat
pkgver=2.2
pkgrel=7
pkgdesc="Library that implements N-gram-based text categorization"
arch=('i686' 'x86_64')
url="http://software.wise-guys.nl/libtextcat/"
license=('BSD')
depends=('glibc')
options=(!libtool)
source=(http://software.wise-guys.nl/download/${pkgname}-${pkgver}.tar.gz)
md5sums=('128cfc86ed5953e57fe0f5ae98b62c2e')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install

	install -D -m644 src/textcat.h ${pkgdir}/usr/include/textcat.h
	mkdir -p ${pkgdir}/usr/share/libtextcat/{LM,ShortTexts}
	install -m644 langclass/conf.txt ${pkgdir}/usr/share/libtextcat
	install -m644 langclass/LM/*.lm ${pkgdir}/usr/share/libtextcat/LM
	install -m644 langclass/ShortTexts/*.txt ${pkgdir}/usr/share/libtextcat/ShortTexts

	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	sed -i 's|LM|/usr/share/libtextcat/LM|' ${pkgdir}/usr/share/libtextcat/conf.txt
}

