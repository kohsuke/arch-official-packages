# $Id$
# Maintainer: Thomas Baechler <thomas@archlinux.org>

pkgname=v86d
pkgver=0.1.10
pkgrel=4
pkgdesc="userspace helper for uvesafb that runs x86 code in an emulated environment"
arch=('i686' 'x86_64')
url="http://dev.gentoo.org/~spock/projects/uvesafb/"
license=('GPL2')
depends=('glibc')
options=('!makeflags')
source=(http://dev.gentoo.org/~spock/projects/uvesafb/archive/v86d-$pkgver.tar.bz2
        v86d
        v86d_hook
        modprobe.uvesafb)
md5sums=('51c792ba7b874ad8c43f0d3da4cfabe0'
         '66ab32602ab29cc5635eaac7f3e42283'
         '5f75b8bc4a7ddf595014591e5db263cb'
         '2d7cc8dc6a41916a13869212d0191147')

build() {
  cd "$srcdir/v86d-$pkgver"
  ./configure --with-x86emu
  # we only need /usr/include/video/uvesafb.h
  make KDIR=/usr
}

package() {
  cd "$srcdir/v86d-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 "$srcdir/v86d" "$pkgdir/usr/lib/initcpio/install/v86d"
  install -D -m644 "$srcdir/v86d_hook" "$pkgdir/usr/lib/initcpio/hooks/v86d"
  install -D -m644 "$srcdir/modprobe.uvesafb" "$pkgdir/usr/lib/modprobe.d/uvesafb.conf"
}
