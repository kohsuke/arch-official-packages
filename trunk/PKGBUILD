# $Id: PKGBUILD 1214 2009-08-14 01:36:33Z dgriffiths $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=physfs
pkgver=2.0.2
pkgrel=1
pkgdesc="A library to provide abstract access to various archives"
arch=('i686' 'x86_64')
url="http://icculus.org/physfs/"
license=('ZLIB')
depends=('zlib' 'readline')
makedepends=('cmake' 'doxygen')
source=(http://icculus.org/physfs/downloads/${pkgname}-${pkgver}.tar.gz)
md5sums=('4e8927c3d30279b03e2592106eb9184a')
sha1sums=('2d3d3cc819ad26542d34451f44050b85635344d0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  export CXXFLAGS="$CXXFLAGS -fno-strict-aliasing"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DPHYSFS_BUILD_TEST=OFF -DPHYSFS_BUILD_WX_TEST=OFF
  make all docs
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}"/usr/share/{doc/physfs,man/man3}
  install -m644 docs/html/* "${pkgdir}/usr/share/doc/physfs"
  install -m644 docs/man/man3/* "${pkgdir}/usr/share/man/man3"

  for i in author Deinit description extension Free Init major Malloc minor opaque patch Realloc url ; do
    mv "${pkgdir}/usr/share/man/man3/$i.3" "${pkgdir}/usr/share/man/man3/PHYSFS_$i.3"
  done

  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
