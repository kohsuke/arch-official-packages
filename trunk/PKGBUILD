# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>

pkgbase=gnome-python-desktop
pkgname=('gnome-python-desktop' 'python-brasero' 'python-bugbuddy' 'python-evince' 'python-evolution' 'python-gnomeapplet' 'python-gnomedesktop' 'python-gnomekeyring' 'python-gtop' 'python-mediaprofiles' 'python-metacity' 'python-rsvg' 'python-totem-plparser' 'python-wnck' 'python-gtksourceview' 'python-gnomeprint')
pkgver=2.30.2
pkgrel=3
arch=(i686 x86_64)
license=('GPL' 'LGPL')
options=('!libtool')
makedepends=('intltool' 'pkg-config' 'gnome-media' 'gnome-panel' 'gtksourceview' 'libgnomeprintui' 'libgtop' 'totem-plparser' 'evince' 'bug-buddy' 'brasero' 'gnome-desktop' 'metacity')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/2.30/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('23aa8dbae4bd3d00a5da1eae9500ba3787af4ad0481073ea53e5bff824bbcebb')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --enable-metacity || return 1
  make || return 1
}

package_gnome-python-desktop() {
  depends=('python-brasero' 'python-bugbuddy' 'python-evince' 'python-evolution' 'python-gnomeapplet' 'python-gnomedesktop' 'python-gnomekeyring' 'python-gtop' 'python-mediaprofiles' 'python-metacity' 'python-rsvg' 'python-totem-plparser' 'python-wnck' 'python-gtksourceview' 'python-gnomeprint')
  pkgdesc="Python bindings for the GNOME desktop environment"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make install-pkgconfigDATA DESTDIR="${pkgdir}"
}

package_python-brasero() {
  depends=('pygtk' 'brasero')
  pkgdesc="Python bindings for brasero"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C braseroburn install DESTDIR="${pkgdir}"
  make -C braseromedia install DESTDIR="${pkgdir}"
}

package_python-bugbuddy() {
  depends=('pygtk' 'bug-buddy')
  pkgdesc="Python module for bug-buddy"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make install-pkgpyexecPYTHON DESTDIR="${pkgdir}"
}

package_python-evince() {
  depends=('pygtk' 'evince')
  pkgdesc="Python bindings for evince"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C evince install DESTDIR="${pkgdir}"
}

package_python-evolution() {
  depends=('pygtk' 'evolution-data-server')
  pkgdesc="Python bindings for evolution"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C evolution install DESTDIR="${pkgdir}"
  ./py-compile --destdir "${pkgdir}" --basedir /usr/lib/python2.6/site-packages/gtk-2.0 evolution/__init__.py
  install -m644 evolution/ecal.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
}

package_python-gnomeapplet() {
  depends=('pygtk' 'libgnomeui' 'gnome-panel')
  pkgdesc="Python bindings for gnome-panel"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C gnomeapplet install DESTDIR="${pkgdir}"
}

package_python-gnomedesktop() {
  depends=('pygtk' 'gnome-desktop')
  pkgdesc="Python bindings for gnome-desktop"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C gnomedesktop install DESTDIR="${pkgdir}"
}

package_python-gnomekeyring() {
  depends=('pygtk' 'libgnome-keyring')
  pkgdesc="Python bindings for libgnome-keyring"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C gnomekeyring install DESTDIR="${pkgdir}"
}

package_python-gtop() {
  depends=('pygtk' 'libgtop')
  pkgdesc="Python bindings for libgtop"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C gtop install DESTDIR="${pkgdir}"
}

package_python-mediaprofiles() {
  depends=('pygtk' 'gnome-media')
  pkgdesc="Python bindings for gnome-media"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C mediaprofiles install DESTDIR="${pkgdir}"
}

package_python-metacity() {
  depends=('pygtk' 'metacity')
  pkgdesc="Python bindings for metacity"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C metacity install DESTDIR="${pkgdir}"
}

package_python-rsvg() {
  depends=('pygtk' 'librsvg')
  pkgdesc="Python bindings for librsvg"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C rsvg install DESTDIR="${pkgdir}"
  install -m755 -d "${pkgdir}/usr/share/pygtk/2.0/defs"
  install -m644 rsvg/rsvg.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
}

package_python-totem-plparser() {
  depends=('pygtk' 'totem-plparser')
  pkgdesc="Python bindings for totem-plparser"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C totem install DESTDIR="${pkgdir}"
  install -m755 -d "${pkgdir}/usr/share/pygtk/2.0/defs"
  install -m644 totem/plparser.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
}

package_python-wnck() {
  depends=('pygtk' 'libwnck')
  pkgdesc="Python bindings for libwnck"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C wnck install DESTDIR="${pkgdir}"
}

package_python-gtksourceview() {
  depends=('pygtk' 'gtksourceview')
  pkgdesc="Python bindings for gtksourceview"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  for dir in gtksourceview docs/gtksourceview; do
    make -C ${dir} install DESTDIR="${pkgdir}"
  done
}

package_python-gnomeprint() {
  depends=('pygtk' 'libgnomeprintui')
  pkgdesc="Python bindings for libgnomeprintui"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  for dir in gnomeprint docs/gnomeprint docs/gnomeprintui; do
    make -C ${dir} install DESTDIR="${pkgdir}"
  done
}
