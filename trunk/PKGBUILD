# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=kobodeluxe
pkgver=0.5.1
pkgrel=3
pkgdesc="An enhanced version of Akira Higuchi's game XKobo, an addictive space shoot'em up"
arch=('i686' 'x86_64')
url="http://www.olofson.net/kobodl/"
license=('GPL' 'LGPL')
depends=('libjpeg' 'libpng' 'sdl_image' 'hicolor-icon-theme' 'gtk-update-icon-cache')
install=kobodeluxe.install
source=(http://www.olofson.net/kobodl/download/KoboDeluxe-${pkgver}.tar.gz \
        kobodeluxe-gcc-4.3.patch kobodeluxe-0.5.1-glibc2.10.patch)
md5sums=('cf765559461213d651c7be042507b767'
         '2b76306ec997fdc64986b4425e5b7814'
         '56115c9a1e3db1b95b731fdcb11f3f4f')
sha1sums=('12abe182ccde52050cd23077092d0424d25c3e75'
          '274dc8e7909bc6f9fab31306c85d992af8b62674'
          '9d1b3b8cef4a16b6a082fed9f93f1dc12b2a3be0')

build() {
  cd "${srcdir}/KoboDeluxe-${pkgver}"
  patch -p0 < ../kobodeluxe-gcc-4.3.patch
  patch -p0 < ../kobodeluxe-0.5.1-glibc2.10.patch
  ./configure --prefix=/usr --localstatedir=/var --sharedstatedir=/var/games
  make
}

package() {
  cd "${srcdir}/KoboDeluxe-${pkgver}"
  make DESTDIR="${pkgdir}" install
 
  tar -xzf icons.tar.gz
  install -D -m644 icons/KDE/kobo-deluxe.desktop "${pkgdir}/usr/share/applications/kobo-deluxe.desktop"
  for i in 16 22 32 48 64 128 ; do
    install -D -m644 icons/KDE/icons/${i}x${i}/kobodl.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/kobodl.png"
  done
  install -D -m644 icons/Ubuntu/kobo-icon.xpm "${pkgdir}/usr/share/pixmaps/kobo-icon.xpm"
  ln -s /usr/share/icons/hicolor/48x48/kobodl.png "${pkgdir}/usr/share/icons/kobodl.png"

  chown root:games "${pkgdir}/usr/bin/kobodl"
  chmod 2755 "${pkgdir}/usr/bin/kobodl"
  chown root:games "${pkgdir}/var/games"
  chmod 775 "${pkgdir}/var/games"
  chown root:games "${pkgdir}/var/games/kobo-deluxe"
  chmod 775 "${pkgdir}/var/games/kobo-deluxe"
  chown root:games "${pkgdir}/var/games/kobo-deluxe/scores"
  chmod 775 "${pkgdir}/var/games/kobo-deluxe/scores"
}
