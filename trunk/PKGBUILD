# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=qtwebkit
pkgver=2.2.3
pkgrel=1
arch=('i686' 'x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
depends=('qt' 'gstreamer0.10-base')
makedepends=('python2' 'mesa' 'gperf')
conflicts=('qt<4.8')
_qtver=4.8.3
source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}-source.tar.gz"
        "ftp://ftp.archlinux.org/other/${pkgname}/qwebview-${_qtver}.tar.xz"
        'bison26.patch'
        'unsupported-gcc-flag.patch')
sha1sums=('852dc408ff5ed872b8174780b9439bf8bdd51a42'
          'cbaebec8d78a42b0a267b7a3461ed44b885f0366'
          'f7cad6cfff9e03e5d2f2b74a0a13b89c637818bc'
          '7c4b27d235cc569a970eb7ad50b20978da07fddd')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}-source
  patch -p0 -i "${srcdir}"/bison26.patch
  patch -p0 -i "${srcdir}"/unsupported-gcc-flag.patch

  # move headers
  mv include Source/

  cd Source
  qmake
  cd ../

  make -C Source

  # Build the QWebView plugin (FS#27914)
  cd "${srcdir}"/${pkgname}-${pkgver}-source/qwebview-${_qtver}/plugins/qwebview
  qmake
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}-source
  make INSTALL_ROOT="${pkgdir}" -C Source install

  cd "${srcdir}"/${pkgname}-${pkgver}-source/qwebview-${_qtver}/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}

# needs python2, qmake, git, gperf
mksource() {
  local _current_dir=$(pwd)
  local _tmp=$(mktemp -d --tmpdir)

  cd ${_tmp}
  git clone git://gitorious.org/+qtwebkit-developers/webkit/qtwebkit.git

  # fetch the make-package.py script
  git clone git://qt.gitorious.org/qtwebkit/tools.git

  # create the qtwebkit tarball
  cd qtwebkit
  git checkout -b ${pkgname}-${pkgver} ${pkgname}-${pkgver}

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    ../tools/make-package.py
  python2 ../tools/make-package.py

  mv ${pkgname}-${pkgver}-source.tar.gz ${_current_dir}/

  cd ..
  
  # create the qwebview plugin tarball
  mkdir qwebview-${_qtver}
  cd qwebview-${_qtver}
  wget http://releases.qt-project.org/qt4/source/qt-everywhere-opensource-src-${_qtver}.tar.gz
  tar xf qt-everywhere-opensource-src-${_qtver}.tar.gz
  mkdir -p ${pkgname}-${pkgver}-source/qwebview-${_qtver}/plugins/
  cp -ra qt-everywhere-opensource-src-${_qtver}/tools/designer/src/plugins/qwebview \
    ${pkgname}-${pkgver}-source/qwebview-${_qtver}/plugins/

  cat > ${pkgname}-${pkgver}-source/qwebview-${_qtver}/plugins/plugins.pro <<"EOF"
TEMPLATE = subdirs
CONFIG += ordered

REQUIRES = !CONFIG(static,shared|static)
contains(QT_CONFIG, webkit): SUBDIRS += qwebview
EOF

  cat > ${_tmp}/header.txt <<"EOF"
INCLUDEPATH += ../../../Source/include
LIBS += -L../../../Source/lib

EOF

  cat ${_tmp}/header.txt qt-everywhere-opensource-src-${_qtver}/tools/designer/src/plugins/plugins.pri > \
    ${pkgname}-${pkgver}-source/qwebview-${_qtver}/plugins/plugins.pri

  tar cJf qwebview-${_qtver}.tar.xz ${pkgname}-${pkgver}-source

  mv qwebview-${_qtver}.tar.xz ${_current_dir}/

  rm -rf ${_tmp}
}
