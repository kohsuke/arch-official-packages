# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ccache
pkgver=3.1.5
pkgrel=1
pkgdesc="A compiler cache"
url="http://ccache.samba.org/"
source=("http://samba.org/ftp/${pkgname}/$pkgname-$pkgver.tar.bz2")
license=('GPL')
arch=('i686' 'x86_64')
depends=('zlib')
md5sums=('f652bd20253bb4aa1440ae50bea3c9e3')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -Dm 755 ccache ${pkgdir}/usr/bin/ccache
  install -Dm 644 ccache.1 ${pkgdir}/usr/share/man/man1/ccache.1

  install -d ${pkgdir}/usr/lib/ccache/bin
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/cc 
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/gcc
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/g++
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/cpp
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/c++
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-cc
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-gcc
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-g++
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-cpp
  ln -sf /usr/bin/ccache ${pkgdir}/usr/lib/ccache/bin/${CHOST}-c++
}
