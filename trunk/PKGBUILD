# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Committer: Manolis Tzanidakis <manolis@archlinux.org>

pkgname=getmail
pkgver=4.22.1
pkgrel=1
pkgdesc="A POP3 mail retriever with reliable Maildir and command delivery."
arch=(any)
url="http://pyropus.ca/software/getmail"
license=("GPL")
depends=('python2')
source=(http://pyropus.ca/software/getmail/old-versions/$pkgname-$pkgver.tar.gz)
md5sums=('611d37b073fa304552f7220e6c05a6ba')
sha1sums=('ab865fe4b3df37a6e93025c6ee15ac1b7c673c13')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 setup.py install --root=${pkgdir}
  # fix some broken shebang calls
  sed -i -e "s|#![ ]*/usr/bin/env python|#!/usr/bin/env python2|" $pkgdir/usr/lib/python2.7/site-packages/getmailcore/__init__.py
  sed -i -e "s|#![ ]*/usr/bin/env python2.3|#!/usr/bin/env python2|" $(find $pkgdir -name '*.py')
}
