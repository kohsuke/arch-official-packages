# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kdesdk-kate
pkgname=('kdebase-kwrite'
         'kdesdk-kate')
pkgver=4.7.80
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
makedepends=('kdelibs ''cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/kate-${pkgver}.tar.bz2"
        'pkgbuild-syntax-highlight.patch')
sha1sums=('6c243a3fc1f114b8c1d9a6d644ec61e3ebf332e8'
          '0a928253bd2077f0264d96a6c8823c69c47b6a8d')

build() {
  cd "${srcdir}"/kate-${pkgver}
  patch -p1 -i "${srcdir}"/pkgbuild-syntax-highlight.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_KTEXTEDITOR=OFF
  make
}

package_kdebase-kwrite() {
  pkgdesc="Text Editor"
  depends=('kdebase-runtime')
  groups=('kde' 'kdebase')
  url="http://www.kde.org/applications/utilities/kwrite/"
  install='kdebase-kwrite.install'

  cd "${srcdir}"/build/kwrite
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/build/part
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/build/doc/kwrite
  make DESTDIR="${pkgdir}" install
}

package_kdesdk-kate() {
  pkgdesc="Advanced Text Editor"
  depends=('kdebase-kwrite')
  groups=('kde' 'kdesdk')
  url="http://www.kde.org/applications/utilities/kate/"
  install='kdesdk-kate.install'

  cd "${srcdir}"/build/kate
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/build/doc/kate
  make DESTDIR="${pkgdir}" install
}
