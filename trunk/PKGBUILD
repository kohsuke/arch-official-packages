# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-ark
pkgver=0.7.4
pkgrel=1
pkgdesc="X.org ark video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel>=1.11.0')
conflicts=('xorg-server<1.11.0')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        #git-fixes.patch
        )
sha1sums=('d95cb9aa56e7db542b3ac8fed9cb519b11f77556')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #patch -Np1 -i "${srcdir}/git-fixes.patch"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
