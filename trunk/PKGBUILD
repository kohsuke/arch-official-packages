# $Id$
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=eclipse
pkgver=3.4
pkgrel=1
pkgdesc="An IDE for Java and other languages"
arch=('i686' 'x86_64')
url="http://eclipse.org"
depends=(j2sdk gtk2 unzip)
install=eclipse.install
makedepends=(zip)
provides=(eclipse)
license="EPL/1.1"
md5sums=(4f7183395f61f70711a1f244faf5d763
         49e913b17cbd3f2807e6b969a81f03d9
         f08e49e6dad13dda8b2e4036b2a3f28c
         0bee44d3b8839d6b217b6a5efb27407f
         ee66a334bc443fd23ad9bc080dc6ea69)
if [ "$CARCH" = "x86_64" ]; then
    _arch=-x86_64
    md5sums=(6498216f6e6b50a203b5ab319af19dbe
             49e913b17cbd3f2807e6b969a81f03d9
             f08e49e6dad13dda8b2e4036b2a3f28c
             0bee44d3b8839d6b217b6a5efb27407f
	     ee66a334bc443fd23ad9bc080dc6ea69)
fi
source=(ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops/R-3.4-200806172000/eclipse-SDK-$pkgver-linux-gtk$_arch.tar.gz
	ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops/R-3.4-200806172000/eclipse-$pkgver-delta-pack.zip
            eclipse.sh
            eclipse.desktop
	    eclipse.ini.patch)

build() {
  cd $startdir/src/eclipse || return 1

  # patch to increase default memory limits
  patch -p0 < ../eclipse.ini.patch || return 1

  # install eclipse
  mkdir $startdir/pkg/opt || return 1
  mv $startdir/src/eclipse $startdir/pkg/opt || return 1

  # install misc
  mkdir -p $startdir/pkg/etc/profile.d $startdir/pkg/usr/share/applications \
  	|| return 1
  install -m755 $startdir/src/eclipse.sh \
  	$startdir/pkg/etc/profile.d/eclipse.sh || return 1
  install -m644 $startdir/src/eclipse.desktop \
  	$startdir/pkg/usr/share/applications/eclipse.desktop || return 1
}
