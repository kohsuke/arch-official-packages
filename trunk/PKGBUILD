# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=liblrdf
pkgver=0.4.0
pkgrel=9
pkgdesc="A library for the manipulation of RDF file in LADSPA plugins"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/lrdf" #"http://sourceforge.net/projects/lrdf"
depends=('raptor' 'ladspa')
makedepends=('pkgconfig')
license=('GPL')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/lrdf/${pkgname}-${pkgver}.tar.gz
	md5.patch
	raptor2.diff
        pkgconfig-raptor.patch)
groups=('ladspa-plugins')
md5sums=('327a5674f671c4b360c6353800226877'
         'a6d231d052dc188cbc4c1039cf3a2003' 
         '80e938469da06a178e03107ca5b41e55'
         '982ae1bd4b39c2fb5cba108fbcbfbad1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/md5.patch"
  patch -Np0 -i "${srcdir}/raptor2.diff"
  patch -Np1 -i "${srcdir}/pkgconfig-raptor.patch"
  autoreconf -vfi
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
