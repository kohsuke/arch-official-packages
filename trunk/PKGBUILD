# $Id$
# Maintainer: 
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kaffeine
pkgver=1.2.2
pkgrel=3
pkgdesc='KDE media player'
license=('GPL')
arch=('i686' 'x86_64')
url="http://kaffeine.kde.org"
depends=('kdebase-runtime' 'xine-lib')
makedepends=('cmake' 'automoc4')
install=kaffeine.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        kaffeine-1.2.2-gcc4.7.patch)
md5sums=('690e48d2e5fe123887109aa9b1bc1c31'
         '48afe5ec99b38fe02782db57a847033f')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/kaffeine-1.2.2-gcc4.7.patch"
}

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
