# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
pkgname=dbus-core
pkgver=1.6.0
pkgrel=3
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=('expat>=2.0.1' 'coreutils' 'filesystem' 'libsystemd' 'shadow') # shadow for install scriptlet FS#29341
makedepends=('libx11')
conflicts=('dbus<1.2.3-2')
options=(!libtool)
install=dbus.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz{,.asc}
        dbus)
md5sums=('16dcae2dd0c76e398381601ac9acdec4'
         '0ebde9920bde83fe26ce32351a7d8954'
         '3675334f5b4442d1c3e9a008f176e85f')

build() {
  cd "${srcdir}/dbus-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --enable-inotify --disable-dnotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --enable-systemd
  make
}

package(){
  cd "${srcdir}/dbus-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/bin/dbus-launch"
  rm -f "${pkgdir}/usr/share/man/man1/dbus-launch.1"
  rm -rf "${pkgdir}/var/run"

  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/dbus" "${pkgdir}/etc/rc.d/"

  #Fix configuration file
  sed -i -e 's|<user>81</user>|<user>dbus</user>|' "${pkgdir}/etc/dbus-1/system.conf"

  install -dm755 "${pkgdir}/usr/share/licenses/dbus-core"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/dbus-core/"
}
