# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=strigi
pkgver=0.7.5
pkgrel=2
pkgdesc="Fast crawling desktop search engine with Qt4 GUI"
arch=('i686' 'x86_64')
url="http://www.vandenoever.info/software/strigi/"
license=('GPL2')
depends=('qt' 'bzip2' 'exiv2' 'libxml2')
makedepends=('cmake' 'pkg-config')
source=("http://www.vandenoever.info/software/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('0559e2ab65d187d30916e9ffe36e0fb6')
options=('!libtool')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_INOTIFY=ON \
    -DENABLE_LOG4CXX=OFF \
    -DENABLE_FAM=OFF \
    -DENABLE_CLUCENE=OFF
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  # strigidaemon isn't needed
  rm -r "${pkgdir}"/usr/bin/{strigicmd,strigidaemon}
  rm -r "${pkgdir}"/usr/include/strigi/{asyncsocketclient,asyncsocket,clientinterface,socketclient}.h
  rm -r "${pkgdir}"/usr/lib/libsearchclient.so{,.0,.0.7.5}
  rm -r "${pkgdir}"/usr/lib/libsearchclient/LibSearchClientConfig.cmake
  rm -r "${pkgdir}"/usr/share/dbus-1/services/{org.freedesktop.xesam.searcher,vandenoever.strigi}.service
}
