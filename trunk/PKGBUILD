# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=pylint
pkgver=0.26.0
pkgrel=2
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="http://www.logilab.org/project/pylint"
license=('GPL')
depends=('python2-logilab-astng' 'python2' 'python2-distribute')
optdepends=('tk: Pylint GUI')
source=("http://download.logilab.org/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('5dcc07d622b4ff7d5751523fe0161989e463ffe6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i "s|/usr/bin/env python|/usr/bin/env python2|" epylint.py

  python2 setup.py build
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

  install -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/* "${pkgdir}/usr/share/man/man1"
}
