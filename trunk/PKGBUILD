# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=aterm
pkgver=1.0.1
pkgrel=3
pkgdesc="An xterm replacement with transparency support"
arch=('i686' 'x86_64')
url="http://aterm.sourceforge.net/"
license=('GPL')
depends=('libx11')
makedepends=('libxt')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('c2eede028e1011e0ec7035cf319c9b5a')
sha1sums=('a975753b415306a0734efc3773de8a86129bb54b')
    
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-transparency=yes \
    --enable-background-image --enable-fading --enable-menubar \
    --enable-graphics
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
