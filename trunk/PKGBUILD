# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=seamonkey
pkgver=1.1.9
pkgrel=1
pkgdesc="SeaMonkey internet suite"
arch=(i686 x86_64)
license=('MPL')
depends=('gtk2>=2.12.10' 'libidl2>=0.8.10' 'mozilla-common>=1.1-2' 'nss>=3.12rc4' 'libxt' 'gcc-libs>=4.3.1')
makedepends=('zip' 'pkgconfig' 'diffutils' 'gcc' 'libgnomeui>=2.22.1')
replaces=('mozilla')
url="http://www.mozilla.org/projects/seamonkey"
source=(ftp://ftp.mozilla.org/pub/mozilla.org/seamonkey/releases/${pkgver}/seamonkey-${pkgver}.source.tar.bz2
        mozconfig 
        seamonkey.desktop
	firefox-2.0-link-layout.patch)
md5sums=('2b08c472164e80922f715c6e96e0bee7'
         '56da79cce7f766b03a127a16f99af1ff'
         '448848e65efcbf1b2be2873b66b6cd48'
         'b933c00957ea793fe940f4d46a85e10e')

build() {
  cd ${startdir}/src/mozilla
  patch -Np1 -i ${startdir}/src/firefox-2.0-link-layout.patch || return 1
  cp ${startdir}/src/mozconfig .mozconfig

  unset CFLAGS
  unset CXXFLAGS

  export LDFLAGS="-Wl,-rpath,/usr/lib/seamonkey-${pkgver}"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS" || return 1
  make -j1 DESTDIR=${startdir}/pkg install || return 1

  install -m755 -d ${startdir}/pkg/usr/share/applications
  install -m755 -d ${startdir}/pkg/usr/share/pixmaps
  install -m644 ${startdir}/src/mozilla/suite/branding/icons/gtk/seamonkey.png \
                ${startdir}/pkg/usr/share/pixmaps/ || return 1
  install -m644 ${startdir}/src/seamonkey.desktop ${startdir}/pkg/usr/share/applications/ || return 1

  rm -f ${startdir}/pkg/usr/lib/pkgconfig/seamonkey-ns{s,pr}.pc
}
