# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=enlightenment17
_pkgname=enlightenment
pkgver=0.17.4
pkgrel=2
pkgdesc="Enlightenment window manager DR17 (aka e17)"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('alsa-lib' 'pam' 'shared-mime-info' 'hicolor-icon-theme'
  'desktop-file-utils' 'e_dbus' 'edje' 'eeze' 'efreet'
  'eio' 'elementary' 'xcb-util-keysyms' 'udisks' 'systemd' 'ttf-font')
optdepends=('connman: network module')
provides=('notification-daemon' 'e')
backup=('etc/enlightenment/sysactions.conf')
options=('!libtool' '!emptydirs')
install=enlightenment17.install
source=(http://download.enlightenment.org/releases/$_pkgname-$pkgver.tar.gz
  'e-applications.menu' 'quickstart.patch' 'sysactions_systemd.patch' 
  'blackscreenfix.patch')
sha1sums=('29f5f4c10bcc7b32f79013ccbc7257abeea78faf'
          'e08cc63cb8a188a06705b42d03e032b9fcfa7ee5'
          '3f53931ae86de8fe99e386aeb097521aac0decd8'
          '86f1c5e36dc274c3101362532378be034e89c593'
          '85f9b1378ebb7a05ed7f0e8db6982d88a0f46be4')
	
prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -p0 < $srcdir/quickstart.patch

  # upstream fix for blackscreen problem on existing configs
  patch -Np1 -i $srcdir/blackscreenfix.patch

  # default to systemd for sysactions
  patch -Np0 < $srcdir/sysactions_systemd.patch

}
build() {
  cd "$srcdir/$_pkgname-$pkgver"

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/enlightenment  \
    --enable-strict \
    --disable-static \
    --enable-pam \
    --disable-device-hal \
    --enable-device-udev \
    --disable-mount-hal \
    --enable-mount-udisks \
    --enable-mount-eeze \
    --enable-elementary \
    --enable-emotion \
    --enable-enotify \
    --disable-ephysics \
    --disable-wayland-clients \
    --disable-conf-wallpaper2 \
    --disable-illume2 \
    --disable-physics \
    --disable-doc
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  make DESTDIR=$pkgdir install

# install license files
  if [ -e $srcdir/$_pkgname-$pkgver/COPYING ]; then
    install -Dm644 $srcdir/$_pkgname-$pkgver/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING
  fi

  if [ -e $srcdir/$_pkgname-$pkgver/COPYING-PLAIN ]; then
    install -Dm644 $srcdir/$_pkgname-$pkgver/COPYING-PLAIN \
        $pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN
  fi

  # install a default applications.menu file (mostly copy from gnome-menus)
  install -Dm644 $srcdir/e-applications.menu \
	$pkgdir/etc/xdg/menus/e-applications.menu
}

