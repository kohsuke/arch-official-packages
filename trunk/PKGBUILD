# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pdns
pkgver=2.9.22
pkgrel=6
pkgdesc="A modern, advanced and high performance authoritative-only nameserver"
arch=(i686 x86_64)
url="http://www.powerdns.com"
license=('GPL')
depends=('gcc-libs' 'zlib' 'postgresql-libs' 'libmysqlclient' 'sqlite3' 'libldap')
makedepends=('boost')
provides=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
replaces=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
conflicts=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
install=pdns.install
options=(!makeflags !libtool)
backup=('etc/powerdns/pdns.conf')
source=(http://downloads.powerdns.com/releases/${pkgname}-${pkgver}.tar.gz
        2.9.18-default-mysql-options.patch
        pdns-2.9.22-gcc44.patch
        pdns.rc
        pdns.conf)
md5sums=('8a6ff842733aca885577eb54e983a1ff'
         '90fb32e843326d14359c508cef855929'
         '530642273005bdabae27792098834ea6'
         '16e2d32e9781be7afa8f1700740bcdb0'
         'a390ab49c4fcde205629be33bddc2e6c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/2.9.18-default-mysql-options.patch"
  patch -Np1 -i "${srcdir}/pdns-2.9.22-gcc44.patch"
  touch AUTHORS NEWS
  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing
  ./configure --prefix=/usr \
    --sysconfdir=/etc/powerdns \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib/powerdns \
    --mandir=/usr/share/man \
    --with-modules="" \
    --with-dynmodules="ldap pipe gmysql gpgsql gsqlite3 geo" \
    --disable-recursor \
    --disable-static
  make
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/rc.d"
  rm "${pkgdir}/etc/powerdns/pdns.conf-dist"
  install -m644 "${srcdir}/pdns.conf" "${pkgdir}/etc/powerdns/"
  install -m755 "${srcdir}/pdns.rc" "${pkgdir}/etc/rc.d/pdns"
}
