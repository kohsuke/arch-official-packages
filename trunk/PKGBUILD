# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgbase=vigra
pkgname=('vigra' 'vigra-doc')
pkgver=1.8.0
pkgrel=2
arch=('i686' 'x86_64')
url="http://hci.iwr.uni-heidelberg.de/vigra/"
license=('custom:MIT')
makedepends=(# runtime deps
             'libpng' 'libtiff' 'gcc-libs' 'sh' 'hdf5>=1.8.7' 'fftw'
	     # additional makedeps
             'cmake' 'python-nose' 'doxygen' 'python-sphinx' 'boost' 'python-numpy')
options=('!libtool')
source=(http://kogs-www.informatik.uni-hamburg.de/~koethe/vigra/${pkgname}-${pkgver}-src.tar.gz
	vigra-1.7.1.gcc460.patch)
md5sums=('15c5544448e529ee60020758ab6be264'
         '25ef8bc26bc38ee67e5b512d2acd0166')
sha1sums=('09f1d506c2748ebeb7d9f1c77ce387f9e7b837d2'
          '1de6381a74dd136ea25df5a27f249187b0dbd17e')

build() {
  cd "${srcdir}"/${pkgbase}-${pkgver}
  patch -Np1 -i ${srcdir}/vigra-1.7.1.gcc460.patch
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYTHON_EXECUTABLE=/usr/bin/python2 \
      -DWITH_VIGRANUMPY=1 \
      -DDOCINSTALL=share/doc 
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make -k check || /bin/true # i686 fails
}

package_vigra() {
  pkgdesc="Computer vision library"
  depends=('libpng' 'libtiff' 'gcc-libs' 'sh' 'hdf5>=1.8.7' 'fftw')
  optdepends=('python2: for python bindings'
              'boost-libs: for python bindings')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # license
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  
  # remove doc
  rm -rf "${pkgdir}"/usr/share/doc
  
  #fix shebang for python2
  sed -i 's|python$|python2|' "$pkgdir"/usr/bin/vigra-config
}

package_vigra-doc() {
  pkgdesc="Computer vision library - documentation and examples"
  #depends=('vigra')
  #arch=('any')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # cleanup
  rm -rf "${pkgdir}"/usr/{bin,include,lib}

  # license
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
