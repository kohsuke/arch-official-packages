# $Id: PKGBUILD 1214 2009-08-14 01:36:33Z dgriffiths $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>

pkgname=physfs
pkgver=2.0.1
pkgrel=1
pkgdesc="A library to provide abstract access to various archives"
arch=('i686' 'x86_64')
url="http://icculus.org/physfs/"
license=('zlib')
depends=('zlib' 'readline')
makedepends=('cmake' 'doxygen')
source=(http://icculus.org/physfs/downloads/${pkgname}-${pkgver}.tar.gz)
md5sums=('df00465fcfa80e87f718961c6dd6b928')
sha1sums=('1e291e8717ce3bb921b74416ec7cbcf212efcaa1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  export CXXFLAGS="$CXXFLAGS -fno-strict-aliasing"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make all docs
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}"/usr/share/{doc/physfs,man/man3}
  install -m644 docs/html/* "${pkgdir}/usr/share/doc/physfs"
  install -m644 docs/man/man3/* "${pkgdir}/usr/share/man/man3"

  for i in author Deinit description extension Free Init major Malloc minor opaque patch Realloc url ; do
    mv "${pkgdir}/usr/share/man/man3/$i.3" "${pkgdir}/usr/share/man/man3/PHYSFS_$i.3"
  done

  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
