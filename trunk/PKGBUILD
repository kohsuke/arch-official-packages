# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-doc-utils
pkgver=0.20.4
pkgrel=1
pkgdesc="Documentation utilities for Gnome"
arch=('any')
license=('GPL' 'LGPL')
depends=('libxslt' 'python2' 'docbook-xml' 'rarian')
makedepends=('pkgconfig' 'intltool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.20/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('97c6465ffb15969a9fe1761f16f30c1028977fb24ecda89a0b479170f6412e4d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
