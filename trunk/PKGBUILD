# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=pylint
pkgname=('python2-pylint' 'python-pylint')
pkgver=0.26.0
pkgrel=3
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="http://www.logilab.org/project/pylint"
license=('GPL')
makedepends=('python2-logilab-astng' 'python-logilab-astng')
optdepends=('tk: Pylint GUI')
conflicts=('pylint<=0.26.0-2')
source=("http://download.logilab.org/pub/${pkgbase}/${pkgbase}-${pkgver}.tar.gz")
sha1sums=('5dcc07d622b4ff7d5751523fe0161989e463ffe6')
install=${pkgbase}-${pkgver}.install

build() {
  cd "${srcdir}"
  cp -a ${pkgbase}-${pkgver}{,-py3}

  cd ${pkgbase}-${pkgver}

  sed -i "s|/usr/bin/env python|/usr/bin/env python2|" epylint.py
  python2 setup.py build

  cd ../${pkgbase}-${pkgver}-py3

  sed -i "s|/usr/bin/env python|/usr/bin/env python3|" epylint.py
  python3 setup.py build
}

package_python2-pylint() {
  replaces=('pylint<=0.26.0-2')
  depends=('python2-logilab-astng')

  cd "${srcdir}"/${pkgbase}-${pkgver}

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

  install -d "${pkgdir}"/usr/share/man/man1
  install -m644 man/* "${pkgdir}/usr/share/man/man1"

  for i in epylint pylint pylint-gui pyreverse symilar; do
     mv "${pkgdir}"/usr/bin/${i}{,2}
     mv "${pkgdir}"/usr/share/man/man1/${i}{,2}.1
  done
}

package_python-pylint() {
  depends=('python-logilab-astng')

  cd "${srcdir}"/${pkgbase}-${pkgver}-py3

  python3 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

  install -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/* "${pkgdir}/usr/share/man/man1"
}
