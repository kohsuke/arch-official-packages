# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.5.4
pkgrel=1
pkgdesc="KDE Base Runtime Environment"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
depends=('kdelibs' 'oxygen-icons' 'xorg-xauth' 'hicolor-icon-theme'
         'smbclient' 'rarian' 'libssh')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs' 'openslp'
             'xine-lib' 'docbook-xsl')
optdepends=('htdig: to build the search index'
            'gdb: drkonq crash handler')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'phonon4.4.3.patch')
sha1sums=('120289753a6c0e3c9611ab21da0157abf0e6e351'
          '31658a8116553847d5654044fc4286adfd821f95')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
    patch -Np0 -i ${srcdir}/phonon4.4.3.patch

    cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
	rm -f ${pkgdir}/usr/share/icons/hicolor/index.theme
	ln -sf /usr/lib/kde4/libexec/kdesu ${pkgdir}/usr/bin/
}
