# $Id$
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libftdi
pkgver=0.18
pkgrel=1
pkgdesc="A library to talk to FTDI chips"
arch=("i686" "x86_64")
url="http://www.intra2net.com/en/developer/libftdi/download.php"
license=("GPL2" "LGPL2.1")
depends=("libusb")
makedepends=("boost")
options=('!libtool')
source=("http://www.intra2net.com/en/developer/libftdi/download/$pkgname-$pkgver.tar.gz")
md5sums=('916f65fa68d154621fc0cf1f405f2726')
sha1sums=('52401db0e7cb90a5d83f82c2859a4f8d44e52579')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '/no-install/d' examples/Makefile.in
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
