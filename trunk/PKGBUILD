# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# See .contrib for older/other contributors

pkgname=eric
pkgver=5.1.7
pkgrel=1
pkgdesc="A full-featured Python 3.x and Ruby IDE in PyQt4"
arch=('any')
url="http://eric-ide.python-projects.org/"
license=('GPL3')
depends=('python-qscintilla')
conflicts=('eric5')
provides=('eric5')
replaces=('eric5')
install=$pkgname.install
source=("http://downloads.sourceforge.net/eric-ide/${pkgname}5-$pkgver.tar.gz"
        'desktop.patch')
md5sums=('a6bdcfba1ebc2cd9a9ade36a3af032a5'
         '6400f0f83634bfe7c332ee84ff6786c7')

build() {
  cd "$srcdir/${pkgname}5-$pkgver"

  # freedesktop.org compatibility
  patch -Np1 -i "$srcdir/desktop.patch"
}

package() {
  cd "$srcdir/${pkgname}5-$pkgver"

  python install.py -c -b /usr/bin -i "$pkgdir"

  # Remove files conflicting with eric4
  # what are we doing here?
  # TODO: tell upstream to fix their stuff
  #   and allow installation of eric4 + eric5
  #   OR package these files separately eg. 'eric-common'
  rm "$pkgdir/usr/share/qt/qsci/api/python/zope-2.10.7.api"
  rm "$pkgdir/usr/share/qt/qsci/api/python/zope-2.11.2.api"
  rm "$pkgdir/usr/share/qt/qsci/api/python/zope-3.3.1.api"
  rm "$pkgdir/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.api"

  # some canonical symlinks
  cd "$pkgdir/usr/bin"
  for i in *; do
    ln -s /usr/bin/$i ${i/eric5/eric}
  done
}

# vim:set ts=2 sw=2 et:
