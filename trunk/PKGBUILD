# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rfkill
pkgver=0.4
pkgrel=7
pkgdesc='Tool for enabling and disabling wireless devices'
url='http://linuxwireless.org/en/users/Documentation/rfkill'
license=('custom')
arch=('i686' 'x86_64')
source=("http://wireless.kernel.org/download/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'service.unblock'
        'service.block'
        'rules.d')
sha1sums=('fccabf5a272ac2891f24dc35449dc6f4c45ff44b'
          '4d0ef151891c32c882dcec8b03ab7204f015d0d1'
          'c44581c4cc19509f53afd0abea1341cc30751fe2'
          '6390eaa793b473098aaa11b13c5254d611a4c13a')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../rules.d "${pkgdir}/usr/lib/udev/rules.d/60-rfkill.rules"
	install -Dm644 ../service.block "${pkgdir}/usr/lib/systemd/system/rfkill-block@.service"
	install -Dm644 ../service.unblock "${pkgdir}/usr/lib/systemd/system/rfkill-unblock@.service"
}
