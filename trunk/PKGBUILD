# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=2.2.9
pkgrel=5
pkgdesc="A personal and small-business financial-accounting application"
arch=('i686' 'x86_64')
url="http://www.gnucash.org"
license=("GPL")
depends=('gtkhtml' 'slib' 'goffice' 'guile' 'libofx' 'gconf>=2.24')
makedepends=('intltool')
optdepends=('evince: for print preview'
            'gnucash-docs: (AUR) for user documentation'
            'perl-finance-quote: for stock information lookups'
            'perl-date-manip: for stock information lookups')
options=('!libtool' '!makeflags')
install=gnucash.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz \
        03_goffice_0-8_support.patch 03_disable_graphing_support.patch)
md5sums=('570eee09a1ae83c9953ec60a7fc146ed' '57aaf609bb6fe4ed863bfddd03da505a'\
         '87943fbebbcf88488369bf1d3651083f')
sha1sums=('8f63c67fcceaae93ceb8827143cc6a470e3a096a' '71e16460ac5f8b277d0217244084bc0099529882'\
         '8eb33bc06498cec34a0b4e86806d417210ca032a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../03_goffice_0-8_support.patch || return 1
  patch -p1 < ../03_disable_graphing_support.patch || return 1
  ./configure --prefix=/usr --mandir=/usr/share/man \
        --sysconfdir=/etc --libexecdir=/usr/lib --enable-ofx || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas || return 1
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
