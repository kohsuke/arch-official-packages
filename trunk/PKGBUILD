# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=gtk-xfce-engine
pkgver=2.9.0
pkgrel=1
pkgdesc="A port of Xfce engine to GTK+-2.0"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('gtk2')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://archive.xfce.org/src/xfce/$pkgname/2.9/$pkgname-$pkgver.tar.bz2)
sha1sums=('ce0ef1220c98dddabaf7917f8d98579dd65f5009')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
