# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-sisimedia
pkgver=0.9.1
pkgrel=1
pkgdesc="X.org SiS 671 video driver"
arch=(i686 x86_64)
url="http://www.linuxconsulting.ro/xorg-drivers/"
license=('custom')
depends=('glibc' 'sis-dri')
makedepends=('xorg-server-devel>=1.11.0' 'xf86dgaproto' 'libdrm' 'xf86driproto' 'mesa' 'glproto')
conflicts=('xorg-server<1.11.0')
options=('!libtool')
source=(http://xorg.freedesktop.org/releases/individual/driver/xf86-video-sis-${pkgver}.tar.bz2
        xf86-video-sis-0.9.1-20102701.patch
        0002-Remove-XFree86-Misc-PassMessage-support.patch
        0003-Fix-build-with-Werror-format-security.patch
        0005-Fix-backlight-off-on-SiS30x.-video-bridges.patch
        0006-Add-IgnoreHotkeyFlag-driver-option.patch
        xf86-video-sis-0.9.1-dump-regs-after-video-init.patch
        0007-Remove-useless-loader-symbol-lists.patch
        0008-update-to-xextproto-7-1-support.patch
        0009-update-for-rac-removal.patch
        0010-change-to-use-abi-version-check.patch
        0011-more-rac-removal.patch)

build() {
  cd "${srcdir}/xf86-video-sis-${pkgver}"
  patch -Np1 -i "${srcdir}/xf86-video-sis-0.9.1-20102701.patch"
  patch -Np1 -i "${srcdir}/0002-Remove-XFree86-Misc-PassMessage-support.patch"
  patch -Np1 -i "${srcdir}/0003-Fix-build-with-Werror-format-security.patch"
  patch -Np1 -i "${srcdir}/0005-Fix-backlight-off-on-SiS30x.-video-bridges.patch"
  patch -Np1 -i "${srcdir}/0006-Add-IgnoreHotkeyFlag-driver-option.patch"
  patch -Np1 -i "${srcdir}/xf86-video-sis-0.9.1-dump-regs-after-video-init.patch"
  patch -Np1 -i "${srcdir}/0007-Remove-useless-loader-symbol-lists.patch"
  patch -Np1 -i "${srcdir}/0008-update-to-xextproto-7-1-support.patch"
  patch -Np1 -i "${srcdir}/0009-update-for-rac-removal.patch"
  patch -Np1 -i "${srcdir}/0010-change-to-use-abi-version-check.patch"
  patch -Np1 -i "${srcdir}/0011-more-rac-removal.patch"

  sed -i -e 's,sis_drv,sisimedia_drv,g' src/Makefile.am
  sed -i -e 's,\"sis\",\"sisimedia\",g' src/sis.h
  sed -i -e 's,sisModuleData,sisimediaModuleData,g' src/sis_driver.c

  autoreconf -fi

  ./configure --prefix=/usr --enable-dri
  make
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
