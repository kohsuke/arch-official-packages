# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ypserv
pkgver=2.26
pkgrel=2
pkgdesc='Linux NIS Server'
arch=('i686' 'x86_64')
url='http://www.linux-nis.org/nis/ypserv/'
license=('GPL2')
depends=('gdbm' 'openslp')
backup=('etc/ypserv.conf' 'etc/netgroup' 'var/yp/securenets')
source=("ftp://ftp.kernel.org/pub/linux/utils/net/NIS/${pkgname}-${pkgver}.tar.gz" \
        'yppasswd'
        'ypserv')
sha1sums=('0d7ab3f04ff7fa5f611e71ea0f3c188659602743'
          '96192b628afe36709496e4801d016c4bff343f0e'
          'b625381bfa6cf62345377a7df30b8f45935206c5')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -D -m644 etc/netgroup "${pkgdir}"/etc/netgroup
	install -D -m644 etc/ypserv.conf "${pkgdir}"/etc/ypserv.conf
	install -D -m644 etc/securenets "${pkgdir}"/var/yp/securenets
	install -D -m755 ../ypserv "${pkgdir}"/etc/rc.d/ypserv
	install -D -m755 ../yppasswd "${pkgdir}"/etc/rc.d/yppasswd
}
