# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=nepomuk-core
pkgver=4.9.2
pkgrel=1
pkgdesc="Contains the central Nepomuk services like file indexing, file system monitoring, query, storage, client libraries"
url="https://projects.kde.org/projects/kde/kdelibs/nepomuk-core"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4' 'doxygen')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'regression-crash.patch')
sha1sums=('a04e632bfa39ac5158c2addf7278bfb27d29cb5b'
          'e6237844c0f9d5838b827068fe768394e0df0cff')

build() {
  # KDEBUG 307388
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/regression-crash.patch
  cd ../

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  # Fix the python shebang
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/nepomuk-simpleresource-rcgen
}
