# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=mono-upnp
pkgver=0.1.1
pkgrel=1
pkgdesc="UPNP binding for Mono/.NET"
arch=('i686' 'x86_64')
url="https://github.com/mono/mono-upnp"
license=('MIT')
depends=('gtk-sharp-2' 'mono-addins' 'taglib-sharp')
source=(ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('35b3e2ab07407eed018a5b24c22241aa')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh --help
  
  sed -i 's/nunit/mono-nunit/' configure tests/*/Makefile.in
  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
