# $Id: PKGBUILD 85242 2010-07-11 02:23:37Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=foobillard++
pkgver=3.4beta
pkgrel=1
pkgdesc="An OpenGL billiard game"
arch=('i686' 'x86_64')
url="http://foobillardplus.sourceforge.net/"
license=('GPL2')
depends=('bzip2' 'freetype2' 'libpng' 'mesa' 'sdl_net')
replaces=('foobillard')
source=(http://downloads.sourceforge.net/foobillardplus/foobillardplus-${pkgver}.tar.gz)
md5sums=('130418a45ba84b4132f9b27712feb919')
sha1sums=('d8723568e460ee62cb913b0c8d7c9a9f557395ee')

build() {
  cd "${srcdir}/foobillardplus-${pkgver}"
  sed -i 's|$prefix/foobillardplus|$prefix|' configure
  sed -i 's|$prefix/data|$prefix/share/foobillardplus|' configure
  sed -i 's|"/data"|"/share/foobillardplus"|' src/language.c
  sed -i 's|/opt/foobillardplus/bin/||' foobillardplus.desktop
  sed -i 's|/opt/foobillardplus/||' foobillardplus.desktop
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/foobillardplus-${pkgver}"
  install -Dm755 src/foobillardplus "${pkgdir}/usr/bin/foobillardplus"
  install -d "${pkgdir}/usr/share/foobillardplus"
  cp -R data/* "${pkgdir}/usr/share/foobillardplus"
  install -Dm644 foobillardplus.desktop "${pkgdir}/usr/share/applications/foobillardplus.desktop"
  install -Dm644 foobillardplus.png "${pkgdir}/usr/share/pixmaps/foobillardplus.png"
  install -Dm644 foobillardplus.xbm "${pkgdir}/usr/share/pixmaps/foobillardplus.xbm"
  find "${pkgdir}" -depth  -type d -name .svn -exec rm -rf  {} \;
}
