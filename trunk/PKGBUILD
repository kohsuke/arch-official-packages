# $Id$
# Maintainer: tobias <tobias@archlinux.org>
#Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=0.8.0
pkgrel=1
pkgdesc="A frontend to the panorama-tools"
arch=(i686 x86_64)
license=('LGPL')
depends=('wxgtk>=2.8.9.2' 'libpano13>=2.9.14' 'boost' 'enblend-enfuse' 'exiv2' \
         'openexr' 'autopano-sift-c')
makedepends=('zip' 'cmake')
optdepends=('perl-exiftool: write metadata on output images')
url="http://hugin.sourceforge.net/"
options=(force)
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('0c88afbd7f8159aac29404ff40370400')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  mkdir mybuild
  cd mybuild
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    ../ \
    -DCMAKE_EXE_LINKER_FLAGS="-lpthread" || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
