# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Andrew Rose <ody@netrux.com>

pkgname=gif2png
pkgver=2.5.4
pkgrel=1
pkgdesc="A GIF to PNG image format converter"
arch=('i686' 'x86_64')
url="http://www.catb.org/~esr/gif2png/"
license=('ZLIB')
depends=('libpng')
optdepends=('python2: for using web2png')
source=("http://www.catb.org/~esr/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('d63ea3fc6b6119589e8d4d6fead30df9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's#env python#env python2#' web2png
  ./configure --prefix=/usr \
              --mandir=/usr/share/man || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
