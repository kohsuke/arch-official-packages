# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer0.10-python
pkgver=0.10.21
pkgrel=1
pkgdesc="Python bindings for GStreamer 0.10"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('pygobject>=2.20.0' 'gstreamer0.10-base>=0.10.32')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://gstreamer.freedesktop.org/src/gst-python/gst-python-${pkgver}.tar.bz2)
md5sums=('31340ae3e877797a10d088a226d74b16')

build() {
  cd "${srcdir}/gst-python-${pkgver}"
  export PYTHON=python2
  sed -i -e 's%^#!.*env python$%#!/usr/bin/env python2%' examples/* gst/extend/*.py
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/gst-python-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
