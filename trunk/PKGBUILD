# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=1.16.0
pkgrel=2
pkgdesc="The command-line interface for Cabal and Hackage."
url="http://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('haskell-http=4000.2.4-1' 'haskell-zlib=0.5.3.3-5' 'haskell-mtl=2.1.2-1' 'haskell-network=2.3.1.0-1' 'haskell-random=1.0.1.1-3')
depends=('ghc' 'gmp' 'zlib')
source=("http://hackage.haskell.org/packages/archive/cabal-install/${pkgver}/cabal-install-${pkgver}.tar.gz"
        '0001-disable-setting-jobs-due-to-cabal-1.16.0-bug-shipped.patch')
md5sums=('da046116eee7664158411fa9e2cf6b14'
         'd4705a398588efe6dbbcf6f4c3185a7d')

build() {
  cd cabal-install-${pkgver}

  patch -Np1 -i ${srcdir}/0001-disable-setting-jobs-due-to-cabal-1.16.0-bug-shipped.patch

  runhaskell Setup configure \
    --enable-shared \
    --enable-split-objs \
    --enable-executable-stripping \
    --prefix=/usr

  runhaskell Setup build
}

package() {
  cd cabal-install-${pkgver}

  runhaskell Setup copy --destdir=$pkgdir

  # rm license installed to wrong location
  rm -rf ${pkgdir}/usr/share/doc

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
    
  # add bash completion
  install -d ${pkgdir}/usr/share/bash-completion/completions
  install -m644 bash-completion/cabal \
    ${pkgdir}/usr/share/bash-completion/completions
}
