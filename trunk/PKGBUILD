# $Id$
# Contributor: Giovanni Scafora <linuxmania@gmail.com>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=microcode_ctl
# Version of the update tool
_toolver=1.17
# Version of the microcode image
_microcodever=20100914
pkgver=${_toolver}.${_microcodever}
pkgrel=1
pkgdesc="A companion to the IA32 microcode kernel driver"
arch=('i686' 'x86_64')
url="http://www.urbanmyth.org/microcode"
license=('GPL')
install=microcode.install
source=(http://www.urbanmyth.org/microcode/$pkgname-${_toolver}.tar.gz
        http://downloadmirror.intel.com/19342/eng/microcode-${_microcodever}.tgz
        microcode)
md5sums=('98a7f06acef8459c8ef2a1b0fb86a99e'
         'c6e765670173dd6e3d9a5c72f6cd1c40'
         '483c198659b8d716bdfbf05a57182394')

build() {
  cd "$srcdir"/$pkgname-${_toolver}
  make all
}

package() {
  cd "$srcdir"/$pkgname-${_toolver}
  make DESTDIR="$pkgdir" \
       PREFIX=/usr \
       RCFILETO=/etc/rc.d \
       RCFILE=../microcode \
       RCFILEFINAL=microcode \
       MANDIR=/usr/share/man/man8 \
       install
  install -D -m644 ../microcode-${_microcodever}.dat "$pkgdir"/etc/microcode.dat
}
