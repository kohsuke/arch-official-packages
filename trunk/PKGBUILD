# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Denis Zawada <deno@rootnode.net>

pkgname=devicekit-power
pkgver=013
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/DeviceKit"
license=('GPL')
depends=('udev>=146' 'libusb>=0.1.12' 'polkit>=0.94' 'pm-utils')
makedepends=('gtk-doc' 'pkgconfig' 'intltool' 'docbook-xsl')
replaces=('devicekit')
options=('!emptydirs' '!libtool')
source=(http://hal.freedesktop.org/releases/DeviceKit-power-${pkgver}.tar.gz
        fix-segfault-history.patch
        check-swap.patch)
md5sums=('4fd56be8344556f55f86d476adbc9ab5'
         'e6126f620264ab4f690bf3173515db2b'
         '873222f0c6a1284d0cea3dda72a6d020')

build() {
  cd "${srcdir}/DeviceKit-power-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-segfault-history.patch" || return 1
  patch -Np1 -i "${srcdir}/check-swap.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/DeviceKit --disable-static || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
