# $Id$
# Contributor: John Proctor <jproctor@prium.net>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>

pkgname=cmucl
pkgver=19e
pkgrel=1
pkgdesc="CMU Common Lisp"
depends=('glibc' 'lesstif')
makedepends=('sed' 'bc')
#license PublicDomain
source=(http://common-lisp.net/project/cmucl/downloads/release/${pkgver}/cmucl-src-${pkgver}.tar.bz2 \
    http://common-lisp.net/project/cmucl/downloads/release/${pkgver}/cmucl-${pkgver}-x86-linux.tar.bz2\
    cmucl-${pkgver}-GNUmakefile.patch)
md5sums=('f6e236bc404afc3952b36a3fd6d8ec79' 'd88262b3a6d65a83683290e5fd4097b3'\
         'd2659f8c70328f55e5554adc594934e5')
url="http://www.cons.org/cmucl/"
arch=(i686)

build() {
  cd $startdir/src
  patch -p0 -i $startdir/src/cmucl-${pkgver}-GNUmakefile.patch  || return 1
  export CMUCLCORE=$startdir/src/lib/cmucl/lib/lisp.core
  # Create target buildroot
  ./src/tools/create-target.sh $CHOST 
  # Use binary-dist lisp to create initial kernel.core
  ./src/tools/build-world.sh $CHOST $startdir/src/bin/lisp
  # build native lisp binary
  ./src/tools/rebuild-lisp.sh $CHOST 
  # Generate lisp.core
  ./src/tools/load-world.sh $CHOST  ${pkgver}
  # rebuild kernel.core using native lisp 
  ./src/tools/build-world.sh $CHOST $CHOST/lisp/lisp
  # "(Re)generate lisp.core"
  ./src/tools/load-world.sh $CHOST ${pkgver} 
  # Build auxilary stuff
  ./src/tools/build-utils.sh $CHOST 
  # make new binary dist archives
  ./src/tools/make-dist.sh $CHOST ${pkgver} x86 linux
  
  mkdir -p $startdir/pkg/usr/lib/cmucl/
  mkdir -p $startdir/pkg/usr/share/man/man1
  mkdir dist
  tar xjf cmucl-${pkgver}-x86-linux.tar.bz2 -C dist 
  tar xjf cmucl-${pkgver}-x86-linux.extra.tar.bz2 -C dist
  cp -a dist/bin ${startdir}/pkg/usr/lib/cmucl
  cp -a dist/lib/cmucl ${startdir}/pkg/usr/lib/ 
  cp -a dist/man/man1 ${startdir}/pkg/usr/share/man/man1 

  ## Setup app-wrapper
  install -m755 -p -D lib/cmucl/sample-wrapper $startdir/pkg/usr/bin/cmucl
  # Fixup paths in wrapper
  sed -i -e "s|^CMUCLLIB=.*|CMUCLLIB=/usr/lib/cmucl/lib|" $startdir/pkg/usr/bin/cmucl
  sed -i -e "s|/<<your-cmucl-path>>|/usr/lib/cmucl|"      $startdir/pkg/usr/bin/cmucl
  # many apps (ie, maxima) expect to find the wrapper named 'lisp' too
  ln -sf cmucl $startdir/pkg/usr/bin/lisp
  ## Unpackaged files
  rm -f $startdir/pkg/usr/lib/cmucl/sample-wrapper

}
