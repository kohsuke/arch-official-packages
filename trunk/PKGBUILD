# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=soprano
pkgver=2.8.0
pkgrel=3
pkgdesc='A library which provides a highly usable object-oriented C++/Qt4 framework for RDF data'
arch=('i686' 'x86_64')
url='http://soprano.sourceforge.net/'
license=('GPL' 'LGPL')
depends=('qt4' 'redland-storage-virtuoso' 'libiodbc' 'virtuoso-base')
makedepends=('cmake' 'openjdk6' 'doxygen')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('273c3403aeb6d8a43e78a4887f50a385')

build() {
  . /etc/profile.d/qt4.sh

  mkdir build
  cd build
  . /etc/profile.d/openjdk6.sh
  # we need the rpath
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSOPRANO_DISABLE_CLUCENE_INDEX=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
