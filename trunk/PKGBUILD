# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Eduardo Lopes (eduol@gmx.net)

pkgname=gksu
pkgver=2.0.2
pkgrel=3
pkgdesc="A graphical frontend for su"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/gksu/index.html"
license=('GPL')
depends=('libgksu>=2.0.12' 'xorg-xauth')
makedepends=('gtk-doc' 'nautilus>=2.26.0' 'intltool')
optdepends=('nautilus')
options=('!libtool')
source=(http://people.debian.org/~kov/gksu/${pkgname}-${pkgver}.tar.gz)
md5sums=('cacbcac3fc272dce01c6ea38354489e2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's|/usr/bin/x-terminal-emulator|-l gnome-terminal|g' gksu.desktop
  echo "TryExec=gnome-terminal" >> gksu.desktop
#  libtoolize --force
#  aclocal
#  autoconf
#  automake
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
