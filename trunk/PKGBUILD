# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gtk
pkgver=1.2.10
pkgrel=11
pkgdesc="The GTK+ toolkit"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('libxi' 'glib' 'sh')
makedepends=('libxt')
options=('!libtool')
install=gtk.install
source=("ftp://ftp.gtk.org/pub/gtk/v1.2/gtk+-${pkgver}.tar.gz"
        'aclocal-fixes.patch')
md5sums=('4d5cb2fc7fb7830e4af9747a36bfce20'
         'c59d4906602d99a7468f7334b6fc3b4e')

build() {
  cd "${srcdir}/gtk+-${pkgver}"
  if [ "${CARCH}" = "x86_64" ]; then
    rm config.guess config.sub
    ln -s /usr/share/libtool/config/config.guess config.guess
    ln -s /usr/share/libtool/config/config.sub config.sub
  fi
  patch -Np0 -i "${srcdir}/aclocal-fixes.patch"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --mandir=/usr/share/man --infodir=/usr/share/info \
    --with-xinput=xfree
  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
