# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=appmenu-qt
pkgver=0.2.4
pkgrel=4
pkgdesc="Allows Qt applications to export menus over DBus to a menu applet"
arch=('i686' 'x86_64')
url='https://launchpad.net/appmenu-qt'
license=('GPL')
depends=('libdbusmenu-qt')
makedepends=('cmake')
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
md5sums=('5266d3e77e424bce19f4bcd8f4ffd4bf')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE="Release"
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
