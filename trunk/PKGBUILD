# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=1.6.3
pkgrel=1
pkgdesc='Input method server supporting various input methods'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://cle.linux.org.tw/trac/'
depends=('gtk2' 'libxtst')
makedepends=('qt' 'gtk3' 'anthy')
optdepends=('qt: support for qt4 input method'
            'gtk3: support for gtk3 input method'
            'anthy: support for anthy input method')
source=("http://www.csie.nctu.edu.tw/~cp76/gcin/download/${pkgname}-${pkgver}.tar.bz2"
        'fix_x86_64_libdir.patch'
        'fix_qt4_paths.patch')
sha1sums=('50522908d3e64319baafb5317c0b4327bbe0fb18'
          'e5c113e50420bf4750f5547641df6ec4b7e437e0'
          '16d004424caed541d5baaa6874f708b1c138ab08')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../fix_x86_64_libdir.patch
	patch -p1 -i ../fix_qt4_paths.patch
	sed '/^GTK2IM=/c GTK2IM=gtk-2.0/2.10.0/immodules' -i gtk-im/Makefile
	sed '/^GTK3IM=/c GTK3IM=gtk-3.0/3.0.0/immodules' -i gtk-im/Makefile # FS#24051
	./configure --prefix=/usr --use_i18n=Y
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
