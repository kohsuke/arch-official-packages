# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ypbind-mt
pkgver=1.33
pkgrel=2
pkgdesc='Linux NIS daemon'
arch=('i686' 'x86_64')
url='http://www.linux-nis.org/nis/ypbind-mt/'
license=('GPL2')
depends=('rpcbind' 'openslp')
optdepends=('yp-tools: to set a domain name')
backup=('etc/yp.conf' 'etc/conf.d/ypbind' 'etc/conf.d/nisdomainname')
source=("ftp://ftp.kernel.org/pub/linux/utils/net/NIS/$pkgname-$pkgver.tar.gz"
        'nisdomainname.conf'
        'ypbind.conf'
        'ypbind')
sha1sums=('49f578d15aa5d4f4130a2e96cd9c0e519263fc88'
          'eb00aecc0679e25a36b007e797f4468b40cb3e8e'
          '07dee386d001fb9e9e6b76dda8af5b2092e5a4a2'
          '112fc2aedfe3f761325b69647b7938bc1be5bfcd')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --disable-dbus-nm
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -D -m644 etc/yp.conf "$pkgdir"/etc/yp.conf
	install -D -m755 ../ypbind "$pkgdir"/etc/rc.d/ypbind
	install -D -m644 ../ypbind.conf "$pkgdir"/etc/conf.d/ypbind
	install -D -m644 ../nisdomainname.conf "$pkgdir"/etc/conf.d/nisdomainname
	install -d -m755 "$pkgdir"/var/yp/binding
}
