# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=gnupg2
pkgver=2.0.9
pkgrel=1
pkgdesc="GNU Privacy Guard 2 - a PGP replacement tool"
arch=(i686 x86_64)
depends=('libldap' 'curl>=7.16.2' 'bzip2' 'zlib' 'libksba>=1.0.2' 'libgcrypt' 'pth' 'libusb' 'libassuan>=1.0.4' 'pinentry' 'dirmngr')
license=('GPL')
options=('!makeflags')
source=(ftp://ftp.gnupg.org/gcrypt/gnupg/gnupg-$pkgver.tar.bz2 gcc-43.patch)
url="http://www.gnupg.org/"

build() {
  cd $startdir/src/gnupg-$pkgver

  # O2 breaks it on x86_64
  [ "$CARCH" = "x86_64" ] && EXTRAOPTS="--disable-optimization"
  # fix gcc 43 compilation
  patch -Np0 -i ../gcc-43.patch || return 1
  ./configure --prefix=/usr --libexecdir=/usr/lib/gnupg2 $EXTRAOPTS
  make || return 1
  make DESTDIR=$startdir/pkg install
  # move conflicting files
  mv $startdir/pkg/usr/share/gnupg{,2}
}
md5sums=('b41f3f6716a74c46a609a71853e85c7b'
         '185f19690d175cd21221ffb806f958ed')
