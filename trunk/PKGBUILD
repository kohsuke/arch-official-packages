# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=vigra
pkgver=1.7.1
pkgrel=3
pkgdesc="Computer vision library"
arch=('i686' 'x86_64')
url="http://hci.iwr.uni-heidelberg.de/vigra/"
license=('custom:MIT')
depends=('libpng' 'libtiff' 'gcc-libs' 'sh' 'hdf5>=1.8.7' 'fftw')
makedepends=('cmake' 'python-nose' 'doxygen' 'python-sphinx' 'boost' 'python-numpy')
optdepends=('python2: for python bindings'
            'boost-libs: for python bindings')
options=('!libtool')
source=(http://kogs-www.informatik.uni-hamburg.de/~koethe/vigra/${pkgname}-${pkgver}-src.tar.gz
	vigra-1.7.1.gcc460.patch)
md5sums=('2bde208e0fd7626770169dd4fa097282'
         '25ef8bc26bc38ee67e5b512d2acd0166')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/vigra-1.7.1.gcc460.patch
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
      -DWITH_VIGRANUMPY=1 \
      -DDOCINSTALL=share/doc 
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # license
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
