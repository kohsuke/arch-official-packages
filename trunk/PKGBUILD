# $Id: PKGBUILD 147983 2012-01-29 11:26:22Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Jed Brown <jed@59A2.org>
# Contributor: Simone Pezzuto <junki.gnu@gmail.com>

pkgname=hdf5-openmpi
_pkgname=hdf5
pkgver=1.8.10
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="General purpose library and file format for storing scientific data (OpenMPI version)"
url="http://www.hdfgroup.org/HDF5/"
license=('custom')
depends=('zlib' 'sh' 'openmpi')
makedepends=('time')
provides=('hdf5')
conficts=('hdf5')
source=(ftp://ftp.hdfgroup.org/HDF5/current/src/${_pkgname}-${pkgver/_/-}.tar.bz2)
options=('!libtool')
sha1sums=('867a91b75ee0bbd1f1b13aecd52e883be1507a2c')

build() {
  cd $srcdir/${_pkgname}-${pkgver/_/-}
  export CFLAGS="${CFLAGS/O2/O0}"
  export CXXFLAGS="${CFLAGS}"
  ./configure \
    CXX="mpicxx" \
    CC="mpicc" \
    FC="mpif90" \
    F9X="mpif90" \
    RUNPARALLEL="mpirun" \
    OMPI_MCA_disable_memory_allocator=1 \
    --prefix=/usr \
    --with-pthread=/usr/lib/ \
    --enable-linux-lfs \
    --enable-unsupported \
    --enable-shared \
    --disable-static \
    --enable-production=yes \
    --with-zlib \
    --with-default-api-version=v18 \
    --enable-parallel=yes \
    --enable-cxx \
    --disable-sharedlib-rpath

  make
}

package() {
  cd $srcdir/${_pkgname}-${pkgver/_/-}

  make -j1 DESTDIR=${pkgdir} install

  install -d -m755 $pkgdir/usr/share/$_pkgname
  mv $pkgdir/usr/share/{hdf5_examples,$_pkgname/examples}

  install -d -m755 $pkgdir/usr/share/licenses/${_pkgname}
  install -m644 $srcdir/${_pkgname}-${pkgver/_/-}/COPYING \
          $pkgdir/usr/share/licenses/${_pkgname}/LICENSE 
}

