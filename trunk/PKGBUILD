# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-geode
pkgver=2.11.13
pkgrel=2
pkgdesc="X.org AMD/Geode LX & NX video driver"
# there is no 64bit geode
arch=(i686)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=13' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.13.0' 'X-ABI-VIDEODRV_VERSION<13' 'X-ABI-VIDEODRV_VERSION>=14')
options=('!libtool')
conflicts=('xf86-video-amd')
replaces=('xf86-video-amd')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2 git.diff)
sha1sums=('0e5e9952de93c024e92b57bfb9e7078f9e163a29'
          '208e5957eb25dc2b5184787025002e281f931b61')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # to fix build now that XAA isn't available in new Xorg apply all git commits
  patch -Np1 -i ${srcdir}/git.diff
  autoreconf -vfi

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
