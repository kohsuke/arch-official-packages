# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=eigen2
pkgver=2.0.16
pkgrel=1
pkgdesc="A lightweight C++ template library for vector and matrix math, a.k.a. linear algebra"
arch=('any')
url='http://eigen.tuxfamily.org/index.php?title=Main_Page'
license=('GPL' 'LGPL3')
makedepends=('cmake' 'pkg-config')
replaces=('eigen')
provides=('eigen')
source=("http://bitbucket.org/eigen/eigen/get/${pkgver}.tar.bz2")
md5sums=('0d651765a1799180f5aeac18acaa6d71')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../eigen-eigen-9ca09dbd70ce \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
