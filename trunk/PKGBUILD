# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: William DÃ­az <wdiaz@archlinux.us>

pkgname=farsight2
pkgver=0.0.22
pkgrel=1
pkgdesc="Audio/Video conference software for Instant Messengers"
arch=('i686' 'x86_64')
url="http://farsight.freedesktop.org"
license=('LGPL')
depends=('libnice>=0.0.13' 'gstreamer0.10-base-plugins>=0.10.30' 'gstreamer0.10-python>=0.10.18' 'python2' 'pygobject>=2.26.0')
optdepends=('gstreamer0.10-good-plugins: for RTP plugin'
            'gstreamer0.10-bad-plugins: for RTP plugin')
options=('!libtool' '!emptydirs')
source=(http://farsight.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('e1f540cf3ebab06c3d7db1f46b44ac88')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's#python-config#python2-config#' python/Makefile.in
  # this makefile is broken
  sed -i -e 's#        #	#' docs/libs/Makefile.in
  ./configure --prefix=/usr \
      --with-package-name='Arch Linux farsight2 package' \
      --with-package-origin='http://archlinux.org' \
      --disable-static \
      --disable-valgrind \
      --disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
