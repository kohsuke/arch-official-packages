# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-utils
pkgver=2.24.1
pkgrel=1
pkgdesc="Common utilities for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libgtop>=2.24.0' 'gnome-panel>=2.24.0')
makedepends=('perlxml' 'gnome-doc-utils>=0.14.0' 'pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
groups=('gnome-extra')
install=gnome-utils.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2
	bgo497489.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/bgo497489.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/gnome-utils \
              --localstatedir=/var --disable-static \
              --enable-hal --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
md5sums=('232937a4bf5c9ebb1bbc86793871d843'
         '22922b4c54438b8808d5b942459da1db')
