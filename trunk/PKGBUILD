# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xulrunner
pkgver=1.9
pkgrel=1
pkgdesc="Mozilla Runtime Environment"
arch=(i686 x86_64)
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2>=2.12.10' 'gcc-libs>=4.3.1' 'libidl2>=0.8.10' 'mozilla-common' 'nss>=3.12rc4' 'libxt' 'lcms' 'hunspell>=1.2.2')
makedepends=('zip' 'pkgconfig' 'diffutils' 'libgnomeui>=2.22.1')
provides=(gecko-sdk)
replaces=(gecko-sdk)
url="http://wiki.mozilla.org/XUL:Xul_Runner"
source=(ftp://ftp.archlinux.org/other/xulrunner/${pkgname}-${pkgver}.tar.bz2
        mozconfig
	mozilla-ps-pdf-simplify-operators.patch
	mozilla-ssl-exception.patch
	100-system-hunspell-corrections.patch
	bzXXX_pc_honour_system_nspr_nss.patch)

build() {
  cd ${startdir}/src/mozilla
  cp ${startdir}/src/mozconfig .mozconfig

  #upstream fixes
  patch -Np1 -i ${srcdir}/mozilla-ps-pdf-simplify-operators.patch || return 1
  patch -Np1 -i ${srcdir}/mozilla-ssl-exception.patch || return 1

  #fix build with system hunspell - gentoo
  patch -Np0 -i ${srcdir}/100-system-hunspell-corrections.patch || return 1

  #fix pkgconfig files when building with system nss/nspr - ubuntu
  patch -Np1 -i ${srcdir}/bzXXX_pc_honour_system_nspr_nss.patch || return 1

  #fix profiling
  unset CFLAGS
  unset CXXFLAGS
  export LDFLAGS="-Wl,-rpath,/usr/lib/xulrunner-1.9"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS" || return 1
  make -j1 DESTDIR=${startdir}/pkg install || return 1

  rm -f ${startdir}/pkg/usr/bin/xulrunner
  ln -sf /usr/lib/xulrunner-1.9/xulrunner ${startdir}/pkg/usr/bin/xulrunner

  # The ubuntu patch adds these... get rid of it, NSS/NSPR has it.
  rm -f ${startdir}/pkg/usr/lib/pkgconfig/mozilla-ns{s,pr}.pc || return 1
}
